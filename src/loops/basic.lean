import analysis.normed_space.add_torsor_bases
import analysis.convex.caratheodory
import analysis.calculus.times_cont_diff
import measure_theory.integral.set_integral
import measure_theory.measure.lebesgue
import topology.algebra.floor_ring
import topology.path_connected
import linear_algebra.affine_space.independent
import loops.homotheties
import to_mathlib.topology.misc

/-!
# Basic definitions and properties of loops
-/

open set function finite_dimensional int (hiding range)
open_locale big_operators topological_space topological_space unit_interval
noncomputable theory

def nhds_set {Œ± : Type*} [topological_space Œ±] (s : set Œ±) : filter Œ± :=
Sup (nhds '' s)

variables {E : Type*} [normed_group E] [normed_space ‚Ñù E]
          {F : Type*} [normed_group F] [normed_space ‚Ñù F] [finite_dimensional ‚Ñù F]
          {F' : Type*} [normed_group F'] [normed_space ‚Ñù F'] [finite_dimensional ‚Ñù F']

local notation `d` := finrank ‚Ñù F

local notation `smooth_on` := times_cont_diff_on ‚Ñù ‚ä§

def smooth_at (f : E ‚Üí F) (x : E) : Prop := ‚àÉ s ‚àà ùìù x, smooth_on f s

section surrounding_points

-- def:surrounds_points
structure surrounding_pts (f : F) (p : fin (d + 1) ‚Üí F) (w : fin (d + 1) ‚Üí ‚Ñù) : Prop :=
(indep : affine_independent ‚Ñù p)
(w_pos : ‚àÄ i, 0 < w i)
(w_sum : ‚àë i, w i = 1)
(avg : ‚àë i, (w i) ‚Ä¢ (p i) = f)


def surrounded (f : F) (s : set F) : Prop :=
‚àÉ p w, surrounding_pts f p w ‚àß ‚àÄ i, p i ‚àà s

lemma surrounded_iff_mem_interior_convex_hull_aff_basis {f : F} {s : set F} :
  surrounded f s ‚Üî ‚àÉ (b : set F)
                     (h‚ÇÄ : b ‚äÜ s)
                     (h‚ÇÅ : affine_independent ‚Ñù (coe : b ‚Üí F))
                     (h‚ÇÇ : affine_span ‚Ñù b = ‚ä§),
                     f ‚àà interior (convex_hull ‚Ñù b) :=
begin
  split,
  { rintros ‚ü®p, w, ‚ü®‚ü®indep, w_pos, w_sum, rfl‚ü©, h_mem‚ü©‚ü©,
    have h_tot : affine_span ‚Ñù (range p) = ‚ä§ :=
      indep.affine_span_eq_top_iff_card_eq_finrank_add_one.mpr (fintype.card_fin _),
    refine ‚ü®range p, range_subset_iff.mpr h_mem, indep.range, h_tot, _‚ü©,
    rw interior_convex_hull_aff_basis indep h_tot,
    intros i,
    rw [‚Üê finset.affine_combination_eq_linear_combination _ _ _ w_sum,
      barycentric_coord_apply_combination_of_mem _ _ (finset.mem_univ i) w_sum],
    exact w_pos i, },
  { rintros ‚ü®b, h‚ÇÄ, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ‚ü©,
    haveI : fintype b := (finite_of_fin_dim_affine_independent ‚Ñù h‚ÇÅ).fintype,
    have hb : fintype.card b = d + 1,
    { rw [‚Üê h‚ÇÅ.affine_span_eq_top_iff_card_eq_finrank_add_one, subtype.range_coe_subtype,
        set_of_mem_eq, h‚ÇÇ], },
    let p := (coe : _ ‚Üí F) ‚àò (fintype.equiv_fin_of_card_eq hb).symm,
    have hp : b = range p,
    { ext x,
      exact ‚ü®by { intros h, use fintype.equiv_fin_of_card_eq hb ‚ü®x, h‚ü©, simp [p], },
             by { rintros ‚ü®y, rfl‚ü©, apply subtype.coe_prop, }‚ü©, },
    rw hp at h‚ÇÄ h‚ÇÇ h‚ÇÉ,
    replace h‚ÇÅ : affine_independent ‚Ñù p :=
      h‚ÇÅ.comp_embedding (fintype.equiv_fin_of_card_eq hb).symm.to_embedding,
    rw [interior_convex_hull_aff_basis h‚ÇÅ h‚ÇÇ, mem_set_of_eq] at h‚ÇÉ,
    refine ‚ü®p, Œª i, barycentric_coord h‚ÇÅ h‚ÇÇ i f, ‚ü®h‚ÇÅ, h‚ÇÉ, _, _‚ü©, Œª i, h‚ÇÄ (mem_range_self i)‚ü©,
    { exact sum_barycentric_coord_apply_eq_one h‚ÇÅ h‚ÇÇ f, },
    { rw [‚Üê finset.univ.affine_combination_eq_linear_combination p _
        (sum_barycentric_coord_apply_eq_one h‚ÇÅ h‚ÇÇ f),
        affine_combination_barycentric_coord_eq_self], }, },
end

--- lem:int_cvx
lemma surrounded_of_convex_hull {f : F} {s : set F} (hs : is_open s) (hsf : f ‚àà convex_hull ‚Ñù s) :
  surrounded f s :=
begin
  rw surrounded_iff_mem_interior_convex_hull_aff_basis,
  obtain ‚ü®t, hts, hai, hf‚ü© := (by simpa only [exists_prop, mem_Union] using convex_hull_eq_union.subst hsf :
    ‚àÉ (t : finset F), (t : set F) ‚äÜ s ‚àß affine_independent ‚Ñù (coe : t ‚Üí F) ‚àß f ‚àà convex_hull ‚Ñù (t : set F)),
  have htne : (t : set F).nonempty := (@convex_hull_nonempty_iff ‚Ñù _ _ _ _ _).mp ‚ü®f, hf‚ü©,
  obtain ‚ü®b, hb‚ÇÅ, hb‚ÇÇ, hb‚ÇÉ, hb‚ÇÑ‚ü© := exists_subset_affine_independent_span_eq_top_of_open hs hts htne hai,
  have hb‚ÇÄ : b.finite, { exact finite_of_fin_dim_affine_independent ‚Ñù hb‚ÇÉ, },
  obtain ‚ü®c, hc‚ü© := interior_convex_hull_nonempty_iff_aff_span_eq_top.mpr hb‚ÇÑ,
  obtain ‚ü®Œµ, hŒµ, hcs‚ü© := homothety_image_subset_of_open c hs hb‚ÇÇ hb‚ÇÄ,
  have hbŒµ := convex.subset_interior_image_homothety_of_one_lt
    (convex_convex_hull ‚Ñù _) hc (1 + Œµ) (lt_add_of_pos_right 1 hŒµ),
  rw affine_map.image_convex_hull at hbŒµ,
  let t : units ‚Ñù := units.mk0 (1 + Œµ) (by linarith),
  refine ‚ü®affine_map.homothety c (t : ‚Ñù) '' b, hcs, _, _, hbŒµ (convex_hull_mono hb‚ÇÅ hf)‚ü©,
  { rwa (affine_equiv.homothety_units_mul_hom c t).affine_independent_set_of_eq_iff, },
  { exact (affine_equiv.homothety_units_mul_hom c t).span_eq_top_iff.mp hb‚ÇÑ, },
end

-- lem:smooth_convex_hull
lemma smooth_surrounding {x : F} {p w} (h : surrounding_pts x p w) :
  ‚àÉ W : F ‚Üí (fin (d+1) ‚Üí F) ‚Üí (fin (d+1) ‚Üí ‚Ñù),
  ‚àÄ·∂† y in ùìù x, ‚àÄ·∂† q in  ùìù p, smooth_at (uncurry W) (y, q) ‚àß
                              ‚àÄ i, W y q i ‚àà Ioo (0 : ‚Ñù) 1 ‚àß
                              ‚àë i, W y q i ‚Ä¢ q i = y :=
sorry

end surrounding_points

namespace path

/-- A loop evaluated at `t / t` is equal to its endpoint. Note that `t / t = 0` for `t = 0`. -/
@[simp] lemma extend_div_self {x : F} (Œ≥ : path x x) (t : ‚Ñù) : Œ≥.extend (t / t) = x :=
by by_cases h : t = 0; simp [h]

/-- Concatenation of two loops which moves through the first loop on `[0, t‚ÇÄ]` and
through the second one on `[t‚ÇÄ, 1]`. All endpoints are assumed to be the same so that this
function is also well-defined for `t‚ÇÄ ‚àà {0, 1}`.
`strans` stands either for a *s*kewed transitivity, or a transitivity with different *s*peeds. -/
def strans {x : F} (Œ≥ Œ≥' : path x x) (t‚ÇÄ : I) : path x x :=
{ to_fun := Œª t, if t ‚â§ t‚ÇÄ then Œ≥.extend (t / t‚ÇÄ) else Œ≥'.extend ((t - t‚ÇÄ) / (1 - t‚ÇÄ)),
  continuous_to_fun :=
  begin
    refine continuous.if_le _ _ continuous_id continuous_const (by simp only [extend_div_self,
      unit_interval.mk_zero, zero_le_one, id.def, zero_div, forall_eq, extend_extends, path.source,
      left_mem_Icc, sub_self]),
    -- TODO: the following are provable by `continuity` but it is too slow
    exacts [Œ≥.continuous_extend.comp continuous_subtype_coe.div_const,
      Œ≥'.continuous_extend.comp (continuous_subtype_coe.sub continuous_const).div_const]
  end,
  source' := by simp only [unit_interval.nonneg', unit_interval.coe_zero,
    unit_interval.mk_zero, zero_le_one,
    if_true, zero_div, comp_app, extend_extends, path.source, left_mem_Icc],
  target' := by simp only [unit_interval.le_one'.le_iff_eq.trans eq_comm, extend_div_self,
    unit_interval.coe_one, implies_true_iff, eq_self_iff_true, comp_app, ite_eq_right_iff]
    {contextual := tt}}

@[simp] lemma strans_zero {x : F} (Œ≥ Œ≥' : path x x) : Œ≥.strans Œ≥' 0 = Œ≥' :=
by { ext t, simp only [strans, path.coe_mk, if_pos, unit_interval.coe_zero,
  div_one, extend_extends',
  unit_interval.nonneg'.le_iff_eq, sub_zero, div_zero, extend_zero, ite_eq_right_iff,
  show (t : ‚Ñù) = 0 ‚Üî t = 0, from (@subtype.ext_iff _ _ t 0).symm, path.source, eq_self_iff_true,
  implies_true_iff] {contextual := tt} }

@[simp] lemma strans_one {x : F} (Œ≥ Œ≥' : path x x) : Œ≥.strans Œ≥' 1 = Œ≥ :=
by { ext t, simp only [strans, unit_interval.le_one', path.coe_mk, if_pos, div_one,
  extend_extends', unit_interval.coe_one] }

@[simp] lemma strans_of_ge {x : F} (Œ≥ Œ≥' : path x x) {t‚ÇÄ t : I} (h : t‚ÇÄ ‚â§ t) :
  Œ≥.strans Œ≥' t‚ÇÄ t = Œ≥'.extend ((t - t‚ÇÄ) / (1 - t‚ÇÄ)) :=
begin
  simp only [path.coe_mk, path.strans, ite_eq_right_iff],
  intro h2, obtain rfl := le_antisymm h h2, simp
end

@[simp] lemma strans_self {x : F} (Œ≥ Œ≥' : path x x) (t‚ÇÄ : I) : Œ≥.strans Œ≥' t‚ÇÄ t‚ÇÄ = x :=
by { simp only [strans, path.coe_mk, extend_div_self, if_pos, le_rfl], }

@[simp] lemma refl_strans_refl {x : F} {t‚ÇÄ : I} : (refl x).strans (refl x) t‚ÇÄ = refl x :=
by { ext s, simp [strans] }

lemma range_strans_left {x : F} {Œ≥ Œ≥' : path x x} {t‚ÇÄ : I} (h : t‚ÇÄ ‚â† 0) :
  range Œ≥ ‚äÜ range (Œ≥.strans Œ≥' t‚ÇÄ) :=
by { rintro _ ‚ü®t, rfl‚ü©, use t * t‚ÇÄ,
  simp [strans, unit_interval.mul_le_right, unit_interval.coe_ne_zero.mpr h] }

lemma range_strans_right {x : F} {Œ≥ Œ≥' : path x x} {t‚ÇÄ : I} (h : t‚ÇÄ ‚â† 1) :
  range Œ≥' ‚äÜ range (Œ≥.strans Œ≥' t‚ÇÄ) :=
begin
  rintro _ ‚ü®t, rfl‚ü©,
  have := mul_nonneg t.2.1 (sub_nonneg.mpr t‚ÇÄ.2.2),
  let t' : I := ‚ü®t‚ÇÄ + t * (1 - t‚ÇÄ), add_nonneg t‚ÇÄ.2.1 this, by { rw [add_comm, ‚Üê le_sub_iff_add_le],
    refine (mul_le_mul_of_nonneg_right t.2.2 $ sub_nonneg.mpr t‚ÇÄ.2.2).trans_eq (one_mul _) }‚ü©,
  have h2 : t‚ÇÄ ‚â§ t' := le_add_of_nonneg_right this,
  have h3 := sub_ne_zero.mpr (unit_interval.coe_ne_one.mpr h).symm,
  use t',
  simp [h2, unit_interval.coe_ne_one.mpr h, h3],
end

-- this lemma is easier if we reorder/reassociate the arguments
lemma _root_.continuous.path_strans {X : Type*} [uniform_space X] [separated_space X]
  [locally_compact_space X] {f : X ‚Üí F} {t : X ‚Üí I} {s : X ‚Üí I}
  {Œ≥ Œ≥' : ‚àÄ x, path (f x) (f x)}
  (hŒ≥ : continuous ‚ÜøŒ≥)
  (hŒ≥' : continuous ‚ÜøŒ≥')
  (hŒ≥0 : ‚àÄ ‚¶Éx s‚¶Ñ, t x = 0 ‚Üí Œ≥ x s = f x)
  (hŒ≥'1 : ‚àÄ ‚¶Éx s‚¶Ñ, t x = 1 ‚Üí Œ≥' x s = f x)
  (ht : continuous t)
  (hs : continuous s) :
  continuous (Œª x, strans (Œ≥ x) (Œ≥' x) (t x) (s x)) :=
begin
  have hŒ≥0 : ‚àÄ {x‚ÇÄ}, t x‚ÇÄ = 0 ‚Üí tendsto_uniformly (Œª x, Œ≥ x) (Œª _, f x‚ÇÄ) (ùìù x‚ÇÄ),
  { intros x‚ÇÄ hx‚ÇÄ, convert continuous.tendsto_uniformly (Œª x, Œ≥ x) hŒ≥ _,
    ext t, rw [hŒ≥0 hx‚ÇÄ] },
  have hŒ≥'1 : ‚àÄ {x‚ÇÄ}, t x‚ÇÄ = 1 ‚Üí tendsto_uniformly (Œª x, Œ≥' x) (Œª _, f x‚ÇÄ) (ùìù x‚ÇÄ),
  { intros x‚ÇÄ hx‚ÇÄ, convert continuous.tendsto_uniformly (Œª x, Œ≥' x) hŒ≥' _,
    ext t, rw [hŒ≥'1 hx‚ÇÄ] },
  refine continuous.if_le _ _ hs ht _,
  { rw [continuous_iff_continuous_at],
    intro x,
    refine (continuous_subtype_coe.comp hs).continuous_at.comp_div_cases (Œª x s, (Œ≥ x).extend s)
      (continuous_subtype_coe.comp ht).continuous_at _ _,
    { intro h,
      refine continuous_at.extend _ _ continuous_at_snd,
      exact hŒ≥.continuous_at.comp (continuous_at_fst.fst.prod continuous_at_snd) },
    { intro h,
      have ht : t x = 0 := subtype.ext h,
      apply filter.tendsto.extend,
      dsimp only, rw [(proj_Icc_surjective _).map_top, extend_zero],
      refine tendsto_prod_top_iff.mpr (hŒ≥0 ht) } },
  { rw [continuous_iff_continuous_at],
    intro x,
    refine ((continuous_subtype_coe.comp hs).sub (continuous_subtype_coe.comp ht))
      .continuous_at.comp_div_cases (Œª x s, (Œ≥' x).extend s)
      (continuous_const.sub $ continuous_subtype_coe.comp ht).continuous_at _ _,
    { intro h,
      refine continuous_at.extend _ _ continuous_at_snd,
      exact hŒ≥'.continuous_at.comp (continuous_at_fst.fst.prod continuous_at_snd) },
    { intro h,
      have ht : t x = 1 := subtype.ext (sub_eq_zero.mp h).symm,
      apply filter.tendsto.extend,
      dsimp only, rw [(proj_Icc_surjective _).map_top, extend_zero],
      refine tendsto_prod_top_iff.mpr (hŒ≥'1 ht) } },
  { rintro x h, rw [h, sub_self, zero_div, extend_div_self, extend_zero] },
end

end path

set_option old_structure_cmd true

variables (E F)

structure loop :=
(to_fun : ‚Ñù ‚Üí F)
(per' : ‚àÄ t, to_fun (t + 1) = to_fun t)

instance : has_coe_to_fun (loop F) (Œª _, ‚Ñù ‚Üí F) := ‚ü®Œª Œ≥, Œ≥.to_fun‚ü©

initialize_simps_projections loop (to_fun ‚Üí apply)

/-- Any function `œÜ : Œ± ‚Üí loop F` can be seen as a function `Œ± √ó ‚Ñù ‚Üí F`. -/
@[uncurry_simps]
instance has_uncurry_loop {Œ± : Type*} : has_uncurry (Œ± ‚Üí loop F) (Œ± √ó ‚Ñù) F := ‚ü®Œª œÜ p, œÜ p.1 p.2‚ü©

variables {E F}

@[simps]
def const_loop (f : F) : loop F :=
‚ü®Œª t, f, by simp‚ü©

namespace loop

@[ext] protected lemma ext : ‚àÄ {Œ≥‚ÇÅ Œ≥‚ÇÇ : loop F}, (Œ≥‚ÇÅ : ‚Ñù ‚Üí F) = Œ≥‚ÇÇ ‚Üí Œ≥‚ÇÅ = Œ≥‚ÇÇ
| ‚ü®x, h1‚ü© ‚ü®.(x), h2‚ü© rfl := rfl

/-- Periodicity of loops restated in terms of the function coercion. -/
lemma per (Œ≥ : loop F) : ‚àÄ t, Œ≥ (t + 1) = Œ≥ t :=
loop.per' Œ≥

protected lemma one (Œ≥ : loop F) : Œ≥ 1 = Œ≥ 0 :=
by { convert Œ≥.per 0, rw [zero_add] }

/-- Transforming a loop by applying function `f`. -/
@[simps]
def transform (Œ≥ : loop F) (f : F ‚Üí F') : loop F' :=
‚ü®Œª t, f (Œ≥ t), Œª t, by rw Œ≥.per‚ü©

/-- Shifting a loop, or equivalently, adding a constant value to a loop -/
@[simps]
def shift (Œ≥ : loop F) (x : F) : loop F := Œ≥.transform (+ x)

/-- The average value of a loop. -/
noncomputable
def average [measurable_space F] [borel_space F] (Œ≥ : loop F) : F := ‚à´ x in Icc 0 1, (Œ≥ x)

def support {X : Type*} [topological_space X] [measurable_space F] [borel_space F] (Œ≥ : X ‚Üí loop F) : set X :=
closure {x | Œ≥ x ‚â† const_loop (Œ≥ x).average}

lemma const_of_not_mem_support {X : Type*} [topological_space X] [measurable_space F] [borel_space F] {Œ≥ : X ‚Üí loop F} {x : X}
  (hx : x ‚àâ support Œ≥) : Œ≥ x = const_loop (Œ≥ x).average :=
begin
  classical,
  by_contradiction H,
  apply hx,
  apply subset_closure,
  exact H
end

lemma continuous_of_family {Œ± : Type*} [topological_space Œ±] {Œ≥ : Œ± ‚Üí loop F} (h : continuous ‚ÜøŒ≥) :
  ‚àÄ a, continuous (Œ≥ a) :=
begin
  intro a,
  rw show (Œ≥ a : ‚Ñù ‚Üí F) = ‚ÜøŒ≥ ‚àò (Œª t, (a, t)), from rfl,
  exact h.comp (continuous_const.prod_mk continuous_id')
end

lemma continuous_of_family_step {Œ± Œ≤ : Type*} [topological_space Œ±] [topological_space Œ≤]
  {Œ≥ : Œ± ‚Üí Œ≤ ‚Üí loop F} (h : continuous ‚ÜøŒ≥) : ‚àÄ a, continuous ‚Üø(Œ≥ a) :=
begin
  intro a,
  rw show ‚Üø(Œ≥ a : Œ≤ ‚Üí loop F) = ‚ÜøŒ≥ ‚àò (Œª t, (a, t)), from rfl,
  exact h.comp (continuous_const.prod_mk continuous_id')
end

lemma add_nat_eq (Œ≥ : loop F) (t : ‚Ñù) : ‚àÄ (n : ‚Ñï), Œ≥ (t+n) = Œ≥ t
| 0 := (add_zero t).symm ‚ñ∏ rfl
| (nat.succ n) := by rw [‚Üê add_nat_eq n, nat.cast_succ, ‚Üê add_assoc, Œ≥.per]

lemma add_int_eq (Œ≥ : loop F) (t : ‚Ñù) (n : ‚Ñ§) : Œ≥ (t+n) = Œ≥ t :=
begin
  induction n using int.induction_on with n hn n hn,
  { norm_cast, rw add_zero },
  { rw [‚Üê hn, int.cast_add, ‚Üê add_assoc, int.cast_one, Œ≥.per] },
  { rw [‚Üê hn, int.cast_sub, add_sub, int.cast_one, ‚Üê Œ≥.per, sub_add_cancel] }
end

lemma fract_eq (Œ≥ : loop F) : ‚àÄ t, Œ≥ (fract t) = Œ≥ t :=
begin
  intro t,
  unfold fract,
  rw [sub_eq_add_neg, ‚Üê int.cast_neg],
  exact Œ≥.add_int_eq _ _
end

lemma comp_fract_eq (Œ≥ : loop F) : Œ≥ ‚àò fract = Œ≥ :=
funext Œ≥.fract_eq

lemma range_eq_image (Œ≥ : loop F) : range Œ≥ = Œ≥ '' I :=
begin
  apply eq_of_subset_of_subset,
  { rw range_subset_iff,
    exact Œª y, ‚ü®fract y, unit_interval.fract_mem y, Œ≥.fract_eq _‚ü© },
  { rintros y ‚ü®x, hx, hxy‚ü©,
    exact ‚ü®x, hxy‚ü© },
end

@[simps]
noncomputable
def of_path {x : F} (Œ≥ : path x x) : loop F :=
{ to_fun := Œª t, Œ≥.extend (fract t),
  per' :=
  begin
    intros t,
    congr' 1,
    rw fract_eq_fract,
    use 1,
    norm_num
  end }

@[simp]
lemma range_of_path {x : F} (Œ≥ : path x x) : range (of_path Œ≥) = range Œ≥ :=
begin
  rw loop.range_eq_image,
  unfold_coes,
  simp only [of_path, image_eq_range],
  congr,
  ext t,
  by_cases ht1 : t.val = 1,
  { have : t = ‚ü®1, right_mem_Icc.mpr zero_le_one‚ü© := subtype.ext_val ht1,
    rw this,
    norm_cast,
    simp only [fract, floor_one, path.extend_zero, int.cast_one, sub_self, subtype.coe_mk],
    exact Œ≥.target.symm },
  { change (t : ‚Ñù) ‚â† 1 at ht1,
    have : fract ‚Üët = t.val,
    { rw fract_eq_iff,
      exact ‚ü®t.2.1, lt_of_le_of_ne t.2.2 ht1, ‚ü®0, sub_self _‚ü©‚ü© },
    simp only [this, Œ≥.extend_extends t.2],
    congr',
    rw subtype.ext_iff_val }
end

lemma of_path_continuous {x : F} (Œ≥ : path x x) : continuous (of_path Œ≥) :=
begin
  simp only [has_coe_to_fun.coe, coe_fn, of_path],
  apply Œ≥.continuous_extend.continuous_on.comp_fract,
  rw [Œ≥.extend_zero, Œ≥.extend_one]
end

-- move
lemma continuous_on.comp_fract'' {Œ± Œ≤ Œ≥ : Type*} [linear_ordered_ring Œ±] [floor_ring Œ±]
  [topological_space Œ±] [order_topology Œ±]
  [topological_add_group Œ±] [topological_space Œ≤] [topological_space Œ≥]
  {s : Œ≤ ‚Üí Œ±}
  {f : Œ≤ ‚Üí Œ± ‚Üí Œ≥}
  (h : continuous_on (uncurry f) $ (univ : set Œ≤).prod (Icc 0 1 : set Œ±))
  (hs : continuous s)
  (hf : ‚àÄ s, f s 0 = f s 1) :
  continuous (Œª x : Œ≤, f x $ fract (s x)) :=
(h.comp_fract' hf).comp (continuous_id.prod_mk hs)

/-- `loop.of_path` is continuous, general version. -/
lemma _root_.continuous.of_path {Œπ : Type*} [topological_space Œπ] (x : Œπ ‚Üí F) (t : Œπ ‚Üí ‚Ñù)
  (Œ≥ : ‚àÄ (i : Œπ), path (x i) (x i)) (hŒ≥ : continuous ‚ÜøŒ≥) (ht : continuous t) :
  continuous (Œª i, of_path (Œ≥ i) (t i)) :=
begin
  change continuous (Œª i : Œπ, (Œª s, (Œ≥ s).extend) i (fract (t i))),
  refine continuous_on.comp_fract'' _ ht _,
  { exact (hŒ≥.comp (continuous_id.prod_map continuous_proj_Icc)).continuous_on },
  { simp only [unit_interval.mk_zero, zero_le_one, path.target, path.extend_extends,
      implies_true_iff, eq_self_iff_true, path.source, right_mem_Icc, left_mem_Icc,
      unit_interval.mk_one] }
end

/-- `loop.of_path` is continuous, where the endpoints of `Œ≥` are fixed. TODO: remove -/
lemma of_path_continuous_family {Œπ : Type*} [topological_space Œπ] {x : F} (Œ≥ : Œπ ‚Üí path x x)
  (h : continuous ‚ÜøŒ≥) : continuous ‚Üø(Œª s, of_path $ Œ≥ s) :=
continuous.of_path _ _ (Œª i : Œπ √ó ‚Ñù, Œ≥ i.1) (h.comp $ continuous_fst.prod_map continuous_id)
  continuous_snd

def round_trip {x y : F} (Œ≥ : path x y) : loop F :=
of_path (Œ≥.trans Œ≥.symm)

lemma round_trip_range {x y : F} {Œ≥ : path x y} : range (round_trip Œ≥) = range Œ≥ :=
by simp [round_trip, range_of_path, path.trans_range, path.symm_range]

lemma round_trip_based_at {x y : F} {Œ≥ : path x y} : round_trip Œ≥ 0 = x :=
begin
  unfold_coes,
  rw [round_trip, of_path],
  simp [fract_zero]
end

lemma round_trip_continuous {x y : F} (Œ≥ : path x y) : continuous (round_trip Œ≥) :=
of_path_continuous _

noncomputable
def round_trip_family {x y : F} (Œ≥ : path x y) : ‚Ñù ‚Üí loop F :=
have key : ‚àÄ {t}, x = Œ≥.extend (min 0 t) := Œª t, (Œ≥.extend_of_le_zero $ min_le_left _ _).symm,
Œª t, round_trip ((Œ≥.truncate 0 t).cast key rfl)

lemma round_trip_family_continuous {x y : F} {Œ≥ : path x y} : continuous ‚Üø(round_trip_family Œ≥) :=
of_path_continuous_family _
  (path.trans_continuous_family _ (Œ≥.truncate_const_continuous_family 0) _ $
    path.symm_continuous_family _ $ Œ≥.truncate_const_continuous_family 0)

lemma round_trip_family_based_at {x y : F} {Œ≥ : path x y} : ‚àÄ t, (round_trip_family Œ≥) t 0 = x :=
Œª t, round_trip_based_at

lemma round_trip_family_zero {x y : F} {Œ≥ : path x y} : (round_trip_family Œ≥) 0 = of_path (path.refl x) :=
begin
  simp only [round_trip_family, round_trip, path.truncate_zero_zero, of_path],
  ext z,
  congr,
  ext t,
  simp [path.refl_symm]
end

lemma round_trip_family_one {x y : F} {Œ≥ : path x y} : (round_trip_family Œ≥) 1 = round_trip Œ≥ :=
begin
  simp only [round_trip_family, round_trip, path.truncate_zero_one, of_path],
  refl
end

end loop
