import analysis.normed_space.add_torsor_bases
import analysis.convex.caratheodory
import analysis.calculus.times_cont_diff
import measure_theory.integral.interval_integral
import measure_theory.measure.lebesgue
import topology.algebra.floor_ring
import topology.path_connected
import linear_algebra.affine_space.independent

import to_mathlib.homotheties
import to_mathlib.smooth_barycentric
import to_mathlib.topology.path
import to_mathlib.linear_algebra.affine_space.basis
import to_mathlib.measure_theory.parametric_interval_integral

/-!
# Basic definitions and properties of loops
-/

open set function finite_dimensional int topological_space
open_locale big_operators topological_space topological_space unit_interval
noncomputable theory

variables {X X' Y Z : Type*} [topological_space X]
variables [topological_space X'] [topological_space Y] [topological_space Z]
variables {E : Type*} [normed_group E] [normed_space ‚Ñù E]
          {F : Type*} [normed_group F] [normed_space ‚Ñù F] 
          {F' : Type*} [normed_group F'] [normed_space ‚Ñù F'] 

set_option old_structure_cmd true

/-! ## Definition and periodicity lemmas -/

variables (X)

/-- A loop is a function with domain `‚Ñù` and is periodic with period 1. -/
structure loop :=
(to_fun : ‚Ñù ‚Üí X)
(per' : ‚àÄ t, to_fun (t + 1) = to_fun t)

instance : has_coe_to_fun (loop X) (Œª _, ‚Ñù ‚Üí X) := ‚ü®Œª Œ≥, Œ≥.to_fun‚ü©

initialize_simps_projections loop (to_fun ‚Üí apply)

/-- Any function `œÜ : Œ± ‚Üí loop X` can be seen as a function `Œ± √ó ‚Ñù ‚Üí X`. -/
@[uncurry_simps]
instance has_uncurry_loop {Œ± : Type*} : has_uncurry (Œ± ‚Üí loop X) (Œ± √ó ‚Ñù) X := ‚ü®Œª œÜ p, œÜ p.1 p.2‚ü©

variables {X}

namespace loop

@[ext] protected lemma ext : ‚àÄ {Œ≥‚ÇÅ Œ≥‚ÇÇ : loop X}, (Œ≥‚ÇÅ : ‚Ñù ‚Üí X) = Œ≥‚ÇÇ ‚Üí Œ≥‚ÇÅ = Œ≥‚ÇÇ
| ‚ü®x, h1‚ü© ‚ü®.(x), h2‚ü© rfl := rfl

instance [has_zero X] : has_zero (loop X) :=
‚ü®{ to_fun := Œª t, 0, per' := Œª t, rfl }‚ü©

/-- The constant loop. -/
@[simps]
def const (f : X) : loop X :=
‚ü®Œª t, f, by simp‚ü©

@[simp] lemma const_zero [has_zero X] : const (0 : X) = (0 : loop X) :=
begin
  ext t,
  refl
end

instance [inhabited X] : inhabited (loop X) :=
‚ü®loop.const (default X)‚ü©

/-- Periodicity of loops restated in terms of the function coercion. -/
lemma per (Œ≥ : loop X) : ‚àÄ t, Œ≥ (t + 1) = Œ≥ t :=
loop.per' Œ≥

protected lemma one (Œ≥ : loop X) : Œ≥ 1 = Œ≥ 0 :=
by { convert Œ≥.per 0, rw [zero_add] }

lemma add_nat_eq (Œ≥ : loop X) (t : ‚Ñù) : ‚àÄ (n : ‚Ñï), Œ≥ (t+n) = Œ≥ t
| 0 := (add_zero t).symm ‚ñ∏ rfl
| (nat.succ n) := by rw [‚Üê add_nat_eq n, nat.cast_succ, ‚Üê add_assoc, Œ≥.per]

lemma add_int_eq (Œ≥ : loop X) (t : ‚Ñù) (n : ‚Ñ§) : Œ≥ (t+n) = Œ≥ t :=
begin
  induction n using int.induction_on with n hn n hn,
  { norm_cast, rw add_zero },
  { rw [‚Üê hn, int.cast_add, ‚Üê add_assoc, int.cast_one, Œ≥.per] },
  { rw [‚Üê hn, int.cast_sub, add_sub, int.cast_one, ‚Üê Œ≥.per, sub_add_cancel] }
end

lemma fract_eq (Œ≥ : loop X) : ‚àÄ t, Œ≥ (fract t) = Œ≥ t :=
begin
  intro t,
  unfold fract,
  rw [sub_eq_add_neg, ‚Üê int.cast_neg],
  exact Œ≥.add_int_eq _ _
end

lemma comp_fract_eq (Œ≥ : loop X) : Œ≥ ‚àò fract = Œ≥ :=
funext Œ≥.fract_eq

lemma range_eq_image (Œ≥ : loop X) : range Œ≥ = Œ≥ '' I :=
begin
  apply eq_of_subset_of_subset,
  { rw range_subset_iff,
    exact Œª y, ‚ü®fract y, unit_interval.fract_mem y, Œ≥.fract_eq _‚ü© },
  { rintros y ‚ü®x, hx, hxy‚ü©,
    exact ‚ü®x, hxy‚ü© },
end

/-- Transforming a loop by applying function `f`. -/
@[simps]
def transform (Œ≥ : loop X) (f : X ‚Üí X') : loop X' :=
‚ü®Œª t, f (Œ≥ t), Œª t, by rw Œ≥.per‚ü©

/-- Shifting a loop, or equivalently, adding a constant value to a loop -/
@[simps]
def shift {F : Type*} [add_group F] [topological_space F] (Œ≥ : loop F) (x : F) : loop F :=
Œ≥.transform (+ x)

/-! ## Support of a loop family -/

/-- A loop is constant if it takes the same value at every time. 
See also `loop.is_const_iff_forall_avg` and `loop.is_const_iff_const_avg` for characterizations in
terms of average values. -/
def is_const (Œ≥ : loop X) := ‚àÄ t s, Œ≥ t = Œ≥ s

/-- The support of a loop family is the closure of the set of parameters where
the loop is not constant. -/
def support (Œ≥ : X ‚Üí loop X') : set X :=
closure {x | ¬¨ (Œ≥ x).is_const}

lemma is_closed_support (Œ≥ : X ‚Üí loop X') : is_closed (loop.support Œ≥) :=
is_closed_closure

lemma continuous_of_family {Œ≥ : X ‚Üí loop X'} (h : continuous ‚ÜøŒ≥) (x : X) : continuous (Œ≥ x) :=
h.comp $ continuous_const.prod_mk continuous_id

lemma continuous_of_family_step {Œ≥ : X ‚Üí Y ‚Üí loop Z} (h : continuous ‚ÜøŒ≥) (x : X) :
  continuous ‚Üø(Œ≥ x) :=
h.comp $ continuous_const.prod_mk continuous_id

/-! ## From paths to loops -/

/-- Turn a path into a loop. -/
@[simps]
noncomputable def of_path {x : X} (Œ≥ : path x x) : loop X :=
{ to_fun := Œª t, Œ≥.extend (fract t),
  per' :=
  begin
    intros t,
    congr' 1,
    exact_mod_cast fract_add_int t 1
  end }

@[simp]
lemma range_of_path {x : X} (Œ≥ : path x x) : range (of_path Œ≥) = range Œ≥ :=
begin
  rw loop.range_eq_image,
  unfold_coes,
  simp only [of_path, image_eq_range],
  congr,
  ext t,
  by_cases ht1 : t.val = 1,
  { have : t = ‚ü®1, right_mem_Icc.mpr zero_le_one‚ü© := subtype.ext_val ht1,
    rw this,
    norm_cast,
    simp only [fract, floor_one, path.extend_zero, int.cast_one, sub_self, subtype.coe_mk],
    exact Œ≥.target.symm },
  { change (t : ‚Ñù) ‚â† 1 at ht1,
    have : fract ‚Üët = t.val,
    { rw fract_eq_iff,
      exact ‚ü®t.2.1, lt_of_le_of_ne t.2.2 ht1, ‚ü®0, sub_self _‚ü©‚ü© },
    simp only [this, Œ≥.extend_extends t.2],
    congr',
    rw subtype.ext_iff_val }
end

lemma of_path_continuous {x : X} (Œ≥ : path x x) : continuous (of_path Œ≥) :=
begin
  simp only [has_coe_to_fun.coe, coe_fn, of_path],
  apply Œ≥.continuous_extend.continuous_on.comp_fract,
  rw [Œ≥.extend_zero, Œ≥.extend_one]
end

/-- `loop.of_path` is continuous, general version. -/
lemma _root_.continuous.of_path (x : X ‚Üí Y) (t : X ‚Üí ‚Ñù)
  (Œ≥ : ‚àÄ i, path (x i) (x i)) (hŒ≥ : continuous ‚ÜøŒ≥) (ht : continuous t) :
  continuous (Œª i, of_path (Œ≥ i) (t i)) :=
begin
  change continuous (Œª i, (Œª s, (Œ≥ s).extend) i (fract (t i))),
  refine continuous_on.comp_fract'' _ ht _,
  { exact (hŒ≥.comp (continuous_id.prod_map continuous_proj_Icc)).continuous_on },
  { simp only [unit_interval.mk_zero, zero_le_one, path.target, path.extend_extends,
      implies_true_iff, eq_self_iff_true, path.source, right_mem_Icc, left_mem_Icc,
      unit_interval.mk_one] }
end

/-- `loop.of_path` is continuous, where the endpoints of `Œ≥` are fixed. TODO: remove -/
lemma of_path_continuous_family {x : Y} (Œ≥ : X ‚Üí path x x)
  (h : continuous ‚ÜøŒ≥) : continuous ‚Üø(Œª s, of_path $ Œ≥ s) :=
continuous.of_path _ _ (Œª i : X √ó ‚Ñù, Œ≥ i.1) (h.comp $ continuous_fst.prod_map continuous_id)
  continuous_snd

/-! ## Round trips -/

/-- The round-trip defined by `Œ≥` is `Œ≥` followed by `Œ≥‚Åª¬π`. -/
def round_trip {x y : X} (Œ≥ : path x y) : loop X :=
of_path (Œ≥.trans Œ≥.symm)

lemma round_trip_range {x y : X} {Œ≥ : path x y} : range (round_trip Œ≥) = range Œ≥ :=
by simp [round_trip, range_of_path, path.trans_range, path.symm_range]

lemma round_trip_based_at {x y : X} {Œ≥ : path x y} : round_trip Œ≥ 0 = x :=
begin
  unfold_coes,
  rw [round_trip, of_path],
  simp [fract_zero]
end

lemma round_trip_continuous {x y : X} (Œ≥ : path x y) : continuous (round_trip Œ≥) :=
of_path_continuous _

/-- The round trip loop family associated to a path `Œ≥`. For each parameter `t`,
the loop `round_trip_family Œ≥ t` backtracks at `Œ≥ t`. -/
noncomputable
def round_trip_family {x y : X} (Œ≥ : path x y) : ‚Ñù ‚Üí loop X :=
have key : ‚àÄ {t}, x = Œ≥.extend (min 0 t) := Œª t, (Œ≥.extend_of_le_zero $ min_le_left _ _).symm,
Œª t, round_trip ((Œ≥.truncate 0 t).cast key rfl)

lemma round_trip_family_continuous {x y : X} {Œ≥ : path x y} : continuous ‚Üø(round_trip_family Œ≥) :=
of_path_continuous_family _
  (path.trans_continuous_family _ (Œ≥.truncate_const_continuous_family 0) _ $
    path.symm_continuous_family _ $ Œ≥.truncate_const_continuous_family 0)

lemma round_trip_family_based_at {x y : X} {Œ≥ : path x y} : ‚àÄ t, (round_trip_family Œ≥) t 0 = x :=
Œª t, round_trip_based_at

lemma round_trip_family_zero {x y : X} {Œ≥ : path x y} :
  (round_trip_family Œ≥) 0 = of_path (path.refl x) :=
begin
  simp only [round_trip_family, round_trip, path.truncate_zero_zero, of_path],
  ext z,
  congr,
  ext t,
  simp [path.refl_symm]
end

lemma round_trip_family_one {x y : X} {Œ≥ : path x y} : (round_trip_family Œ≥) 1 = round_trip Œ≥ :=
begin
  simp only [round_trip_family, round_trip, path.truncate_zero_one, of_path],
  refl
end


section average

/-! ## Average value of a loop -/

variables [measurable_space F] [borel_space F] [second_countable_topology F] [complete_space F]

/-- The average value of a loop. -/
noncomputable def average (Œ≥ : loop F) : F :=
‚à´ x in 0..1, (Œ≥ x)

@[simp]
lemma zero_average : average (0 : loop F) = 0 :=
interval_integral.integral_zero

lemma is_const_iff_forall_avg {Œ≥ : loop F} : Œ≥.is_const ‚Üî ‚àÄ t, Œ≥ t = Œ≥.average :=
begin
  split ; intro h,
  { intro t,
    have : Œ≥ = loop.const (Œ≥ t),
    { ext s,
      rw h s t,
      refl },
    rw this, 
    simp only [average, const_apply, interval_integral.integral_const, one_smul, sub_zero], },
  { intros t s,
    rw [h, h] }
end

@[simp] lemma average_const {f : F} : (loop.const f).average = f :=
by simp [loop.average]

lemma is_const_iff_const_avg {Œ≥ : loop F} : Œ≥.is_const ‚Üî Œ≥ = loop.const Œ≥.average :=
begin
  rw loop.is_const_iff_forall_avg,  
  split,
  { intro h,
    ext s,
    apply h },
  { intros h t,
    rw h,
    simp }
end

lemma const_of_not_mem_support {Œ≥ : X ‚Üí loop F} {x : X}
  (hx : x ‚àâ support Œ≥) : Œ≥ x = loop.const (Œ≥ x).average :=
begin
  classical,
  by_contradiction H,
  apply hx,
  apply subset_closure,
  simp_rw loop.is_const_iff_const_avg,
  exact H
end

lemma continuous_average {E : Type*} [topological_space E] [first_countable_topology E] [locally_compact_space E] {Œ≥ : E ‚Üí loop F}
  (hŒ≥_cont : continuous ‚ÜøŒ≥) : continuous (Œª x, (Œ≥ x).average) :=
continuous_parametric_interval_integral_of_continuous' hŒ≥_cont _ _

/-- The normalization of a loop `Œ≥` is the loop `Œ≥ - Œ≥.average`. -/
def normalize (Œ≥ : loop F) : loop F :=
{ to_fun := Œª t, Œ≥ t - Œ≥.average,
  per' := Œª t, by simp [Œ≥.per] }

@[simp]
lemma normalize_apply (Œ≥ : loop F) (t : ‚Ñù) : loop.normalize Œ≥ t = Œ≥ t - Œ≥.average :=
rfl

end average

end loop

section c1

/-! ## Differentiation of loop families -/

local notation `D` := fderiv ‚Ñù
local notation `‚àÇ‚ÇÅ` := partial_fderiv_fst ‚Ñù
local notation `ùíû` := times_cont_diff ‚Ñù

variables (œÄ : E ‚Üí ‚Ñù) (N : ‚Ñù) (Œ≥ : E ‚Üí loop F)
          (hŒ≥ : is_compact (loop.support Œ≥))

/-- Differential of a loop family with respect to the parameter. -/
def loop.diff (Œ≥ : E ‚Üí loop F) (e : E) : loop (E ‚ÜíL[‚Ñù] F) :=
{ to_fun := Œª t, ‚àÇ‚ÇÅ (Œª e t, Œ≥ e t) e t,
  per' := Œª t, by simp only [partial_fderiv_fst, loop.per] }

@[simp]
lemma loop.diff_apply (Œ≥ : E ‚Üí loop F) (e : E) (t : ‚Ñù) : loop.diff Œ≥ e t = ‚àÇ‚ÇÅ (Œª e t, Œ≥ e t) e t :=
rfl

lemma loop.continuous_diff {Œ≥ : E ‚Üí loop F} (h : ùíû 1 ‚ÜøŒ≥) : continuous (‚Üø(loop.diff Œ≥)) :=
times_cont_diff.continuous_partial_fst (h : _)

lemma times_cont_diff.partial_loop {Œ≥ : E ‚Üí loop F} (hŒ≥_diff : ùíû 1 ‚ÜøŒ≥) :
  ‚àÄ t, ùíû 1 (Œª e, Œ≥ e t) :=
Œª t, hŒ≥_diff.comp ((times_cont_diff_prod_left t).of_le le_top)

variables [measurable_space F] [borel_space F] [finite_dimensional ‚Ñù F]

lemma loop.support_diff {Œ≥ : E ‚Üí loop F} :
  loop.support (loop.diff Œ≥) ‚äÜ loop.support Œ≥ :=
begin
  unfold loop.support,
  erw [closure_compl, closure_compl],
  rw compl_subset_compl,
  intros x hx,
  rw mem_interior_iff_mem_nhds at *,
  rcases mem_nhds_iff.mp hx with ‚ü®U, hU, U_op, hxU‚ü©,
  have U_nhds : U ‚àà ùìù x, from is_open.mem_nhds U_op hxU,
  apply filter.mem_of_superset U_nhds,
  intros y hy,
  have Hy : ‚àÄ t, (Œª z, Œ≥ z t) =·∂†[ùìù y] (Œª z, (Œ≥ z).average),
  { intro t,
    apply filter.mem_of_superset (U_op.mem_nhds hy),
    intros z hz,
    exact loop.is_const_iff_forall_avg.mp (hU hz) t },
  have : ‚àÄ (t : ‚Ñù), loop.diff Œ≥ y t = D (Œª (z : E), (Œ≥ z).average) y := Œª t, (Hy t).fderiv_eq,
  intros t s,
  simp [this]
end

lemma loop.compact_support_diff {Œ≥ : E ‚Üí loop F}  (h' : is_compact (loop.support Œ≥)):
  is_compact (loop.support $ loop.diff Œ≥) :=
compact_of_is_closed_subset h' is_closed_closure loop.support_diff

variables [finite_dimensional ‚Ñù E]
          
lemma loop.average_diff {Œ≥ : E ‚Üí loop F} (hŒ≥_diff : ùíû 1 ‚ÜøŒ≥) (e : E) :
(loop.diff Œ≥ e).average = D (Œª e, (Œ≥ e).average) e :=
begin
  change ùíû 1 ‚Üø(Œª (e : E) (t : ‚Ñù), Œ≥ e t) at hŒ≥_diff,
  simpa only [loop.average, hŒ≥_diff.fderiv_parametric_integral]
end

lemma times_cont_diff.loop_average {Œ≥ : E ‚Üí loop F} {n : with_top ‚Ñï} (hŒ≥_diff : ùíû n ‚ÜøŒ≥) :
  ùíû n (Œª e, (Œ≥ e).average) :=
times_cont_diff_parametric_integral_of_times_cont_diff hŒ≥_diff _ _

lemma loop.diff_normalize {Œ≥ : E ‚Üí loop F} (hŒ≥_diff : ùíû 1 ‚ÜøŒ≥) (e : E) :
  (loop.diff Œ≥ e).normalize = loop.diff (Œª e, (Œ≥ e).normalize) e :=
begin
  ext t x,
  simp only [loop.diff_apply, loop.normalize_apply, partial_fderiv_fst],
  rw [fderiv_sub ((hŒ≥_diff.partial_loop t).differentiable le_rfl).differentiable_at,
      loop.average_diff hŒ≥_diff],
  exact (hŒ≥_diff.loop_average.differentiable le_rfl).differentiable_at
end

variable {Œ≥}

lemma times_cont_diff_average {n : with_top ‚Ñï} (hŒ≥_diff : ùíû n ‚ÜøŒ≥) : ùíû n (Œª x, (Œ≥ x).average) :=
times_cont_diff_parametric_primitive_of_times_cont_diff hŒ≥_diff times_cont_diff_const 0

lemma times_cont_diff_sub_average {n : with_top ‚Ñï} (hŒ≥_diff : ùíû n ‚ÜøŒ≥) :
  ùíû n ‚Üø(Œª (x : E) (t : ‚Ñù), (Œ≥ x) t - (Œ≥ x).average) :=
hŒ≥_diff.sub ((times_cont_diff_average hŒ≥_diff).comp times_cont_diff_fst)

end c1
