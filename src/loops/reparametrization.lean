import analysis.calculus.specific_functions
import measure_theory.integral.periodic
import geometry.manifold.partition_of_unity

import notations
import loops.surrounding
import loops.delta_mollifier

import to_mathlib.order.hom.basic
import to_mathlib.geometry.manifold.partition_of_unity
import to_mathlib.algebra.periodic
import to_mathlib.analysis.cont_diff

/-!
#Â The reparametrization lemma
-/

noncomputable theory

open set function measure_theory interval_integral
open_locale topological_space unit_interval manifold big_operators

variables {E F : Type*}
variables [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]
variables [normed_group F] [normed_space â„ F] [finite_dimensional â„ F]
variables [measurable_space F] [borel_space F]

local notation `Î¹` := fin (finite_dimensional.finrank â„ F + 1)

structure smooth_surrounding_family (g : E â†’ F) :=
(smooth_surrounded : ğ’ âˆ g)
(to_fun : E â†’ loop F)
(smooth : ğ’ âˆ â†¿to_fun)
(surrounds : âˆ€ x, (to_fun x).surrounds $ g x)

namespace smooth_surrounding_family

variables {g : E â†’ F} (Î³ : smooth_surrounding_family g) (x y : E)

instance : has_coe_to_fun (smooth_surrounding_family g) (Î» _, E â†’ loop F) := âŸ¨to_funâŸ©

protected lemma continuous : continuous (Î³ x) :=
begin
  apply continuous_uncurry_left x,
  exact Î³.smooth.continuous,
end

include Î³ x

def surrounding_parameters_at : Î¹ â†’ â„ := classical.some (Î³.surrounds x)

def surrounding_points_at : Î¹ â†’ F := Î³ x âˆ˜ Î³.surrounding_parameters_at x

def surrounding_weights_at : Î¹ â†’ â„ := classical.some (classical.some_spec (Î³.surrounds x))

lemma surround_pts_points_weights_at :
  surrounding_pts (g x) (Î³.surrounding_points_at x) (Î³.surrounding_weights_at x) :=
classical.some_spec _

/-- Note that we are mollifying the loop `Î³ y` at the surrounding parameters for `Î³ x`. -/
def approx_surrounding_points_at (Î· : â„) (i : Î¹) : F :=
(Î³ y).mollify Î· (Î³.surrounding_parameters_at x i)

lemma approx_surrounding_points_at_smooth (Î· : â„) :
  ğ’ âˆ (Î» y, Î³.approx_surrounding_points_at x y Î·) :=
begin
  refine cont_diff_pi.mpr (Î» i, _),
  by_cases hÎ· : Î· = 0,
  { suffices : ğ’ âˆ (Î» y, Î³ y (Î³.surrounding_parameters_at x i)),
    { simpa [approx_surrounding_points_at, loop.mollify, hÎ·], },
    exact Î³.smooth.comp (cont_diff_prod_left (Î³.surrounding_parameters_at x i)), },
  { suffices : ğ’ âˆ (Î»y, âˆ« s in 0..1, delta_mollifier Î· (Î³.surrounding_parameters_at x i) s â€¢ Î³ y s),
    { simpa [approx_surrounding_points_at, loop.mollify, hÎ·], },
    refine cont_diff_parametric_integral_of_cont_diff (cont_diff.smul _ Î³.smooth) 0 1,
    exact (delta_mollifier_smooth' hÎ· _).snd', },
end

/-- The key property from which it should be easy to construct `local_centering_density`,
`local_centering_density_nhd` etc below. -/
lemma eventually_exists_surrounding_pts_approx_surrounding_points_at : âˆ€á¶  (yÎ· : E Ã— â„) in ğ“ (x, 0),
  âˆƒ w, surrounding_pts (g yÎ·.1) (Î³.approx_surrounding_points_at x yÎ·.1 yÎ·.2) w :=
sorry

/- This is an auxiliary definition to help construct `centering_density` below.

Given `x : E`, it represents a smooth probability distribution on the circle with the property that:
`âˆ« s in 0..1, Î³.local_centering_density x y s â€¢ Î³ y s = g y`
for all `y` in a neighbourhood of `x` (see `local_centering_density_average` below). -/
def local_centering_density [decidable_pred (âˆˆ affine_bases Î¹ â„ F)] : E â†’ â„ â†’ â„ := Î» y,
begin
  choose n hnâ‚ hnâ‚‚ using filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  choose u hu v hv huv using mem_nhds_prod_iff.mp hnâ‚,
  choose Î· hÎ· hÎ·v using metric.mem_nhds_iff'.mp hv,
  exact âˆ‘ i, (eval_barycentric_coords Î¹ â„ F (g y) (Î³.approx_surrounding_points_at x y Î·) i) â€¢
    (delta_mollifier Î· (Î³.surrounding_parameters_at x i)),
end

def local_centering_density_mp : â„ :=
begin
  choose n hnâ‚ hnâ‚‚ using filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  choose u hu v hv huv using mem_nhds_prod_iff.mp hnâ‚,
  choose Î· hÎ· hÎ·v using metric.mem_nhds_iff'.mp hv,
  exact Î·,
end

lemma local_centering_density_mp_ne_zero : Î³.local_centering_density_mp x â‰  0 :=
begin
  let h := filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  let v := classical.some ((classical.some_spec
    (classical.some_spec (mem_nhds_prod_iff.mp (classical.some (classical.some_spec h)))))),
  let hv : v âˆˆ ğ“ (0 : â„) := classical.some (classical.some_spec (classical.some_spec
    (classical.some_spec (mem_nhds_prod_iff.mp (classical.some (classical.some_spec h)))))),
  exact ne_of_gt (classical.some (classical.some_spec (metric.mem_nhds_iff'.mp hv))),
end

lemma local_centering_density_spec [decidable_pred (âˆˆ affine_bases Î¹ â„ F)] :
  Î³.local_centering_density x y =
  âˆ‘ i, (eval_barycentric_coords Î¹ â„ F (g y)
    (Î³.approx_surrounding_points_at x y (Î³.local_centering_density_mp x)) i) â€¢
    (delta_mollifier (Î³.local_centering_density_mp x) (Î³.surrounding_parameters_at x i)) :=
rfl

def local_centering_density_nhd : set E :=
begin
  choose n hnâ‚ hnâ‚‚ using filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  choose u hu v hv huv using mem_nhds_prod_iff.mp hnâ‚,
  exact (interior u),
end

omit x

lemma local_centering_density_nhd_is_open :
  is_open $ Î³.local_centering_density_nhd x :=
is_open_interior

lemma local_centering_density_nhd_self_mem :
  x âˆˆ Î³.local_centering_density_nhd x :=
begin
  let h := filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  exact mem_interior_iff_mem_nhds.mpr (classical.some (classical.some_spec (mem_nhds_prod_iff.mp
    (classical.some (classical.some_spec h))))),
end

lemma local_centering_density_nhd_covers :
  univ âŠ† â‹ƒ x, Î³.local_centering_density_nhd x :=
Î» x hx, mem_Union.mpr âŸ¨x, Î³.local_centering_density_nhd_self_mem xâŸ©

lemma approx_surrounding_points_at_of_local_centering_density_nhd
  (hy : y âˆˆ Î³.local_centering_density_nhd x) : âˆƒ w,
  surrounding_pts (g y) (Î³.approx_surrounding_points_at x y (Î³.local_centering_density_mp x)) w :=
begin
  -- Another ludicrous proof ğŸ™„
  let h := filter.eventually_iff_exists_mem.mp
    (Î³.eventually_exists_surrounding_pts_approx_surrounding_points_at x),
  let nÎ· := classical.some h,
  let hnÎ· := mem_nhds_prod_iff.mp (classical.some (classical.some_spec h)),
  let n := classical.some hnÎ·,
  let hn := classical.some_spec hnÎ·,
  change y âˆˆ interior n at hy,
  let v := classical.some (classical.some_spec hn),
  let hv : v âˆˆ ğ“ (0 : â„) := classical.some (classical.some_spec (classical.some_spec hn)),
  let Î· := classical.some (metric.mem_nhds_iff'.mp hv),
  let hÎ·â‚ : 0 < Î· := classical.some (classical.some_spec (metric.mem_nhds_iff'.mp hv)),
  let hÎ·â‚‚ := classical.some_spec (classical.some_spec (metric.mem_nhds_iff'.mp hv)),
  change âˆƒ w, surrounding_pts (g y) (Î³.approx_surrounding_points_at x y Î·) w,
  suffices : (y, Î·) âˆˆ nÎ·,
  { exact classical.some_spec (classical.some_spec h) _ this, },
  apply classical.some_spec (classical.some_spec (classical.some_spec hn)),
  change y âˆˆ n âˆ§ Î· âˆˆ v,
  refine âŸ¨interior_subset hy, hÎ·â‚‚ _âŸ©,
  change Î· âˆˆ metric.closed_ball (0 : â„) Î·,
  rw [mem_closed_ball_zero_iff, real.norm_eq_abs, abs_eq_self.mpr hÎ·â‚.le],
end

lemma approx_surrounding_points_at_mem_affine_bases (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  Î³.approx_surrounding_points_at x y (Î³.local_centering_density_mp x) âˆˆ affine_bases Î¹ â„ F :=
(classical.some_spec
  (Î³.approx_surrounding_points_at_of_local_centering_density_nhd x y hy)).mem_affine_bases

variables [decidable_pred (âˆˆ affine_bases Î¹ â„ F)]

@[simp] lemma local_centering_density_pos (t : â„) (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  0 < Î³.local_centering_density x y t :=
begin
  simp only [Î³.local_centering_density_spec x, fintype.sum_apply, pi.smul_apply,
    algebra.id.smul_eq_mul],
  refine finset.sum_pos (Î» i hi, _) finset.univ_nonempty,
  refine mul_pos _ (delta_mollifier_pos (Î³.local_centering_density_mp_ne_zero x) _ _),
  obtain âŸ¨w, hwâŸ© := Î³.approx_surrounding_points_at_of_local_centering_density_nhd x y hy,
  convert hw.w_pos i,
  rw â† hw.coord_eq_w,
  simp [eval_barycentric_coords, Î³.approx_surrounding_points_at_mem_affine_bases x y hy],
end

lemma local_centering_density_periodic (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  periodic (Î³.local_centering_density x y) 1 :=
periodic.sum $
  Î» i, periodic.smul (delta_mollifier_periodic (Î³.local_centering_density_mp_ne_zero x) _) _

lemma local_centering_density_smooth_on :
  smooth_on â†¿(Î³.local_centering_density x) $
    (Î³.local_centering_density_nhd x) Ã—Ë¢ (univ : set â„) :=
begin
  let hâ‚€ := (Î» (yt : E Ã— â„) (hyt : yt âˆˆ (Î³.local_centering_density_nhd x) Ã—Ë¢ (univ : set â„)),
    congr_fun (Î³.local_centering_density_spec x yt.fst) yt.snd),
  refine cont_diff_on.congr _ hâ‚€,
  simp only [fintype.sum_apply, pi.smul_apply, algebra.id.smul_eq_mul],
  refine cont_diff_on.sum (Î» i hi, cont_diff_on.mul _ (cont_diff.cont_diff_on _)),
  { let w : F Ã— (Î¹ â†’ F) â†’ â„ := Î» z, eval_barycentric_coords Î¹ â„ F z.1 z.2 i,
    let z : E â†’ F Ã— (Î¹ â†’ F) :=
      (prod.map g (Î» y, Î³.approx_surrounding_points_at x y (Î³.local_centering_density_mp x))) âˆ˜
      (Î» x, (x, x)),
    change smooth_on ((w âˆ˜ z) âˆ˜ prod.fst) (Î³.local_centering_density_nhd x Ã—Ë¢ univ),
    rw prod_univ,
    refine cont_diff_on.comp _ cont_diff_fst.cont_diff_on subset.rfl,
    have hâ‚ := smooth_barycentric Î¹ â„ F (fintype.card_fin _),
    have hâ‚‚ : ğ’ âˆ (eval i : (Î¹ â†’ â„) â†’ â„) := cont_diff_apply i,
    refine (hâ‚‚.comp_cont_diff_on hâ‚).comp _ _,
    { have hâ‚ƒ := eq.subset (mk_diag_preimage_prod_self (Î³.local_centering_density_nhd x)).symm,
      refine cont_diff_on.comp _ (cont_diff_id.prod cont_diff_id).cont_diff_on hâ‚ƒ,
      refine (Î³.smooth_surrounded).cont_diff_on.prod_map (cont_diff.cont_diff_on _),
      exact Î³.approx_surrounding_points_at_smooth x _, },
    { intros y hy,
      simp [z, Î³.approx_surrounding_points_at_mem_affine_bases x y hy], }, },
  { refine cont_diff.comp _ cont_diff_snd,
    exact delta_mollifier_smooth' (Î³.local_centering_density_mp_ne_zero x) _, },
end

lemma local_centering_density_continuous (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  continuous (Î» t, Î³.local_centering_density x y t) :=
begin
  refine continuous_iff_continuous_at.mpr (Î» t, _),
  have hyt : Î³.local_centering_density_nhd x Ã—Ë¢ univ âˆˆ ğ“ (y, t) :=
    mem_nhds_prod_iff'.mpr âŸ¨Î³.local_centering_density_nhd x, univ,
      Î³.local_centering_density_nhd_is_open x, hy, is_open_univ, mem_univ t, rfl.subsetâŸ©,
  exact ((Î³.local_centering_density_smooth_on x).continuous_on.continuous_at hyt).comp
    (continuous.prod.mk y).continuous_at,
end

@[simp] lemma local_centering_density_integral_eq_one (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  âˆ« s in 0..1, Î³.local_centering_density x y s = 1 :=
begin
  let Î· := Î³.local_centering_density_mp x,
  let hÎ·â‚ := Î³.local_centering_density_mp_ne_zero x,
  simp only [Î³.local_centering_density_spec x, prod.forall, exists_prop, gt_iff_lt,
    fintype.sum_apply, pi.smul_apply, algebra.id.smul_eq_mul, finset.sum_smul],
  rw interval_integral.integral_sum,
  { have h : Î³.approx_surrounding_points_at x y Î· âˆˆ affine_bases Î¹ â„ F :=
      Î³.approx_surrounding_points_at_mem_affine_bases x y hy,
    simp_rw [â† smul_eq_mul, interval_integral.integral_smul,
      delta_mollifier_integral_eq_one (Î³.local_centering_density_mp_ne_zero x),
      algebra.id.smul_eq_mul, mul_one, eval_barycentric_coords_apply_of_mem_bases Î¹ â„ F (g y) h,
      affine_basis.coords_apply, affine_basis.sum_coord_apply_eq_one], },
  { simp_rw â† smul_eq_mul,
    refine Î» i hi, (continuous.const_smul _ _).interval_integrable 0 1,
    exact (delta_mollifier_smooth' hÎ·â‚ (Î³.surrounding_parameters_at x i)).continuous },
end

@[simp] lemma local_centering_density_average (hy : y âˆˆ Î³.local_centering_density_nhd x) :
  âˆ« s in 0..1, Î³.local_centering_density x y s â€¢ Î³ y s = g y :=
begin
  let Î· := Î³.local_centering_density_mp x,
  let hÎ·â‚ := Î³.local_centering_density_mp_ne_zero x,
  simp only [Î³.local_centering_density_spec x, prod.forall, exists_prop, gt_iff_lt,
    fintype.sum_apply, pi.smul_apply, algebra.id.smul_eq_mul, finset.sum_smul],
  rw interval_integral.integral_sum,
  { simp_rw [mul_smul, interval_integral.integral_smul,
      â† (Î³ y).mollify_eq_of_ne_zero Î· (Î³.surrounding_parameters_at x _) hÎ·â‚],
    change âˆ‘ i, _ â€¢ (Î³.approx_surrounding_points_at x y Î· i) = _,
    have h : Î³.approx_surrounding_points_at x y Î· âˆˆ affine_bases Î¹ â„ F :=
      Î³.approx_surrounding_points_at_mem_affine_bases x y hy,
    erw [eval_barycentric_coords_apply_of_mem_bases Î¹ â„ F (g y) h],
    simpa using affine_basis.affine_combination_coord_eq_self (affine_basis.mk _ h.1 h.2) (g y), },
  { simp_rw mul_smul,
    refine Î» i hi, ((continuous.smul _ (Î³.continuous y)).const_smul _).interval_integrable 0 1,
    exact (delta_mollifier_smooth' hÎ·â‚ (Î³.surrounding_parameters_at x i)).continuous },
end

/-- This the key construction. It represents a smooth probability distribution on the circle with
the property that:
`âˆ« s in 0..1, Î³.centering_density x s â€¢ Î³ x s = g x`
for all `x : E` (see `centering_density_average` below).

It is constructed from `local_centering_density` using a partition of unity
(see `centering_density_eq_exists_pou` below). -/
def centering_density : E â†’ â„ â†’ â„ :=
begin
  choose p hp using
    @smooth_partition_of_unity.exists_is_subordinate _ _ _ _ _ _ _ ğ“˜(â„, E) _ _ _ _ _ _ _
    is_closed_univ (Î³.local_centering_density_nhd) (Î³.local_centering_density_nhd_is_open)
    Î³.local_centering_density_nhd_covers,
  exact Î» x t, âˆ‘á¶  (y : E), (p y x) * Î³.local_centering_density y x t,
end
omit Î³

lemma centering_density_def :
  âˆƒ (p : smooth_partition_of_unity E ğ“˜(â„, E) E)
    (hp : p.is_subordinate Î³.local_centering_density_nhd),
    âˆ€ x t, Î³.centering_density x t = âˆ‘á¶  y, (p y x) * Î³.local_centering_density y x t :=
let h := @smooth_partition_of_unity.exists_is_subordinate _ _ _ _ _ _ _ ğ“˜(â„, E) _ _ _ _ _ _ _
  is_closed_univ (Î³.local_centering_density_nhd) (Î³.local_centering_density_nhd_is_open)
  Î³.local_centering_density_nhd_covers in
âŸ¨classical.some h, classical.some_spec h, Î» x y, rflâŸ©

lemma centering_density_eq_exists_pou_nhd_finset_sum :
  âˆƒ (p : smooth_partition_of_unity E ğ“˜(â„, E) E)
    (hp : p.is_subordinate Î³.local_centering_density_nhd),
    âˆ€ (x : E), âˆƒ (ys : finset E) {n : set E} (hnâ‚ : n âˆˆ ğ“ x)
      (hnâ‚‚ : n âŠ† â‹‚ y âˆˆ ys, Î³.local_centering_density_nhd y),
      âˆ€ (z âˆˆ n) t, âˆ‘ y in ys, p y z = 1 âˆ§
      Î³.centering_density z t = âˆ‘ y in ys, p y z * Î³.local_centering_density y z t :=
begin
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_def,
  refine âŸ¨p, hp, Î» x, _âŸ©,
  obtain âŸ¨ys, n, hnâ‚, hnâ‚‚, hnâ‚ƒâŸ© := p.exists_finset_nhd hp x (Î³.local_centering_density_nhd_is_open),
  refine âŸ¨ys, n, hnâ‚, hnâ‚‚, Î» z hz t, âŸ¨_, _âŸ©âŸ©,
  { rw [â† finsum_eq_sum_of_support_to_finset_subset' _ (hnâ‚ƒ z hz), p.sum_eq_one (mem_univ z)], },
  { rw hp',
    suffices : support (Î» y, p y z * Î³.local_centering_density y z t) âŠ† ys,
    { exact finsum_eq_sum_of_support_to_finset_subset' _ this, },
    refine subset.trans (Î» y hy, _) (hnâ‚ƒ z hz),
    rintros (contra : p y z = 0),
    simpa [contra] using hy, },
end

@[simp] lemma centering_density_pos (t : â„) :
  0 < Î³.centering_density x t :=
begin
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_eq_exists_pou_nhd_finset_sum,
  obtain âŸ¨ys, n, hnâ‚, hnâ‚‚, hnâ‚ƒâŸ© := hp' x,
  obtain âŸ¨hxâ‚, hxâ‚‚âŸ© := hnâ‚ƒ x (mem_of_mem_nhds hnâ‚) t,
  rw hxâ‚‚,
  have hxâ‚€ : âˆ€ y âˆˆ ys, 0 â‰¤ p y x  := Î» y hy, p.nonneg y x,
  refine (convex_Ioi (0 : â„)).sum_mem hxâ‚€ hxâ‚ (Î» y hy, _),
  simp only [subset_Interâ‚‚_iff] at hnâ‚‚,
  exact Î³.local_centering_density_pos y x t (hnâ‚‚ y hy (mem_of_mem_nhds hnâ‚)),
end

lemma centering_density_periodic :
  periodic (Î³.centering_density x) 1 :=
begin
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_def,
  have : âˆ€ y t,
    p y x * Î³.local_centering_density y x (t + 1) = p y x * Î³.local_centering_density y x t,
  { intros,
    by_cases h : x âˆˆ Î³.local_centering_density_nhd y,
    { rw Î³.local_centering_density_periodic y x h, },
    { suffices : x âˆ‰ support (p y), { simp [nmem_support.mp this], },
      exact set.not_mem_subset (subset_tsupport _) (set.not_mem_subset (hp y) h), }, },
  intros t,
  simp_rw [hp', this],
end

lemma centering_density_smooth :
  -- ğ’ âˆ â†¿Î³.centering_density :=
  ğ’ âˆ $ uncurry (Î» x t, Î³.centering_density x t) :=
begin
  rw cont_diff_iff_cont_diff_at,
  rintros âŸ¨x, tâŸ©,
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_eq_exists_pou_nhd_finset_sum,
  obtain âŸ¨ys, n, hnâ‚, hnâ‚‚, hnâ‚ƒâŸ© := hp' x,
  have hnâ‚„ : n Ã—Ë¢ (univ : set â„) âˆˆ ğ“ (x, t) :=
    mem_nhds_prod_iff.mpr âŸ¨n, hnâ‚, univ, filter.univ_mem, rfl.subsetâŸ©,
  refine cont_diff_within_at.cont_diff_at
    (cont_diff_on.cont_diff_within_at _ (mem_of_mem_nhds hnâ‚„)) hnâ‚„,
  let f : E Ã— â„ â†’ â„ := Î» zt, âˆ‘ y in ys, p y zt.1 * Î³.local_centering_density y zt.1 zt.2,
  have hf : âˆ€ zs âˆˆ n Ã—Ë¢ (univ : set â„), (uncurry Î³.centering_density) zs = f zs,
  { rintros âŸ¨z, sâŸ© hz,
    simp only [prod_mk_mem_set_prod_eq, mem_univ, and_true] at hz,
    simp [hnâ‚ƒ z hz s], },
  apply cont_diff_on.congr _ hf,
  refine cont_diff_on.sum (Î» y hy, cont_diff_on.mul (cont_diff.cont_diff_on _) _),
  { refine cont_diff.comp _ cont_diff_fst,
    rw â† cont_mdiff_iff_cont_diff,
    exact (p y).cont_mdiff },
  { suffices : n Ã—Ë¢ (univ : set â„) âŠ† (Î³.local_centering_density_nhd y) Ã—Ë¢ (univ : set â„),
    { exact (Î³.local_centering_density_smooth_on y).mono this, },
    simp only [subset_Interâ‚‚_iff] at hnâ‚‚,
    exact prod_mono (hnâ‚‚ y hy) rfl.subset, },
end

@[simp] lemma centering_density_integral_eq_one :
  âˆ« s in 0..1, Î³.centering_density x s = 1 :=
begin
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_def,
  have h_int : âˆ€ y, interval_integrable
    (Î» t, p y x â€¢ Î³.local_centering_density y x t) volume 0 1,
  { intros y,
    by_cases hy : x âˆˆ Î³.local_centering_density_nhd y,
    { refine continuous.interval_integrable (continuous.const_smul _ (p y x)) _ _,
      exact Î³.local_centering_density_continuous _ _ hy, },
    { suffices : x âˆ‰ support (p y), { simp [nmem_support.mp this], },
      exact Î» contra, hy (hp _ (subset_tsupport _ contra)), }, },
  have h_supp : (support (Î» y t, p y x â€¢ Î³.local_centering_density y x t)).finite,
  { refine set.finite.subset (p.locally_finite.point_finite x) (Î» y hy, _),
    simp only [ne.def, mem_set_of_eq, mem_support],
    intros contra,
    simpa only [mem_support, contra, zero_smul, ne.def, pi.zero_def] using hy, },
  simp_rw [hp', â† smul_eq_mul, integral_finsum h_int h_supp, interval_integral.integral_smul],
  suffices : âˆ€ y z, z âˆˆ univ âˆ© Î³.local_centering_density_nhd y â†’
    âˆ« t in 0..1, Î³.local_centering_density y z t = 1,
  { let f := Î» y z, âˆ« t in 0..1, Î³.local_centering_density y z t,
    exact p.finsum_smul_eq hp f 1 this (mem_univ x), },
  intros y z hyz,
  simp only [univ_inter] at hyz,
  exact Î³.local_centering_density_integral_eq_one y z hyz,
end

@[simp] lemma centering_density_average :
  âˆ« s in 0..1, Î³.centering_density x s â€¢ Î³ x s = g x :=
begin
  obtain âŸ¨p, hp, hp'âŸ© := Î³.centering_density_def,
  have h_int : âˆ€ y, interval_integrable
    (Î» t, p y x â€¢ Î³.local_centering_density y x t â€¢ Î³ x t) volume 0 1,
  { intros y,
    by_cases hy : x âˆˆ Î³.local_centering_density_nhd y,
    { refine continuous.interval_integrable (continuous.const_smul _ (p y x)) _ _,
      refine continuous.smul _ (Î³.smooth.continuous.comp (continuous.prod.mk x)),
      exact Î³.local_centering_density_continuous _ _ hy, },
    { suffices : x âˆ‰ support (p y), { simp [nmem_support.mp this], },
      exact Î» contra, hy (hp _ (subset_tsupport _ contra)), }, },
  have h_supp : (support (Î» y t, p y x â€¢ Î³.local_centering_density y x t â€¢ Î³ x t)).finite,
  { refine set.finite.subset (p.locally_finite.point_finite x) (Î» y hy, _),
    simp only [ne.def, mem_set_of_eq, mem_support],
    intros contra,
    simpa only [mem_support, contra, zero_smul, ne.def, pi.zero_def] using hy, },
  simp_rw [hp', finsum_smul, mul_smul, integral_finsum h_int h_supp,
    interval_integral.integral_smul],
  suffices : âˆ€ (y x : E), x âˆˆ univ âˆ© Î³.local_centering_density_nhd y â†’
    âˆ« s in 0..1, Î³.local_centering_density y x s â€¢ Î³ x s = g x,
  { exact p.finsum_smul_eq hp _ _ this (mem_univ x), },
  intros y x' hx',
  simp only [univ_inter] at hx',
  exact Î³.local_centering_density_average _ _ hx',
end

lemma centering_density_continuous :
  continuous (Î³.centering_density x) :=
begin
  apply continuous_uncurry_left x,
  exact Î³.centering_density_smooth.continuous,
end

lemma centering_density_interval_integrable (tâ‚ tâ‚‚ : â„) :
  interval_integrable (Î³.centering_density x) volume tâ‚ tâ‚‚ :=
(Î³.centering_density_continuous x).interval_integrable tâ‚ tâ‚‚

@[simp] lemma integral_add_one_centering_density (t : â„) :
  âˆ« s in 0..t+1, Î³.centering_density x s = (âˆ« s in 0..t, Î³.centering_density x s) + 1 :=
begin
  have hâ‚ := Î³.centering_density_interval_integrable x 0 t,
  have hâ‚‚ := Î³.centering_density_interval_integrable x t (t + 1),
  simp [â† integral_add_adjacent_intervals hâ‚ hâ‚‚,
    (Î³.centering_density_periodic x).interval_integral_add_eq t 0],
end

lemma deriv_integral_centering_density_pos (t : â„) :
  0 < deriv (Î» t, âˆ« s in 0..t, Î³.centering_density x s) t :=
begin
  rw interval_integral.deriv_integral_right (Î³.centering_density_interval_integrable _ _ _)
    ((Î³.centering_density_continuous x).strongly_measurable_at_filter volume (ğ“ t))
    (centering_density_continuous Î³ x).continuous_at,
  exact centering_density_pos Î³ x t
end

lemma strict_mono_integral_centering_density :
  strict_mono $ Î» t, âˆ« s in 0..t, Î³.centering_density x s :=
strict_mono_of_deriv_pos (Î³.deriv_integral_centering_density_pos x)

lemma surjective_integral_centering_density :
  surjective $ Î» t, âˆ« s in 0..t, Î³.centering_density x s :=
begin
  have : continuous (Î» t, âˆ« s in 0..t, Î³.centering_density x s),
  { exact continuous_primitive (Î³.centering_density_interval_integrable x) 0, },
  exact equivariant_map.surjective
    âŸ¨Î» t, âˆ« s in 0..t, Î³.centering_density x s, Î³.integral_add_one_centering_density xâŸ© this
end

def reparametrize : E â†’ equivariant_equiv := Î» x,
({ to_fun := Î» t, âˆ« s in 0..t, Î³.centering_density x s,
  inv_fun := (strict_mono.order_iso_of_surjective _
    (Î³.strict_mono_integral_centering_density x)
    (Î³.surjective_integral_centering_density x)).symm,
  left_inv := strict_mono.order_iso_of_surjective_symm_apply_self _ _ _,
  right_inv := Î» t, strict_mono.order_iso_of_surjective_self_symm_apply _ _ _ t,
  map_zero' := integral_same,
  eqv' := Î³.integral_add_one_centering_density x, } : equivariant_equiv).symm

lemma coe_reparametrize_symm :
  ((Î³.reparametrize x).symm : â„ â†’ â„) = Î» t, âˆ« s in 0..t, Î³.centering_density x s :=
rfl

lemma reparametrize_symm_apply (t : â„) :
  (Î³.reparametrize x).symm t = âˆ« s in 0..t, Î³.centering_density x s :=
rfl

@[simp] lemma integral_reparametrize (t : â„) :
  âˆ« s in 0..(Î³.reparametrize x t), Î³.centering_density x s = t :=
by simp [â† reparametrize_symm_apply]

lemma has_deriv_at_reparametrize_symm (s : â„) :
  has_deriv_at (Î³.reparametrize x).symm (Î³.centering_density x s) s :=
integral_has_deriv_at_right
  (Î³.centering_density_interval_integrable x 0 s)
  ((Î³.centering_density_continuous x).strongly_measurable_at_filter _ _)
  (Î³.centering_density_continuous x).continuous_at

lemma reparametrize_smooth :
  -- ğ’ âˆ â†¿Î³.reparametrize :=
  ğ’ âˆ $ uncurry (Î» x t, Î³.reparametrize x t) :=
begin
  let f : E â†’ â„ â†’ â„ := Î» x t, âˆ« s in 0..t, Î³.centering_density x s,
  change ğ’ âŠ¤ (Î» p : E Ã— â„, (strict_mono.order_iso_of_surjective (f p.1) _ _).symm p.2),
  apply cont_diff_parametric_symm_of_deriv_pos,
  { exact cont_diff_parametric_primitive_of_cont_diff'' Î³.centering_density_smooth 0 },
  { exact Î» x, deriv_integral_centering_density_pos Î³ x }
end

@[simp] lemma reparametrize_average :
  ((Î³ x).reparam $ (Î³.reparametrize x).equivariant_map).average = g x :=
begin
  change âˆ« (s : â„) in 0..1, Î³ x (Î³.reparametrize x s) = g x,
  have hâ‚ : âˆ€ s,
    s âˆˆ interval 0 (1 : â„) â†’ has_deriv_at (Î³.reparametrize x).symm (Î³.centering_density x s) s :=
    Î» s hs, Î³.has_deriv_at_reparametrize_symm x s,
  have hâ‚‚ : continuous_on (Î» s, Î³.centering_density x s) (interval 0 1) :=
    (Î³.centering_density_continuous x).continuous_on,
  have hâ‚ƒ : continuous (Î» s, Î³ x (Î³.reparametrize x s)) :=
    (Î³.continuous x).comp (continuous_uncurry_left x Î³.reparametrize_smooth.continuous),
  rw [â† (Î³.reparametrize x).symm.map_zero, â† (Î³.reparametrize x).symm.map_one,
    â† integral_comp_smul_deriv hâ‚ hâ‚‚ hâ‚ƒ],
  simp,
end

end smooth_surrounding_family
