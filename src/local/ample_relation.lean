import local.ample_set
import local.dual_pair
import local.relation

/-! # Slices of first order relations

Recal that a first order partial differential relation for maps between real normed vector spaces
`E` and `F` is a set `R` in `one_jet E F := E Ã— F Ã— (E â†’L[â„] F)`. In this file we study slices
of such relations. The word slice is meant to convey the idea of intersecting with an affine
subspace. Here we fix `(x, y, Ï†) : one_jet E F` and some hyperplane `H` in `E`. The points
`x` and `y` are fixed and we will take a slice in `E â†’L[â„] F` by intersecting `R` with the affine
subspace of linear maps that coincide with `Ï†` on `H`.

It will be convenient for convex integration purposes to identify this slice with `F`. There is
no natural identification but we can build one by fixing more data that a hyperplane in `E`.
Namely we fix `p : dual_pair E` (where `ker p.Ï€` is the relevant hyperplane) and reformulate
"linear map that coincides with `Ï†` on `H`" as `p.update Ï† w` for some `w : F`.

This `slice` definition allows to define `rel_loc.is_ample`, the ampleness condition for first
order relations: a relation is ample if all its slices are ample sets.

At the end of the file we consider 1-jet sections and slices corresponding to points in their image.
-/

variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
variables {F : Type*} [normed_add_comm_group F] [normed_space â„ F]
variables {X : Type*} [inner_product_space â„ X]
variables {R : rel_loc E F}
open set

/-! ## Slices and ampleness -/
namespace rel_loc

/-- The slice of a local relation `R : rel_loc E F` for a dual pair `p` at a jet `Î¸` is
the set of `w` in `F` such that updating `Î¸` using `p` and `w` leads to a jet in `R`. -/
def slice (R : rel_loc E F) (p : dual_pair E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)) : set F :=
{w | (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R}

lemma mem_slice (R : rel_loc E F) {p : dual_pair E} {Î¸ : E Ã— F Ã— (E â†’L[â„] F)} {w : F} :
  w âˆˆ R.slice p Î¸ â†” (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R :=
iff.rfl

/-- A relation is ample if all its slices are ample. -/
def is_ample (R : rel_loc E F) : Prop := âˆ€ (p : dual_pair E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)),
ample_set (R.slice p Î¸)

lemma is_ample.mem_hull (h : is_ample R) {Î¸ : E Ã— F Ã— (E â†’L[â„] F)}
  (hÎ¸ : Î¸ âˆˆ R) (v : F) (p) : v âˆˆ hull (connected_component_in (R.slice p Î¸) (Î¸.2.2 p.v)) :=
begin
  rw h p Î¸ (Î¸.2.2 p.v),
  exact mem_univ _,
  rw [mem_slice, p.update_self, prod.mk.eta, prod.mk.eta],
  exact hÎ¸
end

lemma slice_update {Î¸ : E Ã— F Ã— (E â†’L[â„] F)}
  {p : dual_pair E} (x : F) :
  R.slice p (Î¸.1, Î¸.2.1, (p.update Î¸.2.2 x)) = R.slice p Î¸ :=
begin
  ext1 w,
  dsimp [slice],
  rw [p.update_update]
end

/-- In order to check ampleness, it suffices to consider slices through elements of the relation. -/
lemma is_ample_iff : R.is_ample â†”
  âˆ€ â¦ƒÎ¸ : one_jet E Fâ¦„ (p : dual_pair E), Î¸ âˆˆ R â†’ ample_set (R.slice p Î¸) :=
begin
  simp_rw [is_ample],
  refine âŸ¨Î» h Î¸ p hÎ¸, h p Î¸, Î» h p Î¸ w hw, _âŸ©,
  dsimp [slice] at hw,
  have := h p hw,
  rw [slice_update] at this,
  exact this w hw
end

open_locale pointwise

lemma slice_of_ker_eq_ker {Î¸ : one_jet E F}
  {p p' : dual_pair E} (hpp' : p.Ï€ = p'.Ï€) :
  R.slice p Î¸ = Î¸.2.2 (p.v - p'.v) +áµ¥ R.slice p' Î¸ :=
begin
  rcases Î¸ with âŸ¨x, y, Ï†âŸ©,
  have key : âˆ€ w, p'.update Ï† w = p.update Ï† (w + Ï† (p.v - p'.v)),
  { intros w,
    simp only [dual_pair.update, hpp', map_sub, add_right_inj],
    congr' 2,
    abel },
  ext w,
  simp only [slice, mem_set_of_eq, map_sub, vadd_eq_add, mem_vadd_set_iff_neg_vadd_mem, key],
  have : -(Ï† p.v - Ï† p'.v) + w + (Ï† p.v - Ï† p'.v) = w,
  abel,
  rw this,
end

lemma ample_slice_of_ample_slice {Î¸ : one_jet E F}
  {p p' : dual_pair E} (hpp' : p.Ï€ = p'.Ï€) (h : ample_set (R.slice p Î¸)) :
  ample_set (R.slice p' Î¸) :=
begin
  rw slice_of_ker_eq_ker hpp'.symm,
  exact ample_set.vadd h
end

lemma ample_slice_of_forall (R : rel_loc E F) {x y Ï†} (p : dual_pair E)
  (h : âˆ€ w, (x, y, p.update Ï† w) âˆˆ R) : ample_set (R.slice p (x, y, Ï†)) :=
begin
  rw show R.slice p (x, y, Ï†) = univ, from eq_univ_of_forall h,
  exact ample_set_univ
end

end rel_loc

open rel_loc

/-! ## Slices for 1-jet sections and formal solutions. -/

namespace jet_sec

/-- The slice associated to a jet section and a dual pair at some point. -/
def slice_at (ğ“• : jet_sec E F) (R : rel_loc E F) (p : dual_pair E) (x : E) : set F :=
R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- A 1-jet section `ğ“•` is short for a dual pair `p` at a point `x` if the derivative of
the function `ğ“•.f` at `x` is in the convex hull of the relevant connected component of the
corresponding slice. -/
def is_short_at (ğ“• : jet_sec E F) (R : rel_loc E F) (p : dual_pair E) (x : E) : Prop :=
D ğ“•.f x p.v âˆˆ hull (connected_component_in (ğ“•.slice_at R p x) $ ğ“•.Ï† x p.v)

end jet_sec

namespace rel_loc.formal_sol

/-- The slice associated to a formal solution and a dual pair at some point. -/
def slice_at (ğ“• : formal_sol R) (p : dual_pair E) (x : E) : set F :=
R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- A formal solution `ğ“•` is short for a dual pair `p` at a point `x` if the derivative of
the function `ğ“•.f` at `x` is in the convex hull of the relevant connected component of the
corresponding slice. -/
def is_short_at (ğ“• : formal_sol R) (p : dual_pair E) (x : E) : Prop :=
D ğ“•.f x p.v âˆˆ hull (connected_component_in (ğ“•.slice_at p x) $ ğ“•.Ï† x p.v)

end rel_loc.formal_sol

lemma rel_loc.is_ample.is_short_at (hR : is_ample R) (ğ“• : formal_sol R) (p : dual_pair E)
  (x : E) : ğ“•.is_short_at p x :=
hR.mem_hull (ğ“•.is_sol x) _ p
