import analysis.asymptotics.asymptotics
import linear_algebra.dual
import measure_theory.integral.interval_integral
import analysis.calculus.parametric_integral

import to_mathlib.topology.periodic
import to_mathlib.analysis.calculus
import to_mathlib.measure_theory.parametric_interval_integral
import to_mathlib.measure_theory.borel_space

import notations

import loops.basic
import local.dual_pair

/-! # TheilliÃ¨re's corrugation operation

This files introduces the fundamental calculus tool of convex integration. The version of convex
integration that we formalize is TheilliÃ¨re's corrugation-based convex integration.
It improves a map `f : E â†’ F` by adding to it some corrugation map
`Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (Î³ x t - (Î³ x).average)` where `Î³` is a family of loops in
`F` parametrized by `E` and `N` is some (very large) real number.

Under the assumption that `âˆ€ x, (Î³ x).average = D f x p.v` for some dual pair `p`, this improved
map will have a derivative which is almost a value of `Î³ x` in direction `p.v` and almost the
derivative of `f` in direction `ker p.Ï€`. More precisely, its derivative will be almost
`p.update (D f x) (Î³ x (N*p.Ï€ x))`. In addition the improved map will be very close to `f`
in Câ° topology. All those "almost" and "very close" refer to the asymptotic behavior when `N`
is very large.

The main definition is `corrugation`. The main results are:
* `fderiv_corrugated_map` computing the difference between `D (f + corrugation p.Ï€ N Î³) x` and
    `p.update (D f x) (Î³ x (N*p.Ï€ x)) + corrugation.remainder p.Ï€ N Î³ x`
* `remainder_c0_small_on` saying this difference is very small
* `corrugation.c0_small_on` saying that corrugations are Câ°-small

-/

noncomputable theory

open set function finite_dimensional asymptotics filter topological_space int measure_theory
     continuous_linear_map
open_locale topological_space unit_interval


variables
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  {F : Type*} [normed_add_comm_group F] [normed_space â„ F] [finite_dimensional â„ F]
  {G : Type*} [normed_add_comm_group G] [normed_space â„ G]
  {H : Type*} [normed_add_comm_group H] [normed_space â„ H] [finite_dimensional â„ H]
  {Ï€ : E â†’L[â„] â„} (N : â„) (Î³ : E â†’ loop F)

open_locale borelize

/-- TheilliÃ¨re's corrugations. -/
def corrugation (Ï€ : E â†’L[â„] â„) (N : â„) (Î³ : E â†’ loop F) : E â†’ F :=
Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (Î³ x t - (Î³ x).average)

local notation `ğ’¯` := corrugation Ï€

/-- The integral appearing in corrugations is periodic. -/
lemma per_corrugation (Î³ : loop F) (hÎ³ : âˆ€ s t, interval_integrable Î³ volume s t) :
  one_periodic (Î» s, âˆ« t in 0..s, Î³ t - Î³.average) :=
begin
  have int_avg : âˆ€ s t,  interval_integrable (Î» u : â„, Î³.average) volume s t,
    from Î» s t, interval_integrable_const,
  intros s,
  have intâ‚ : interval_integrable (Î» t, Î³ t - Î³.average) volume 0 s,
    from (hÎ³ _ _).sub (int_avg _ _),
  have intâ‚‚ : interval_integrable (Î» t, Î³ t - Î³.average) volume s (s + 1),
    from (hÎ³ _ _).sub (int_avg _ _),
  have intâ‚ƒ : interval_integrable Î³ volume s (s + 1), from hÎ³ _ _,
  have intâ‚„ : interval_integrable (Î» t, Î³.average) volume s (s + 1), from int_avg _ _,
  dsimp only,
  /- Rmk: Lean doesn't want to rewrite using `interval_integral.integral_sub` without being
    given the integrability assumptions :-( -/
  rw [â† interval_integral.integral_add_adjacent_intervals intâ‚ intâ‚‚,
      interval_integral.integral_sub intâ‚ƒ intâ‚„, Î³.periodic.interval_integral_add_eq s 0,
      zero_add, loop.average],
  simp only [add_zero, add_tsub_cancel_left, interval_integral.integral_const, one_smul, sub_self]
end

@[simp] lemma corrugation_const {x : E} (h : (Î³ x).is_const) : ğ’¯ N Î³ x = 0 :=
begin
  unfold corrugation,
  rw loop.is_const_iff_const_avg at h,
  rw h,
  simp only [add_zero, interval_integral.integral_const, loop.const_apply, loop.average_const, smul_zero, sub_self]
end

variables (Î³ Ï€ N)

lemma corrugation.support : support (ğ’¯ N Î³) âŠ† loop.support Î³ :=
begin
  intros x x_in,
  apply subset_closure,
  intro h,
  apply x_in,
  simp [h]
end

lemma corrugation_eq_zero (x âˆ‰ loop.support Î³) : corrugation Ï€ N Î³ x = 0 :=
nmem_support.mp (Î» hx, H (corrugation.support N Î³ hx))

lemma corrugation.c0_small_on [first_countable_topology E]
  [locally_compact_space E] {Î³ : â„ â†’ E â†’ loop F} {K : set E} (hK : is_compact K)
  (h_le : âˆ€ x, âˆ€ t â‰¤ 0, Î³ t x = Î³ 0 x) (h_ge : âˆ€ x, âˆ€ t â‰¥ 1, Î³ t x = Î³ 1 x)
  (hÎ³_cont : continuous â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ (x âˆˆ K) t, âˆ¥ğ’¯ N (Î³ t) xâˆ¥ < Îµ :=
begin
  have cont' : continuous â†¿(Î» (q : â„ Ã— E)  t, âˆ« t in 0..t, (Î³ q.1 q.2) t - (Î³ q.1 q.2).average),
  { refine continuous_parametric_interval_integral_of_continuous _ continuous_snd,
    refine (hÎ³_cont.compâ‚ƒ continuous_fst.fst.fst continuous_fst.fst.snd continuous_snd).sub _,
    refine loop.continuous_average _,
    exact hÎ³_cont.compâ‚ƒ continuous_fst.fst.fst.fst continuous_fst.fst.fst.snd continuous_snd },
  rcases cont'.bounded_on_compact_of_one_periodic _ ((is_compact_Icc : is_compact I).prod hK) with âŸ¨C, hCâŸ©,
  { apply (const_mul_one_div_lt Îµ_pos C).mono,
    intros N hN x hx t,
    rw [corrugation, norm_smul, mul_comm],
    apply (mul_le_mul_of_nonneg_right _ (norm_nonneg $ 1/N)).trans_lt hN,
    cases le_or_lt t 0 with ht ht,
    { rw h_le x t ht,
      apply hC (0, x),
      simp [hx] },
    { cases le_or_lt 1 t with ht' ht',
      { rw h_ge x t ht',
        apply hC (1, x),
        simp [hx] },
      { exact hC (t, x) (mk_mem_prod âŸ¨ht.le, ht'.leâŸ© hx) _ } } },
  { rintros âŸ¨t, xâŸ©,
    apply per_corrugation,
    intros a b,
    exact (hÎ³_cont.compâ‚ƒ continuous_const continuous_const continuous_id).interval_integrable _ _ }
end

variables {Î³}

lemma corrugation.cont_diff' {n : â„•âˆ} {Î³ : G â†’ E â†’ loop F} (hÎ³_diff : ğ’ n â†¿Î³)
  {x : H â†’ E} (hx : ğ’ n x) {g : H â†’ G} (hg : ğ’ n g) :
  ğ’ n (Î» h, ğ’¯ N (Î³ $ g h) $ x h) :=
begin
  apply cont_diff.const_smul,
  apply cont_diff_parametric_primitive_of_cont_diff,
  { apply cont_diff.sub,
    { exact hÎ³_diff.compâ‚ƒ hg.fst' hx.fst' cont_diff_snd },
    { apply cont_diff_average,
      exact hÎ³_diff.compâ‚ƒ hg.fst'.fst' hx.fst'.fst' cont_diff_snd } },
  { apply cont_diff_const.mul (Ï€.cont_diff.comp hx) },
end

lemma corrugation.cont_diff [finite_dimensional â„ E] {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) :
  ğ’ n (ğ’¯ N Î³) :=
(cont_diff_parametric_primitive_of_cont_diff
  (cont_diff_sub_average hÎ³_diff) (Ï€.cont_diff.const_smul N) 0).const_smul _

notation `âˆ‚â‚` := partial_fderiv_fst â„

/--
The remainder appearing when differentiating a corrugation.
-/
def corrugation.remainder (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ loop F) : E â†’ (E â†’L[â„] F) :=
Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), âˆ‚â‚ (Î» x t, (Î³ x).normalize t) x t

local notation `R` := corrugation.remainder Ï€

variable [finite_dimensional â„ E]

lemma remainder_eq (N : â„) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) :
R N Î³ = Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (loop.diff Î³ x).normalize t :=
by { simp_rw loop.diff_normalize h, refl }

-- The next lemma is a restatement of the above to emphasize that remainder is a corrugation
-- but it won't be used directly
lemma remainder_eq_corrugation (N : â„) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) :
R N Î³ = ğ’¯ N (Î» x, (loop.diff Î³ x)) :=
remainder_eq _ _ h

@[simp]
lemma remainder_eq_zero (N : â„) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) {x : E} (hx : x âˆ‰ loop.support Î³) :
  R N Î³ x = 0 :=
begin
  have hx' : x âˆ‰ loop.support (loop.diff Î³), from (Î» h, hx $ loop.support_diff h),
  simp [remainder_eq Ï€ N h, loop.normalize_of_is_const (loop.is_const_of_not_mem_support hx')]
end

lemma corrugation.fderiv_eq {N : â„} (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) :
  D (ğ’¯ N Î³) = Î» x : E, (Î³ x (N*Ï€ x) - (Î³ x).average) â¬ Ï€ + R N Î³ x :=
begin
  ext1 xâ‚€,
  have hÏ€_diff := Ï€.cont_diff,
  have diff := cont_diff_sub_average hÎ³_diff,
  have key :=
    (has_fderiv_at_parametric_primitive_of_cont_diff' diff (hÏ€_diff.const_smul N) xâ‚€ 0).2,
  erw [fderiv_const_smul key.differentiable_at,
       key.fderiv,
       smul_add, add_comm],
  congr' 1,
  rw [fderiv_const_smul (hÏ€_diff.differentiable le_rfl).differentiable_at N, Ï€.fderiv],
  simp only [smul_smul, inv_mul_cancel hN, one_div, algebra.id.smul_eq_mul, one_smul,
              continuous_linear_map.comp_smul]
end

lemma corrugation.fderiv_apply (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) (x v : E) :
  D (ğ’¯ N Î³) x v = (Ï€ v) â€¢ (Î³ x (N*Ï€ x) - (Î³ x).average) + R N Î³ x v :=
by simp only [corrugation.fderiv_eq hN hÎ³_diff, to_span_singleton_apply, add_apply,
              coe_comp', comp_app]

lemma fderiv_corrugated_map (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) {f : E â†’ F} (hf : ğ’ 1 f)
  (p : dual_pair E) {x} (hfÎ³ : (Î³ x).average = D f x p.v) :
D (f + corrugation p.Ï€ N Î³) x = p.update (D f x) (Î³ x (N*p.Ï€ x)) + corrugation.remainder p.Ï€ N Î³ x :=
begin
  ext v,
  erw fderiv_add (hf.differentiable le_rfl).differentiable_at
      ((corrugation.cont_diff N hÎ³_diff).differentiable le_rfl).differentiable_at,
  simp_rw [continuous_linear_map.add_apply, corrugation.fderiv_apply N hN hÎ³_diff, hfÎ³,
    dual_pair.update, continuous_linear_map.add_apply,p.Ï€.comp_to_span_singleton_apply, add_assoc],
end

lemma remainder.smooth {Î³ : G â†’ E â†’ loop F} (hÎ³_diff : ğ’ âˆ â†¿Î³)
  {x : H â†’ E} (hx : ğ’ âˆ x) {g : H â†’ G} (hg : ğ’ âˆ g) :
  ğ’ âˆ (Î» h, R N (Î³ $ g h) $ x h) :=
begin
  apply cont_diff.const_smul,
  apply cont_diff_parametric_primitive_of_cont_diff,
  { let Ïˆ : E â†’ (H Ã— â„) â†’ F := Î» x q, (Î³ (g q.1) x).normalize q.2,
    change ğ’ âŠ¤ (Î» (q : H Ã— â„), âˆ‚â‚ Ïˆ (x q.1) (q.1, q.2)),
    refine (cont_diff.cont_diff_top_partial_fst _).compâ‚‚ hx.fst'
      (cont_diff_fst.prod cont_diff_snd),
    dsimp [Ïˆ, loop.normalize],
    apply cont_diff.sub,
    apply hÎ³_diff.compâ‚ƒ hg.fst'.snd' cont_diff_fst cont_diff_snd.snd,
    apply cont_diff_average,
    exact hÎ³_diff.compâ‚ƒ hg.fst'.snd'.fst' cont_diff_fst.fst' cont_diff_snd },
  { exact cont_diff_const.mul (Ï€.cont_diff.comp hx) },
end

lemma remainder_c0_small_on {K : set E} (hK : is_compact K)
  (hÎ³_diff : ğ’ 1 â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x âˆˆ K, âˆ¥R N Î³ xâˆ¥ < Îµ :=
begin
  have : âˆ€ N : â„, R N Î³ = ğ’¯ N (loop.diff Î³),
  { intro N,
    exact remainder_eq Ï€ N hÎ³_diff },
  simp_rw (Î» N, remainder_eq Ï€ N hÎ³_diff),
  let g : â„ â†’ E â†’ loop (E â†’L[â„] F) := Î» t, (loop.diff Î³),
  have g_le : âˆ€ x (t : â„), t â‰¤ 0 â†’ g t x = g 0 x, from Î» _ _ _, rfl,
  have g_ge : âˆ€ x (t : â„), t â‰¥ 1 â†’ g t x = g 1 x, from Î» _ _ _, rfl,
  have g_cont : continuous â†¿g, from (loop.continuous_diff hÎ³_diff).snd',
  apply (corrugation.c0_small_on hK g_le g_ge g_cont Îµ_pos).mono,
  intros N H x x_in,
  exact H x x_in 0
end
