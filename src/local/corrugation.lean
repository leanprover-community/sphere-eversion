import analysis.asymptotics.asymptotics
import linear_algebra.dual
import measure_theory.integral.interval_integral
import analysis.calculus.parametric_integral

import to_mathlib.topology.periodic
import to_mathlib.analysis.calculus
import to_mathlib.measure_theory.parametric_interval_integral

import loops.basic

noncomputable theory


local notation `D` := fderiv â„
local notation `âˆ‚â‚` := partial_fderiv_fst â„
local notation `ğ’` := times_cont_diff â„
local notation u ` â¬ `:70 Ï†:65 :=
  continuous_linear_map.comp (continuous_linear_map.to_span_singleton â„ u) Ï†

open set function finite_dimensional asymptotics filter topological_space int measure_theory
     continuous_linear_map
open_locale topological_space

section c0
variables {E : Type*}
          {F : Type*} [normed_group F] [normed_space â„ F] [measurable_space F] [borel_space F]
          [finite_dimensional â„ F]

/-- TheilliÃ¨re's corrugations. -/
def corrugation (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ loop F) : E â†’ F :=
Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (Î³ x t - (Î³ x).average)

def corrugate (f : E â†’ F) (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ loop F) : E â†’ F :=
Î» x, f x + (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (Î³ x t - (Î³ x).average)

lemma per_corrugation (Î³ : loop F) (hÎ³ : âˆ€ s t, interval_integrable Î³ volume s t) :
  one_periodic (Î» s, âˆ« t in 0..s, Î³ t - Î³.average) :=
begin
  have int_avg : âˆ€ s t,  interval_integrable (Î» u : â„, Î³.average) volume s t,
    from Î» s t, interval_integrable_const,
  intros s,
  have intâ‚ : interval_integrable (Î» t, Î³ t - Î³.average) volume 0 s,
    from (hÎ³ _ _).sub (int_avg _ _),
  have intâ‚‚ : interval_integrable (Î» t, Î³ t - Î³.average) volume s (s + 1),
    from (hÎ³ _ _).sub (int_avg _ _),
  have intâ‚ƒ : interval_integrable Î³ volume s (s + 1), from hÎ³ _ _,
  have intâ‚„ : interval_integrable (Î» t, Î³.average) volume s (s + 1), from int_avg _ _,
  dsimp only,
  /- Rmk: Lean doesn't want to rewrite using `interval_integral.integral_sub` without being
    given the integrability assumptions :-( -/
  rw [â† interval_integral.integral_add_adjacent_intervals intâ‚ intâ‚‚,
      interval_integral.integral_sub intâ‚ƒ intâ‚„, interval_integral_periodic Î³.per hÎ³, loop.average],
  simp only [add_zero, add_tsub_cancel_left, interval_integral.integral_const, one_smul, sub_self]
end

variables (Ï€ : E â†’ â„) (N : â„) {Î³ : E â†’ loop F} [topological_space E]

@[simp] lemma corrugation_const {x : E} (h : (Î³ x).is_const) : corrugation Ï€ N Î³ x = 0 :=
begin
  unfold corrugation,
  rw loop.is_const_iff_const_avg at h,
  rw h,
  simp only [add_zero, interval_integral.integral_const, loop.const_apply, loop.average_const, smul_zero, sub_self]
end

lemma corrugation.support : support (corrugation Ï€ N Î³) âŠ† loop.support Î³ :=
begin
  intros x x_in,
  apply subset_closure,
  intro h,
  apply x_in,
  simp [h]
end

/-- If a loop family has compact support then the corresponding corrugation is
small uniformly in the source point. -/
lemma corrugation.c0_small [first_countable_topology E] [t2_space E]
  [locally_compact_space E] (hÎ³ : is_compact (loop.support Î³))
  (hÎ³_cont : continuous â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ¥corrugation Ï€ N Î³ xâˆ¥ < Îµ :=
begin
  obtain âŸ¨C, hCâŸ© : âˆƒ C, âˆ€ x b, âˆ¥âˆ« t in 0..b, (Î³ x t - (Î³ x).average)âˆ¥ â‰¤ C,
  { apply continuous.bounded_of_one_periodic_of_compact _ _ hÎ³,
    { intros x hx,
      ext t,
      rw loop.eq_const_of_not_mem_support hx,
      simp },
    { let Ï† : E â†’ â„ â†’ F := Î» x s, (Î³ x) s - (Î³ x).average,
      have cont_Ï† : continuous (Î» p : E Ã— â„, Ï† p.1 p.2),
        from hÎ³_cont.sub ((loop.continuous_average hÎ³_cont).comp continuous_fst),
      exact continuous_parametric_primitive_of_continuous cont_Ï† },
    { intro x,
      exact per_corrugation _ (loop.continuous_of_family hÎ³_cont x).interval_integrable } },
  apply (const_mul_one_div_lt Îµ_pos C).mono,
  intros N hN x,
  rw [corrugation, norm_smul, mul_comm],
  exact lt_of_le_of_lt (mul_le_mul_of_nonneg_right (hC x (N*Ï€ x)) (norm_nonneg $ 1/N)) hN,
end

-- We also need this variation... TODO: think of a common case
lemma corrugation.c0_small' [first_countable_topology E] [t2_space E]
  [locally_compact_space E] {Î³ : â„ â†’ E â†’ loop F} {K : set E} (hK : is_compact K)
  (h_supp : âˆ€ x âˆ‰ K, âˆ€ t, (Î³ t x).is_const)
  (h_le : âˆ€ x, âˆ€ t â‰¤ 0, Î³ t x = Î³ 0 x) (h_ge : âˆ€ x, âˆ€ t â‰¥ 1, Î³ t x = Î³ 1 x)
  (hÎ³_cont : continuous â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x t, âˆ¥corrugation Ï€ N (Î³ t) xâˆ¥ < Îµ :=
sorry


end c0

section c1

variables {E : Type*} [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]
          {F : Type*} [normed_group F] [normed_space â„ F] [measurable_space F] [borel_space F]
          [finite_dimensional â„ F]


variables (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ loop F)
          (hÎ³ : is_compact (loop.support Î³))

def corrugation.remainder (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ loop F) : E â†’ (E â†’L[â„] F) :=
Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), âˆ‚â‚ (Î» x t, (Î³ x).normalize t) x t

local notation `R` := corrugation.remainder Ï€
local notation `ğ’¯` := corrugation Ï€

lemma remainder_eq (N : â„) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) :
R N Î³ = Î» x, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (loop.diff Î³ x).normalize t :=
by { simp_rw loop.diff_normalize h, refl }

lemma remainder_eq' (x : E) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) :
(Î» (N : â„), R N Î³ x) = Î» N, (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (loop.diff Î³ x).normalize t :=
begin
  ext N,
  rw remainder_eq Ï€ _ h
end

@[simp]
lemma remainder_eq_zero (N : â„) {Î³ : E â†’ loop F} (h : ğ’ 1 â†¿Î³) {x : E} (hx : x âˆ‰ loop.support Î³) :
  R N Î³ x = 0 :=
begin
  have hx' : x âˆ‰ loop.support (loop.diff Î³), from (Î» h, hx $ loop.support_diff h),
  simp [remainder_eq Ï€ N h, loop.normalize_of_is_const (loop.is_const_of_not_mem_support hx')]
end

variables {Ï€} {Î³}

lemma corrugation.times_cont_diff {n : with_top â„•} (hÏ€_diff : ğ’ n Ï€) (hÎ³_diff : ğ’ n â†¿Î³) :
  ğ’ n (ğ’¯ N Î³) :=
begin
  apply times_cont_diff.const_smul,
  apply times_cont_diff_parametric_primitive_of_times_cont_diff _ (hÏ€_diff.const_smul N) 0,
  exact times_cont_diff_sub_average hÎ³_diff
end

lemma corrugation.fderiv_eq (hN : N â‰  0) (hÏ€_diff : ğ’ 1 Ï€) (hÎ³_diff : ğ’ 1 â†¿Î³) :
  D (ğ’¯ N Î³) = Î» x : E, (Î³ x (N*Ï€ x) - (Î³ x).average) â¬ (D Ï€ x) + R N Î³ x :=
begin
  ext1 xâ‚€,
  have diff := times_cont_diff_sub_average hÎ³_diff,
  have key :=
    (has_fderiv_at_parametric_primitive_of_times_cont_diff' diff (hÏ€_diff.const_smul N) xâ‚€ 0).2,
  erw [fderiv_const_smul key.differentiable_at,
       key.fderiv,
       smul_add, add_comm],
  congr' 1,
  rw fderiv_const_smul (hÏ€_diff.differentiable le_rfl).differentiable_at N,
  simp only [smul_smul, inv_mul_cancel hN, one_div, algebra.id.smul_eq_mul, one_smul,
              continuous_linear_map.comp_smul]
end

lemma corrugation.fderiv_apply (hN : N â‰  0) (hÏ€_diff : ğ’ 1 Ï€) (hÎ³_diff : ğ’ 1 â†¿Î³) (x v : E) :
  D (ğ’¯ N Î³) x v = (D Ï€ x v) â€¢ (Î³ x (N*Ï€ x) - (Î³ x).average) + R N Î³ x v :=
by simp only [corrugation.fderiv_eq N hN hÏ€_diff hÎ³_diff, to_span_singleton_apply, add_apply,
              coe_comp', comp_app]


lemma remainder_c0_small (hÎ³ : is_compact (loop.support Î³))
  (hÎ³_diff : ğ’ 1 â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ¥R N Î³ xâˆ¥ < Îµ :=
begin
  have : is_compact (loop.support $ loop.diff Î³),
    from loop.compact_support_diff hÎ³,
  apply (corrugation.c0_small Ï€ this (loop.continuous_diff hÎ³_diff) Îµ_pos).mono,
  intros N H x,
  have key := congr_fun (remainder_eq' Ï€ x hÎ³_diff) N,
  dsimp only at key,
  rw key,
  exact H x
end

variables {Ï€}

lemma corrugation.fderiv (hÏ€ : ğ’ 1 Ï€) (hÎ³_diff : ğ’ 1 â†¿Î³) (hÎ³_supp : is_compact (loop.support Î³)) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ¥D (ğ’¯ N Î³) x - (Î³ x (N*Ï€ x) - (Î³ x).average) â¬ (D Ï€ x)âˆ¥ < Îµ :=
begin
  apply ((remainder_c0_small Ï€ hÎ³_supp hÎ³_diff Îµ_pos).and (eventually_ne_at_top (0 : â„))).mono,
  rintros N âŸ¨hN, N_neâŸ© x,
  simpa only [corrugation.fderiv_eq N N_ne hÏ€ hÎ³_diff, add_sub_cancel'] using hN x
end

lemma corrugation.fderiv_ker (hÏ€ : ğ’ 1 Ï€) (hÎ³_diff : ğ’ 1 â†¿Î³) (hÎ³_supp : is_compact (loop.support Î³)) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ€ w âˆˆ (D Ï€ x : E â†’â‚—[â„] â„).ker,
  âˆ¥D (ğ’¯ N Î³) x wâˆ¥ â‰¤ Îµ*âˆ¥wâˆ¥ :=
begin
  apply (corrugation.fderiv hÏ€ hÎ³_diff hÎ³_supp Îµ_pos).mono,
  intros N hN x w hw,
  let Ï† := D (corrugation Ï€ N Î³) x - (Î³ x (N * Ï€ x) - (Î³ x).average) â¬ D Ï€ x,
  rw linear_map.mem_ker at hw,
  change D Ï€ x w = 0 at hw,
  have : D (corrugation Ï€ N Î³) x w = Ï† w,
  { simp only [Ï†, hw, continuous_linear_map.coe_comp', continuous_linear_map.coe_sub', sub_zero,
               comp_app, pi.sub_apply, continuous_linear_map.map_zero] },
  rw this,
  exact Ï†.le_of_op_norm_le (hN x).le w
end

lemma corrugation.fderiv_u (hÏ€ : ğ’ 1 Ï€) (hÎ³_diff : ğ’ 1 â†¿Î³) (hÎ³_supp : is_compact (loop.support Î³))
  {u : E} (hu : âˆ€ x, D Ï€ x u = 1) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ¥D (ğ’¯ N Î³) x u - (Î³ x (N*Ï€ x) - (Î³ x).average)âˆ¥ â‰¤  Îµ*âˆ¥uâˆ¥ :=
begin
  apply (corrugation.fderiv hÏ€ hÎ³_diff hÎ³_supp Îµ_pos).mono,
  intros N hN x,
  let Ï† := D (corrugation Ï€ N Î³) x - (Î³ x (N * Ï€ x) - (Î³ x).average) â¬ D Ï€ x,
  rw show (D (corrugation Ï€ N Î³) x) u - (Î³ x (N * Ï€ x) - (Î³ x).average) = Ï† u,
    by simp [Ï†, hu, continuous_linear_map.to_span_singleton_apply],
  exact le_of_op_norm_le Ï† (hN x).le u
end

structure corrugation_data (f : E â†’ F) (U : set E):=
(Ï€ : E â†’L[â„] â„)
(u : E)
(hu : âˆ¥uâˆ¥ = 1)
(hÏ€u : Ï€ u = 1)
(Î³ : E â†’ loop F)
(hÎ³_diff : ğ’ âŠ¤ â†¿Î³)
(hÎ³_supp : is_compact (loop.support Î³))
(hÎ³_avg : âˆ€ {x}, x âˆˆ U â†’ (Î³ x).average = D f x u)

def corrugation_data.fun {f : E â†’ F} {U : set E} (d : corrugation_data f U) : â„ â†’ E â†’ F :=
Î» N, f + corrugation d.Ï€ N d.Î³

lemma corrugation_data.C1_Ï€ {f : E â†’ F} {U : set E} (d : corrugation_data f U) : ğ’ 1 d.Ï€ :=
d.Ï€.times_cont_diff

lemma corrugation_data.C1_Î³ {f : E â†’ F} {U : set E} (d : corrugation_data f U) : ğ’ 1 â†¿d.Î³ :=
d.hÎ³_diff.of_le le_top

lemma corrugation_data.DÏ€u {f : E â†’ F} {U : set E} (d : corrugation_data f U) :
  âˆ€ x, D d.Ï€ x d.u = 1  :=
Î» x, by { rw d.Ï€.fderiv, exact d.hÏ€u }

lemma corrugation_data.C1_corrugation {f : E â†’ F} {U : set E} (d : corrugation_data f U) (N : â„) :
  ğ’ 1 (corrugation â‡‘(d.Ï€) N d.Î³) :=
corrugation.times_cont_diff N d.C1_Ï€ d.C1_Î³

lemma corrugation_data.Dfun {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f) (N : â„) :
  D(d.fun N) = D f + D (corrugation d.Ï€ N d.Î³) :=
begin
  ext x,
  erw [corrugation_data.fun, fderiv_add],
  refl,
  apply (hf.differentiable le_rfl).differentiable_at,
  apply ((d.C1_corrugation N).differentiable le_rfl).differentiable_at,
end

lemma corrugation_data.C1_fun {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f) (N : â„) :
  ğ’ 1 (d.fun N) :=
hf.add (d.C1_corrugation N)

lemma corrugation_data.c0_close {f : E â†’ F} {U : set E} (d : corrugation_data f U) {Îµ : â„} (Îµ_pos : 0 < Îµ)  :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ¥d.fun N x - f xâˆ¥ < Îµ :=
begin
  apply (corrugation.c0_small d.Ï€ d.hÎ³_supp d.hÎ³_diff.continuous Îµ_pos).mono,
  intros N hN x,
  simpa [corrugation_data.fun] using hN x
end

lemma corrugation_data.deriv_ker_Ï€ {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f) {Îµ : â„} (Îµ_pos : 0 < Îµ)  :
  âˆ€á¶  N in at_top, âˆ€ x, âˆ€ w âˆˆ d.Ï€.ker, âˆ¥D (d.fun N) x w - D f x wâˆ¥ â‰¤ Îµ*âˆ¥wâˆ¥ :=
begin
  apply (corrugation.fderiv_ker d.C1_Ï€ d.C1_Î³ d.hÎ³_supp Îµ_pos).mono,
  simp_rw d.Ï€.fderiv,
  intros N hN x w w_in,
  simpa [d.Dfun hf] using hN x w w_in
end

lemma corrugation_data.deriv_u {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f) {Îµ : â„} (Îµ_pos : 0 < Îµ)  :
  âˆ€á¶  N in at_top, âˆ€ x âˆˆ U, âˆ¥D (d.fun N) x d.u -  d.Î³ x (N*d.Ï€ x)âˆ¥ â‰¤ Îµ :=
begin
  apply (corrugation.fderiv_u d.C1_Ï€ d.C1_Î³ d.hÎ³_supp d.DÏ€u Îµ_pos).mono,
  intros N hN x x_in,
  specialize hN x,
  rw [d.hÎ³_avg x_in, d.hu, mul_one] at hN,
  convert hN using 2,
  rw [d.Dfun hf],
  abel
end

lemma theilliere {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f) {Îµ : â„} (Îµ_pos : 0 < Îµ)  :
  âˆ€á¶  N in at_top, âˆ€ x,
âˆ¥d.fun N x - f xâˆ¥ < Îµ âˆ§ (((âˆ€ w âˆˆ d.Ï€.ker, âˆ¥D (d.fun N) x w - D f x wâˆ¥ â‰¤ Îµ*âˆ¥wâˆ¥) âˆ§ x âˆˆ U â†’ âˆ¥D (d.fun N) x d.u -  d.Î³ x (N*d.Ï€ x)âˆ¥ â‰¤ Îµ)) :=
begin
  apply ((d.c0_close Îµ_pos).and ((d.deriv_ker_Ï€ hf Îµ_pos).and (d.deriv_u hf Îµ_pos))).mono,
  tauto
end

lemma corrugation_data.relative {f : E â†’ F} {U : set E} (d : corrugation_data f U) (hf : ğ’ 1 f)
  {x : E} (hx : x âˆ‰ loop.support d.Î³) (hN : N â‰  0) :
  d.fun N x = f x âˆ§
  (âˆ€ w âˆˆ d.Ï€.ker, D (d.fun N) x w = D f x w) âˆ§
  (x âˆˆ U â†’ D (d.fun N) x d.u = d.Î³ x (N*d.Ï€ x)) :=
begin
  have hx' := loop.is_const_of_not_mem_support hx,
  refine âŸ¨_, _, _âŸ©,
  { change f x + corrugation d.Ï€ N d.Î³ x = _,
    rw [corrugation_const d.Ï€ N hx', add_zero] },
  { intros w w_in,
    rw [d.Dfun hf, corrugation.fderiv_eq N hN d.C1_Ï€ d.C1_Î³],
    simp only [add_zero, continuous_linear_map.coe_comp', pi.add_apply, map_zero,
               eq_self_iff_true, function.comp_app, continuous_linear_map.add_apply,
               d.Ï€.fderiv, continuous_linear_map.mem_ker.mp w_in, remainder_eq_zero d.Ï€ N d.C1_Î³ hx] },
  { intros x_in,
    simp [d.Dfun hf, corrugation.fderiv_apply N hN d.C1_Ï€ d.C1_Î³,
     remainder_eq_zero d.Ï€ N d.C1_Î³ hx,
     d.DÏ€u, d.hÏ€u,
     d.hÎ³_avg x_in, smul_sub] }
end

end c1
