import analysis.calculus.specific_functions
import topology.metric_space.hausdorff_distance

import to_mathlib.topology.misc
import to_mathlib.topology.nhds_set
import to_mathlib.topology.hausdorff_distance
import to_mathlib.linear_algebra.basic

import local.dual_pair
import local.ample
import notations

/-!
# Local partial differential relations and their formal solutions

This file defines `rel_loc E F`, the type of first order partial differential relations
for maps between two real normed spaces `E` and `F`.

To any `R : rel_loc E F` we associate the type `sol R` of maps `f : E â†’ F` of
solutions of `R`, and its formal counterpart `formal_sol R`.

The h-principle question is whether we can deform any formal solution into a solution.
The type of deformations is `htpy_jet_sec E F` (homotopies of 1-jet sections).
Implementation note: the time parameter `t` is any real number, but all the homotopies we will
construct will be constant for `t â‰¤ 0` and `t â‰¥ 1`. It looks like this imposes more smoothness
constraints at `t = 0` and `t = 1` (requiring flat functions), but this is needed for smooth
concatenations anyway.

This file also defines the ampleness conditions for these relations. Together with openness,
this will guarantee the h-principle (in some other file).
-/

noncomputable theory

open set function module (dual) real filter
open_locale unit_interval topological_space

variables (E : Type*) [normed_add_comm_group E] [normed_space â„ E]
variables (F : Type*) [normed_add_comm_group F] [normed_space â„ F]
variables (P : Type*) [normed_add_comm_group P] [normed_space â„ P]


@[derive metric_space]
def one_jet := E Ã— F Ã— (E â†’L[â„] F)

/-- A first order relation for maps between real vector spaces. -/
def rel_loc := set (one_jet E F)

instance : has_mem (E Ã— F Ã— (E â†’L[â„] F)) (rel_loc E F) := set.has_mem


variables {E F}

namespace rel_loc

/-- The slice of a local relation `R : rel_loc E F` for a dual pair `p` at a jet `Î¸` is
the set of `w` in `F` such that updating `Î¸` using `p` and `w` leads to a jet in `R`. -/
def slice (R : rel_loc E F) (p : dual_pair' E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)) : set F :=
{w | (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R}

lemma mem_slice (R : rel_loc E F) {p : dual_pair' E} {Î¸ : E Ã— F Ã— (E â†’L[â„] F)} {w : F} :
  w âˆˆ R.slice p Î¸ â†” (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R :=
iff.rfl

/-- A relation is ample if all its slices are ample. -/
def is_ample (R : rel_loc E F) : Prop := âˆ€ (p : dual_pair' E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)),
ample_set (R.slice p Î¸)

/- FIXME: the proof below is awful. -/
lemma is_ample.mem_hull {R : rel_loc E F} (h : is_ample R) {Î¸ : E Ã— F Ã— (E â†’L[â„] F)}
  (hÎ¸ : Î¸ âˆˆ R) (v : F) (p) : v âˆˆ hull (connected_component_in (R.slice p Î¸) (Î¸.2.2 p.v)) :=
begin
  rw h p Î¸ (Î¸.2.2 p.v) _,
  exact mem_univ _,
  dsimp [rel_loc.slice],
  rw p.update_self,
  cases Î¸,
  cases Î¸_snd,
  exact hÎ¸
end

lemma slice_update {R : rel_loc E F} {Î¸ : E Ã— F Ã— (E â†’L[â„] F)}
  {p : dual_pair' E} (x : F) :
  R.slice p (Î¸.1, Î¸.2.1, (p.update Î¸.2.2 x)) = R.slice p Î¸ :=
begin
  ext1 w,
  dsimp [slice],
  rw [p.update_update]
end

/-- In order to check ampleness, it suffices to consider slices through elements of the relation. -/
lemma is_ample_iff {R : rel_loc E F} : R.is_ample â†”
  âˆ€ â¦ƒÎ¸ : one_jet E Fâ¦„ (p : dual_pair' E), Î¸ âˆˆ R â†’ ample_set (R.slice p Î¸) :=
begin
  simp_rw [is_ample],
  refine âŸ¨Î» h Î¸ p hÎ¸, h p Î¸, Î» h p Î¸ w hw, _âŸ©,
  dsimp [slice] at hw,
  have := h p hw,
  rw [slice_update] at this,
  exact this w hw
end


open_locale pointwise

lemma slice_of_ker_eq_ker {R : rel_loc E F} {Î¸ : one_jet E F}
  {p p' : dual_pair' E} (hpp' : p.Ï€ = p'.Ï€) :
  R.slice p Î¸ = Î¸.2.2 (p.v - p'.v) +áµ¥ R.slice p' Î¸ :=
begin
  rcases Î¸ with âŸ¨x, y, Ï†âŸ©,
  have key : âˆ€ w, p'.update Ï† w = p.update Ï† (w + Ï† (p.v - p'.v)),
  { intros w,
    simp only [dual_pair'.update, hpp', map_sub, add_right_inj],
    congr' 2,
    abel },
  ext w,
  simp only [slice, mem_set_of_eq, map_sub, vadd_eq_add, mem_vadd_set_iff_neg_vadd_mem, key],
  have : -(Ï† p.v - Ï† p'.v) + w + (Ï† p.v - Ï† p'.v) = w,
  abel,
  rw this,
end

lemma ample_slice_of_ample_slice {R : rel_loc E F} {Î¸ : one_jet E F}
  {p p' : dual_pair' E} (hpp' : p.Ï€ = p'.Ï€) (h : ample_set (R.slice p Î¸)) :
  ample_set (R.slice p' Î¸) :=
begin
  rw slice_of_ker_eq_ker hpp'.symm,
  exact ample_set.vadd h
end

/-- A solution to a local relation `R`. -/
@[ext] structure sol (R : rel_loc E F) :=
(f : E â†’ F)
(f_diff : ğ’ âˆ f)
(is_sol : âˆ€ x, (x, f x, D f x) âˆˆ R)

variables (E)

@[ext] structure jet_sec (F : Type*) [normed_add_comm_group F] [normed_space â„ F] :=
(f : E â†’ F)
(f_diff : ğ’ âˆ f)
(Ï† : E â†’ E â†’L[â„] F)
(Ï†_diff : ğ’ âˆ Ï†)

variables {E}

def jet_sec.is_formal_sol (ğ“• : jet_sec E F) (R : rel_loc E F) : Prop :=
âˆ€ x, (x, ğ“•.f x, ğ“•.Ï† x) âˆˆ R

/-- A formal solution to a local relation `R` over a set `U`. -/
@[ext] structure formal_sol (R : rel_loc E F) extends jet_sec E F :=
(is_sol : âˆ€ x, (x, f x, Ï† x) âˆˆ R)

instance (R : rel_loc E F) : has_coe (formal_sol R) (jet_sec E F):=
âŸ¨formal_sol.to_jet_secâŸ©

@[simp] lemma formal_sol.to_jet_sec_eq_coe {R : rel_loc E F} (ğ“• : formal_sol R) :
ğ“•.to_jet_sec = (ğ“• : jet_sec E F) := rfl

@[simp] lemma formal_sol.coe_is_formal_sol  {R : rel_loc E F} (ğ“• : formal_sol R) :
  (ğ“• : jet_sec E F).is_formal_sol R := ğ“•.is_sol

def jet_sec.is_formal_sol.formal_sol  {ğ“• : jet_sec E F} {R : rel_loc E F}
  (h : ğ“•.is_formal_sol R) : formal_sol R :=
{is_sol := h, ..ğ“•}

/-- Inclusion of solutions into formal solutions. -/
def sol.to_formal_sol {R : rel_loc E F}  (ğ“• : sol R) : formal_sol R :=
{ f := ğ“•.f,
  f_diff := ğ“•.f_diff,
  Ï† := D ğ“•.f,
  Ï†_diff := (cont_diff_top_iff_fderiv.mp ğ“•.f_diff).2,
  is_sol := ğ“•.is_sol }

end rel_loc

namespace rel_loc.jet_sec

open rel_loc

instance : has_coe_to_fun (jet_sec E F) (Î» S, E â†’ F Ã— (E â†’L[â„] F)) :=
âŸ¨Î» ğ“•, Î» x, (ğ“•.f x, ğ“•.Ï† x)âŸ©

instance (R : rel_loc E F) (U : set E) : has_coe_to_fun (formal_sol R) (Î» S, E â†’ F Ã— (E â†’L[â„] F)) :=
âŸ¨Î» ğ“•, Î» x, (ğ“•.f x, ğ“•.Ï† x)âŸ©

@[simp] lemma formal_sol.coe_apply  {R : rel_loc E F} (ğ“• : formal_sol R) (x : E) :
(ğ“• : jet_sec E F) x = ğ“• x := rfl

lemma jet_sec.eq_iff {ğ“• ğ“•' : jet_sec E F} {x : E} :
  ğ“• x = ğ“•' x â†” ğ“•.f x = ğ“•'.f x âˆ§ ğ“•.Ï† x = ğ“•'.Ï† x :=
begin
  split,
  { intro h,
    exact âŸ¨congr_arg prod.fst h, congr_arg prod.snd hâŸ© },
  { rintros âŸ¨h, h'âŸ©,
    ext1,
    exacts [h, h'] }
end

variables  {R : rel_loc E F}

lemma formal_sol.eq_iff {ğ“• ğ“•' : formal_sol R} {x : E} :
  ğ“• x = ğ“•' x â†” ğ“•.f x = ğ“•'.f x âˆ§ ğ“•.Ï† x = ğ“•'.Ï† x :=
jet_sec.eq_iff

/-- The slice associated to a jet section and a dual pair at some point. -/
def slice_at (ğ“• : jet_sec E F) (R : rel_loc E F) (p : dual_pair' E) (x : E) : set F :=
R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- The slice associated to a formal solution and a dual pair at some point. -/
def _root_.rel_loc.formal_sol.slice_at (ğ“• : formal_sol R) (p : dual_pair' E) (x : E) : set F :=
R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- A jet section `ğ“•` is holonomic if its linear map part at `x`
is the derivative of its function part at `x`. -/
def is_holonomic_at (ğ“• : jet_sec E F) (x : E) : Prop := D ğ“•.f x = ğ“•.Ï† x

def _root_.rel_loc.formal_sol.is_holonomic_at (ğ“• : formal_sol R) (x : E) : Prop := D ğ“•.f x = ğ“•.Ï† x

lemma _root_.rel_loc.formal_sol.is_holonomic_at_congr (ğ“• ğ“•' : formal_sol R) {s : set E}
  (h : âˆ€á¶  x near s, ğ“• x = ğ“•' x) : âˆ€á¶  x near s, ğ“•.is_holonomic_at x â†” ğ“•'.is_holonomic_at x :=
begin
  apply h.eventually_nhds_set.mono,
  intros x hx,
  have hf : ğ“•.f =á¶ [ğ“ x] ğ“•'.f,
  { apply hx.mono,
    simp_rw formal_sol.eq_iff,
    tauto },
  unfold rel_loc.formal_sol.is_holonomic_at,
  rw [hf.fderiv_eq, (formal_sol.eq_iff.mp hx.self_of_nhds).2]
end

lemma _root_.rel_loc.sol.is_holonomic {R : rel_loc E F} (ğ“• : sol R) (x : E) :
  ğ“•.to_formal_sol.is_holonomic_at x :=
by simp [rel_loc.sol.to_formal_sol, rel_loc.formal_sol.is_holonomic_at]

/-- A formal solution of `R` over `U` that is holonomic at every point of `U`
comes from a genuine solution. -/
def _root_.rel_loc.formal_sol.to_sol (ğ“• : formal_sol R) (h : âˆ€ x, ğ“•.to_jet_sec.is_holonomic_at x) : sol R :=
{ f := ğ“•.f,
  f_diff := ğ“•.f_diff,
  is_sol := Î» x, ((h x).symm â–¸ (ğ“•.is_sol x)) }

lemma to_sol_to_formal_sol (ğ“• : sol R) :
  ğ“•.to_formal_sol.to_sol (Î» x, ğ“•.is_holonomic x) = ğ“• :=
by { ext x, refl }

/-- A formal solution `ğ“•` of `R` is partially holonomic in the direction of some subspace `E'`
if its linear map part at `x` is the derivative of its function part at `x` in restriction to
`E'`. -/
def is_part_holonomic_at (ğ“• : jet_sec E F) (E' : submodule â„ E) (x : E) :=
âˆ€ v âˆˆ E', D ğ“•.f x v = ğ“•.Ï† x v

lemma _root_.filter.eventually.is_part_holonomic_at_congr {ğ“• ğ“•' : jet_sec E F} {s : set E}
  (h : âˆ€á¶  x near s, ğ“• x = ğ“•' x) (E' : submodule â„ E) :
  âˆ€á¶  x near s, ğ“•.is_part_holonomic_at E' x â†” ğ“•'.is_part_holonomic_at E' x :=
begin
  apply h.eventually_nhds_set.mono,
  intros x hx,
  have hf : ğ“•.f =á¶ [ğ“ x] ğ“•'.f,
  { apply hx.mono,
    dsimp only,
    simp_rw jet_sec.eq_iff,
    tauto },
  unfold rel_loc.jet_sec.is_part_holonomic_at,
  rw [hf.fderiv_eq, (jet_sec.eq_iff.mp hx.self_of_nhds).2]
end

lemma is_part_holonomic_at.sup (ğ“• : jet_sec E F) {E' E'' : submodule â„ E} {x : E}
  (h' : ğ“•.is_part_holonomic_at E' x) (h'' : ğ“•.is_part_holonomic_at E'' x) :
  ğ“•.is_part_holonomic_at (E' âŠ” E'') x :=
Î» v : E, linear_map.eq_on_sup h' h''

lemma _root_.rel_loc.jet_sec.is_part_holonomic_at.mono {ğ“• : jet_sec E F}
  {E' E'' : submodule â„ E} {x : E} (h : ğ“•.is_part_holonomic_at E' x) (h' : E'' â‰¤ E') :
  ğ“•.is_part_holonomic_at E'' x :=
Î» v v_in, h v $ set_like.coe_subset_coe.mpr h' v_in

def _root_.rel_loc.formal_sol.is_part_holonomic_at (ğ“• : formal_sol R) (E' : submodule â„ E) (x : E) :=
âˆ€ v âˆˆ E', D ğ“•.f x v = ğ“•.Ï† x v

lemma _root_.rel_loc.formal_sol.is_part_holonomic_at.mono {ğ“• : formal_sol R}
  {E' E'' : submodule â„ E} {x : E} (h : ğ“•.is_part_holonomic_at E' x) (h' : E'' â‰¤ E') :
  ğ“•.is_part_holonomic_at E'' x :=
Î» v v_in, h v $ set_like.coe_subset_coe.mpr h' v_in

lemma _root_.is_part_holonomic_top {ğ“• : jet_sec E F} {x : E} :
  is_part_holonomic_at ğ“• âŠ¤ x â†” is_holonomic_at ğ“• x :=
begin
  simp only [is_part_holonomic_at, submodule.mem_top, forall_true_left, is_holonomic_at],
  rw [â† funext_iff, continuous_linear_map.coe_fn_injective.eq_iff]
end

@[simp] lemma is_part_holonomic_bot (ğ“• : jet_sec E F) :
  is_part_holonomic_at ğ“• âŠ¥ = Î» x, true :=
begin
  ext x,
  simp only [is_part_holonomic_at, submodule.mem_bot, forall_eq, map_zero, eq_self_iff_true]
end


lemma mem_slice (ğ“• : formal_sol R) (p : dual_pair' E) {x : E} :
  ğ“•.Ï† x p.v âˆˆ ğ“•.slice_at p x :=
by simpa [rel_loc.formal_sol.slice_at, rel_loc.slice] using  ğ“•.is_sol x

/-- A formal solution `ğ“•` is short for a dual pair `p` at a point `x` if the derivative of
the function `ğ“•.f` at `x` is in the convex hull of the relevant connected component of the
corresponding slice. -/
def is_short_at (ğ“• : jet_sec E F) (R : rel_loc E F) (p : dual_pair' E) (x : E) : Prop :=
D ğ“•.f x p.v âˆˆ hull (connected_component_in (ğ“•.slice_at R p x) $ ğ“•.Ï† x p.v)

def _root_.rel_loc.formal_sol.is_short_at (ğ“• : formal_sol R)(p : dual_pair' E) (x : E) : Prop :=
D ğ“•.f x p.v âˆˆ hull (connected_component_in (ğ“•.slice_at p x) $ ğ“•.Ï† x p.v)

lemma _root_.rel_loc.is_ample.is_short_at {R : rel_loc E F} (hR : is_ample R) (ğ“• : formal_sol R) (p : dual_pair' E)
  (x : E) : ğ“•.is_short_at p x :=
hR.mem_hull (ğ“•.is_sol x) _ p

end rel_loc.jet_sec

section htpy_jet_sec

open rel_loc

variables (E F)

/-- A homotopy of sections of JÂ¹(E, F). -/
structure htpy_jet_sec :=
(f : â„ â†’ E â†’ F)
(f_diff : ğ’ âˆ â†¿f)
(Ï† : â„ â†’ E â†’ E â†’L[â„] F)
(Ï†_diff : ğ’ âˆ â†¿Ï†)

variables  {E F} {R : rel_loc E F}

instance : has_coe_to_fun (htpy_jet_sec E F) (Î» S, â„ â†’ jet_sec E F) :=
âŸ¨Î» S t,
 { f := S.f t,
   f_diff := S.f_diff.comp (cont_diff_const.prod cont_diff_id),
   Ï† := S.Ï† t,
   Ï†_diff := S.Ï†_diff.comp (cont_diff_const.prod cont_diff_id) }âŸ©

namespace htpy_jet_sec

lemma cont_diff_f (ğ“• : htpy_jet_sec E F) {n : â„•âˆ} : ğ’ n â†¿ğ“•.f :=
ğ“•.f_diff.of_le le_top

lemma cont_diff_Ï† (ğ“• : htpy_jet_sec E F) {n : â„•âˆ} : ğ’ n â†¿ğ“•.Ï† :=
ğ“•.Ï†_diff.of_le le_top

end htpy_jet_sec

/-- The constant homotopy of formal solutions at a given formal solution. It will be used
as junk value for constructions of formal homotopies that need additional assumptions and also
for trivial induction initialization. -/
def rel_loc.jet_sec.const_htpy (ğ“• : jet_sec E F) : htpy_jet_sec E F :=
{ f := Î» t, ğ“•.f,
  f_diff := ğ“•.f_diff.snd',
  Ï† := Î» t, ğ“•.Ï†,
  Ï†_diff := ğ“•.Ï†_diff.snd' }

@[simp] lemma rel_loc.jet_sec.const_htpy_apply (ğ“• : jet_sec E F) :
  âˆ€ t, ğ“•.const_htpy t = ğ“• :=
Î» t, by ext x ; refl

/-- A smooth step function on `â„`. -/
def smooth_step : â„ â†’ â„ := Î» t, smooth_transition (2 * t - 1/2)

lemma smooth_step.smooth : ğ’ âˆ smooth_step :=
smooth_transition.cont_diff.comp $ (cont_diff_id.const_smul (2 : â„)).sub cont_diff_const

@[simp]
lemma smooth_step.zero : smooth_step 0 = 0 :=
begin
  apply smooth_transition.zero_of_nonpos,
  norm_num
end

@[simp]
lemma smooth_step.one : smooth_step 1 = 1 :=
begin
  apply smooth_transition.one_of_one_le,
  norm_num
end

lemma smooth_step.mem (t : â„) : smooth_step t âˆˆ I :=
âŸ¨smooth_transition.nonneg _, smooth_transition.le_one _âŸ©

lemma smooth_step.abs_le (t : â„) : |smooth_step t| â‰¤ 1 :=
abs_le.mpr âŸ¨by linarith [(smooth_step.mem t).1], smooth_transition.le_one _âŸ©

lemma smooth_step.of_lt {t : â„} (h : t < 1/4) : smooth_step t = 0 :=
begin
  apply smooth_transition.zero_of_nonpos,
  linarith
end


lemma smooth_step.of_gt {t : â„} (h : 3/4 < t) : smooth_step t = 1 :=
begin
  apply smooth_transition.one_of_one_le,
  linarith
end

lemma htpy_jet_sec_comp_aux {f g : â„ â†’ E â†’ F} (hf : ğ’ âˆ â†¿f) (hg : ğ’ âˆ â†¿g)
  (hfg : f 1 = g 0) :
  ğ’ âˆ â†¿(Î» t x, if t â‰¤ 1/2 then f (smooth_step $ 2*t) x else g (smooth_step $ 2*t - 1) x : â„ â†’ E â†’ F) :=
begin
  have sâ‚ : ğ’ âˆ (Î» p : â„ Ã— E, (smooth_step $ 2*p.1, p.2)),
  { change ğ’ âˆ ((prod.map smooth_step id) âˆ˜ (Î» p : â„ Ã— E, (2*p.1, p.2))),
    apply (smooth_step.smooth.prod_map cont_diff_id).comp,
    apply cont_diff.prod,
    apply cont_diff_const.mul cont_diff_fst,
    apply cont_diff_snd },
  replace hf := hf.comp sâ‚,
  have sâ‚‚ : ğ’ âˆ (Î» p : â„ Ã— E, (smooth_step $ 2*p.1 - 1, p.2)),
  { change ğ’ âˆ ((prod.map smooth_step id) âˆ˜ (Î» p : â„ Ã— E, (2*p.1 - 1, p.2))),
    apply (smooth_step.smooth.prod_map cont_diff_id).comp,
    apply cont_diff.prod,
    apply cont_diff.sub,
    apply cont_diff_const.mul cont_diff_fst,
    apply cont_diff_const,
    apply cont_diff_snd },
  replace hg := hg.comp sâ‚‚,
  rw cont_diff_iff_cont_diff_at at *,
  rintros âŸ¨tâ‚€ , xâ‚€âŸ©,
  rcases lt_trichotomy tâ‚€ (1/2) with ht|rfl|ht,
  { apply (hf (tâ‚€, xâ‚€)).congr_of_eventually_eq,
    have : (Iio (1/2) : set â„) Ã—Ë¢ univ âˆˆ ğ“ (tâ‚€, xâ‚€),
      from prod_mem_nhds_iff.mpr âŸ¨Iio_mem_nhds ht, univ_memâŸ©,
    filter_upwards [this] with p hp,
    cases p with t x,
    replace hp : t < 1/2 := (prod_mk_mem_set_prod_eq.mp hp).1,
    change ite (t â‰¤ 1 / 2) (f (smooth_step (2 * t)) x) (g (smooth_step (2 * t - 1)) x) = _,
    rw if_pos hp.le,
    refl },
  { apply (hf (1/2, xâ‚€)).congr_of_eventually_eq,
    have : (Ioo (3/8) (5/8) : set â„) Ã—Ë¢ univ âˆˆ ğ“ (1/(2 : â„), xâ‚€),
    { refine prod_mem_nhds_iff.mpr âŸ¨Ioo_mem_nhds _ _, univ_memâŸ© ; norm_num },
    filter_upwards [this] with p hp,
    cases p with t x,
    cases (prod_mk_mem_set_prod_eq.mp hp).1 with lt_t t_lt,
    change ite (t â‰¤ 1 / 2) (f (smooth_step (2 * t)) x) (g (smooth_step (2 * t - 1)) x) = _,
    split_ifs,
    { refl },
    { change g _ x = f (smooth_step $ 2*t) x,
      apply congr_fun,
      rw [show smooth_step (2 * t - 1) = 0, by { apply smooth_step.of_lt, linarith },
          show smooth_step (2 * t) = 1, by { apply smooth_step.of_gt, linarith }, hfg] }, },
  { apply (hg (tâ‚€, xâ‚€)).congr_of_eventually_eq,
    have : (Ioi (1/2) : set â„) Ã—Ë¢ univ âˆˆ ğ“ (tâ‚€, xâ‚€),
      from prod_mem_nhds_iff.mpr âŸ¨Ioi_mem_nhds ht, univ_memâŸ©,
    filter_upwards [this] with p hp,
    cases p with t x,
    replace hp : Â¬ (t â‰¤ 1/2) := by push_neg ; exact (prod_mk_mem_set_prod_eq.mp hp).1,
    change ite (t â‰¤ 1 / 2) (f (smooth_step (2 * t)) x) (g (smooth_step (2 * t - 1)) x) = _,
    rw if_neg hp,
    refl }
end
/- begin
  have c3 : âˆ€ {n}, ğ’ n (Î» t : â„, 2 * t) :=
  Î» n, cont_diff_const.mul cont_diff_id,
  have c4 : âˆ€ {n}, ğ’ n â†¿(Î» t : â„, 2 * t - 1) :=
  Î» n, (cont_diff_const.mul cont_diff_id).sub cont_diff_const,
  have c5 : âˆ€ {n}, ğ’ n (Î» t, smooth_step $ 2 * t) :=
  Î» n, smooth_transition.cont_diff.comp c3,
  have c6 : âˆ€ {n}, ğ’ n â†¿(Î» t, smooth_step $ 2*t - 1) :=
  Î» n, smooth_transition.cont_diff.comp c4,
  have h1 : âˆ€ {n}, ğ’ n â†¿(Î» t, f (smooth_step $ 2*t)) :=
  Î» n, hf.compâ‚‚ c5.fst' cont_diff_snd,
  have h2 : âˆ€ {n}, ğ’ n â†¿(Î» t, g (smooth_step $ 2*t - 1)) :=
  Î» n, hg.compâ‚‚ c6.fst' cont_diff_snd,
  refine h1.if_le_of_fderiv h2 cont_diff_fst cont_diff_const _,
  rintro âŸ¨t, xâŸ© n ht,
  dsimp only at ht,
  subst ht,
  simp [has_uncurry.uncurry],
  cases n,
  { simp [iterated_fderiv_zero_eq_comp, hfg], },
  rw [iterated_fderiv_of_partial, iterated_fderiv_of_partial],
  { simp [has_uncurry.uncurry, hfg],
    congr' 1,
    refine (iterated_fderiv_comp (hf.compâ‚‚ cont_diff_id cont_diff_const) c5 _).trans _,
    convert continuous_multilinear_map.comp_zero _,
    { ext1 i, refine (iterated_fderiv_comp smooth_transition.cont_diff c3 _).trans _,
      convert continuous_multilinear_map.zero_comp _, simp },
    refine (iterated_fderiv_comp (hg.compâ‚‚ cont_diff_id cont_diff_const) c6 _).trans _,
    convert continuous_multilinear_map.comp_zero _,
    { ext1 i, refine (iterated_fderiv_comp smooth_transition.cont_diff c4 _).trans _,
      convert continuous_multilinear_map.zero_comp _, simp [has_uncurry.uncurry] } },
  { exact Î» x, h2.compâ‚‚ cont_diff_const cont_diff_id },
  { exact Î» y, h2.compâ‚‚ cont_diff_id cont_diff_const },
  { exact Î» x, h1.compâ‚‚ cont_diff_const cont_diff_id },
  { exact Î» y, h1.compâ‚‚ cont_diff_id cont_diff_const },
end -/

/-- Concatenation of homotopies of formal solution. The result depend on our choice of
a smooth step function in order to keep smoothness with respect to the time parameter. -/
def htpy_jet_sec.comp (ğ“• ğ“– : htpy_jet_sec E F) (h : ğ“• 1 = ğ“– 0) : htpy_jet_sec E F :=
{ f := Î» t x, if t â‰¤ 1/2 then ğ“•.f (smooth_step $ 2*t) x else ğ“–.f (smooth_step $ 2*t - 1) x,
  f_diff :=
  htpy_jet_sec_comp_aux ğ“•.f_diff ğ“–.f_diff (show (ğ“• 1).f = (ğ“– 0).f, by rw h),
  Ï† := Î» t x, if t â‰¤ 1/2 then ğ“•.Ï† (smooth_step $ 2*t) x else  ğ“–.Ï† (smooth_step $ 2*t - 1) x,
  Ï†_diff :=
  htpy_jet_sec_comp_aux ğ“•.Ï†_diff ğ“–.Ï†_diff (show (ğ“• 1).Ï† = (ğ“– 0).Ï†, by rw h) }

@[simp]
lemma htpy_jet_sec.comp_of_le (ğ“• ğ“– : htpy_jet_sec E F) (h) {t : â„} (ht : t â‰¤ 1/2) :
  ğ“•.comp ğ“– h t = ğ“• (smooth_step $ 2*t) :=
begin
  dsimp [htpy_jet_sec.comp],
  ext x,
  change (if t â‰¤ 1/2 then _ else  _) = _,
  rw if_pos ht,
  refl,
  ext1 x,
  change (if t â‰¤ 1 / 2 then _ else _) = (ğ“• _).Ï† x,
  rw if_pos ht,
  refl
end


@[simp]
lemma htpy_jet_sec.comp_0 (ğ“• ğ“– : htpy_jet_sec E F) (h) : ğ“•.comp ğ“– h 0 = ğ“• 0 :=
begin
  rw htpy_jet_sec.comp_of_le _ _ h (by norm_num : (0 : â„) â‰¤ 1/2),
  simp
end

@[simp]
lemma htpy_jet_sec.comp_of_not_le (ğ“• ğ“– : htpy_jet_sec E F) (h) {t : â„} (ht : Â¬ t â‰¤ 1/2) :
  ğ“•.comp ğ“– h t = ğ“– (smooth_step $ 2*t - 1) :=
begin
  dsimp [htpy_jet_sec.comp],
  ext x,
  change (if t â‰¤ 1/2 then _ else  _) = _,
  rw if_neg ht,
  refl,
  ext1 x,
  change (if t â‰¤ 1 / 2 then _ else _) = (ğ“– _).Ï† x,
  rw if_neg ht,
  refl
end

@[simp]
lemma htpy_jet_sec.comp_1 (ğ“• ğ“– : htpy_jet_sec E F) (h) : ğ“•.comp ğ“– h 1 = ğ“– 1 :=
begin
  rw htpy_jet_sec.comp_of_not_le _ _ h (by norm_num : Â¬ (1 : â„) â‰¤ 1/2),
  norm_num
end

end htpy_jet_sec

/-- A homotopy of formal solutions is a 1-parameter family of formal solutions. -/
@[ext] structure rel_loc.htpy_formal_sol (R : rel_loc E F) extends htpy_jet_sec E F :=
(is_sol : âˆ€ t x, (x, f t x, Ï† t x) âˆˆ R)

open rel_loc

instance (R : rel_loc E F) : has_coe_to_fun (htpy_formal_sol R) (Î» S, â„ â†’ jet_sec E F) :=
âŸ¨Î» S t,
 { f := S.f t,
   f_diff := S.f_diff.comp (cont_diff_const.prod cont_diff_id),
   Ï† := S.Ï† t,
   Ï†_diff := S.Ï†_diff.comp (cont_diff_const.prod cont_diff_id) }âŸ©
