import analysis.calculus.specific_functions
import topology.metric_space.hausdorff_distance

import to_mathlib.topology.misc
import to_mathlib.topology.nhds_set
import to_mathlib.topology.hausdorff_distance
import to_mathlib.linear_algebra.basic

import local.one_jet

/-!
# Local partial differential relations and their formal solutions

This file defines `rel_loc E F`, the type of first order partial differential relations
for maps between two real normed spaces `E` and `F`.

To any `R : rel_loc E F` we associate the type `sol R` of maps `f : E â†’ F` of
solutions of `R`, and its formal counterpart `formal_sol R`.

The h-principle question is whether we can deform any formal solution into a solution.
The type of deformations is `htpy_jet_sec E F` (homotopies of 1-jet sections).
-/

noncomputable theory

open set function module (dual) real filter
open_locale unit_interval topological_space

variables (E : Type*) [normed_add_comm_group E] [normed_space â„ E]
variables (F : Type*) [normed_add_comm_group F] [normed_space â„ F]
variables (P : Type*) [normed_add_comm_group P] [normed_space â„ P]

/-- A first order relation for maps between real vector spaces. -/
def rel_loc := set (one_jet E F)

instance : has_mem (E Ã— F Ã— (E â†’L[â„] F)) (rel_loc E F) := set.has_mem


variables {E F}

/-- A predicate stating that a 1-jet section is a formal solution to a first order relation for
maps between vector spaces. -/
def jet_sec.is_formal_sol (ğ“• : jet_sec E F) (R : rel_loc E F) : Prop :=
âˆ€ x, (x, ğ“•.f x, ğ“•.Ï† x) âˆˆ R

namespace rel_loc

/-- A solution to a local relation `R`. -/
@[ext] structure sol (R : rel_loc E F) :=
(f : E â†’ F)
(f_diff : ğ’ âˆ f)
(is_sol : âˆ€ x, (x, f x, D f x) âˆˆ R)

/-- A formal solution to a local relation `R`. -/
@[ext] structure formal_sol (R : rel_loc E F) extends jet_sec E F :=
(is_sol : âˆ€ x, (x, f x, Ï† x) âˆˆ R)

instance (R : rel_loc E F) : has_coe (formal_sol R) (jet_sec E F):=
âŸ¨formal_sol.to_jet_secâŸ©

@[simp] lemma formal_sol.to_jet_sec_eq_coe {R : rel_loc E F} (ğ“• : formal_sol R) :
ğ“•.to_jet_sec = (ğ“• : jet_sec E F) := rfl

@[simp] lemma formal_sol.coe_is_formal_sol  {R : rel_loc E F} (ğ“• : formal_sol R) :
  (ğ“• : jet_sec E F).is_formal_sol R := ğ“•.is_sol

/-- Bundling a formal solution from a 1-jet section that is a formal solution. -/
def _root_.jet_sec.is_formal_sol.formal_sol  {ğ“• : jet_sec E F} {R : rel_loc E F}
  (h : ğ“•.is_formal_sol R) : formal_sol R :=
{is_sol := h, ..ğ“•}

/-- Inclusion of solutions into formal solutions. -/
def sol.to_formal_sol {R : rel_loc E F}  (ğ“• : sol R) : formal_sol R :=
{ f := ğ“•.f,
  f_diff := ğ“•.f_diff,
  Ï† := D ğ“•.f,
  Ï†_diff := (cont_diff_top_iff_fderiv.mp ğ“•.f_diff).2,
  is_sol := ğ“•.is_sol }

instance (R : rel_loc E F) : has_coe_to_fun (formal_sol R) (Î» S, E â†’ F Ã— (E â†’L[â„] F)) :=
âŸ¨Î» ğ“•, Î» x, (ğ“•.f x, ğ“•.Ï† x)âŸ©

@[simp] lemma formal_sol.coe_apply  {R : rel_loc E F} (ğ“• : formal_sol R) (x : E) :
(ğ“• : jet_sec E F) x = ğ“• x := rfl

variables  {R : rel_loc E F}

lemma formal_sol.eq_iff {ğ“• ğ“•' : formal_sol R} {x : E} :
  ğ“• x = ğ“•' x â†” ğ“•.f x = ğ“•'.f x âˆ§ ğ“•.Ï† x = ğ“•'.Ï† x :=
jet_sec.eq_iff

/-- A formal solution (f, Ï†) is holonomic at `x` if the differential of `f` at `x` is `Ï† x`. -/
def formal_sol.is_holonomic_at (ğ“• : formal_sol R) (x : E) : Prop := D ğ“•.f x = ğ“•.Ï† x

-- TODO: this should come from a lemma about `jet_sec`
lemma formal_sol.is_holonomic_at_congr (ğ“• ğ“•' : formal_sol R) {s : set E}
  (h : âˆ€á¶  x near s, ğ“• x = ğ“•' x) : âˆ€á¶  x near s, ğ“•.is_holonomic_at x â†” ğ“•'.is_holonomic_at x :=
begin
  apply h.eventually_nhds_set.mono,
  intros x hx,
  have hf : ğ“•.f =á¶ [ğ“ x] ğ“•'.f,
  { apply hx.mono,
    simp_rw rel_loc.formal_sol.eq_iff,
    tauto },
  unfold rel_loc.formal_sol.is_holonomic_at,
  rw [hf.fderiv_eq, (rel_loc.formal_sol.eq_iff.mp hx.self_of_nhds).2]
end

lemma sol.is_holonomic {R : rel_loc E F} (ğ“• : sol R) (x : E) :
  ğ“•.to_formal_sol.is_holonomic_at x :=
by simp [rel_loc.sol.to_formal_sol, rel_loc.formal_sol.is_holonomic_at]

/-- A formal solution of `R` that is holonomic comes from a genuine solution. -/
def formal_sol.to_sol (ğ“• : formal_sol R) (h : âˆ€ x, ğ“•.to_jet_sec.is_holonomic_at x) : sol R :=
{ f := ğ“•.f,
  f_diff := ğ“•.f_diff,
  is_sol := Î» x, ((h x).symm â–¸ (ğ“•.is_sol x)) }

lemma to_sol_to_formal_sol (ğ“• : sol R) :
  ğ“•.to_formal_sol.to_sol (Î» x, ğ“•.is_holonomic x) = ğ“• :=
by { ext x, refl }

/-- A formal solution (f, Ï†) is partially holonomic along a subspace `E'` at `x` if the
differential of `f` at `x` coincides with `Ï† x` on `E'`. -/
def formal_sol.is_part_holonomic_at (ğ“• : formal_sol R) (E' : submodule â„ E) (x : E) :=
âˆ€ v âˆˆ E', D ğ“•.f x v = ğ“•.Ï† x v

lemma formal_sol.is_part_holonomic_at.mono {ğ“• : formal_sol R}
  {E' E'' : submodule â„ E} {x : E} (h : ğ“•.is_part_holonomic_at E' x) (h' : E'' â‰¤ E') :
  ğ“•.is_part_holonomic_at E'' x :=
Î» v v_in, h v $ set_like.coe_subset_coe.mpr h' v_in

variable (P)
/-- A family of formal solutions is a 1-parameter family of formal solutions. -/
@[ext] structure family_formal_sol (R : rel_loc E F) extends family_jet_sec E F P :=
(is_sol : âˆ€ t x, (x, f t x, Ï† t x) âˆˆ R)

/-- A homotopy of formal solutions is a 1-parameter family of formal solutions. -/
@[reducible] def htpy_formal_sol (R : rel_loc E F) := R.family_formal_sol â„

def htpy_formal_sol.to_htpy_jet_sec {R : rel_loc E F} (ğ“• : R.htpy_formal_sol) : htpy_jet_sec E F :=
ğ“•.to_family_jet_sec

open rel_loc

instance (R : rel_loc E F) : has_coe_to_fun (family_formal_sol P R) (Î» S, P â†’ jet_sec E F) :=
âŸ¨Î» S t,
 { f := S.f t,
   f_diff := S.f_diff.comp (cont_diff_const.prod cont_diff_id),
   Ï† := S.Ï† t,
   Ï†_diff := S.Ï†_diff.comp (cont_diff_const.prod cont_diff_id) }âŸ©

end rel_loc
