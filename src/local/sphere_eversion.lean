import to_mathlib.analysis.inner_product_space.rotation
import to_mathlib.analysis.inner_product_space.dual
import local.parametric_h_principle

/-!
This is file proves the existence of a sphere eversion from the local verson of the h-principle.

We define the relation of immersions `R = immersion_sphere_rel âŠ† JÂ¹(E, F)` which consist of all
`(x, y, Ï•)` such that if `x` is outside a ball around the origin with chosen radius `R < 1` then
`Ï•` must be injective on `(â„ âˆ™ x)á—®` (the orthogonal complement of the span of `x`).
We show that `R` is open and ample.

Furthermore, we define a formal solution of sphere eversion that is holonomic near `0` and `1`.
We have to be careful since we're not actually working on the sphere,
but in the ambient space `E â‰ƒ â„Â³`.
See `loc_formal_eversion` for the choice and constaints of the solution.

Finally, we obtain the existence of sphere eversion from the parametric local h-principle,
proven in `local.parametric_h_principle`.
-/
noncomputable theory

open metric finite_dimensional set function rel_loc filter (hiding mem_map) inner_product_space
  submodule linear_map (ker)
open_locale topology real_inner_product_space

section sphere_eversion

variables
{E : Type*} [normed_add_comm_group E] [inner_product_space â„ E]
{F : Type*} [normed_add_comm_group F] [inner_product_space â„ F]

local notation `ð•ŠÂ²` := sphere (0 : E) 1
local notation `dim` := finrank â„
local notation `pr[`x`]á—®` := orthogonal_projection (â„ âˆ™ x)á—®
local notation (name := dot_print_only) R ` âˆ™ `:1000 x := submodule.span R {x}
local notation (name := dot_local) R ` âˆ™ `:1000 x :=
  submodule.span R (@singleton _ _ set.has_singleton x)
local notation `B` := ball (0 : E) 0.9

/-- A map between vector spaces is a immersion viewed as a map on the sphere, when its
derivative at `x âˆˆ ð•ŠÂ²` is injective on the orthogonal complement of `x`
(the tangent space to the sphere). Note that this implies `f` is differentiable at every point
`x âˆˆ ð•ŠÂ²` since otherwise `D f x = 0`.
-/
def sphere_immersion (f : E â†’ F) : Prop :=
âˆ€ x âˆˆ ð•ŠÂ², inj_on (D f x) (â„ âˆ™ x)á—®

variables (E F)

/-- The relation of immersionsof a two-sphere into its ambient Euclidean space. -/
def immersion_sphere_rel : rel_loc E F :=
{w : one_jet E F | w.1 âˆ‰ B â†’ inj_on w.2.2 (â„ âˆ™ w.1)á—® }

local notation `R` := immersion_sphere_rel E F

variables {E F}

@[simp] lemma mem_loc_immersion_rel {x y Ï†} :
  (âŸ¨x, y, Ï†âŸ© : one_jet E F) âˆˆ immersion_sphere_rel E F â†” x âˆ‰ B â†’ inj_on Ï† (â„ âˆ™ x)á—® :=
iff.rfl

lemma sphere_immersion_of_sol (f : E â†’ F) :
  (âˆ€ x âˆˆ ð•ŠÂ², (x, f x, fderiv â„ f x) âˆˆ immersion_sphere_rel E F) â†’ sphere_immersion f :=
begin
  intros h x x_in,
  have : x âˆ‰ B,
  { rw mem_sphere_zero_iff_norm at x_in,
    norm_num [x_in] },
  exact h x x_in this
end

lemma mem_slice_iff_of_not_mem {x : E} {w : F} {Ï† : E â†’L[â„] F} {p : dual_pair E}
  (hx : x âˆ‰ B) (y : F) : w âˆˆ slice R p (x, y, Ï†) â†” inj_on (p.update Ï† w) (â„ âˆ™ x)á—® :=
begin
  change (x âˆ‰ B â†’ inj_on (p.update Ï† w) (â„ âˆ™ x)á—®) â†” inj_on (p.update Ï† w) (â„ âˆ™ x)á—®,
  simp_rw [eq_true_intro hx, true_implies_iff]
end

section assume_finite_dimensional

variables [finite_dimensional â„ E]

-- The following is extracted from `loc_immersion_rel_open` because it is slow to typecheck
lemma loc_immersion_rel_open_aux {xâ‚€ : E} {yâ‚€ : F} {Ï†â‚€ : E â†’L[â„] F} (hxâ‚€ : xâ‚€ âˆ‰ B)
  (H : inj_on Ï†â‚€ (â„ âˆ™ xâ‚€)á—®) :
  âˆ€á¶  (p : one_jet E F) in ð“ (xâ‚€, yâ‚€, Ï†â‚€), âŸªxâ‚€, p.1âŸ« â‰  0 âˆ§
  injective ((p.2.2.comp $ (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp (â„ âˆ™ xâ‚€)á—®.subtypeL) :=
begin
  -- This is true at (xâ‚€, yâ‚€, Ï†â‚€) and is an open condition because `p â†¦ âŸªxâ‚€, p.1âŸ«` and
  -- `p â†¦ (p.2.2.comp $ (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€` are continuous
  set jâ‚€ := subtypeL (â„ âˆ™ xâ‚€)á—®,
  let f : one_jet E F â†’ â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) :=
      Î» p, (âŸªxâ‚€, p.1âŸ«, (p.2.2.comp $ (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€),
  let P : â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) â†’ Prop :=
      Î» q, q.1 â‰  0 âˆ§ injective q.2,
  have xâ‚€_ne : xâ‚€ â‰  0,
  { refine Î» hxâ‚€', hxâ‚€ _,
    rw hxâ‚€',
    apply mem_ball_self,
    norm_num },
  -- The following suffices looks stupid but is much faster than using the change tactic.
  suffices : âˆ€á¶  (p : one_jet E F) in ð“ (xâ‚€, yâ‚€, Ï†â‚€), P (f p), { exact this },
  apply continuous_at.eventually,
  { refine (continuous_at_const.inner continuous_at_fst).prod _,
    apply continuous_at.compL,
    { apply continuous_at.compL,
      exact continuous_at_snd.comp continuous_at_snd,
      -- Faster than change.
      suffices : continuous_at ((Î» x, (â„ âˆ™ x)á—®.subtypeL.comp pr[x]á—®) âˆ˜ prod.fst) (xâ‚€, yâ‚€, Ï†â‚€),
      { exact this },
      apply continuous_at.comp _ continuous_at_fst,
      exact continuous_at_orthogonal_projection_orthogonal xâ‚€_ne },
    exact continuous_at_const },
  { exact (continuous_fst.is_open_preimage _ is_open_compl_singleton).inter
          (continuous_snd.is_open_preimage _ continuous_linear_map.is_open_injective) },
  { split,
    { change âŸªxâ‚€, xâ‚€âŸ« â‰  0,
      apply (inner_self_eq_zero.not).mpr xâ‚€_ne },
    { change injective (Ï†â‚€ âˆ˜ (coe âˆ˜ (pr[xâ‚€]á—® âˆ˜ coe))),
      rw [orthogonal_projection_comp_coe, comp.right_id],
      exact inj_on_iff_injective.mp H } }
end

lemma loc_immersion_rel_open : is_open (immersion_sphere_rel E F) :=
begin
  dsimp only [immersion_sphere_rel],
  rw is_open_iff_mem_nhds,
  rintros âŸ¨xâ‚€, yâ‚€, Ï†â‚€âŸ© (H : xâ‚€ âˆ‰ B â†’ inj_on Ï†â‚€ (â„ âˆ™ xâ‚€)á—®),
  change âˆ€á¶  (p : one_jet E F) in ð“ (xâ‚€, yâ‚€, Ï†â‚€), _,
  by_cases hxâ‚€ : xâ‚€ âˆˆ B,
  { have : âˆ€á¶  (p : one_jet E F) in ð“ (xâ‚€, yâ‚€, Ï†â‚€), p.1 âˆˆ B,
    { rw nhds_prod_eq,
      apply (is_open_ball.eventually_mem hxâ‚€).prod_inl },
    apply this.mono,
    rintros âŸ¨x, y, Ï†âŸ© (hx : x âˆˆ B) (Hx : x âˆ‰ B),
    exact (Hx hx).elim },
  { replace H := H hxâ‚€,
    set jâ‚€ := subtypeL (â„ âˆ™ xâ‚€)á—®,
    let f : one_jet E F â†’ â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) :=
      Î» p, (âŸªxâ‚€, p.1âŸ«, (p.2.2.comp $ (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€),
    let P : â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) â†’ Prop :=
      Î» q, q.1 â‰  0 âˆ§ injective q.2,
    have : âˆ€á¶  (p : one_jet E F) in ð“ (xâ‚€, yâ‚€, Ï†â‚€), P (f p),
    { exact loc_immersion_rel_open_aux hxâ‚€ H },
    apply this.mono, clear this,
    rintros âŸ¨x, y, Ï†âŸ© âŸ¨hxxâ‚€ : âŸªxâ‚€, xâŸ« â‰  0, HÏ†âŸ© (hx : x âˆ‰ B),
    dsimp only [P, f] at HÏ†,
    change inj_on Ï† (â„ âˆ™ x)á—®,
    have : range ((subtypeL (â„ âˆ™ x)á—®) âˆ˜ pr[x]á—® âˆ˜ jâ‚€) = (â„ âˆ™ x)á—®,
    { rw function.surjective.range_comp,
      exact subtype.range_coe,
      exact (orthogonal_projection_orthogonal_line_iso hxxâ‚€).surjective },
    rw â† this, clear this,
    exact function.injective.inj_on_range HÏ† },
end

variables [finite_dimensional â„ F]

-- In the next lemma the assumption `dim E = n + 1` is for convenience
-- using `finrank_orthogonal_span_singleton`. We could remove it to treat empty spheres...
lemma loc_immersion_rel_ample (n : â„•) [fact (dim E = n+1)] (h : finrank â„ E â‰¤ finrank â„ F) :
  (immersion_sphere_rel E F).is_ample :=
begin
  classical, -- gives a minor speedup
  rw is_ample_iff,
  rintro âŸ¨x, y, Ï†âŸ© p h_mem,
  by_cases hx : x âˆˆ B,
  { apply ample_slice_of_forall,
    intros w,
    simp only [hx, mem_loc_immersion_rel, not_true, is_empty.forall_iff] },
  have x_ne : x â‰  0,
  { rintro rfl,
    apply hx,
    apply mem_ball_self,
    norm_num1 },
  have hÏ† : inj_on Ï† (â„ âˆ™ x)á—® := h_mem hx, clear h_mem,
  let u : E := (inner_product_space.to_dual â„ E).symm p.Ï€,
  have u_ne : u â‰  0,
  { exact (inner_product_space.to_dual â„ E).symm.apply_ne_zero p.pi_ne_zero },
  by_cases H : ker p.Ï€ = (â„ âˆ™ x)á—®,
  { have key : âˆ€ w, eq_on (p.update Ï† w) Ï† (â„ âˆ™ x)á—®,
    { intros w x,
      rw â† H,
      exact p.update_ker_pi Ï† w },
    exact ample_slice_of_forall _ p (Î» w _, hÏ†.congr (key w).symm) },
  obtain âŸ¨v', v'_in, hv', hÏ€v'âŸ© :
    âˆƒ v' : E, v' âˆˆ (â„ âˆ™ x)á—® âˆ§ (â„ âˆ™ x)á—® = (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®) âŠ” (â„ âˆ™ v') âˆ§ p.Ï€ v' = 1,
  { have ne_z : p.Ï€ (pr[x]á—® u) â‰  0,
    { rw â† to_dual_symm_apply,
      change Â¬ âŸªu, pr[x]á—® uâŸ« = 0,
      rw inner_projection_self_eq_zero_iff.not,
      contrapose! H,
      rw orthogonal_orthogonal at H,
      rw [â† orthogonal_span_to_dual_symm, span_singleton_eq_span_singleton_of_ne u_ne H],
      apply_instance },
    have ne_z' : (p.Ï€ $ pr[x]á—® u)â»Â¹ â‰  0,
    { exact inv_ne_zero ne_z },
    refine âŸ¨(p.Ï€ $ pr[x]á—® u)â»Â¹ â€¢ pr[x]á—® u, (â„ âˆ™ x)á—®.smul_mem _ (pr[x]á—® u).2, _, _âŸ©,
    { have := orthogonal_line_inf_sup_line u x,
      rw [â† orthogonal_span_to_dual_symm p.Ï€,
        span_singleton_smul_eq ne_z'.is_unit],
      exact (orthogonal_line_inf_sup_line u x).symm },
    rw [p.Ï€.map_smul, smul_eq_mul, inv_mul_cancel ne_z] },
  let p' : dual_pair E := { Ï€ := p.Ï€, v := v', pairing := hÏ€v' },
  apply ample_slice_of_ample_slice (show p'.Ï€ = p.Ï€, from rfl),
  suffices : slice R p' (x, y, Ï†) = (map Ï† (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®))á¶œ,
  { rw [this],
    apply ample_of_two_le_codim,
    let Î¦ := Ï†.to_linear_map,
    suffices : 2 â‰¤ dim (F â§¸ map Î¦ (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®)),
    { rw â† finrank_eq_rank,
      exact_mod_cast this },
    apply le_of_add_le_add_right,
    rw submodule.finrank_quotient_add_finrank (map Î¦ $ ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®),
    have : dim (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® : submodule â„ E) + 1 = n,
    { have eq := submodule.finrank_sup_add_finrank_inf_eq (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®) (span â„ {v'}),
      have eqâ‚ : dim (â„ âˆ™ x)á—® = n, from finrank_orthogonal_span_singleton x_ne,
      have eqâ‚‚ : ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® âŠ“ span â„ {v'} = (âŠ¥ : submodule â„ E),
      { erw [inf_left_right_swap, inf_comm, â† inf_assoc, p'.inf_eq_bot, bot_inf_eq] },
      have eqâ‚ƒ : dim (span â„ {v'}) = 1, apply finrank_span_singleton p'.v_ne_zero,
      rw [â† hv', eqâ‚, eqâ‚ƒ, eqâ‚‚] at eq,
      simpa only [finrank_bot] using eq.symm },
    have : dim E = n+1, from fact.out _,
    linarith [finrank_map_le Î¦ (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®)] },
  ext w,
  rw mem_slice_iff_of_not_mem hx y,
  rw inj_on_iff_injective,
  let j := (â„ âˆ™ x)á—®.subtypeL,
  let p'' : dual_pair (â„ âˆ™ x)á—® := âŸ¨p.Ï€.comp j, âŸ¨v', v'_inâŸ©, hÏ€v'âŸ©,
  have eq : ((â„ âˆ™ x)á—® : set E).restrict (p'.update Ï† w) = (p''.update (Ï†.comp j) w),
  { ext z,
    simp only [dual_pair.update, restrict_apply, continuous_linear_map.add_apply,
      continuous_linear_map.coe_comp', coe_subtypeL', submodule.coe_subtype, comp_app, coe_mk] },
  have eq' : map (Ï†.comp j) (ker p''.Ï€) = map Ï† (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®),
  { have : map â†‘j (ker p''.Ï€) = ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®,
    { ext z,
      simp only [mem_map, linear_map.mem_ker, continuous_linear_map.coe_comp',
                coe_subtypeL', submodule.coe_subtype, comp_app, mem_inf],
      split,
      { rintros âŸ¨t, ht, rflâŸ©,
        rw [continuous_linear_map.coe_coe, subtypeL_apply],
        exact âŸ¨ht, t.2âŸ© },
      { rintros âŸ¨hz, z_inâŸ©,
        exact âŸ¨âŸ¨z, z_inâŸ©, hz, rflâŸ© }, },
    erw [â† this, map_comp],
    refl },
  rw [eq, p''.injective_update_iff, mem_compl_iff, eq'],
  exact iff.rfl,
  rw â† show ((â„ âˆ™ x)á—® : set E).restrict Ï† = Ï†.comp j, by { ext, refl },
  exact hÏ†.injective
end

end assume_finite_dimensional

/-- The main ingredient of the linear map in the formal eversion of the sphere. -/
def loc_formal_eversion_aux_Ï† [fact (dim E = 3)] (Ï‰ : orientation â„ E (fin 3))
  (t : â„) (x : E) : E â†’L[â„] E :=
Ï‰.rot (t, x) - (2 * t) â€¢ (submodule.subtypeL (â„ âˆ™ x) âˆ˜L orthogonal_projection (â„ âˆ™ x))

section assume_finite_dimensional

variables [fact (dim E = 3)] [finite_dimensional â„ E] (Ï‰ : orientation â„ E (fin 3))

lemma smooth_at_loc_formal_eversion_aux_Ï† {p : â„ Ã— E} (hx : p.2 â‰  0) :
  cont_diff_at â„ âˆž (uncurry (loc_formal_eversion_aux_Ï† Ï‰)) p :=
begin
  refine (Ï‰.cont_diff_rot hx).sub _,
  refine cont_diff_at.smul (cont_diff_at_const.mul cont_diff_at_fst) _,
  exact (cont_diff_at_orthogonal_projection_singleton hx).comp p cont_diff_at_snd
end

/-- A formal eversion of `ð•ŠÂ²`, viewed as a homotopy. -/
def loc_formal_eversion_aux : htpy_jet_sec E E :=
{ f := Î» (t : â„) (x : E), (1 - 2 * smooth_step t) â€¢ x,
  Ï† := Î» t x, smooth_step (â€–xâ€– ^ 2) â€¢ loc_formal_eversion_aux_Ï† Ï‰ (smooth_step t) x,
  f_diff := cont_diff.smul (cont_diff_const.sub $ cont_diff_const.mul $
    smooth_step.smooth.comp cont_diff_fst) cont_diff_snd,
  Ï†_diff := begin
    refine cont_diff_iff_cont_diff_at.mpr (Î» x, _),
    cases eq_or_ne x.2 0 with hx hx,
    { refine cont_diff_at_const.congr_of_eventually_eq _, exact 0,
      have : ((Î» x, â€–xâ€– ^ 2) â»Â¹' Iio (1/4)) âˆˆ ð“ (0 : E),
      { refine is_open.mem_nhds _ _,
        exact (is_open_Iio.preimage (cont_diff_norm_sq â„ : ð’ž âˆž _).continuous),
        simp_rw [mem_preimage, norm_zero, zero_pow two_pos, mem_Iio],
        norm_num },
      have : ((Î» x, smooth_step (â€–xâ€– ^ 2)) â»Â¹' {0}) âˆˆ ð“ (0 : E),
      { refine mem_of_superset this _,
        rw @preimage_comp _ _ _ _ smooth_step,
        refine preimage_mono _,
        intros x hx,
        rw [mem_preimage, mem_singleton_iff, smooth_step.of_lt hx] },
      have : ((Î» p : â„ Ã— E, smooth_step (â€–p.2â€– ^ 2)) â»Â¹' {0}) âˆˆ ð“ x,
      { rw [â† hx] at this, exact continuous_at_snd.preimage_mem_nhds this },
      refine eventually_of_mem this _,
      rintro âŸ¨t, xâŸ© hx,
      simp_rw [mem_preimage, mem_singleton_iff] at hx,
      show smooth_step (â€–xâ€– ^ 2) â€¢ loc_formal_eversion_aux_Ï† Ï‰ (smooth_step t) x = 0,
      simp_rw [hx, zero_smul] },
    refine cont_diff_at.smul _ _,
    refine (smooth_step.smooth.comp $ (cont_diff_norm_sq â„).comp cont_diff_snd).cont_diff_at,
    exact (smooth_at_loc_formal_eversion_aux_Ï† Ï‰
      (show (prod.map smooth_step id x).2 â‰  0, from hx)).comp x
      (smooth_step.smooth.prod_map cont_diff_id).cont_diff_at,
     end }

/-- A formal eversion of `ð•ŠÂ²` into its ambient Euclidean space.
The corresponding map `E â†’ E` is roughly a linear homotopy from `id` at `t = 0` to `- id` at
`t = 1`. The continuous linear maps are roughly rotations with angle `t * Ï€`. However, we have to
keep track of a few complications:
* We need the formal solution to be holonomic near `0` and `1`.
  Therefore, we compose the above maps with a smooth step function that is constant `0` near `t = 0`
  and constant `1` near `t = 1`.
* We need to modify the derivative of `Ï‰.rot` to also have the right behavior on `(â„ âˆ™ x)`
  at `t = 1` (it is the identity, but it should be `-id`). Therefore, we subtract
  `(2 * t) â€¢ (submodule.subtypeL (â„ âˆ™ x) âˆ˜L orthogonal_projection (â„ âˆ™ x))`,
  which is `2t` times the identity on `(â„ âˆ™ x)`.
* We have to make sure the family of continuous linear map is smooth at `x = 0`. Therefore, we
  multiply the family with a factor of `smooth_step (â€–xâ€– ^ 2)`.
-/
def loc_formal_eversion : htpy_formal_sol (immersion_sphere_rel E E) :=
{ is_sol := begin
    intros t x,
    change x âˆ‰ B â†’
      inj_on (smooth_step (â€–xâ€– ^ 2) â€¢ loc_formal_eversion_aux_Ï† Ï‰ (smooth_step t) x) (â„ âˆ™ x)á—®,
    intros hx,
    have h2x : smooth_step (â€–xâ€– ^ 2) = 1,
    { refine smooth_step.of_gt _,
      rw [mem_ball, not_lt, dist_zero_right] at hx,
      refine (show (3 : â„)/4 < 0.9 ^ 2, by norm_num).trans_le _,
      rwa [sq_le_sq, show |(0.9 : â„)| = 0.9, by norm_num, abs_norm] },
    rw [h2x, one_smul],
    have h3x : x â‰  0,
    { rintro rfl, apply hx, exact mem_ball_self (by norm_num) },
    refine (eq_on.inj_on_iff _).mpr (Ï‰.inj_on_rot_of_ne (smooth_step t) h3x),
    intros v hv,
    simp_rw [loc_formal_eversion_aux_Ï†, continuous_linear_map.sub_apply,
      continuous_linear_map.smul_apply, continuous_linear_map.comp_apply,
      orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hv,
      _root_.map_zero, smul_zero, sub_zero],
  end,
  .. loc_formal_eversion_aux Ï‰ }

@[simp]
lemma loc_formal_eversion_f (t : â„) :
  (loc_formal_eversion Ï‰ t).f = Î» x : E, ((1 : â„) - 2 * smooth_step t) â€¢ x :=
rfl

lemma loc_formal_eversion_Ï† (t : â„) (x : E) (v : E) :
  (loc_formal_eversion Ï‰ t).Ï† x v = smooth_step (â€–xâ€– ^ 2) â€¢
    (Ï‰.rot (smooth_step t, x) v -
    (2 * smooth_step t) â€¢ orthogonal_projection (â„ âˆ™ x) v) :=
rfl

lemma loc_formal_eversion_zero (x : E) : (loc_formal_eversion Ï‰ 0).f x = x :=
by simp

lemma loc_formal_eversion_one (x : E) : (loc_formal_eversion Ï‰ 1).f x = -x :=
by simp [show (1 : â„) - 2 = -1, by norm_num]

lemma loc_formal_eversion_hol_at_zero {t : â„} (ht : t < 1/4) {x : E}
  (hx : smooth_step (â€–xâ€– ^ 2) = 1) : (loc_formal_eversion Ï‰ t).is_holonomic_at x :=
by simp_rw [jet_sec.is_holonomic_at, loc_formal_eversion_f, continuous_linear_map.ext_iff,
    loc_formal_eversion_Ï†, smooth_step.of_lt ht, hx, Ï‰.rot_zero, mul_zero, zero_smul, sub_zero,
    show (has_smul.smul (1 : â„) : E â†’ E) = id, from funext (one_smul â„), fderiv_id, function.id_def,
    eq_self_iff_true, implies_true_iff]

lemma loc_formal_eversion_hol_at_one {t : â„} (ht : 3/4 < t) {x : E}
  (hx : smooth_step (â€–xâ€– ^ 2) = 1) : (loc_formal_eversion Ï‰ t).is_holonomic_at x :=
begin
  simp_rw [jet_sec.is_holonomic_at, loc_formal_eversion_f, continuous_linear_map.ext_iff,
    loc_formal_eversion_Ï†, smooth_step.of_gt ht, hx],
  intro v,
  simp_rw [mul_one, show (1 : â„) - 2 = -1, by norm_num,
    show (has_smul.smul (-1 : â„) : E â†’ E) = Î» x, - x, from funext (Î» v, by rw [neg_smul, one_smul]),
    fderiv_neg, fderiv_id', continuous_linear_map.neg_apply, continuous_linear_map.id_apply],
  obtain âŸ¨v', hv', v, hv, rflâŸ© := submodule.exists_sum_mem_mem_orthogonal (â„ âˆ™ x) v,
  simp_rw [continuous_linear_map.map_add, Ï‰.rot_one _ hv, Ï‰.rot_eq_of_mem_span (1, x) hv'],
  simp_rw [neg_add, submodule.coe_add, orthogonal_projection_eq_self_iff.mpr hv',
    orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hv, submodule.coe_zero,
    add_zero, two_smul, one_smul],
  abel
end

lemma loc_formal_eversion_hol :
  âˆ€á¶  (p : â„ Ã— E) near {0, 1} Ã—Ë¢ ð•ŠÂ², (loc_formal_eversion Ï‰ p.1).is_holonomic_at p.2 :=
begin
  have : (Iio (1/4 : â„) âˆª Ioi (3/4)) Ã—Ë¢ ((Î» x, â€–xâ€– ^ 2) â»Â¹' Ioi (3/4)) âˆˆ
    ð“Ë¢ (({0, 1} : set â„) Ã—Ë¢ ð•ŠÂ²),
  { refine (is_open.mem_nhds_set _).mpr _,
    exact (is_open_Iio.union is_open_Ioi).prod
      (is_open_Ioi.preimage (cont_diff_norm_sq â„ : ð’ž âˆž _).continuous),
    rintro âŸ¨s, xâŸ© âŸ¨hs, hxâŸ©,
    refine âŸ¨_, _âŸ©,
    simp_rw [mem_insert_iff, mem_singleton_iff] at hs,
    rcases hs with rfl|rfl,
    exact or.inl (show (0 : â„) < 1 / 4, by norm_num),
    exact or.inr (show (3 / 4 : â„) < 1, by norm_num),
    simp_rw [mem_sphere_zero_iff_norm] at hx,
    simp_rw [mem_preimage, hx, one_pow, mem_Ioi],
    norm_num },
  have : (Iio (1/4 : â„) âˆª Ioi (3/4)) Ã—Ë¢ ((Î» x, smooth_step (â€–xâ€– ^ 2)) â»Â¹' {1}) âˆˆ
    ð“Ë¢ (({0, 1} : set â„) Ã—Ë¢ ð•ŠÂ²),
  { refine mem_of_superset this (prod_mono subset.rfl _),
    rw @preimage_comp _ _ _ _ smooth_step,
    refine preimage_mono _,
    intros x hx,
    rw [mem_preimage, mem_singleton_iff, smooth_step.of_gt hx] },
  refine eventually_of_mem this _,
  rintro âŸ¨t, xâŸ© âŸ¨ht|ht, hxâŸ©,
  { exact loc_formal_eversion_hol_at_zero Ï‰ ht hx },
  { exact loc_formal_eversion_hol_at_one Ï‰ ht hx }
end

end assume_finite_dimensional

open_locale unit_interval

theorem sphere_eversion_of_loc [fact (dim E = 3)] :
  âˆƒ f : â„ â†’ E â†’ E,
  (ð’ž âˆž â†¿f) âˆ§
  (âˆ€ x âˆˆ ð•ŠÂ², f 0 x = x) âˆ§
  (âˆ€ x âˆˆ ð•ŠÂ², f 1 x = -x) âˆ§
  âˆ€ t âˆˆ I, sphere_immersion (f t) :=
begin
  classical,
  borelize E,
  have rankE := fact.out (dim E = 3),
  haveI : finite_dimensional â„ E := finite_dimensional_of_finrank_eq_succ rankE,
  let Ï‰ : orientation â„ E (fin 3) :=
    ((std_orthonormal_basis _ _).reindex $ fin_congr (fact.out _ : dim E = 3)).to_basis.orientation,
  have is_closed_pair : is_closed ({0, 1} : set â„) :=
  (by simp : ({0, 1} : set â„).finite).is_closed,
  obtain âŸ¨f, hâ‚, hâ‚‚, hâ‚ƒâŸ© :=
    (loc_formal_eversion Ï‰).exists_sol loc_immersion_rel_open (loc_immersion_rel_ample 2 le_rfl)
    ({0, 1} Ã—Ë¢ ð•ŠÂ²) (is_closed_pair.prod is_closed_sphere) ð•ŠÂ² (is_compact_sphere 0 1)
    (loc_formal_eversion_hol Ï‰),
  refine âŸ¨f, hâ‚, _, _, _âŸ©,
  { intros x hx, rw [hâ‚‚ (0, x) (mk_mem_prod (by simp) hx), loc_formal_eversion_zero] },
  { intros x hx, rw [hâ‚‚ (1, x) (mk_mem_prod (by simp) hx), loc_formal_eversion_one] },
  { exact Î» t ht, sphere_immersion_of_sol _ (Î» x hx, hâ‚ƒ x hx t ht) },
end

/- Stating the full statement with all type-class arguments and no uncommon notation. -/
example (E : Type*) [normed_add_comm_group E] [inner_product_space â„ E] [fact (finrank â„ E = 3)] :
  âˆƒ f : â„ â†’ E â†’ E,
  (cont_diff â„ âŠ¤ (uncurry f)) âˆ§
  (âˆ€ x âˆˆ sphere (0 : E) 1, f 0 x = x) âˆ§
  (âˆ€ x âˆˆ sphere (0 : E) 1, f 1 x = -x) âˆ§
  âˆ€ t âˆˆ unit_interval, sphere_immersion (f t) :=
sphere_eversion_of_loc

end sphere_eversion
