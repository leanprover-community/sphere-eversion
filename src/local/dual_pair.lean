import analysis.calculus.cont_diff
import linear_algebra.dual

import notations
import to_mathlib.analysis.normed_space.operator_norm
import to_mathlib.analysis.calculus
import to_mathlib.linear_algebra.basic



noncomputable theory

open function continuous_linear_map

section no_norm
variables (E : Type*) {E' F G : Type*}
variables [add_comm_group E] [module â„ E] [topological_space E]
variables [add_comm_group E'] [module â„ E'] [topological_space E']
variables [normed_add_comm_group F] [normed_space â„ F] [normed_add_comm_group G] [normed_space â„ G]

-- TODO: move mathlib's dual_pair out of the root namespace!

/-- A continuous linear form `Ï€` and a vector `v` that pair to one. In particular `ker Ï€` is a
hyperplane and `v` spans a complement of this hyperplane. -/
structure dual_pair' :=
(Ï€ : E â†’L[â„] â„)
(v : E)
(pairing : Ï€ v = 1)

namespace dual_pair'
variables {E F}

local attribute [simp] continuous_linear_map.to_span_singleton_apply

lemma ker_pi_ne_top (p : dual_pair' E) : p.Ï€.ker â‰  âŠ¤ :=
begin
  intro H,
  have : (p.Ï€ : E â†’â‚—[â„] â„) p.v = 1 := p.pairing,
  simpa [linear_map.ker_eq_top.mp H]
end

/-- Given a dual pair `p`, `p.span_v` is the line spanned by `p.v`. -/
def span_v (p : dual_pair' E) : submodule â„ E := submodule.span â„ {p.v}

lemma mem_span_v (p : dual_pair' E) {u : E} : u âˆˆ p.span_v â†” âˆƒ t : â„, u = t â€¢ p.v :=
by simp [dual_pair'.span_v, submodule.mem_span_singleton, eq_comm]

/-- Update a continuous linear map `Ï† : E â†’L[â„] F` using a dual pair `p` on `E` and a
vector `w : F`. The new map coincides with `Ï†` on `ker p.Ï€` and sends `p.v` to `w`. -/
def update (p : dual_pair' E) (Ï† : E â†’L[â„] F) (w : F) : E â†’L[â„] F :=
Ï† + (w - Ï† p.v) â¬ p.Ï€

@[simp]
lemma update_ker_pi (p : dual_pair' E) (Ï† : E â†’L[â„] F) (w : F) {u} (hu : u âˆˆ p.Ï€.ker) :
  p.update Ï† w u = Ï† u :=
begin
  rw continuous_linear_map.mem_ker at hu,
  simp only [update, hu, continuous_linear_map.to_span_singleton_apply, add_zero,
             continuous_linear_map.coe_comp', comp_app, zero_smul, continuous_linear_map.add_apply]
end

@[simp]
lemma update_v (p : dual_pair' E) (Ï† : E â†’L[â„] F) (w : F) :
  p.update Ï† w p.v = w :=
by simp only [update, p.pairing, continuous_linear_map.to_span_singleton_apply,
              continuous_linear_map.coe_comp', add_sub_cancel'_right, one_smul, comp_app,
              continuous_linear_map.add_apply]

@[simp]
lemma update_self (p : dual_pair' E) (Ï† : E â†’L[â„] F)  :
  p.update Ï† (Ï† p.v) = Ï† :=
by simp only [update, add_zero, continuous_linear_map.to_span_singleton_zero,
              continuous_linear_map.zero_comp, sub_self]

lemma inf_eq_bot (p : dual_pair' E) : p.Ï€.ker âŠ“ p.span_v = âŠ¥ :=
begin
  rw eq_bot_iff,
  intros x hx,
  have : p.Ï€ x = 0 âˆ§ âˆƒ a : â„, a â€¢ p.v = x,
    by simpa [dual_pair'.span_v, submodule.mem_span_singleton] using hx,
  rcases this with âŸ¨H, t, rflâŸ©,
  rw [p.Ï€.map_smul, p.pairing, algebra.id.smul_eq_mul, mul_one] at H,
  simp [H]
end

lemma sup_eq_top (p : dual_pair' E) : p.Ï€.ker âŠ” p.span_v = âŠ¤ :=
begin
  rw submodule.sup_eq_top_iff,
  intro x,
  refine âŸ¨x - p.Ï€ x â€¢ p.v, _, p.Ï€ x â€¢ p.v, _, _âŸ©;
  simp [dual_pair'.span_v, submodule.mem_span_singleton, p.pairing]
end

lemma decomp (p : dual_pair' E) (e : E) : âˆƒ u âˆˆ p.Ï€.ker, âˆƒ t : â„, e = u + tâ€¢p.v :=
begin
  have : e âˆˆ p.Ï€.ker âŠ” p.span_v,
  { rw p.sup_eq_top,
    exact submodule.mem_top },
  simp_rw [submodule.mem_sup, dual_pair'.mem_span_v] at this,
  rcases this with âŸ¨u, hu, -, âŸ¨t, rflâŸ©, rflâŸ©,
  use [u, hu, t, rfl]
end

/-- Map a dual pair under a linear equivalence. -/
@[simps] def map (p : dual_pair' E) (L : E â‰ƒL[â„] E') : dual_pair' E' :=
âŸ¨p.Ï€ âˆ˜L â†‘L.symm, L p.v, (congr_arg p.Ï€ $ L.symm_apply_apply p.v).trans p.pairingâŸ©

lemma update_comp_left (p : dual_pair' E) (Ïˆ : F â†’L[â„] G) (Ï† : E â†’L[â„] F) (w : F) :
  p.update (Ïˆ âˆ˜L Ï†) (Ïˆ w) = Ïˆ âˆ˜L p.update Ï† w :=
begin
  ext1 u,
  simp only [update, add_apply, continuous_linear_map.comp_apply, to_span_singleton_apply,
    Ïˆ.map_add, Ïˆ.map_smul, Ïˆ.map_sub],
end

lemma update_comp_right (p : dual_pair' E) (Ïˆ : E' â†’L[â„] F) (Ï† : E â‰ƒL[â„] E') (w : F) :
  p.update (Ïˆ âˆ˜L â†‘Ï†) w = (p.map Ï†).update Ïˆ w âˆ˜L â†‘Ï† :=
begin
  ext1 u,
  simp only [update, add_apply, continuous_linear_map.comp_apply, to_span_singleton_apply, map,
    continuous_linear_equiv.coe_coe, Ï†.symm_apply_apply],
end

lemma map_update_comp_right (p : dual_pair' E) (Ïˆ : E â†’L[â„] F) (Ï† : E â‰ƒL[â„] E') (w : F) :
  (p.map Ï†).update (Ïˆ âˆ˜L â†‘Ï†.symm) w = p.update Ïˆ w âˆ˜L â†‘Ï†.symm :=
begin
  -- todo: use `update_comp_right`
  ext1 u,
  simp only [update, add_apply, continuous_linear_map.comp_apply, to_span_singleton_apply, map,
    continuous_linear_equiv.coe_coe, Ï†.symm_apply_apply],
end

@[simp] lemma injective_update_iff
  (p : dual_pair' E) {Ï† : E â†’L[â„] F} (hÏ† : injective Ï†) {w : F} :
  injective (p.update Ï† w) â†” w âˆ‰ p.Ï€.ker.map Ï† :=
begin
  change injective (p.update Ï† w) â†” w âˆ‰ Ï† '' p.Ï€.ker,
  split,
  { rintros h âŸ¨u, hu, rflâŸ©,
    have : p.update Ï† (Ï† u) p.v = Ï† u,
    exact p.update_v Ï† (Ï† u),
    conv_rhs at this { rw â†  p.update_ker_pi Ï† (Ï† u) hu },
    rw â† h this at hu,
    simp only [set_like.mem_coe, continuous_linear_map.mem_ker] at hu,
    rw p.pairing at hu,
    linarith },
  { intros h u u' huu',
    rcases p.decomp u with âŸ¨a, ha, t, rflâŸ©,
    rcases p.decomp u' with âŸ¨a', ha', t', rflâŸ©,
    suffices : (t - t') â€¢ p.v = a' - a,
    { rw [sub_smul] at this,
      rw eq_add_of_sub_eq' this,
      abel },
    have : Ï† a + t â€¢ w = Ï† a' + t' â€¢ w,
      by simpa only [(p.update Ï† w).map_add, ha, ha', update_ker_pi, update_v,
        continuous_linear_map.map_smul] using huu',
    have hw : (t -t') â€¢ w = Ï† (a' - a),
    { rw [sub_smul, Ï†.map_sub],
      rw eq_sub_of_add_eq this,
      abel },
    have haa' : a' - a âˆˆ p.Ï€.ker := p.Ï€.ker.sub_mem ha' ha,
    have ht : t - t' = 0,
    { by_contra' ht,
      apply h,
      refine âŸ¨(t - t')â»Â¹ â€¢ (a' - a), p.Ï€.ker.smul_mem _ haa', _âŸ©,
      have := congr_arg (Î» u : F, (t - t')â»Â¹ â€¢ u) hw,
      simp only [ht, inv_smul_smulâ‚€, ne.def, not_false_iff, map_sub] at this,
      rwa [â† Ï†.map_sub, â† Ï†.map_smul, eq_comm] at this },
    rw [eq_comm, ht, zero_smul] at hw âŠ¢,
    rw â† Ï†.map_zero at hw,
    exact hÏ† hw }
end

open finite_dimensional

lemma two_le_rank_of_rank_lt_rank [finite_dimensional â„ E] [finite_dimensional â„ E']
  (p : dual_pair' E) (h : finrank â„ E < finrank â„ E') (Ï† : E â†’L[â„] E') :
  2 â‰¤ module.rank â„ (E' â§¸ submodule.map Ï† p.Ï€.ker) :=
begin
  suffices : 2 â‰¤ finrank â„ (E' â§¸ p.Ï€.ker.map Ï†.to_linear_map),
  { rw â† finrank_eq_dim,
    exact_mod_cast this },
  apply le_of_add_le_add_right,
  rw submodule.finrank_quotient_add_finrank (p.Ï€.ker.map Ï†.to_linear_map),
  have := calc finrank â„ (p.Ï€.ker.map Ï†.to_linear_map)
        â‰¤ finrank â„ p.Ï€.ker : finrank_map_le â„ Ï†.to_linear_map p.Ï€.ker
    ...  < finrank â„ E : submodule.finrank_lt (le_top.lt_of_ne p.ker_pi_ne_top),
  linarith,
end

end dual_pair'
end no_norm

namespace dual_pair'
variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
          {F : Type*} [normed_add_comm_group F] [normed_space â„ F]


/- In the next two lemmas, finite dimensionality of `E` is clearly uneeded, but allows
to use `cont_diff_clm_apply` and `continuous_clm_apply`. -/

lemma smooth_update [finite_dimensional â„ E] (p : dual_pair' E)
  {G : Type*} [normed_add_comm_group G] [normed_space â„ G]
  {Ï† : G â†’ (E â†’L[â„] F)} (hÏ† : ğ’ âˆ Ï†) {w : G â†’ F} (hw : ğ’ âˆ w) :
  ğ’ âˆ (Î» g, p.update (Ï† g) (w g)) :=
begin
  apply hÏ†.add,
  rw cont_diff_clm_apply,
  intro y,
  exact (hw.sub (cont_diff_clm_apply.mp hÏ† p.v)).const_smul _,
end

lemma continuous_update [finite_dimensional â„ E] (p : dual_pair' E)
  {X : Type*} [topological_space X]
  {Ï† : X â†’ (E â†’L[â„] F)} (hÏ† : continuous Ï†) {w : X â†’ F} (hw : continuous w) :
  continuous (Î» g, p.update (Ï† g) (w g)) :=
begin
  apply hÏ†.add,
  rw continuous_clm_apply,
  intro y,
  exact (hw.sub (continuous_clm_apply.mp hÏ† p.v)).const_smul _
end

/-- Given a finite basis `e : basis Î¹ â„ E`, and `i : Î¹`, `e.dual_pair' i`
is given by the `i`th basis element and its dual. -/
def _root_.basis.dual_pair' [finite_dimensional â„ E] {Î¹ : Type*} [fintype Î¹] [decidable_eq Î¹]
  (e : basis Î¹ â„ E) (i : Î¹) : dual_pair' E :=
{ Ï€ := (e.dual_basis i).to_continuous_linear_map,
  v := e i,
  pairing := by simp only [basis.coord_apply, finsupp.single_eq_same, basis.repr_self,
                           linear_map.coe_to_continuous_linear_map', basis.coe_dual_basis] }

end dual_pair'
