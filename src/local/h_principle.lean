import to_mathlib.analysis.normed_group
import to_mathlib.linear_algebra.basis
import to_mathlib.topology.algebra.order.basic

import loops.exists

import local.corrugation
import local.ample_relation

import interactive_expr
set_option trace.filter_inst_type true

/-!
# Local h-principle for open and ample relations

This file proves lem:h_principle_open_ample_loc from the blueprint. This is the local
version of the h-principle for open and ample relations. The proof brings together the
main result `exist_loops` from the loop folder (Chapter 1 in the blueprint) and
the corrugation technique.

One formalization issue is that the whole construction carries around a lot of data.
On paper it is easy to state one lemma listing once all this data and proving many properties.
Here it is more convenient to give each property its own lemma so carrying around data,
assumptions and constructions requires some planning. Our way to mitigate this issue
is to use two ad-hoc structures `landscape` and `step_landscape` which partly bundle
all this.

The `landscape` structure record three sets in a vector space, a closed
set `C` and two nested compact sets `Kâ‚€` and `Kâ‚`. This is the ambiant data for
the local h-principle result. We call this partly bundled because it doesn't include
the data of the formal solution we want to improve. Instead we have a Prop-valued
structure `landscape.accepts` that takes a landscape and a formal solution and assert
some compatibility conditions. There are four conditions, which is already enough
motivation to introduce a structure instead of one definition using the logical
conjunction operator that would lead to awkward and error prone access to the
individual conditions.

The proof of this proposition involves an induction on a flag of subspaces (nested
subspaces of increasing dimensions). For the purpose of this induction we use
a second structure `step_landscape` that extends `landscape` with two more pieces
of data, a subspace and a dual pair, and a compatibility condition, namely the subspace
has to be in the hyperplane defined by the dual pair.

In this setup, given `(L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•)`,
the loop family constructed by Chapter 2 is `L.loop h`. Together with corrugation,
it is used to build `L.improve_step h` which is the homotopy of 1-jet sections improving
the formal solution `ğ“•` in that step of the main inductive proof. A rather long series of
lemmas prove all the required properties of that homotopy, corresponding to
lemma lem:integration_step from the blueprint.

The inductive proof itself is the proof of `rel_loc.formal_sol.improve`.
Here all conclusions are stated at once this the induction requires to know about each
of them to proceed to the next step. We could have introduced one more ad-hoc structure
to record those conclusion but this isn't needed (at least in that Chapter) since we
need to access its components only once.

-/

noncomputable theory

open_locale unit_interval classical filter topological_space
open filter set rel_loc linear_map (ker)

variables (E : Type*) [normed_add_comm_group E] [normed_space â„ E]
          {F : Type*} [normed_add_comm_group F] [normed_space â„ F]
          {G : Type*} [normed_add_comm_group G] [normed_space â„ G]

/--
The setup for local h-principle is two compact subsets `Kâ‚€ âŠ† Kâ‚` in `E` with
`Kâ‚€ âŠ† interior Kâ‚` and a closed subset `C`.
-/
structure landscape :=
(C Kâ‚€ Kâ‚ : set E)
(hC : is_closed C)
(hKâ‚€ : is_compact Kâ‚€)
(hKâ‚ : is_compact Kâ‚)
(hâ‚€â‚ : Kâ‚€ âŠ† interior Kâ‚)

section improve_step
/-!
## Improvement step

This section proves lem:integration_step.
-/

/--
The setup for a one-step improvement towards a local h-principle is two compact subsets
`Kâ‚€ âŠ† Kâ‚` in `E` with `Kâ‚€ âŠ† interior Kâ‚` and a closed subset `C`
together with a dual pair `p` and a subspace `E'` of the corresponding hyperplane `ker p.Ï€`.
-/
structure step_landscape extends landscape E :=
(E' : submodule â„ E)
(p : dual_pair E)
(hEp : E' â‰¤ ker p.Ï€)

variables {E}

open_locale classical

variables (R : rel_loc E F)

namespace step_landscape

/-- A one-step improvement landscape accepts a formal solution if it can improve it. -/
structure accepts (L : step_landscape E) (ğ“• : jet_sec E F) : Prop :=
(h_op : is_open R)
(hKâ‚€ : âˆ€á¶  x near L.Kâ‚€, ğ“•.is_part_holonomic_at L.E' x)
(h_short : âˆ€ x, ğ“•.is_short_at R L.p x)
(hC : âˆ€á¶  x near L.C, ğ“•.is_holonomic_at x)

/-- The union of all slices of `R` corresponding to `ğ“•`. -/
def Î© (L : step_landscape E) (ğ“• : jet_sec E F) : set (E Ã— F) :=
{p | p.2 âˆˆ ğ“•.slice_at R L.p p.1}
--â‹ƒ x, ({x} : set E) Ã—Ë¢ (connected_component_in (ğ“•.slice_at R L.p x) $ ğ“•.Ï† x L.p.v)

/-- The linear form in a `step_landscape`, coming from the underlying dual pair. -/
def Ï€ (L : step_landscape E) : E â†’L[â„] â„ := L.p.Ï€

/-- The vector in a `step_landscape`, coming from the underlying dual pair. -/
def v (L : step_landscape E) : E := L.p.v

/-- One more compact set in the landscape: Kâ‚ âˆ© C, needed as an input to the
loop construction. -/
def K (L : step_landscape E) : set E := L.Kâ‚ âˆ© L.C

/-- The base function for the loop family associated in any jet section in a
step landscape. -/
def b (L : step_landscape E) (ğ“• : jet_sec E F) : E â†’ F := Î» x, ğ“•.Ï† x L.v

/-- The desired average for the loop family associated in any jet section in a
step landscape. -/
def g (L : step_landscape E) (ğ“• : jet_sec E F) : E â†’ F := Î» x, D ğ“•.f x L.v

lemma is_compact_K (L : step_landscape E) : is_compact L.K :=
L.hKâ‚.inter_right L.hC

variables {R}

lemma accepts.open [finite_dimensional â„ E]  {L : step_landscape E} {ğ“• : jet_sec E F} (h : L.accepts R ğ“•) :
  is_open (L.Î© R ğ“•) :=
begin
  set Ïˆ : E Ã— F â†’ one_jet E F := Î» p, (p.1, ğ“•.f p.1, L.p.update (ğ“•.Ï† p.1) p.2),
  change is_open {p : E Ã— F | Ïˆ p âˆˆ R},
  apply is_open.preimage _ h.h_op,
  apply continuous_fst.prod_mk (ğ“•.f_diff.continuous.fst'.prod_mk _),
  exact L.p.continuous_update ğ“•.Ï†_diff.continuous.fst' continuous_snd
end

lemma smooth_b (L : step_landscape E) (ğ“• : jet_sec E F) :
  ğ’ âˆ (L.b ğ“•) :=
(continuous_linear_map.apply â„ F L.v).cont_diff.comp ğ“•.Ï†_diff

lemma smooth_g (L : step_landscape E) (ğ“• : jet_sec E F) :
  ğ’ âˆ (L.g ğ“•) :=
(continuous_linear_map.apply â„ F L.v).cont_diff.comp (cont_diff_top_iff_fderiv.mp ğ“•.f_diff).2

lemma accepts.rel {L : step_landscape E} {ğ“• : jet_sec E F} (h : L.accepts R ğ“•) :
  âˆ€á¶  (x : E) near L.K, (L.g ğ“•) x = (L.b ğ“•) x :=
begin
  apply (h.hC.filter_mono $ monotone_nhds_set (inter_subset_right L.Kâ‚ L.C)).mono,
  intros x hx,
  dsimp [jet_sec.is_holonomic_at] at hx,
  dsimp [step_landscape.g, step_landscape.b],
  rw hx
end

variables [finite_dimensional â„ E]  [finite_dimensional â„ F]

open_locale borelize

/-- The loop family to use in some landscape to improve a formal solution. -/
def loop (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•) :
â„ â†’ E â†’ loop F :=
classical.some (exist_loops L.is_compact_K h.open (L.smooth_g ğ“•) (L.smooth_b ğ“•) h.rel h.h_short)

lemma nice (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•) :
  nice_loop (L.g â†‘ğ“•) (L.b â†‘ğ“•) (Î© R L ğ“•) L.K (L.loop h) :=
classical.some_spec $ exist_loops L.is_compact_K h.open (L.smooth_g ğ“•) (L.smooth_b ğ“•) h.rel h.h_short

lemma update_zero (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•) (x : E) (s : â„) :
L.p.update (ğ“•.Ï† x) ((L.loop h 0 x) s) = ğ“•.Ï† x :=
begin
  rw (L.nice h).t_zero x s,
  exact L.p.update_self _,
end

lemma loop_smooth (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•) :
  ğ’ âˆ â†¿(L.loop h) :=
(L.nice h).smooth

lemma loop_smooth' (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•)
  {t : G â†’ â„} (ht : ğ’ âˆ t) {s : G â†’ â„} (hs : ğ’ âˆ s) {x : G â†’ E} (hx : ğ’ âˆ x) :
  ğ’ âˆ (Î» g, L.loop h (t g) (x g) (s g)) :=
(L.loop_smooth h).comp (ht.prod $ hx.prod hs)

lemma loop_C1 (L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•) :
âˆ€ t, ğ’ 1 â†¿(L.loop h t) :=
Î» t, (L.loop_smooth' h cont_diff_const cont_diff_snd cont_diff_fst).of_le le_top

variables (L : step_landscape E)

/-- The cut-off function associated to a step landscape, equal to one near Kâ‚€ and
zero outside Kâ‚. -/
def Ï (L : step_landscape E) : E â†’ â„ :=
(exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚).some

lemma Ï_smooth (L : step_landscape E) : ğ’ âˆ L.Ï :=
(exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚).some_spec.1

lemma Ï_mem (L : step_landscape E) (x : E) : L.Ï x âˆˆ I :=
(exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚).some_spec.2.2.2 x

lemma Ï_le (L : step_landscape E) (x : E) : |L.Ï x| â‰¤ 1 :=
begin
  cases L.Ï_mem x with h h',
  rw abs_le,
  refine âŸ¨_, h'âŸ©,
  linarith
end

lemma hÏâ‚€ (L : step_landscape E) : âˆ€á¶  x near L.Kâ‚€, L.Ï x = 1 :=
(exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚).some_spec.2.1

lemma hÏ_compl_Kâ‚ (L : step_landscape E) {x : E} : x âˆ‰ L.Kâ‚ â†’ L.Ï x = 0 :=
(exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚).some_spec.2.2.1 x

/--
Homotopy of formal solutions obtained by corrugation in the direction of `p : dual_pair E`
in some landscape to improve a formal solution `ğ“•` from being `L.E'`-holonomic to
`L.E' âŠ” span {p.v}`-holonomic near `L.Kâ‚€`.
-/
def improve_step {ğ“• : formal_sol R} (h : L.accepts R ğ“•) (N : â„) : htpy_jet_sec E F :=
{ f := Î» t x, ğ“•.f x + (smooth_step t*L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x,
  f_diff :=  ğ“•.f_diff.snd'.add $
    (smooth_step.smooth.fst'.mul L.Ï_smooth.snd').smul $
    corrugation.cont_diff' N (L.loop_smooth h) cont_diff_snd cont_diff_fst,
  Ï† := Î» t x, L.p.update (ğ“•.Ï† x) (L.loop h (smooth_step t*L.Ï x) x $ N * L.Ï€ x) +
                (smooth_step t*L.Ï x) â€¢ (corrugation.remainder L.p.Ï€ N (L.loop h 1) x),
  Ï†_diff := begin
    apply cont_diff.add,
    apply L.p.smooth_update,
    apply ğ“•.Ï†_diff.snd',
    apply L.loop_smooth',
    exact smooth_step.smooth.fst'.mul L.Ï_smooth.snd',
    apply cont_diff_const.mul L.Ï€.cont_diff.snd',
    exact cont_diff_snd,
    apply cont_diff.smul,
    exact smooth_step.smooth.fst'.mul L.Ï_smooth.snd',
    exact remainder.smooth _ _ (L.loop_smooth h) cont_diff_snd cont_diff_const
  end }

variables {L} {ğ“• : formal_sol R} (h : L.accepts R ğ“•) (N : â„)

@[simp]
lemma improve_step_apply (t : â„) (x : E) :
  L.improve_step h N t x = (ğ“•.f x + (smooth_step t*L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x,
  L.p.update (ğ“•.Ï† x) (L.loop h (smooth_step t*L.Ï x) x $ N * L.Ï€ x) +
                 (smooth_step t*L.Ï x) â€¢ (corrugation.remainder L.p.Ï€ N (L.loop h 1) x)) :=
by { simp [improve_step, h], refl }

@[simp]
lemma improve_step_apply_f (t : â„) (x : E) :
  (L.improve_step h N t).f x = ğ“•.f x + (smooth_step t*L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x :=
rfl

@[simp]
lemma improve_step_apply_Ï† (t : â„) (x : E) :
  (L.improve_step h N t).Ï† x = L.p.update (ğ“•.Ï† x) (L.loop h (smooth_step t*L.Ï x) x $ N * L.Ï€ x) +
                 (smooth_step t*L.Ï x) â€¢ (corrugation.remainder L.p.Ï€ N (L.loop h 1) x) :=
rfl

lemma improve_step_of_support (t : â„) {x : E}
  (H : âˆ€ t, x âˆ‰ loop.support (L.loop h t)) :
  L.improve_step h N t x = ğ“• x :=
begin
  have : âˆ€ t s, L.loop h t x s = ğ“•.Ï† x L.v,
      { intros t s,
        rw loop.is_const_of_not_mem_support (H t) s 0,
        apply (L.nice h).s_zero x t },
  rw [improve_step_apply h, corrugation_eq_zero _ _ _ _ (H t),
      remainder_eq_zero _ _ (L.loop_C1 h 1) (H 1)],
  simp only [formal_sol.to_jet_sec_eq_coe, smul_zero, add_zero, this],
  erw L.p.update_self,
  refl
end

lemma improve_step_rel_t_eq_0 : L.improve_step h N 0 = ğ“• :=
begin
  ext x,
  { rw improve_step_apply_f h,
    simp [(L.nice h).t_zero x] },
  { ext x,
    rw improve_step_apply_Ï† h,
    simp only [formal_sol.to_jet_sec_eq_coe, zero_mul, smooth_step.zero, zero_smul, add_zero],
    erw L.update_zero h, refl }
end

lemma improve_step_rel_compl_Kâ‚ {x} (hx : x âˆ‰ L.Kâ‚) (t) : L.improve_step h N t x = ğ“• x :=
begin
  rw [improve_step_apply h, L.hÏ_compl_Kâ‚ hx],
  simp only [formal_sol.to_jet_sec_eq_coe, mul_zero, zero_smul, add_zero],
  erw L.update_zero h,
  refl
end

lemma improve_step_rel_K : âˆ€á¶  x near L.K, âˆ€ t, L.improve_step h N t x = ğ“• x :=
begin
  have : âˆ€á¶  x near L.K, âˆ€ t, x âˆ‰ loop.support (L.loop h t),
  { apply (L.nice h).rel_K.eventually_nhds_set.mono,
    intros x hx t,
    apply loop.not_mem_support,
    apply hx.mono,
    intros y hy,
    exact loop.is_const_of_eq (hy t) },
  apply this.mono,
  intros x hx t,
  exact improve_step_of_support _ _ _ hx
end

lemma improve_step_rel_C : âˆ€á¶  x near L.C, âˆ€ t, L.improve_step h N t x = ğ“• x :=
begin
  apply eventually.filter_mono (L.hKâ‚.is_closed.nhds_set_le_sup' L.C),
  rw eventually_sup,
  split,
  { apply improve_step_rel_K },
  { rw eventually_principal,
    exact Î» x, improve_step_rel_compl_Kâ‚ h N }
end

-- In the next lemma we reintroduce F to appaise the unused argument linter since
-- `finite_dimensional â„ F` isn't needed here.

lemma bu_lt {F : Type*} [normed_add_comm_group F] [normed_space â„ F]
  (t : â„) (x : E) {v : F} {Îµ : â„} (hv : â€–vâ€– < Îµ) :
  â€–(smooth_step t * L.Ï x) â€¢ vâ€– < Îµ :=
calc â€–(smooth_step t * L.Ï x) â€¢ vâ€– = |smooth_step t| * |L.Ï x| * â€–vâ€– : by
             rw [norm_smul, real.norm_eq_abs, abs_mul]
... â‰¤ â€–vâ€– : mul_le_of_le_one_left (norm_nonneg _) (mul_le_one (smooth_step.abs_le t)
                                                          (abs_nonneg _) (L.Ï_le x))
... < Îµ : hv

lemma improve_step_c0_close {Îµ : â„} (Îµ_pos : 0 < Îµ) :
  âˆ€á¶  N in at_top, âˆ€ x t, â€–(L.improve_step h N t).f x - ğ“•.f xâ€– â‰¤ Îµ :=
begin
  set Î³ := L.loop h,
  have Î³_cont : continuous â†¿(Î» t x, Î³ t x) := (L.nice h).smooth.continuous,
  have Î³_C1 : ğ’ 1 â†¿(Î³ 1) := ((L.nice h).smooth.comp (cont_diff_prod_mk_right 1)).of_le le_top,
  apply ((corrugation.c0_small_on L.hKâ‚ (L.nice h).t_le_zero (L.nice h).t_ge_one Î³_cont Îµ_pos).and $
        remainder_c0_small_on L.Ï€ L.hKâ‚ Î³_C1 Îµ_pos).mono,
  rintros N âŸ¨H, H'âŸ© x t,
  by_cases hx : x âˆˆ L.Kâ‚,
  { rw [improve_step_apply_f h],
    suffices : â€–(smooth_step t * L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) xâ€– â‰¤ Îµ, by simpa,
    exact (bu_lt _ _ $ H _ hx t).le },
  { rw show (L.improve_step h N t).f x = ğ“•.f x, from congr_arg prod.fst (improve_step_rel_compl_Kâ‚ h N hx t),
    simp [Îµ_pos.le] }
end

lemma improve_step_part_hol {N : â„} (hN : N â‰  0) :
  âˆ€á¶  x near L.Kâ‚€, (L.improve_step h N 1).is_part_holonomic_at (L.E' âŠ” L.p.span_v) x :=
begin
  have Î³_C1 : ğ’ 1 â†¿(L.loop h 1) := ((L.nice h).smooth.comp (cont_diff_prod_mk_right 1)).of_le le_top,
  let ğ“•' : jet_sec E F :=
  { f := Î» x, ğ“•.f x + corrugation L.Ï€ N (L.loop h 1) x,
    f_diff := ğ“•.f_diff.add
     (corrugation.cont_diff' _ (L.loop_smooth h) cont_diff_id cont_diff_const),
    Ï† := Î» x , L.p.update (ğ“•.Ï† x) (L.loop h 1 x $ N * L.Ï€ x) +
               corrugation.remainder L.p.Ï€ N (L.loop h 1) x,
    Ï†_diff := begin
      apply cont_diff.add,
      apply L.p.smooth_update,
      apply ğ“•.Ï†_diff,
      apply L.loop_smooth',
      apply cont_diff_const,
      apply cont_diff_const.mul L.Ï€.cont_diff,
      exact cont_diff_id,
      exact remainder.smooth _ _ (L.loop_smooth h) cont_diff_id cont_diff_const
    end },
  have H : âˆ€á¶  x near L.Kâ‚€, L.improve_step h N 1 x = ğ“•' x,
  { apply L.hÏâ‚€.mono,
    intros x hx,
    simp [improve_step_apply h, hx],
    refl },
  have fderiv_ğ“•' := Î» x, fderiv_corrugated_map N hN Î³_C1 (ğ“•.f_diff.of_le le_top) L.p ((L.nice h).avg x),
  rw eventually_congr (H.is_part_holonomic_at_congr (L.E' âŠ” L.p.span_v)),
  apply h.hKâ‚€.mono,
  intros x hx,
  apply jet_sec.is_part_holonomic_at.sup,
  { intros u hu,
    have hu_ker := L.hEp hu,
    dsimp [ğ“•'],
    erw [fderiv_ğ“•', continuous_linear_map.add_apply, L.p.update_ker_pi _ _ hu_ker,
         L.p.update_ker_pi _ _ hu_ker, hx u hu] },
  { intros u hu,
    rcases submodule.mem_span_singleton.mp hu with âŸ¨l, rflâŸ©,
    rw [(D ğ“•'.f x).map_smul, (ğ“•'.Ï† x).map_smul],
    apply congr_arg,
    dsimp [ğ“•'],
    erw [fderiv_ğ“•', L.p.update_v, continuous_linear_map.add_apply, L.p.update_v],
    refl }
end

lemma improve_step_formal_sol :
  âˆ€á¶  N in at_top, âˆ€ t, (L.improve_step h N t).is_formal_sol R :=
begin
  set Î³ := L.loop h,
  have Î³_cont : continuous â†¿(Î» t x, Î³ t x) := (L.nice h).smooth.continuous,
    have Î³_C1 : ğ’ 1 â†¿(Î³ 1) := ((L.nice h).smooth.comp (cont_diff_prod_mk_right 1)).of_le le_top,
  set K := (Î» p : E Ã— â„ Ã— â„, (p.1, ğ“•.f p.1, L.p.update (ğ“•.Ï† p.1) (L.loop h p.2.1 p.1 p.2.2))) '' (L.Kâ‚ Ã—Ë¢ (I Ã—Ë¢ I)),
  have K_cpt : is_compact K,
  { refine (L.hKâ‚.prod (is_compact_Icc.prod is_compact_Icc)).image _,
    refine continuous_fst.prod_mk (ğ“•.f_diff.continuous.fst'.prod_mk _ ),
    apply L.p.continuous_update ğ“•.Ï†_diff.continuous.fst',
    change continuous (â†¿(L.loop h) âˆ˜ (Î» (g : E Ã— â„ Ã— â„), (g.snd.fst, g.fst, g.snd.snd))),
    exact (L.loop_smooth h).continuous.compâ‚ƒ continuous_snd.fst continuous_fst continuous_snd.snd },
  have K_sub : K âŠ† R,
  { rintros _ âŸ¨âŸ¨x, t, sâŸ©, âŸ¨x_in, t_in, s_inâŸ©, rflâŸ©,
    exact (L.nice h).mem_Î© x t s },
  obtain âŸ¨Îµ, Îµ_pos, hÎµâŸ© : âˆƒ Îµ, 0 < Îµ âˆ§ metric.thickening Îµ K âŠ† R,
    from  K_cpt.exists_thickening_subset_open h.h_op K_sub,

  apply ((corrugation.c0_small_on L.hKâ‚ (L.nice h).t_le_zero (L.nice h).t_ge_one Î³_cont Îµ_pos).and $
         remainder_c0_small_on L.Ï€ L.hKâ‚ Î³_C1 Îµ_pos).mono,
  rintros N âŸ¨H, H'âŸ© t x,
  by_cases hxKâ‚ : x âˆˆ L.Kâ‚,
  { apply hÎµ,
    rw metric.mem_thickening_iff,
    refine âŸ¨(x, ğ“•.f x, L.p.update (ğ“•.Ï† x) $ L.loop h (smooth_step t*L.Ï x) x $ N * L.Ï€ x), _, _âŸ©,
    { simp only [hxKâ‚, formal_sol.to_jet_sec_eq_coe, exists_prop, mem_set_of_eq, eq_self_iff_true, true_and, K],
      exact âŸ¨âŸ¨x, smooth_step t * L.Ï x, int.fract (N * L.Ï€ x)âŸ©,
            âŸ¨hxKâ‚, unit_interval.mul_mem (smooth_step.mem t) (L.Ï_mem x),
              unit_interval.fract_mem _âŸ©, by simp only [loop.fract_eq]âŸ© },
    { simp only [h, improve_step_apply_f, formal_sol.to_jet_sec_eq_coe, improve_step_apply_Ï†],
      rw [prod.dist_eq, max_lt_iff, prod.dist_eq, max_lt_iff],
      refine âŸ¨by simpa using Îµ_pos, _, _âŸ© ; dsimp only ; rw dist_self_add_left,
      { exact (bu_lt _ _ $ H _ hxKâ‚ _) },
      { exact (bu_lt _ _ $ H' _ hxKâ‚) } } },
  { rw [show ((L.improve_step h N) t).f x = ğ“•.f x,
          from congr_arg prod.fst $ improve_step_rel_compl_Kâ‚ h N hxKâ‚ t,
        show ((L.improve_step h N) t).Ï† x = ğ“•.Ï† x,
          from congr_arg prod.snd $ improve_step_rel_compl_Kâ‚ h N hxKâ‚ t],
    exact ğ“•.is_sol _ }
end

end step_landscape

end improve_step

section improve
/-!
## Full improvement

This section proves lem:h_principle_open_ample_loc.
-/

open finite_dimensional submodule step_landscape

variables {E} [finite_dimensional â„ E] [finite_dimensional â„ F]
  {R : rel_loc E F} (h_op : is_open R) (h_ample : R.is_ample)
variables (L : landscape E)
variables {Îµ : â„} (Îµ_pos : 0 < Îµ)

include h_op h_ample Îµ_pos

/--
Homotopy of formal solutions obtained by successive corrugations in some landscape `L` to improve a
formal solution `ğ“•` until it becomes holonomic near `L.Kâ‚€`.
-/
lemma rel_loc.formal_sol.improve (ğ“• : formal_sol R)
  (h_hol : âˆ€á¶  x near L.C, ğ“•.is_holonomic_at x) :
  âˆƒ H : htpy_jet_sec E F,
    (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
    (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
    (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x ) âˆ§
    (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
    (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– â‰¤ Îµ) âˆ§
    (âˆ€ t, (H t).is_formal_sol R) âˆ§
    (âˆ€á¶  x near L.Kâ‚€, (H 1).is_holonomic_at x) :=
begin
  let n := finrank â„ E,
  let e := fin_basis â„ E,
  let E' := e.flag,
  suffices : âˆ€ k : fin (n + 1), âˆ€ Î´ > (0 : â„), âˆƒ H : htpy_jet_sec E F,
    (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
    (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
    (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x ) âˆ§
    (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
    (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– â‰¤ Î´) âˆ§
    (âˆ€ t, (H t).is_formal_sol R) âˆ§
    (âˆ€á¶  x near L.Kâ‚€, (H 1).is_part_holonomic_at (E' k) x),
  { simpa only [show E' (fin.last n) = âŠ¤, from e.flag_last, jet_sec.is_part_holonomic_top] using
      this (fin.last n) Îµ Îµ_pos },
  clear Îµ_pos Îµ,
  intro k,
  apply fin.induction_on k ; clear k,
  { intros Î´ Î´_pos,
    use ğ“•.to_jet_sec.const_htpy,
    simp [show E' 0 = âŠ¥, from e.flag_zero, le_of_lt Î´_pos] },
  { rintros k HH Î´ Î´_pos,
    rcases HH (Î´/2) (half_pos Î´_pos) with âŸ¨H, hHâ‚€, hHâ‚, hHC, hHKâ‚, hHc0, hH_sol, hH_holâŸ©, clear HH,
    let S : step_landscape E :=
    { E' := E' k,
      p := e.dual_pair k,
      hEp := by simpa only [E', basis.dual_pair] using e.flag_le_ker_dual k,
      ..L},
    set Hâ‚ : formal_sol R := (hH_sol 1).formal_sol,
    have h_span : S.E' âŠ” S.p.span_v = E' k.succ := e.flag_span_succ k,
    have acc : S.accepts R Hâ‚ :=
    { h_op := h_op,
      hKâ‚€ := begin
        apply hH_hol.mono,
        intros x hx,
        dsimp [S],
        convert hx,
        rw [â† fin.coe_eq_cast_succ, coe_coe]
      end,
      h_short := Î» x, h_ample.is_short_at Hâ‚ S.p x,
      hC := begin
        apply h_hol.congr (formal_sol.is_holonomic_at_congr _ _ _),
        apply hHC.mono,
        tauto,
      end  },
    have hHâ‚_rel_C : âˆ€á¶  (x : E) near S.C, Hâ‚ x = ğ“• x,
    { apply hHC.mono,
      intros x hx,
      apply hx },
    have hHâ‚_C : âˆ€á¶  (x : E) near S.to_landscape.C, Hâ‚.is_holonomic_at x,
    { apply h_hol.congr (formal_sol.is_holonomic_at_congr _ _ _),
      exact (h_hol.and hHâ‚_rel_C).mono (Î» x hx, hx.2.symm) },
    have hHâ‚_Kâ‚ : âˆ€ x âˆ‰ L.Kâ‚, Hâ‚ x = ğ“• x,
    { intros x hx,
      apply hHKâ‚ x hx },
    obtain âŸ¨N, âŸ¨hN_close, hN_solâŸ©, hNneqâŸ© :=
      (((improve_step_c0_close acc $ half_pos Î´_pos).and
      (improve_step_formal_sol acc)).and $ eventually_ne_at_top (0 :â„)).exists,
    have glue : H 1 = S.improve_step acc N 0,
    { rw improve_step_rel_t_eq_0,
      refl  },
    refine âŸ¨H.comp (S.improve_step acc N) glue, _, _, _, _, _, _, _âŸ©,
    { apply (H.comp_le_0 _ _).mono,
      intros t ht,
      rw ht,
      exact hHâ‚€.on_set 0 right_mem_Iic }, -- t = 0
    { apply (H.comp_ge_1 _ _).mono,
      intros t ht,
      rw [ht, H.comp_1] },
    { -- rel C
      apply (hHC.and $ hHâ‚_rel_C.and $ improve_step_rel_C acc N).mono,
      rintros x âŸ¨hx, hx', hx''âŸ© t,
      by_cases ht : t â‰¤ 1/2,
      { simp only [ht, hx, htpy_jet_sec.comp_of_le]},
      { simp only [ht, hx', hx'', htpy_jet_sec.comp_of_not_le, not_false_iff]} },
    { -- rel Kâ‚
      intros x hx t,
      by_cases ht : t â‰¤ 1/2,
      { simp only [ht, hx, hHKâ‚, htpy_jet_sec.comp_of_le, not_false_iff]},
      { simp only [ht, hx, hHâ‚_Kâ‚, improve_step_rel_compl_Kâ‚, htpy_jet_sec.comp_of_not_le,
                   not_false_iff] } },
    { -- Câ°-close
      intros x t,
      by_cases ht : t â‰¤ 1/2,
      { apply le_trans _ (half_le_self $ le_of_lt Î´_pos),
        simp only [ht, hHc0, htpy_jet_sec.comp_of_le]},
      { simp only [ht, htpy_jet_sec.comp_of_not_le, not_false_iff],
        rw â† add_halves Î´,
        exact norm_sub_le_add_of_le (hN_close _ _) (hHc0 _ _) } },
    { -- formal solution
      intros t,
      by_cases ht : t â‰¤ 1/2,
      { simp only [ht, hH_sol, htpy_jet_sec.comp_of_le]},
      { simp only [ht, hN_sol, htpy_jet_sec.comp_of_not_le, not_false_iff] } },
    {  -- part-hol E' (k + 1)
      rw [â† h_span, htpy_jet_sec.comp_1],
      apply improve_step_part_hol acc hNneq } }
end

/-- A repackaging of `rel_loc.formal_sol.improve` for convenience. -/
lemma rel_loc.formal_sol.improve_htpy (ğ“• : formal_sol R)
  (h_hol : âˆ€á¶  x near L.C, ğ“•.is_holonomic_at x) :
  âˆƒ H : htpy_formal_sol R,
    (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
    (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
    (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x ) âˆ§
    (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
    (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– â‰¤ Îµ)  âˆ§
    (âˆ€á¶  x near L.Kâ‚€, (H 1).is_holonomic_at x) :=
begin
  rcases ğ“•.improve h_op h_ample L Îµ_pos h_hol with âŸ¨H, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚†, hâ‚‡âŸ©,
  exact âŸ¨{is_sol := hâ‚†, ..H}, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚‡âŸ©
end

/-- A repackaging of `rel_loc.formal_sol.improve` for convenience. -/
lemma rel_loc.formal_sol.improve_htpy' (ğ“• : formal_sol R)
  (h_hol : âˆ€á¶  x near L.C, ğ“•.is_holonomic_at x) :
  âˆƒ H : htpy_formal_sol R,
    (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
    (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
    (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x ) âˆ§
    (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
    (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– < Îµ)  âˆ§
    (âˆ€á¶  x near L.Kâ‚€, (H 1).is_holonomic_at x) :=
begin
  rcases ğ“•.improve h_op h_ample L (half_pos Îµ_pos) h_hol with âŸ¨H, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚†, hâ‚‡âŸ©,
  exact âŸ¨{is_sol := hâ‚†, ..H}, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, Î» x t, (hâ‚… x t).trans_lt (half_lt_self Îµ_pos), hâ‚‡âŸ©
end


/-- This is a version of Lemma `lem:improve_htpy_loc` from the blueprint.
The blueprint should be updated to match this. -/
lemma rel_loc.htpy_formal_sol.improve (ğ“• : htpy_formal_sol R) {A : set E} (hA : is_closed A)
  (h_A : âˆ€á¶  x near A, (ğ“• 0).is_holonomic_at x âˆ§ âˆ€ t, ğ“• t x = ğ“• 0 x)
  (h_C : âˆ€á¶  x near L.C, (ğ“• 1).is_holonomic_at x)
  (h_t_0 : âˆ€á¶  t near Iic (0 : â„), ğ“• t = ğ“• 0)
  (h_t_1 : âˆ€á¶  t near Ici (1 : â„), ğ“• t = ğ“• 1) :
  âˆƒ ğ“•' : htpy_formal_sol R,
    (âˆ€á¶  t near Iic (0 : â„), ğ“•' t = ğ“• 0) âˆ§
    (âˆ€á¶  t near Ici (1 : â„), ğ“•' t = ğ“•' 1) âˆ§
    (âˆ€á¶  x near A, âˆ€ t, ğ“•' t x = ğ“• 0 x) âˆ§
    (âˆ€ t x, x âˆ‰ L.Kâ‚ â†’ ğ“•' t x = ğ“• t x) âˆ§
    (âˆ€ x t, (âˆƒ t', ğ“•' t x = ğ“• t' x) âˆ¨ â€–(ğ“•' t).f x - (ğ“• 1).f xâ€– < Îµ) âˆ§
    (âˆ€á¶  x near A âˆª (L.C âˆª L.Kâ‚€), (ğ“•' 1).is_holonomic_at x) :=
begin
  let ğ“•â‚ : formal_sol R :=
  { is_sol := ğ“•.is_sol 1,
    ..ğ“• 1 },
  have h_CA : âˆ€á¶  x near L.C âˆª A, (ğ“• 1).is_holonomic_at x,
  { apply h_C.union,
    apply h_A.eventually_nhds_set.mono,
    rintros x hx,
    apply hx.self_of_nhds.1.congr,
    apply hx.mono,
    intros y hy,
    rw hy.2 1 },
  let L' : landscape E :=
  { C := L.C âˆª A,
    hC := L.hC.union hA,
    ..L},
  rcases ğ“•â‚.improve_htpy h_op h_ample L' (half_pos Îµ_pos) h_CA with
    âŸ¨ğ“–, hğ“–â‚€, hğ“–â‚, hğ“–CA : âˆ€á¶  x near L.C âˆª A, _, hğ“–Kâ‚, hğ“–dist, hğ“–Kâ‚€âŸ©,
  obtain âŸ¨Ï, hÏ_ge, hÏ_le, hÏ_Kâ‚€, hÏ_Kâ‚, hÏ_smoothâŸ© : âˆƒ Ï : E â†’ â„,
    (âˆ€ x, 1 â‰¤ Ï x) âˆ§
    (âˆ€ x, Ï x â‰¤ 2) âˆ§
    (âˆ€á¶  x near L.Kâ‚€, Ï x = 2) âˆ§
    (âˆ€ x âˆ‰ L.Kâ‚, Ï x = 1) âˆ§
    ğ’ âˆ Ï,
  { rcases exists_cont_diff_one_nhds_of_interior L.hKâ‚€.is_closed L.hâ‚€â‚ with
      âŸ¨f, f_smooth, fKâ‚€, fKâ‚, hfâŸ©,
    refine âŸ¨Î» x, f x + 1, Î» x, _, Î» x, _, _, Î» x hx, _, _âŸ©,
    { linarith [(hf x).1] },
    { linarith [(hf x).2] },
    { apply fKâ‚€.mono,
      rintros x hx,
      linarith [hx] },
    { linarith [fKâ‚ x hx] },
    { exact f_smooth.add cont_diff_const }, },
  obtain âŸ¨Ï‡, hÏ‡_ge, hÏ‡_le, hÏ‡_ğ“•, hÏ‡_0, hÏ‡_1, hÏ‡_smoothâŸ© : âˆƒ Ï‡ : â„ â†’ â„,
    (âˆ€ t, 0 â‰¤ Ï‡ t) âˆ§
    (âˆ€ t, Ï‡ t â‰¤ 1) âˆ§
    (âˆ€ t, ğ“• (Ï‡ t) = ğ“• t) âˆ§
    (âˆ€á¶  t near Iic (0 : â„), Ï‡ t = 0) âˆ§
    (âˆ€á¶  t near Ici (1 : â„), Ï‡ t = 1) âˆ§
    ğ’ âˆ Ï‡,
  { obtain âŸ¨Î·, Î·_pos, hÎ·, hÎ·â‚€, hÎ·â‚âŸ© : âˆƒ Î· > (0 : â„), (Î· < 1/4) âˆ§ (âˆ€ t â‰¤ Î·, ğ“• t = ğ“• 0) âˆ§
                                                                 (âˆ€ t â‰¥ 1- Î·, ğ“• t = ğ“• 1),
    { rcases (has_basis_nhds_set_Iic' (0 : â„)).eventually_iff.mp h_t_0 with âŸ¨Î·â‚€, Î·â‚€_pos, hÎ·â‚€âŸ©,
      rcases (has_basis_nhds_set_Ici' (1 : â„)).eventually_iff.mp h_t_1 with âŸ¨Î·â‚, Î·â‚_pos, hÎ·â‚âŸ©,
      refine âŸ¨min (min Î·â‚€ (1 - Î·â‚)) (1/8), _, _, _, _âŸ©,
      { apply lt_min (lt_min Î·â‚€_pos _) _ ; linarith },
      { apply lt_of_le_of_lt,
        apply min_le_right,
        norm_num },
      { intros t ht,
        apply hÎ·â‚€,
        exact (le_min_iff.mp (le_min_iff.mp ht).1).1 },
      { intros t ht,
        apply hÎ·â‚,
        change Î·â‚ â‰¤ t,
        have : 1 - t â‰¤  min (min Î·â‚€ (1 - Î·â‚)) (1 / 8), by linarith,
        linarith [(le_min_iff.mp (le_min_iff.mp this).1).2] } },
    have : Icc Î· (1 - Î·) âŠ† interior (Icc (Î·/2) (1 - Î·/2)),
    { rw interior_Icc,
      apply Icc_subset_Ioo ; linarith },
    rcases exists_interpolation_of_interior is_closed_Icc this cont_diff_id smooth_step.smooth with
      âŸ¨Ï‡, Ï‡_smooth, hÏ‡, hÏ‡', hÏ‡'': âˆ€ t, Ï‡ t âˆˆ segment â„ t (smooth_step t)âŸ©,
    have F : Icc (Î·/2) (1 - Î·/2) âŠ† Icc 0 1,
    { have I1 : 0 â‰¤ Î· / 2, from (half_pos Î·_pos).le,
      have I2 : 1 - Î·/2 â‰¤ 1, by linarith,
      exact Icc_subset_Icc I1 I2 },
    have hÏ‡â‚ƒ : âˆ€ t < Î·/2, Ï‡ t = 0,
    { intros t ht,
      rw hÏ‡',
      apply smooth_step.of_lt,
      linarith,
      simp only [mem_Icc, not_and_distrib, not_le, or.inl ht] },
    have hÏ‡â‚„ : âˆ€ t > 1 - Î·/2, Ï‡ t = 1,
    { rintros t (ht : 1- Î·/2 < t),
      rw hÏ‡',
      apply smooth_step.of_gt,
      linarith,
      simp only [mem_Icc, not_and_distrib, not_le, or.inr ht] },
    refine âŸ¨Ï‡, Î» t, _, Î» t, _, Î» t, _, _, _, Ï‡_smoothâŸ©,
    { by_cases ht : t âˆˆ Icc (Î·/2) (1 - Î·/2),
      { exact ((convex_Icc (0 : â„) 1).segment_subset (F ht) (smooth_step.mem t) (hÏ‡'' t)).1 },
      { rw hÏ‡' t ht,
        exact (smooth_step.mem _).1 } },
    { by_cases ht : t âˆˆ Icc (Î·/2) (1 - Î·/2),
      { exact ((convex_Icc (0 : â„) 1).segment_subset (F ht) (smooth_step.mem t) (hÏ‡'' t)).2 },
      { rw hÏ‡' t ht,
        exact (smooth_step.mem _).2 } },
    { by_cases ht : t âˆˆ Icc Î· (1 - Î·),
      { rw hÏ‡.on_set t ht,
        refl },
      { rw [mem_Icc, not_and_distrib] at ht,
        cases ht with ht ht ; rw not_le at ht,
        { rw hÎ·â‚€ t ht.le,
          have := hÏ‡'' t,
          rw smooth_step.of_lt (ht.trans hÎ·) at this,
          apply hÎ·â‚€,
          cases le_or_lt t 0 with ht' ht',
          { rw segment_eq_Icc ht' at this,
            cases this,
            linarith },
          { rw [segment_symm, segment_eq_Icc ht'.le] at this,
            cases this,
            linarith } },
        { have ht' : 3/4 < t,
          { linarith },
          rw hÎ·â‚ t ht.le,
          have := hÏ‡'' t,
          rw smooth_step.of_gt ht' at this,
          apply hÎ·â‚,
          cases le_or_lt t 1 with ht' ht',
          { rw segment_eq_Icc ht' at this,
            cases this,
            linarith },
          { rw [segment_symm, segment_eq_Icc ht'.le] at this,
            cases this,
            linarith } } } },
    { have : Iio (Î·/2) âˆˆ ğ“Ë¢ (Iic (0 : â„)),
      { rw mem_nhds_set_iff_forall,
        intros t ht,
        apply Iio_mem_nhds,
        exact lt_of_le_of_lt ht (half_pos Î·_pos) },
      apply mem_of_superset this,
      exact hÏ‡â‚ƒ },
    { have : Ioi (1 - Î·/2) âˆˆ ğ“Ë¢ (Ici (1 : â„)),
      { rw mem_nhds_set_iff_forall,
        intros t ht,
        apply Ioi_mem_nhds,
        apply lt_of_lt_of_le _ ht,
        linarith },
      apply mem_of_superset this,
      exact hÏ‡â‚„ } },
  have hÏ‡_1' : Ï‡ 1 = 1,
  { exact hÏ‡_1.on_set _ left_mem_Ici },
  have Hdiff : ğ’ âˆ (Î» p : â„ Ã— E, (Ï‡ p.1 * Ï p.2, p.2)),
  { exact (cont_diff_mul.comp (hÏ‡_smooth.prod_map hÏ_smooth)).prod cont_diff_snd },
  have Hdiff' : ğ’ âˆ (Î» p : â„ Ã— E, (Ï‡ p.1 * Ï p.2 - 1, p.2)),
  { exact ((cont_diff_mul.comp $ hÏ‡_smooth.prod_map hÏ_smooth).sub cont_diff_const).prod
          cont_diff_snd },
  have Hcont : continuous (Î» p : â„ Ã— E, Ï‡ p.1 * Ï p.2),
  { exact continuous_fst.comp Hdiff.continuous },
  have Hcont_sub : continuous (Î» p : â„ Ã— E, Ï‡ p.1 * Ï p.2 - 1),
  { exact Hcont.sub continuous_const },
  set ğ“•' : htpy_formal_sol R :=
  { f := Î» t x, if Ï‡ t * Ï x â‰¤ 1 then (ğ“• $ Ï‡ t * Ï x).f x else (ğ“– $ Ï‡ t * Ï x - 1).f x,
    f_diff := begin
      have H : ğ’ âˆ (Î» p : â„ Ã— E, (ğ“• $ Ï‡ p.1 * Ï p.2).f p.2),
      { exact (rel_loc.family_formal_sol.to_family_jet_sec ğ“•).cont_diff_f.comp Hdiff },
      have H' : ğ’ âˆ (Î» p : â„ Ã— E, (ğ“– $ Ï‡ p.1 * Ï p.2 - 1).f p.2),
      { exact (rel_loc.family_formal_sol.to_family_jet_sec ğ“–).cont_diff_f.comp Hdiff' },
      rw cont_diff_iff_cont_diff_at at *,
      rintros âŸ¨tâ‚€, xâ‚€âŸ©,
      rcases lt_trichotomy (Ï‡ tâ‚€ * Ï xâ‚€) 1 with ht|ht|ht,
      { apply (H (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), Ï‡ p.1 * Ï p.2 < 1,
        { exact (Hcont.continuous_at : continuous_at _ (tâ‚€, xâ‚€)).eventually' _
                (Iio_mem_nhds ht : Iio 1 âˆˆ ğ“ (Ï‡ tâ‚€ * Ï xâ‚€))},
        apply this.mono,
        rintros _ h,
        exact if_pos h.le },
      { apply (H (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have h : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), ğ“• (Ï‡ p.1 * Ï p.2) = ğ“• 1,
        { apply eventually.filter_mono (tendsto_iff_comap.mp Hcont.continuous_at),
          rw [eventually_comap, ht],
          rw eventually_nhds_set_iff at h_t_1,
          apply (h_t_1 1 left_mem_Ici).mono,
          rintros t ht p rfl,
          exact ht },
        have h' : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), ğ“– (Ï‡ p.1 * Ï p.2 - 1) = ğ“• 1,
        { apply eventually.filter_mono (tendsto_iff_comap.mp Hcont_sub.continuous_at),
          rw [eventually_comap, ht, sub_self],
          rw eventually_nhds_set_iff at hğ“–â‚€,
          apply (hğ“–â‚€ 0 left_mem_Ici).mono,
          rintros t ht p rfl,
          exact ht },
        apply (h.and h').mono,
        rintros âŸ¨t, xâŸ© htx,
        change (if _ then _ else _) = _,
        split_ifs,
        { refl },
        { rw [htx.2, htx.1] } },
      { apply (H' (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), 1 < Ï‡ p.1 * Ï p.2,
        { exact Hcont.continuous_at.eventually' _ (Ioi_mem_nhds ht) },
        apply this.mono,
        rintros _ h,
        exact if_neg h.not_le },
    end,
    Ï† := Î» t x, if Ï‡ t * Ï x â‰¤ 1 then (ğ“• $ Ï‡ t * Ï x).Ï† x else (ğ“– $ Ï‡ t * Ï x - 1).Ï† x,
    Ï†_diff := begin
      -- TODO: remove the huge code duplication here. Only the first two `have` differ from the
      -- previous smoothness proof.
      have H : ğ’ âˆ (Î» p : â„ Ã— E, (ğ“• $ Ï‡ p.1 * Ï p.2).Ï† p.2),
      { exact (rel_loc.family_formal_sol.to_family_jet_sec ğ“•).cont_diff_Ï†.comp Hdiff },
      have H' : ğ’ âˆ (Î» p : â„ Ã— E, (ğ“– $ Ï‡ p.1 * Ï p.2 - 1).Ï† p.2),
      { exact (rel_loc.family_formal_sol.to_family_jet_sec ğ“–).cont_diff_Ï†.comp Hdiff' },
      rw cont_diff_iff_cont_diff_at at *,
      rintros âŸ¨tâ‚€, xâ‚€âŸ©,
      rcases lt_trichotomy (Ï‡ tâ‚€ * Ï xâ‚€) 1 with ht|ht|ht,
      { apply (H (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), Ï‡ p.1 * Ï p.2 < 1,
        { exact (Hcont.continuous_at : continuous_at _ (tâ‚€, xâ‚€)).eventually' _
                (Iio_mem_nhds ht : Iio 1 âˆˆ ğ“ (Ï‡ tâ‚€ * Ï xâ‚€))},
        apply this.mono,
        rintros _ h,
        exact if_pos h.le },
      { apply (H (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have h : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), ğ“• (Ï‡ p.1 * Ï p.2) = ğ“• 1,
        { apply eventually.filter_mono (tendsto_iff_comap.mp Hcont.continuous_at),
          rw [eventually_comap, ht],
          rw eventually_nhds_set_iff at h_t_1,
          apply (h_t_1 1 left_mem_Ici).mono,
          rintros t ht p rfl,
          exact ht },
        have h' : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), ğ“– (Ï‡ p.1 * Ï p.2 - 1) = ğ“• 1,
        { apply eventually.filter_mono (tendsto_iff_comap.mp Hcont_sub.continuous_at),
          rw [eventually_comap, ht, sub_self],
          rw eventually_nhds_set_iff at hğ“–â‚€,
          apply (hğ“–â‚€ 0 left_mem_Ici).mono,
          rintros t ht p rfl,
          exact ht },
        apply (h.and h').mono,
        rintros âŸ¨t, xâŸ© htx,
        change (if _ then _ else _) = _,
        split_ifs,
        { refl },
        { rw [htx.2, htx.1] } },
      { apply (H' (tâ‚€, xâ‚€)).congr_of_eventually_eq,
        have : âˆ€á¶  p : â„ Ã— E in ğ“ (tâ‚€, xâ‚€), 1 < Ï‡ p.1 * Ï p.2,
        { exact Hcont.continuous_at.eventually' _ (Ioi_mem_nhds ht) },
        apply this.mono,
        rintros _ h,
        exact if_neg h.not_le }
    end,
  is_sol := begin
    intros t x,
    dsimp only,
    split_ifs,
    apply ğ“•.is_sol,
    apply ğ“–.is_sol
  end },
  have hğ“•'_apply : âˆ€ t x, ğ“•' t x = if Ï‡ t * Ï x â‰¤ 1 then ğ“• (Ï‡ t*Ï x) x else ğ“– (Ï‡ t * Ï x - 1) x,
  { intros t x,
    apply prod.ext,
    change (if _ then (ğ“• _).f x else (ğ“– _).f x) = _,
    split_ifs ; refl,
    change (if _ then (ğ“• _).Ï† x else (ğ“– _).Ï† x) = _,
    split_ifs ; refl },
  have hğ“•'_f_apply : âˆ€ t x, (ğ“•' t).f x = if Ï‡ t * Ï x â‰¤ 1 then (ğ“• $ Ï‡ t * Ï x).f x else (ğ“– $ Ï‡ t * Ï x - 1).f x,
  { intros t x,
    change (ğ“•' t x).1 = _,
    rw hğ“•'_apply,
    split_ifs ; refl },
  refine âŸ¨ğ“•', _, _, _, _, _, _âŸ©,
  { apply (hÏ‡_0.and h_t_0).mono,
    rintros t âŸ¨ht, ht'âŸ©,
    apply jet_sec.ext' (Î» x, _),
    rw hğ“•'_apply,
    simp only [ht, zero_mul, zero_le_one, if_true] },
  { apply (hÏ‡_1.and h_t_1).mono,
    rintros t âŸ¨ht, ht'âŸ©,
    apply jet_sec.ext' (Î» x, _),
    rw [hğ“•'_apply, ht, one_mul, hğ“•'_apply, hÏ‡_1.on_set 1 left_mem_Ici, one_mul] },
  { rw [nhds_set_union, eventually_sup] at hğ“–CA,
    apply (h_A.and hğ“–CA.2).mono (Î» x hx, _),
    intro t,
    rw [hğ“•'_apply],
    split_ifs,
    { apply hx.1.2 },
    { rw hx.2,
      apply hx.1.2 } },
  { intros t x hx,
    rw [hğ“•'_apply, hÏ_Kâ‚ x hx, mul_one, if_pos (hÏ‡_le _), hÏ‡_ğ“•] },
  { intros x t,
    by_cases H : Ï‡ t * Ï x â‰¤ 1,
    { left,
      simp only [hğ“•'_apply, if_pos H],
      exact âŸ¨_, rflâŸ© },
    { right,
      simp only [hğ“•'_f_apply, if_neg H],
      exact lt_of_le_of_lt (hğ“–dist _ _) (half_lt_self Îµ_pos) }, },
  { rw [nhds_set_union, eventually_sup] at hğ“–CA h_CA,
    apply filter.eventually.union,
    { apply ((hğ“–CA.2.and h_A).eventually_nhds_set.and h_CA.2).mono,
      rintro xâ‚€ hxâ‚€,
      apply hxâ‚€.2.congr,
      apply hxâ‚€.1.mono,
      rintros y âŸ¨hy, -, hy'âŸ©,
      rw [hğ“•'_apply],
      split_ifs,
      { rw [hy' (Ï‡ 1*_), hy'] },
      { rw hy,
        refl } },
    { apply filter.eventually.union,
      { -- TODO: the following can probably be simplified
        refine (hğ“–CA.1.eventually_nhds_set.and h_C).mono _,
        intros xâ‚€ hxâ‚€,
        by_cases H : Ï‡ 1 * Ï xâ‚€ â‰¤ 1,
        { apply hxâ‚€.2.congr,
          apply hxâ‚€.1.mono,
          rintros x hx,
          rw [hğ“•'_apply],
          split_ifs,
          { rw [hÏ‡_1', one_mul, show Ï x = 1, from h.antisymm (hÏ_ge _)] at h âŠ¢ },
          { rw hx,
            refl }, },
        { apply hxâ‚€.2.congr,
          apply (hxâ‚€.1).mono,
          rintros x hx,
          rw [hğ“•'_apply],
          split_ifs,
          rw [hÏ‡_1', one_mul, show Ï x = 1, from h.antisymm (hÏ_ge _)] at h âŠ¢,
          rw hx,
          refl } },
      { apply (hÏ_Kâ‚€.eventually_nhds_set.and hğ“–Kâ‚€).mono,
        rintros x âŸ¨hx, hx'âŸ©,
        apply hx'.congr,
        apply hx.mono,
        intros y hy,
        rw [hğ“•'_apply, if_neg, hy, hÏ‡_1'],
        congr' 2, ring,
        rw [hy, hÏ‡_1'],
        norm_num } } },
end

end improve
