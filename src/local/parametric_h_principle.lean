import local.h_principle

/-!
In this file we prove the parametric version of the local h-principle.

We will not use this to prove the global version of the h-principle, but we do use this to conclude
the existence of sphere eversion from the local h-principle, which is proven in `local.h_principle`.

The parametric h-principle states the following: Suppose that `R` is a local relation,
`ğ“•â‚€ : P â†’ JÂ¹(E, F)` is a family of formal solutions of `R` that is holonomic near some set
`C âŠ† P Ã— E`, `K âŠ† P Ã— E` is compact and `Îµ : â„`,
then there exists a homotopy `ğ“• : â„ Ã— P â†’ JÂ¹(E, F)` between `ğ“•` and a solution that is holonomic
near `K`, that agrees with `ğ“•â‚€` near `C` and is everywhere `Îµ`-close to `ğ“•â‚€`
-/

noncomputable theory

open metric finite_dimensional set function rel_loc linear_map (ker)
open_locale topological_space pointwise

section parameter_space

variables
{E : Type*} [normed_add_comm_group E] [normed_space â„ E]
{F : Type*} [normed_add_comm_group F] [normed_space â„ F]
-- `G` will be `â„` in the proof of the parametric h-principle.
-- It indicates the homotopy variable `t`.
{G : Type*} [normed_add_comm_group G] [normed_space â„ G]
{P : Type*} [normed_add_comm_group P] [normed_space â„ P]


variables {R : rel_loc E F}

/-- The projection `JÂ¹(P Ã— E, F) â†’ JÂ¹(E, F)`. -/
def one_jet_snd : one_jet (P Ã— E) F â†’ one_jet E F :=
Î» p, (p.1.2, p.2.1, p.2.2 âˆ˜L fderiv â„ (Î» y, (p.1.1, y)) p.1.2)

lemma continuous_one_jet_snd :
  continuous (one_jet_snd : one_jet (P Ã— E) F â†’ one_jet E F) :=
continuous_fst.snd.prod_mk $ continuous_snd.fst.prod_mk $ continuous_snd.snd.clm_comp $
  continuous.fderiv (cont_diff_fst.fst.prod_map cont_diff_id) continuous_fst.snd le_top

lemma one_jet_snd_eq (p : one_jet (P Ã— E) F) :
  one_jet_snd p = (p.1.2, p.2.1, p.2.2 âˆ˜L continuous_linear_map.inr â„ P E) :=
by simp_rw [one_jet_snd, fderiv_prod_right]

variables (P)
/-- The relation `R.relativize P` (`ğ“¡ ^ P` in the blueprint) is the relation on `JÂ¹(P Ã— E, F)`
induced by `R`. -/
def rel_loc.relativize (R : rel_loc E F) : rel_loc (P Ã— E) F :=
one_jet_snd â»Â¹' R
variables {P}

lemma rel_loc.mem_relativize (R : rel_loc E F) (w : one_jet (P Ã— E) F) :
 w âˆˆ R.relativize P â†” (w.1.2, w.2.1, w.2.2 âˆ˜L continuous_linear_map.inr â„ P E) âˆˆ R :=
by simp_rw [rel_loc.relativize, mem_preimage, one_jet_snd_eq]

lemma rel_loc.is_open_relativize (R : rel_loc E F) (h2 : is_open R) :
  is_open (R.relativize P) :=
h2.preimage continuous_one_jet_snd

lemma relativize_slice {Ïƒ : one_jet (P Ã— E) F}
  {p : dual_pair (P Ã— E)}
  (q : dual_pair E)
  (hpq : p.Ï€.comp (continuous_linear_map.inr â„ P E) = q.Ï€) :
  (R.relativize P).slice p Ïƒ =
  Ïƒ.2.2 (p.v - (0, q.v)) +áµ¥ R.slice q (one_jet_snd Ïƒ) :=
begin
  have h2pq : âˆ€ x : E, p.Ï€ ((0 : P), x) = q.Ï€ x := Î» x, congr_arg (Î» f : E â†’L[â„] â„, f x) hpq,
  ext1 w,
  have h1 : (p.update Ïƒ.2.2 w).comp (continuous_linear_map.inr â„ P E) =
    q.update (one_jet_snd Ïƒ).2.2 (-Ïƒ.2.2 (p.v - (0, q.v)) +áµ¥ w),
  { ext1 x,
    simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
      â† continuous_linear_map.map_neg, neg_sub],
    obtain âŸ¨u, hu, t, rflâŸ© := q.decomp x,
    have hv : (0, q.v) - p.v âˆˆ ker p.Ï€,
    { rw [linear_map.mem_ker, map_sub, p.pairing, h2pq, q.pairing, sub_self] },
    have hup : ((0 : P), u) âˆˆ ker p.Ï€ := (h2pq u).trans hu,
    rw [q.update_apply _ hu, â† prod.zero_mk_add_zero_mk, map_add, p.update_ker_pi _ _ hup,
      â† prod.smul_zero_mk, map_smul, vadd_eq_add],
    nth_rewrite 0 [â† sub_add_cancel (0, q.v) p.v],
    rw [map_add, p.update_ker_pi _ _ hv, p.update_v, one_jet_snd_eq],
    refl },
  have := preimage_vadd_neg (show F, from Ïƒ.2.2 (p.v - (0, q.v))) (R.slice q (one_jet_snd Ïƒ)),
  dsimp only at this,
  simp_rw [â† this, mem_preimage, mem_slice, R.mem_relativize, h1],
  refl,
end

lemma relativize_slice_eq_univ {Ïƒ : one_jet (P Ã— E) F}
  {p : dual_pair (P Ã— E)}
  (hp : p.Ï€.comp (continuous_linear_map.inr â„ P E) = 0) :
  ((R.relativize P).slice p Ïƒ).nonempty â†”
  (R.relativize P).slice p Ïƒ = univ :=
begin
  have h2p : âˆ€ x : E, p.Ï€ ((0 : P), x) = 0 := Î» x, congr_arg (Î» f : E â†’L[â„] â„, f x) hp,
  have : âˆ€ y : F, (p.update Ïƒ.2.2 y).comp (continuous_linear_map.inr â„ P E) =
    Ïƒ.2.2.comp (continuous_linear_map.inr â„ P E),
  { intro y,
    ext1 x,
    simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
      p.update_ker_pi _ _ (h2p x)] },
  simp_rw [set.nonempty, eq_univ_iff_forall, mem_slice, R.mem_relativize, this, exists_const,
    forall_const]
end

variables (P)

lemma rel_loc.is_ample.relativize (hR : R.is_ample) : (R.relativize P).is_ample :=
begin
  intros p Ïƒ,
  let p2 := p.Ï€.comp (continuous_linear_map.inr â„ P E),
  rcases eq_or_ne p2 0 with h|h,
  { intros w hw,
    rw [(relativize_slice_eq_univ h).mp âŸ¨w, hwâŸ©, connected_component_in_univ,
      preconnected_space.connected_component_eq_univ, convex_hull_univ] },
  obtain âŸ¨u', hu'âŸ© := continuous_linear_map.exists_ne_zero h,
  let u := (p2 u')â»Â¹ â€¢ u',
  let q : dual_pair E :=
  âŸ¨p2, u, by rw [p2.map_smul, smul_eq_mul, inv_mul_cancel hu']âŸ©,
  rw [relativize_slice q rfl],
  exact (hR q _).vadd
end

variables {P}

/-- Turn a family of sections of `JÂ¹(E, E')` parametrized by `P` into a section of `JÂ¹(P Ã— E, E')`.
-/
@[simps]
def family_jet_sec.uncurry (S : family_jet_sec E F P) : jet_sec (P Ã— E) F :=
{ f := Î» p, S.f p.1 p.2,
  Ï† := Î» p, fderiv â„ (Î» z : P Ã— E, S.f z.1 p.2) p +
    S.Ï† p.1 p.2 âˆ˜L fderiv â„ prod.snd p,
  f_diff := S.f_diff,
  Ï†_diff := begin
    refine (cont_diff.fderiv _ cont_diff_id le_top).add (S.Ï†_diff.clm_comp _),
    { exact S.f_diff.comp (cont_diff_snd.fst.prod cont_diff_fst.snd) },
    { exact cont_diff.fderiv cont_diff_snd.snd cont_diff_id le_top }
    end }

lemma family_jet_sec.uncurry_Ï†' (S : family_jet_sec E F P) (p : P Ã— E) :
  (S.uncurry).Ï† p = fderiv â„ (Î» z, S.f z p.2) p.1 âˆ˜L continuous_linear_map.fst â„ P E +
  S.Ï† p.1 p.2 âˆ˜L continuous_linear_map.snd â„ P E :=
begin
  simp_rw [S.uncurry_Ï†, fderiv_snd, add_left_inj],
  refine (fderiv_comp p
    ((S.f_diff.comp (cont_diff_id.prod cont_diff_const)).differentiable le_top p.1)
    differentiable_at_fst).trans _,
  rw [fderiv_fst],
  refl,
end

lemma family_jet_sec.uncurry_mem_relativize (S : family_jet_sec E F P) {s : P}
  {x : E} : ((s, x), S.uncurry (s, x)) âˆˆ R.relativize P â†” (x, S s x) âˆˆ R :=
begin
  simp_rw [rel_loc.relativize, mem_preimage, one_jet_snd_eq, jet_sec.coe_apply, S.uncurry_f,
    S.uncurry_Ï†'],
  congr' 2,
  refine prod.ext rfl (prod.ext rfl _),
  ext v,
  simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.add_apply,
    continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
    continuous_linear_map.coe_fst', continuous_linear_map.coe_snd',
    continuous_linear_map.map_zero, zero_add],
  refl,
end

lemma family_jet_sec.is_holonomic_at_uncurry (S : family_jet_sec E F P) {p : P Ã— E} :
  S.uncurry.is_holonomic_at p â†” (S p.1).is_holonomic_at p.2 :=
begin
  simp_rw [jet_sec.is_holonomic_at, S.uncurry_Ï†],
  rw [show S.uncurry.f = Î» x, S.uncurry.f x, from rfl, funext S.uncurry_f,
    show (Î» x : P Ã— E, S.f x.1 x.2) = â†¿S.f, from rfl],
  simp_rw [fderiv_prod_eq_add (S.f_diff.differentiable le_top _), fderiv_snd],
  refine (add_right_inj _).trans _,
  have := fderiv_comp p ((S p.1).f_diff.cont_diff_at.differentiable_at le_top)
    differentiable_at_snd,
  rw [show D (Î» (z : P Ã— E), â†¿(S.f) (p.fst, z.snd)) p = _, from this, fderiv_snd,
    (show surjective (continuous_linear_map.snd â„ P E), from prod.snd_surjective)
      .clm_comp_injective.eq_iff],
  refl
end

/-- Turn a family of formal solutions of `R âŠ† JÂ¹(E, E')` parametrized by `P` into a formal solution
of `R.relativize P`. -/
def rel_loc.family_formal_sol.uncurry (S : R.family_formal_sol P) : formal_sol (R.relativize P) :=
begin
  refine âŸ¨S.to_family_jet_sec.uncurry, _âŸ©,
  rintro âŸ¨s, xâŸ©,
  exact S.to_family_jet_sec.uncurry_mem_relativize.mpr (S.is_sol s x)
end

lemma rel_loc.family_formal_sol.uncurry_Ï†' (S : R.family_formal_sol P) (p : P Ã— E) :
  (S.uncurry p).2 = fderiv â„ (Î» z, S.f z p.2) p.1 âˆ˜L continuous_linear_map.fst â„ P E +
  S.Ï† p.1 p.2 âˆ˜L continuous_linear_map.snd â„ P E :=
S.to_family_jet_sec.uncurry_Ï†' p

/-- Turn a family of sections of `JÂ¹(P Ã— E, F)` parametrized by `G` into a family of sections of
`JÂ¹(E, F)` parametrized by `G Ã— P`. -/
def family_jet_sec.curry (S : family_jet_sec (P Ã— E) F G) :
  family_jet_sec E F (G Ã— P) :=
{ f := Î» p x, (S p.1).f (p.2, x),
  Ï† := Î» p x, (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (Î» x, (p.2, x)) x,
  f_diff := S.f_diff.comp (cont_diff_prod_assoc : cont_diff â„ âŠ¤ (equiv.prod_assoc G P E)),
  Ï†_diff := begin
    refine (S.Ï†_diff.comp (cont_diff_prod_assoc : cont_diff â„ âŠ¤ (equiv.prod_assoc G P E))).clm_comp
      _,
    refine cont_diff.fderiv _ cont_diff_snd le_top,
    exact cont_diff_fst.fst.snd.prod cont_diff_snd
  end }

lemma family_jet_sec.curry_f (S : family_jet_sec (P Ã— E) F G) (p : G Ã— P)
  (x : E) : (S.curry p).f x = (S p.1).f (p.2, x) :=
rfl

lemma family_jet_sec.curry_Ï† (S : family_jet_sec (P Ã— E) F G) (p : G Ã— P)
  (x : E) : (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (Î» x, (p.2, x)) x :=
rfl

lemma family_jet_sec.curry_Ï†' (S : family_jet_sec (P Ã— E) F G) (p : G Ã— P)
  (x : E) : (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L continuous_linear_map.inr â„ P E :=
begin
  rw [S.curry_Ï†],
  congr' 1,
  refine ((differentiable_at_const _).fderiv_prod differentiable_at_id).trans _,
  rw [fderiv_id, fderiv_const],
  refl,
end

lemma family_jet_sec.is_holonomic_at_curry
  (S : family_jet_sec (P Ã— E) F G)
  {t : G} {s : P} {x : E} (hS : (S t).is_holonomic_at (s, x)) :
  (S.curry (t, s)).is_holonomic_at x :=
begin
  simp_rw [jet_sec.is_holonomic_at, S.curry_Ï†] at hS âŠ¢,
  rw [show (S.curry (t, s)).f = Î» x, (S.curry (t, s)).f x, from rfl, funext (S.curry_f _)],
  dsimp only,
  refine (fderiv_comp x ((S t).f_diff.cont_diff_at.differentiable_at le_top)
    ((differentiable_at_const _).prod differentiable_at_id)).trans _,
  rw [id, hS],
  refl,
end

lemma family_jet_sec.curry_mem (S : family_jet_sec (P Ã— E) F G)
  {p : G Ã— P} {x : E} (hR : ((p.2, x), S p.1 (p.2, x)) âˆˆ R.relativize P) :
  (x, S.curry p x) âˆˆ R :=
begin
  simp_rw [rel_loc.relativize, mem_preimage, jet_sec.coe_apply, one_jet_snd_eq, S.curry_Ï†'] at hR âŠ¢,
  exact hR
end

/-- Turn a family of formal solutions of `R.relativize P` parametrized by `G` into a family of
formal solutions of `R` parametrized by `G Ã— P`. -/
def rel_loc.family_formal_sol.curry (S : family_formal_sol G (R.relativize P)) :
  family_formal_sol (G Ã— P) R :=
âŸ¨S.to_family_jet_sec.curry, Î» p x, S.to_family_jet_sec.curry_mem (S.is_sol _ _)âŸ©

lemma rel_loc.family_formal_sol.curry_Ï† (S : family_formal_sol G (R.relativize P)) (p : G Ã— P)
  (x : E) : (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (Î» x, (p.2, x)) x :=
rfl

lemma rel_loc.family_formal_sol.curry_Ï†' (S : family_formal_sol G (R.relativize P)) (p : G Ã— P)
  (x : E) : (S.curry p x).2 = (S p.1 (p.2, x)).2 âˆ˜L continuous_linear_map.inr â„ P E :=
S.to_family_jet_sec.curry_Ï†' p x

lemma curry_eq_iff_eq_uncurry {ğ“• : family_formal_sol G (R.relativize P)}
  {ğ“•â‚€ : R.family_formal_sol P} {t : G} {x : E} {s : P}
  (h : ğ“• t (s, x) = ğ“•â‚€.uncurry (s, x)) :
  (ğ“•.curry (t, s)) x = ğ“•â‚€ s x :=
begin
  simp_rw [prod.ext_iff] at h âŠ¢,
  refine âŸ¨h.1, _âŸ©,
  simp_rw [ğ“•.curry_Ï†', h.2, ğ“•â‚€.uncurry_Ï†'],
  ext v,
  simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.add_apply,
    continuous_linear_map.comp_apply,
    continuous_linear_map.inr_apply, continuous_linear_map.coe_fst',
    continuous_linear_map.coe_snd', continuous_linear_map.map_zero, zero_add],
  refl
end

end parameter_space

section parametric_h_principle

variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [finite_dimensional â„ E]
          {F : Type*} [normed_add_comm_group F] [normed_space â„ F] [finite_dimensional â„ F]
          {G : Type*} [normed_add_comm_group G] [normed_space â„ G]
          {P : Type*} [normed_add_comm_group P] [normed_space â„ P] [finite_dimensional â„ P]

variables {R : rel_loc E F} (h_op: is_open R) (h_ample: R.is_ample) (L : landscape E)
include h_op h_ample

/- The local parametric h-principle. -/
lemma rel_loc.family_formal_sol.improve_htpy
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (C : set (P Ã— E)) (hC : is_closed C) (K : set (P Ã— E)) (hK : is_compact K)
  (ğ“•â‚€ : family_formal_sol P R)
  (h_hol : âˆ€á¶  (p : P Ã— E) near C, (ğ“•â‚€ p.1).is_holonomic_at p.2) :
  âˆƒ ğ“• : family_formal_sol (â„ Ã— P) R,
    (âˆ€ s x, ğ“• (0, s) x = ğ“•â‚€ s x) âˆ§
    (âˆ€á¶  (p : P Ã— E) near C, âˆ€ t, ğ“• (t, p.1) p.2 = ğ“•â‚€ p.1 p.2) âˆ§
    (âˆ€ s x t, âˆ¥(ğ“• (t, s)).f x - ğ“•â‚€.f s xâˆ¥ â‰¤ Îµ)  âˆ§
    (âˆ€á¶  (p : P Ã— E) near K, (ğ“• (1, p.1)).is_holonomic_at p.2) :=
begin
  let parametric_landscape : landscape (P Ã— E) :=
  { C := C,
    Kâ‚€ := K,
    Kâ‚ := (exists_compact_superset hK).some,
    hC := hC,
    hKâ‚€ := hK,
    hKâ‚ := (exists_compact_superset hK).some_spec.1,
    hâ‚€â‚ := (exists_compact_superset hK).some_spec.2 },
  obtain âŸ¨ğ“•, hâ‚, -, hâ‚‚, -, hâ‚„, hâ‚…âŸ© :=
    ğ“•â‚€.uncurry.improve_htpy (R.is_open_relativize h_op) (h_ample.relativize P)
    parametric_landscape Îµ_pos (h_hol.mono (Î» p hp, ğ“•â‚€.is_holonomic_at_uncurry.mpr hp)),
  have hâ‚ : âˆ€ p, ğ“• 0 p = ğ“•â‚€.uncurry p,
  { intro p, rw hâ‚.on_set 0 right_mem_Iic, refl },
  refine âŸ¨ğ“•.curry, _, _, _, _âŸ©,
  { intros s x, exact curry_eq_iff_eq_uncurry (hâ‚ (s, x)) },
  { refine hâ‚‚.mono _, rintro âŸ¨s, xâŸ© hp t, exact curry_eq_iff_eq_uncurry (hp t) },
  { intros s x t, exact (hâ‚„ (s, x) t) },
  { refine hâ‚….mono _, rintros âŸ¨s, xâŸ© hp, exact ğ“•.to_family_jet_sec.is_holonomic_at_curry hp }
end

open filter
open_locale unit_interval

/--
A corollary of the local parametric h-principle, forgetting the homotopy and `Îµ`-closeness,
and just stating the existence of a solution that is holonomic near `K`.
Furthermore, we assume that `P = â„` and `K` is of the form `compact set Ã— I`.
This is sufficient to prove sphere eversion. -/
lemma rel_loc.htpy_formal_sol.exists_sol (ğ“•â‚€ : R.htpy_formal_sol)
  (C : set (â„ Ã— E)) (hC : is_closed C) (K : set E) (hK : is_compact K)
  (h_hol : âˆ€á¶  (p : â„ Ã— E) near C, (ğ“•â‚€ p.1).is_holonomic_at p.2) :
  âˆƒ f : â„ â†’ E â†’ F,
    (ğ’ âˆ $ uncurry f) âˆ§
    (âˆ€ p âˆˆ C, f (p : â„ Ã— E).1 p.2 = (ğ“•â‚€ p.1).f p.2) âˆ§
    (âˆ€ x âˆˆ K, âˆ€ t âˆˆ I, (x, f t x, D (f t) x) âˆˆ R) :=
begin
  obtain âŸ¨ğ“•, hâ‚, hâ‚‚, -, hâ‚„âŸ© :=
    ğ“•â‚€.improve_htpy h_op h_ample zero_lt_one C hC (I Ã—Ë¢ K) (is_compact_Icc.prod hK) h_hol,
  refine âŸ¨Î» s, (ğ“• (1, s)).f, _, _, _âŸ©,
  { exact ğ“•.f_diff.comp ((cont_diff_const.prod cont_diff_id).prod_map cont_diff_id) },
  { intros p hp, exact (prod.ext_iff.mp (hâ‚‚.nhds_set_forall_mem p hp 1)).1 },
  { intros x hx t ht,
    rw [show D (ğ“• (1, t)).f x = (ğ“• (1, t)).Ï† x, from
      hâ‚„.nhds_set_forall_mem (t, x) (mk_mem_prod ht hx)],
    exact ğ“•.is_sol (1, t) x },
end

end parametric_h_principle
