import order.filter.germ
import topology.constructions

import topology.connected
import topology.separation

import to_mathlib.topology.nhds_set

open_locale topology
open filter set

/-- The value associated to a germ at a point. This is the common value
shared by all representatives at the given point. -/
def filter.germ.value {X Î± : Type*} [topological_space X] {x : X} (Ï† : germ (ğ“ x) Î±) : Î± :=
quotient.lift_on' Ï† (Î» f, f x) (Î» f g h, by { dsimp only, rw eventually.self_of_nhds h })

/-- Given a predicate on germs `P : Î  x : X, germ (ğ“ x) Y â†’ Prop` and `A : set X`,
build a new predicate on germs `restrict_germ_predicate P A` such that
`(âˆ€ x, restrict_germ_predicate P A x f) â†” âˆ€á¶  x near A, P x f`, see
`forall_restrict_germ_predicate_iff` for this equivalence. -/
def restrict_germ_predicate {X Y : Type*} [topological_space X]
  (P : Î  x : X, germ (ğ“ x) Y â†’ Prop) (A : set X) : Î  x : X, germ (ğ“ x) Y â†’ Prop :=
Î» x Ï†, quotient.lift_on' Ï† (Î» f, x âˆˆ A â†’ âˆ€á¶  y in ğ“ x, P y f) begin
  have : âˆ€ f f' : X â†’ Y, f =á¶ [ğ“ x] f' â†’ (âˆ€á¶  y in ğ“ x, P y f) â†’ âˆ€á¶  y in ğ“ x, P y f',
  { intros f f' hff' hf,
    apply (hf.and $ eventually.eventually_nhds hff').mono,
    rintros y âŸ¨hy, hy'âŸ©,
    rwa germ.coe_eq.mpr (eventually_eq.symm hy') },
  exact Î» f f' hff', propext $ forall_congr $ Î» _, âŸ¨this f f' hff', this f' f hff'.symmâŸ©,
end

lemma filter.eventually.germ_congr {X Y : Type*} [topological_space X]
  {P : Î  x : X, germ (ğ“ x) Y â†’ Prop} {A : set X} {f g : X â†’ Y}
  (hf : âˆ€á¶  x in ğ“Ë¢ A, P x f) (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) : âˆ€á¶  x in ğ“Ë¢ A, P x g :=
begin
  rw eventually_nhds_set_iff at *,
  intros x hx,
  apply ((hf x hx).and (h x hx).eventually_nhds).mono,
  intros y hy,
  convert hy.1 using 1,
  apply quotient.sound,
  exact hy.2
end


lemma restrict_germ_predicate_congr {X Y : Type*} [topological_space X]
  {P : Î  x : X, germ (ğ“ x) Y â†’ Prop} {A : set X} {f g : X â†’ Y} {x : X}
  (hf : restrict_germ_predicate P A x f) (h : âˆ€á¶  z in ğ“Ë¢ A, g z = f z) :
  restrict_germ_predicate P A x g :=
begin
  intros hx,
  apply ((hf hx).and $ eventually_nhds_set_iff.mp h x hx).eventually_nhds.mono,
  intros y hy,
  rw eventually_and at hy,
  convert hy.1.self_of_nhds using 1,
  apply quotient.sound,
  exact hy.2,
end


lemma forall_restrict_germ_predicate_iff {X Y : Type*} [topological_space X]
  {P : Î  x : X, germ (ğ“ x) Y â†’ Prop} {A : set X} {f : X â†’ Y} :
  (âˆ€ x, restrict_germ_predicate P A x f) â†” âˆ€á¶  x in ğ“Ë¢ A, P x f :=
by { rw eventually_nhds_set_iff, exact iff.rfl }

lemma  forall_restrict_germ_predicate_of_forall {X Y : Type*} [topological_space X]
  {P : Î  x : X, germ (ğ“ x) Y â†’ Prop} {A : set X} {f : X â†’ Y} (h : âˆ€ x, P x f) :
  âˆ€ x, restrict_germ_predicate P A x f :=
forall_restrict_germ_predicate_iff.mpr (eventually_of_forall h)

lemma filter.eventually_eq.comp_fun {Î± Î² Î³ : Type*} {f g : Î² â†’ Î³} {l : filter Î±} {l' : filter Î²}
  (h : f =á¶ [l'] g) {Ï† : Î± â†’ Î²} (hÏ† : tendsto Ï† l l') : f âˆ˜ Ï† =á¶ [l] g âˆ˜ Ï† :=
hÏ† h

def filter.germ.slice_left {X Y Z : Type*} [topological_space X] [topological_space Y] {p : X Ã— Y}
  (P : germ (ğ“ p) Z) : germ (ğ“ p.1) Z :=
P.lift_on (Î» f, ((Î» x', f (x', p.2)) : germ (ğ“ p.1) Z))
  (Î» f g hfg, @quotient.sound _ ((ğ“ p.1).germ_setoid Z) _ _
     (hfg.comp_fun begin
       rw â† (prod.mk.eta : (p.1, p.2) = p),
       exact (continuous.prod.mk_left p.2).continuous_at,
     end))

def filter.germ.slice_right {X Y Z : Type*} [topological_space X] [topological_space Y] {p : X Ã— Y}
  (P : germ (ğ“ p) Z) : germ (ğ“ p.2) Z :=
P.lift_on (Î» f, ((Î» y, f (p.1, y)) : germ (ğ“ p.2) Z))
  (Î» f g hfg, @quotient.sound _ ((ğ“ p.2).germ_setoid Z) _ _
     (hfg.comp_fun begin
       rw â† (prod.mk.eta : (p.1, p.2) = p),
       exact (continuous.prod.mk p.1).continuous_at,
     end))

def filter.germ.is_constant {X Y : Type*} [topological_space X] {x} (P : germ (ğ“ x) Y) : Prop :=
P.lift_on (Î» f, âˆ€á¶  x' in ğ“ x, f x' = f x) begin
  suffices : âˆ€ (f g : X â†’ Y), f =á¶ [ğ“ x] g â†’
     (âˆ€á¶  x' in ğ“ x, f x' = f x) â†’ âˆ€á¶  x' in ğ“ x, g x' = g x,
  from Î» f g hfg, propext âŸ¨Î» h, this f g hfg h, Î» h, this g f hfg.symm hâŸ©,
  rintros f g hfg hf,
  apply (hf.and hfg).mono (Î» x' hx', _),
  rw [â† hx'.2, hx'.1, hfg.eq_of_nhds],
end

lemma eq_of_germ_is_constant {X Y : Type*} [topological_space X] [preconnected_space X]
  {f : X â†’ Y} (h : âˆ€ x : X, (f : germ (ğ“ x) Y).is_constant) (x x' : X) : f x = f x' :=
begin
  revert x,
  erw â† eq_univ_iff_forall,
  apply is_clopen.eq_univ _ (âŸ¨x', rflâŸ© : {x | f x = f x'}.nonempty),
  refine âŸ¨is_open_iff_eventually.mpr (Î» x hx, hx â–¸ h x), _âŸ©,
  rw is_closed_iff_frequently,
  rintros x hx,
  rcases (eventually.and_frequently (h x) hx).exists with âŸ¨x'', HâŸ©,
  exact H.1.symm.trans H.2
end

lemma eq_of_germ_is_constant_on {X Y : Type*} [topological_space X]
  {f : X â†’ Y} {s : set X} (h : âˆ€ x âˆˆ s, (f : germ (ğ“ x) Y).is_constant)
  (hs : is_preconnected s) {x x' : X} (x_in : x âˆˆ s) (x'_in : x' âˆˆ s) : f x = f x' :=
begin
  haveI := is_preconnected_iff_preconnected_space.mp hs,
  let F : s â†’ Y := f âˆ˜ coe,
  change F âŸ¨x, x_inâŸ© = F âŸ¨x', x'_inâŸ©,
  apply eq_of_germ_is_constant,
  rintros âŸ¨x, hxâŸ©,
  have : continuous_at (coe : s â†’ X) âŸ¨x, hxâŸ©,
  exact continuous_at_subtype_coe,
  exact this (h x hx)
end
