/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import to_mathlib.analysis.inner_product_space
import to_mathlib.linear_algebra.finrank
import analysis.inner_product_space.dual
import analysis.inner_product_space.orientation

/-! # The cross-product on an oriented real inner product space of dimension three -/

noncomputable theory

open_locale real_inner_product_space
open finite_dimensional

local attribute [instance] fact_finite_dimensional_of_finrank_eq_succ

variables (E : Type*) [inner_product_space â„ E]

/-- The identification of a finite-dimensional inner product space with its algebraic dual. -/
private def to_dual [finite_dimensional â„ E] : E â‰ƒâ‚—[â„] (E â†’â‚—[â„] â„) :=
(inner_product_space.to_dual â„ E).to_linear_equiv â‰ªâ‰«â‚— linear_map.to_continuous_linear_map.symm

namespace orientation
variables {E} [fact (finrank â„ E = 3)] (Ï‰ : orientation â„ E (fin 3))

include Ï‰

/-- Linear map from `E` to `E â†’â‚—[â„] E` constructed from a 3-form `Î©` on `E` and an identification of
`E` with its dual.  Effectively, the Hodge star operation.  (Under appropriate hypotheses it turns
out that the image of this map is in `ğ”°ğ”¬(E)`, the skew-symmetric operators, which can be identified
with `Î›Â²E`.) -/
def cross_product : E â†’â‚—[â„] (E â†’â‚—[â„] E) :=
begin
  let z : alternating_map â„ E â„ (fin 0) â‰ƒâ‚—[â„] â„ :=
    alternating_map.const_linear_equiv_of_is_empty.symm,
  let y : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E â†’â‚—[â„] â„ :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 0)) â„ z) âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  let y' : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E :=
    (linear_map.llcomp â„ (alternating_map â„ E â„ (fin 1)) (E â†’â‚—[â„] â„) E (to_dual E).symm) y,
  let u : alternating_map â„ E â„ (fin 2) â†’â‚—[â„] E â†’â‚—[â„] E :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 1)) _ y') âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  exact u âˆ˜â‚— (alternating_map.curry_left_linear_map Ï‰.volume_form),
end

local infix `Ã—â‚ƒ`:100 := Ï‰.cross_product

lemma cross_product_apply_self (v : E) : v Ã—â‚ƒ v = 0 := by simp [cross_product]

lemma inner_cross_product_apply (u v w : E) : âŸªu Ã—â‚ƒ v, wâŸ« = Ï‰.volume_form ![u, v, w] :=
by simp only [cross_product, to_dual, linear_equiv.trans_symm, linear_equiv.symm_symm,
  linear_isometry_equiv.to_linear_equiv_symm, alternating_map.curry_left_linear_map_apply,
  linear_map.coe_comp, function.comp_app, linear_map.llcomp_apply,
  linear_equiv.coe_coe, linear_equiv.trans_apply, linear_isometry_equiv.coe_to_linear_equiv,
  linear_isometry_equiv.norm_map, submodule.coe_norm,
  inner_product_space.to_dual_symm_apply, alternating_map.curry_left_apply_apply,
  alternating_map.const_linear_equiv_of_is_empty_symm_apply,
  eq_self_iff_true, linear_map.coe_to_continuous_linear_map', matrix.zero_empty]

lemma inner_cross_product_apply_self (u : E) (v : (â„ âˆ™ u)á—®) : âŸªu Ã—â‚ƒ v, uâŸ« = 0 :=
begin
  rw Ï‰.inner_cross_product_apply u v u,
  refine Ï‰.volume_form.map_eq_zero_of_eq ![u, v, u] _ (_ : (0 : fin 3) â‰  2),
  { simp },
  { norm_num }
end

lemma inner_cross_product_apply_apply_self (u : E) (v : (â„ âˆ™ u)á—®) : âŸªu Ã—â‚ƒ v, vâŸ« = 0 :=
begin
  rw Ï‰.inner_cross_product_apply u v v,
  refine Ï‰.volume_form.map_eq_zero_of_eq ![u, v, v] _ (_ : (1 : fin 3) â‰  2),
  { simp },
  { norm_num }
end

attribute [irreducible] cross_product

/-- The map `cross_product`, upgraded from linear to continuous-linear; useful for calculus. -/
def cross_product' : E â†’L[â„] (E â†’L[â„] E) :=
(â†‘(linear_map.to_continuous_linear_map : (E â†’â‚—[â„] E) â‰ƒâ‚—[â„] (E â†’L[â„] E))
  âˆ˜â‚— (Ï‰.cross_product)).to_continuous_linear_map

@[simp] lemma cross_product'_apply (v : E) : Ï‰.cross_product' v = (Ï‰.cross_product v).to_continuous_linear_map := rfl

lemma norm_cross_product (u : E) (v : (â„ âˆ™ u)á—®) : âˆ¥u Ã—â‚ƒ vâˆ¥ = âˆ¥uâˆ¥ * âˆ¥vâˆ¥ :=
begin
  classical,
  refine le_antisymm _ _,
  { cases eq_or_lt_of_le (norm_nonneg (u Ã—â‚ƒ v)) with h h,
    { rw â† h,
      positivity },
    refine le_of_mul_le_mul_right' _ h,
    rw â† real_inner_self_eq_norm_mul_norm,
    simpa only [inner_cross_product_apply, fin.mk_zero, fin.prod_univ_succ, finset.card_singleton,
      finset.prod_const, fintype.univ_of_subsingleton, matrix.cons_val_fin_one, matrix.cons_val_succ,
      matrix.cons_val_zero, mul_assoc, nat.nat_zero_eq_zero, pow_one, submodule.coe_norm]
      using Ï‰.volume_form_apply_le ![u, v, u Ã—â‚ƒ v] },
  let K : submodule â„ E := submodule.span â„ ({u, v} : set E),
  haveI : nontrivial Ká—®,
  { apply @finite_dimensional.nontrivial_of_finrank_pos â„,
    have : finrank â„ K â‰¤ _ := finrank_span_insert_le {(v:E)} u,
    have : finrank â„ _ â‰¤ 1 := finrank_span_singleton_le (v:E),
    have : finrank â„ K + finrank â„ Ká—® = finrank â„ E := K.finrank_add_finrank_orthogonal,
    have : finrank â„ E = 3 := fact.out _,
    linarith },
  obtain âŸ¨w, hwâŸ© : âˆƒ w : Ká—®, w â‰  0 := exists_ne 0,
  have hw' : (w:E) â‰  0 := Î» h, hw (submodule.coe_eq_zero.mp h),
  have H : pairwise (Î» i j, âŸª![u, v, w] i, ![u, v, w] jâŸ« = 0),
  { intros i j hij,
    have h1 : âŸªu, vâŸ« = 0 := v.2 _ (submodule.mem_span_singleton_self _),
    have h2 : âŸª(v:E), wâŸ« = 0 := w.2 _ (submodule.subset_span (by simp)),
    have h3 : âŸªu, wâŸ« = 0 := w.2 _ (submodule.subset_span (by simp)),
    fin_cases i; fin_cases j; norm_num at hij; simp [h1, h2, h3]; rw real_inner_comm; assumption },
  refine le_of_mul_le_mul_right' _ (by rwa norm_pos_iff : 0 < âˆ¥wâˆ¥),
  -- Cauchy-Schwarz inequality for `u Ã—â‚ƒ v` and `w`
  simpa only [inner_cross_product_apply, Ï‰.abs_volume_form_apply_of_pairwise_orthogonal H,
    inner_cross_product_apply, fin.mk_zero, fin.prod_univ_succ, finset.card_singleton,
    finset.prod_const, fintype.univ_of_subsingleton, matrix.cons_val_fin_one, matrix.cons_val_succ,
    matrix.cons_val_zero, nat.nat_zero_eq_zero, pow_one, mul_assoc]
    using abs_real_inner_le_norm (u Ã—â‚ƒ v) w,
end

lemma isometry_on_cross_product (u : metric.sphere (0:E) 1) (v : (â„ âˆ™ (u:E))á—®) : âˆ¥u Ã—â‚ƒ vâˆ¥ = âˆ¥vâˆ¥ :=
by simp [norm_cross_product]

end orientation
