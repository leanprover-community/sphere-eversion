import analysis.calculus.specific_functions
import to_mathlib.topology.misc

noncomputable theory

open set function filter
open_locale topological_space

lemma is_compact.bdd_above_norm {X : Type*} [topological_space X] {E : Type*} [normed_add_comm_group E]
  {s : set X} (hs : is_compact s) {f : X â†’ E} (hf : continuous f) : âˆƒ M > 0, âˆ€ x âˆˆ s, âˆ¥f xâˆ¥ â‰¤ M :=
begin
  cases (hs.image (continuous_norm.comp hf)).bdd_above with M hM,
  refine âŸ¨max 1 M, zero_lt_one.trans_le (le_max_left _ _), Î» x hx, _âŸ©,
  exact le_max_of_le_right (hM (set.mem_image_of_mem (norm âˆ˜ f) hx))
end

namespace real

lemma smooth_transition_proj_I {x : â„} :
  smooth_transition (proj_I x) = smooth_transition x :=
begin
  cases le_total (0 : â„) x with hx hx,
  cases le_total (1 : â„) x with h2x h2x,
  { rw [proj_I_eq_one.mpr h2x, smooth_transition.one_of_one_le h2x,
      smooth_transition.one_of_one_le le_rfl], },
  { rw [proj_I_eq_self.mpr âŸ¨hx, h2xâŸ©] },
  { rw [proj_I_eq_zero.mpr hx, smooth_transition.zero_of_nonpos hx,
      smooth_transition.zero_of_nonpos le_rfl], }
end

lemma smooth_transition.continuous_at {x : â„} : continuous_at smooth_transition x :=
smooth_transition.continuous.continuous_at

end real

section cont_diff_fderiv
/-! In this section we prove that the derivative of a parametric function is smooth, assuming the
  input function is smooth enough. We also do this for `cont_diff_within_at` and `fderiv_within`
  (needed for manifolds)
  We also need some random other lemmas that we didn't bother to put in the right place yet. -/


namespace set

variables {Î± Î² Î³ Î´ : Type*}
lemma image_prod_mk_subset_prod {f : Î± â†’ Î²} {g : Î± â†’ Î³} {s : set Î±} :
  (Î» x, (f x, g x)) '' s âŠ† (f '' s) Ã—Ë¢ (g '' s) :=
by { rintros _ âŸ¨x, hx, rflâŸ©, exact mk_mem_prod (mem_image_of_mem f hx) (mem_image_of_mem g hx) }

lemma maps_to.subset_preimage {f : Î± â†’ Î²} {s : set Î±} {t : set Î²} (hf : maps_to f s t) :
  s âŠ† f â»Â¹' t := hf

end set
open set


namespace asymptotics
variables {Î± E F E' F' : Type*} [topological_space Î±] [has_norm E] [has_norm E']
variables [seminormed_add_comm_group F] [seminormed_add_comm_group F']
variables {x : Î±} {s : set Î±}

lemma is_O_with_insert {C : â„} {g : Î± â†’ E} {g' : Î± â†’ E'} (h : âˆ¥g xâˆ¥ â‰¤ C * âˆ¥g' xâˆ¥) :
  is_O_with C (ğ“[insert x s] x) g g' â†” is_O_with C (ğ“[s] x) g g' :=
by simp_rw [is_O_with, nhds_within_insert, eventually_sup, eventually_pure, h, true_and]

lemma is_o_insert {g : Î± â†’ F} {g' : Î± â†’ F'} (h : g x = 0) :
  g =o[ğ“[insert x s] x] g' â†” g =o[ğ“[s] x] g' :=
begin
  simp_rw [is_o],
  refine forall_congr (Î» c, forall_congr (Î» hc, _)),
  rw [is_O_with_insert],
  rw [h, norm_zero],
  exact mul_nonneg hc.le (norm_nonneg _)
end

end asymptotics


section fderiv

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
variables {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
variables {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
variables {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]
variables {G' : Type*} [normed_add_comm_group G'] [normed_space ğ•œ G']


section -- basic topology

variables {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] {x y : Î±} {s s' t : set Î±}

lemma insert_mem_nhds_within_of_subset_insert [t1_space Î±] (hu : t âŠ† insert y s) :
  insert x s âˆˆ ğ“[t] x :=
begin
  rcases eq_or_ne x y with rfl|h,
  { exact mem_of_superset self_mem_nhds_within hu },
  rw [â† union_singleton, union_comm, â† diff_subset_iff, diff_eq] at hu,
  exact mem_of_superset (inter_mem_nhds_within _ (compl_singleton_mem_nhds h))
    (hu.trans (subset_insert _ _)),
end

lemma prod_mem_nhds_within {t t' : set Î²} {x : Î± Ã— Î²}
  (hs : s âˆˆ ğ“[s'] x.1) (ht : t âˆˆ ğ“[t'] x.2) : s Ã—Ë¢ t âˆˆ ğ“[s' Ã—Ë¢ t'] x :=
begin
  rw [mem_nhds_within] at hs ht âŠ¢,
  obtain âŸ¨u, hu, hxu, husâŸ© := hs,
  obtain âŸ¨v, hv, hxv, hvtâŸ© := ht,
  exact âŸ¨u Ã—Ë¢ v, hu.prod hv, âŸ¨hxu, hxvâŸ©, prod_inter_prod.subset.trans $ prod_mono hus hvtâŸ©,
end


end

section

variables {f : E â†’ F} {s : set E} {x : E} {f' : E â†’L[ğ•œ] F} {n : â„•âˆ}

theorem has_fderiv_within_at.comp_of_mem {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : set F}
  (hg : has_fderiv_within_at g g' t (f x)) (hf : has_fderiv_within_at f f' s x)
  (hst : tendsto f (ğ“[s] x) (ğ“[t] f x)) :
  has_fderiv_within_at (g âˆ˜ f) (g'.comp f') s x :=
has_fderiv_at_filter.comp x hg hf hst

lemma cont_diff_within_at.comp_of_mem
  {s : set E} {t : set F} {g : F â†’ G} {f : E â†’ F} (x : E)
  (hg : cont_diff_within_at ğ•œ n g t (f x))
  (hf : cont_diff_within_at ğ•œ n f s x) (hs : t âˆˆ ğ“[f '' s] f x) :
  cont_diff_within_at ğ•œ n (g âˆ˜ f) s x :=
(hg.mono_of_mem hs).comp x hf (subset_preimage_image f s)

end


variables {f : G â†’ E â†’ F} {g : G â†’ E} {s : set G} {t : set E} {x : G} {n m : â„•âˆ} {u : set (G Ã— E)}


lemma has_fderiv_within_at_insert {g' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at g g' (insert x s) x â†” has_fderiv_within_at g g' s x :=
begin
  simp_rw [has_fderiv_within_at, has_fderiv_at_filter],
  rw [asymptotics.is_o_insert],
  simp only [sub_self, g'.map_zero]
end

alias has_fderiv_within_at_insert â†” has_fderiv_within_at.of_insert has_fderiv_within_at.insert

lemma cont_diff_within_at_insert :
  cont_diff_within_at ğ•œ n g (insert x s) x â†” cont_diff_within_at ğ•œ n g s x :=
by simp_rw [cont_diff_within_at, insert_eq_of_mem (mem_insert _ _)]

alias cont_diff_within_at_insert â†” cont_diff_within_at.of_insert cont_diff_within_at.insert

lemma has_fderiv_within_at_diff_singleton {g' : G â†’L[ğ•œ] E} :
  has_fderiv_within_at g g' (s \ {x}) x â†” has_fderiv_within_at g g' s x :=
by rw [â† has_fderiv_within_at_insert, insert_diff_singleton, has_fderiv_within_at_insert]

-- replaces 2 mathlib lemmas
lemma cont_diff_within_at_succ_iff_has_fderiv_within_at_of_mem' {f : E â†’ F} {s : set E} {x : E}
  {n : â„•} :
  cont_diff_within_at ğ•œ (n + 1 : â„•) f s x
  â†” âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ âˆƒ f' : E â†’ E â†’L[ğ•œ] F,
    (âˆ€ x âˆˆ u, has_fderiv_within_at f (f' x) s x) âˆ§ cont_diff_within_at ğ•œ n f' s x :=
begin
  refine âŸ¨Î» hf, hf.has_fderiv_within_at_nhds, _âŸ©,
  rw [â† cont_diff_within_at_insert, cont_diff_within_at_succ_iff_has_fderiv_within_at,
    insert_eq_of_mem (mem_insert _ _)],
  rintro âŸ¨u, hu, hus, f', huf', hf'âŸ©,
  refine âŸ¨u, hu, f', Î» y hy, (huf' y hy).insert.mono_of_mem _, hf'.insert.mono husâŸ©,
  exact insert_mem_nhds_within_of_subset_insert hus,
end

/-- One direction of `cont_diff_within_at_succ_iff_has_fderiv_within_at`, but where all derivatives
  are taken within the same set. Version for partial derivatives. -/
lemma cont_diff_within_at.has_fderiv_within_at_nhdsâ‚‚ {n : â„•}
  (hf : cont_diff_within_at ğ•œ (n+1) (function.uncurry f) u (x, g x))
  (hg : cont_diff_within_at ğ•œ n g s x)
  (hst : insert x s Ã—Ë¢ t âŠ† u) -- can be weakened to only consider points near `(x, g x)`
  (hu : u âˆˆ ğ“[(Î» x, (x, g x)) '' s] (x, g x)) :
  âˆƒ u âˆˆ ğ“[insert x s] x, u âŠ† insert x s âˆ§ âˆƒ f' : G â†’ E â†’L[ğ•œ] F,
    (âˆ€ x âˆˆ u, has_fderiv_within_at (f x) (f' x) t (g x)) âˆ§
    cont_diff_within_at ğ•œ n (Î» x, f' x) s x :=
begin
  obtain âŸ¨v, hv, hvs, f', hvf', hf'âŸ© := hf.has_fderiv_within_at_nhds,
  refine âŸ¨(Î» z, (z, g z)) â»Â¹' v âˆ© insert x s, _, inter_subset_right _ _,
    Î» z, (f' (z, g z)).comp (continuous_linear_map.inr ğ•œ G E), _, _âŸ©,
  { refine inter_mem _ self_mem_nhds_within,
    have := mem_of_mem_nhds_within (mem_insert _ _) hv,
    refine mem_nhds_within_insert.mpr âŸ¨this, _âŸ©,
    refine (continuous_within_at_id.prod hg.continuous_within_at).preimage_mem_nhds_within' _,
    rw [â† nhds_within_le_iff] at hu hv âŠ¢,
    refine (hu.trans $ nhds_within_mono _ $ subset_insert _ _).trans hv },
  { intros z hz,
    have := hvf' (z, g z) hz.1,
    refine this.comp _ (has_fderiv_at_prod_mk_right _ _).has_fderiv_within_at _,
    exact maps_to'.mpr ((image_prod_mk_subset_prod_right hz.2).trans hst) },
  { exact (hf'.continuous_linear_map_comp $ (continuous_linear_map.compL ğ•œ E (G Ã— E) F).flip
      (continuous_linear_map.inr ğ•œ G E)).comp_of_mem x
      (cont_diff_within_at_id.prod hg) hu },
end

-- simplify/replace mathlib lemmas using this
lemma cont_diff_within_at.fderiv_withinâ‚‚'
  (hf : cont_diff_within_at ğ•œ n (function.uncurry f) u (x, g x))
  (hg : cont_diff_within_at ğ•œ m g s x)
  (ht : âˆ€á¶  y in ğ“[insert x s] x, unique_diff_within_at ğ•œ t (g y))
  (hmn : m + 1 â‰¤ n)
  (hst : insert x s Ã—Ë¢ t âŠ† u)
  (hu : u âˆˆ ğ“[(Î» x, (x, g x)) '' s] (x, g x)) :
  cont_diff_within_at ğ•œ m (Î» x, fderiv_within ğ•œ (f x) t (g x)) s x :=
begin
  have : âˆ€ k : â„•, (k : with_top â„•) â‰¤ m â†’
    cont_diff_within_at ğ•œ k (Î» x, fderiv_within ğ•œ (f x) t (g x)) s x,
  { intros k hkm,
    obtain âŸ¨v, hv, -, f', hvf', hf'âŸ© :=
      (hf.of_le $ (add_le_add_right hkm 1).trans hmn).has_fderiv_within_at_nhdsâ‚‚ (hg.of_le hkm)
      hst hu,
    refine hf'.congr_of_eventually_eq_insert _,
    filter_upwards [hv, ht],
    exact Î» y hy h2y, (hvf' y hy).fderiv_within h2y },
  induction m using with_top.rec_top_coe,
  { obtain rfl := eq_top_iff.mpr hmn,
    rw [cont_diff_within_at_top],
    exact Î» m, this m le_top },
  exact this m le_rfl
end

lemma cont_diff_within_at_fderiv_within'
  (hf : cont_diff_within_at ğ•œ n (function.uncurry f) u (x, g x))
  (hg : cont_diff_within_at ğ•œ m g s x)
  (ht : unique_diff_on ğ•œ t)
  (hmn : m + 1 â‰¤ n)
  (hst : insert x s Ã—Ë¢ t âŠ† u) -- maybe weaken
  (hgx : âˆ€á¶  x' in ğ“[insert x s] x, g x' âˆˆ t)
  (hu : u âˆˆ ğ“[(Î» x, (x, g x)) '' s] (x, g x)) -- remove
  :
  cont_diff_within_at ğ•œ m (Î» x, fderiv_within ğ•œ (f x) t (g x)) s x :=
hf.fderiv_withinâ‚‚' hg (hgx.mono (Î» y hy, ht _ hy)) hmn hst hu
-- $
--   begin
--     have := nhds_within_le_comap (continuous_within_at_id.prod hg.continuous_within_at),
--     have := hgx.filter_mono (nhds_within_mono _ $ subset_insert _ _),
--     refine mem_of_superset (inter_mem self_mem_nhds_within _) _,

--   end

lemma cont_diff_within_at_fderiv_within
  (hf : cont_diff_within_at ğ•œ n (function.uncurry f) u (x, g x))
  (hg : cont_diff_within_at ğ•œ m g s x)
  (ht : unique_diff_on ğ•œ t)
  (hmn : m + 1 â‰¤ n) (hx : x âˆˆ s)
  (hst : s Ã—Ë¢ t âŠ† u) -- maybe weaken
  (hgx : âˆ€á¶  x' in ğ“[s] x, g x' âˆˆ t)
  (hu : u âˆˆ ğ“[(Î» x, (x, g x)) '' s] (x, g x)) -- remove
  :
  cont_diff_within_at ğ•œ m (Î» x, fderiv_within ğ•œ (f x) t (g x)) s x :=
by { rw [â† insert_eq_self.mpr hx] at hst hgx,
  exact cont_diff_within_at_fderiv_within' hf hg ht hmn hst hgx hu }

lemma cont_diff_at.cont_diff_at_fderiv
  (hf : cont_diff_at ğ•œ n (function.uncurry f) (x, g x))
  (hg : cont_diff_at ğ•œ m g x)
  (hmn : m + 1 â‰¤ n) :
  cont_diff_at ğ•œ m (Î» x, fderiv ğ•œ (f x) (g x)) x :=
begin
  simp_rw [â† fderiv_within_univ],
  exact (cont_diff_within_at_fderiv_within hf.cont_diff_within_at hg.cont_diff_within_at
    unique_diff_on_univ hmn (mem_univ x) (subset_univ _) (eventually_of_forall (Î» x, mem_univ _))
    univ_mem).cont_diff_at univ_mem,
end

lemma cont_diff.fderiv {f : E â†’ F â†’ G} {g : E â†’ F} {n m : â„•âˆ}
  (hf : cont_diff ğ•œ m $ uncurry f) (hg : cont_diff ğ•œ n g) (hnm : n + 1 â‰¤ m) :
    cont_diff ğ•œ n (Î» x, fderiv ğ•œ (f x) (g x)) :=
cont_diff_iff_cont_diff_at.mpr $ Î» x, hf.cont_diff_at.cont_diff_at_fderiv hg.cont_diff_at hnm

lemma continuous.fderiv {f : E â†’ F â†’ G} {g : E â†’ F} {n : â„•âˆ}
  (hf : cont_diff ğ•œ n $ uncurry f) (hg : continuous g) (hn : 1 â‰¤ n):
    continuous (Î» x, fderiv ğ•œ (f x) (g x)) :=
(hf.fderiv (cont_diff_zero.mpr hg) hn).continuous

end fderiv

end cont_diff_fderiv

section calculus
open continuous_linear_map
variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
          {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
          {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
          {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]
          {n : â„•âˆ}

lemma fderiv_prod_left {xâ‚€ : E} {yâ‚€ : F} :
  fderiv ğ•œ (Î» x, (x, yâ‚€)) xâ‚€ = continuous_linear_map.inl ğ•œ E F :=
begin
  refine (differentiable_at_id.fderiv_prod (differentiable_at_const yâ‚€)).trans _,
  rw [fderiv_id, fderiv_const],
  refl
end

lemma fderiv_prod_right {xâ‚€ : E} {yâ‚€ : F} :
  fderiv ğ•œ (Î» y, (xâ‚€, y)) yâ‚€ = continuous_linear_map.inr ğ•œ E F :=
begin
  refine ((differentiable_at_const xâ‚€).fderiv_prod differentiable_at_id).trans _,
  rw [fderiv_id, fderiv_const],
  refl
end


lemma has_fderiv_at.partial_fst {Ï† : E â†’ F â†’ G} {Ï†' : E Ã— F â†’L[ğ•œ] G} {eâ‚€ : E} {fâ‚€ : F}
  (h : has_fderiv_at (uncurry Ï†) Ï†' (eâ‚€, fâ‚€)) :
  has_fderiv_at (Î» e, Ï† e fâ‚€) (Ï†'.comp (inl ğ•œ E F)) eâ‚€ :=
h.comp eâ‚€ $ has_fderiv_at_prod_mk_left eâ‚€ fâ‚€

lemma has_fderiv_at.partial_snd {Ï† : E â†’ F â†’ G} {Ï†' : E Ã— F â†’L[ğ•œ] G} {eâ‚€ : E} {fâ‚€ : F}
  (h : has_fderiv_at (uncurry Ï†) Ï†' (eâ‚€, fâ‚€)) :
  has_fderiv_at (Î» f, Ï† eâ‚€ f) (Ï†'.comp (inr ğ•œ E F)) fâ‚€ :=
h.comp fâ‚€ $ has_fderiv_at_prod_mk_right eâ‚€ fâ‚€

variable (ğ•œ)

/-- The first partial derivative of a binary function. -/
def partial_fderiv_fst {F : Type*} (Ï† : E â†’ F â†’ G) : E â†’ F â†’ E â†’L[ğ•œ] G :=
Î» (eâ‚€ : E) (fâ‚€ : F), fderiv ğ•œ (Î» e, Ï† e fâ‚€) eâ‚€

/-- The second partial derivative of a binary function. -/
def partial_fderiv_snd {E : Type*} (Ï† : E â†’ F â†’ G) : E â†’ F â†’ F â†’L[ğ•œ] G :=
Î» (eâ‚€ : E) (fâ‚€ : F), fderiv ğ•œ (Î» f, Ï† eâ‚€ f) fâ‚€

local notation `âˆ‚â‚` := partial_fderiv_fst
local notation `âˆ‚â‚‚` := partial_fderiv_snd

variable {ğ•œ}

lemma fderiv_partial_fst {Ï† : E â†’ F â†’ G} {Ï†' : E Ã— F â†’L[ğ•œ] G} {eâ‚€ : E} {fâ‚€ : F}
  (h : has_fderiv_at (uncurry Ï†) Ï†' (eâ‚€, fâ‚€)) :
  âˆ‚â‚ ğ•œ Ï† eâ‚€ fâ‚€ = Ï†'.comp (inl ğ•œ E F) :=
h.partial_fst.fderiv

lemma fderiv_partial_snd {Ï† : E â†’ F â†’ G} {Ï†' : E Ã— F â†’L[ğ•œ] G} {eâ‚€ : E} {fâ‚€ : F}
  (h : has_fderiv_at (uncurry Ï†) Ï†' (eâ‚€, fâ‚€)) :
  âˆ‚â‚‚ ğ•œ Ï† eâ‚€ fâ‚€ = Ï†'.comp (inr ğ•œ E F) :=
h.partial_snd.fderiv

lemma differentiable_at.has_fderiv_at_partial_fst {Ï† : E â†’ F â†’ G} {eâ‚€ : E} {fâ‚€ : F}
  (h : differentiable_at ğ•œ (uncurry Ï†) (eâ‚€, fâ‚€)) :
  has_fderiv_at (Î» e, Ï† e fâ‚€) (partial_fderiv_fst ğ•œ Ï† eâ‚€ fâ‚€) eâ‚€ :=
(h.comp eâ‚€ $ differentiable_at_id.prod $ differentiable_at_const fâ‚€).has_fderiv_at

lemma differentiable_at.has_fderiv_at_partial_snd {Ï† : E â†’ F â†’ G} {eâ‚€ : E} {fâ‚€ : F}
  (h : differentiable_at ğ•œ (uncurry Ï†) (eâ‚€, fâ‚€)) :
has_fderiv_at (Î» f, Ï† eâ‚€ f) (partial_fderiv_snd ğ•œ Ï† eâ‚€ fâ‚€) fâ‚€ :=
begin
  rw fderiv_partial_snd h.has_fderiv_at,
  exact h.has_fderiv_at.partial_snd
end

lemma cont_diff.partial_fst {Ï† : E â†’ F â†’ G} {n : â„•âˆ}
  (h : cont_diff ğ•œ n $ uncurry Ï†) (fâ‚€ : F) : cont_diff ğ•œ n (Î» e, Ï† e fâ‚€) :=
h.comp $ cont_diff_prod_mk_left fâ‚€

lemma cont_diff.partial_snd {Ï† : E â†’ F â†’ G} {n : â„•âˆ}
  (h : cont_diff ğ•œ n $ uncurry Ï†) (eâ‚€ : E) : cont_diff ğ•œ n (Î» f, Ï† eâ‚€ f) :=
h.comp $ cont_diff_prod_mk_right eâ‚€

/-- Precomposition by a continuous linear map as a continuous linear map between spaces of
continuous linear maps. -/
def continuous_linear_map.comp_rightL (Ï† : E â†’L[ğ•œ] F) : (F â†’L[ğ•œ] G) â†’L[ğ•œ] (E â†’L[ğ•œ] G) :=
(compL ğ•œ E F G).flip Ï†

/-- Postcomposition by a continuous linear map as a continuous linear map between spaces of
continuous linear maps. -/
def continuous_linear_map.comp_leftL (Ï† : F â†’L[ğ•œ] G) : (E â†’L[ğ•œ] F) â†’L[ğ•œ] (E â†’L[ğ•œ] G) :=
compL ğ•œ E F G Ï†

lemma differentiable.fderiv_partial_fst {Ï† : E â†’ F â†’ G} (hF : differentiable ğ•œ (uncurry Ï†)) :
  â†¿(âˆ‚â‚ ğ•œ Ï†) = (Î» Ïˆ : E Ã— F â†’L[ğ•œ] G, Ïˆ.comp (inl ğ•œ E F)) âˆ˜ (fderiv ğ•œ $ uncurry Ï†) :=
begin
  have : âˆ€ p : E Ã— F, has_fderiv_at (uncurry Ï†) _ p,
  { intro p,
    exact (hF p).has_fderiv_at },
  dsimp [partial_fderiv_fst],
  rw funext (Î» x : E , funext $ Î» t : F, (this (x, t)).partial_fst.fderiv),
  ext âŸ¨y, tâŸ©,
  refl
end

lemma differentiable.fderiv_partial_snd {Ï† : E â†’ F â†’ G} (hF : differentiable ğ•œ (uncurry Ï†)) :
  â†¿(âˆ‚â‚‚ ğ•œ Ï†) = (Î» Ïˆ : E Ã— F â†’L[ğ•œ] G, Ïˆ.comp (inr ğ•œ E F)) âˆ˜ (fderiv ğ•œ $ uncurry Ï†) :=
begin
  have : âˆ€ p : E Ã— F, has_fderiv_at (uncurry Ï†) _ p,
  { intro p,
    exact (hF p).has_fderiv_at },
  dsimp [partial_fderiv_snd],
  rw funext (Î» x : E , funext $ Î» t : F, (this (x, t)).partial_snd.fderiv),
  ext âŸ¨y, tâŸ©,
  refl
end

/-- The first partial derivative of `Ï† : ğ•œ â†’ F â†’ G` seen as a function from `ğ•œ â†’ F â†’ G`-/
def partial_deriv_fst (Ï† : ğ•œ â†’ F â†’ G) := Î» k f, âˆ‚â‚ ğ•œ Ï† k f 1

/-- The second partial derivative of `Ï† : E â†’ ğ•œ â†’ G` seen as a function from `E â†’ ğ•œ â†’ G`-/
def partial_deriv_snd (Ï† : E â†’ ğ•œ â†’ G) := Î» e k, âˆ‚â‚‚ ğ•œ Ï† e k 1

lemma partial_fderiv_fst_eq_smul_right (Ï† : ğ•œ â†’ F â†’ G) (k : ğ•œ) (f : F) :
  âˆ‚â‚ ğ•œ Ï† k f = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (partial_deriv_fst Ï† k f) := deriv_fderiv.symm

@[simp]
lemma partial_fderiv_fst_one (Ï† : ğ•œ â†’ F â†’ G) (k : ğ•œ) (f : F) :
  âˆ‚â‚ ğ•œ Ï† k f 1 = partial_deriv_fst Ï† k f :=
by simp only [partial_fderiv_fst_eq_smul_right, smul_right_apply, one_apply, one_smul]

lemma partial_fderiv_snd_eq_smul_right (Ï† : E â†’ ğ•œ â†’ G) (e : E) (k : ğ•œ) :
  âˆ‚â‚‚ ğ•œ Ï† e k  = smul_right (1 : ğ•œ â†’L[ğ•œ] ğ•œ) (partial_deriv_snd Ï† e k) := deriv_fderiv.symm

lemma partial_fderiv_snd_one (Ï† : E â†’ ğ•œ â†’ G) (e : E) (k : ğ•œ) :
  âˆ‚â‚‚ ğ•œ Ï† e k 1 = partial_deriv_snd Ï† e k :=
by simp only [partial_fderiv_snd_eq_smul_right, smul_right_apply, one_apply, one_smul]

@[to_additive]
lemma with_top.le_mul_self {Î± : Type*} [canonically_ordered_monoid Î±] (n m : Î±) :
  (n : with_top Î±) â‰¤ (m * n : Î±) :=
with_top.coe_le_coe.mpr le_mul_self

@[to_additive]
lemma with_top.le_self_mul {Î± : Type*} [canonically_ordered_monoid Î±] (n m : Î±) :
  (n : with_top Î±) â‰¤ (n * m : Î±) :=
with_top.coe_le_coe.mpr le_self_mul

lemma cont_diff.cont_diff_partial_fst {Ï† : E â†’ F â†’ G} {n : â„•}
  (hF : cont_diff ğ•œ (n + 1) (uncurry Ï†)) : cont_diff ğ•œ n â†¿(âˆ‚â‚ ğ•œ Ï†) :=
begin
  cases cont_diff_succ_iff_fderiv.mp hF with hFâ‚ hFâ‚‚,
  rw (hF.one_of_succ.differentiable le_rfl).fderiv_partial_fst,
  apply cont_diff.comp _ hFâ‚‚,
  exact ((inl ğ•œ E F).comp_rightL : (E Ã— F â†’L[ğ•œ] G) â†’L[ğ•œ] E â†’L[ğ•œ] G).cont_diff
end

lemma cont_diff.cont_diff_partial_fst_apply {Ï† : E â†’ F â†’ G} {n : â„•}
  (hF : cont_diff ğ•œ (n + 1) (uncurry Ï†)) {x : E} : cont_diff ğ•œ n â†¿(Î» x' y, âˆ‚â‚ ğ•œ Ï† x' y x) :=
(continuous_linear_map.apply ğ•œ G x).cont_diff.comp hF.cont_diff_partial_fst

lemma cont_diff.continuous_partial_fst {Ï† : E â†’ F â†’ G} {n : â„•}
  (h : cont_diff ğ•œ ((n + 1 : â„•) : â„•âˆ) $ uncurry Ï†) : continuous â†¿(âˆ‚â‚ ğ•œ Ï†) :=
h.cont_diff_partial_fst.continuous

lemma cont_diff.cont_diff_top_partial_fst {Ï† : E â†’ F â†’ G} (hF : cont_diff ğ•œ âŠ¤ (uncurry Ï†)) :
  cont_diff ğ•œ âŠ¤ â†¿(âˆ‚â‚ ğ•œ Ï†) :=
cont_diff_top.mpr (Î» n, (cont_diff_top.mp hF (n + 1)).cont_diff_partial_fst)

lemma cont_diff.cont_diff_partial_snd {Ï† : E â†’ F â†’ G} {n : â„•}
  (hF : cont_diff ğ•œ (n + 1) (uncurry Ï†)) : cont_diff ğ•œ n â†¿(âˆ‚â‚‚ ğ•œ Ï†) :=
begin
  cases cont_diff_succ_iff_fderiv.mp hF with hFâ‚ hFâ‚‚,
  rw (hF.one_of_succ.differentiable le_rfl).fderiv_partial_snd,
  apply cont_diff.comp _ hFâ‚‚,
  exact ((inr ğ•œ E F).comp_rightL : (E Ã— F â†’L[ğ•œ] G) â†’L[ğ•œ] F â†’L[ğ•œ] G).cont_diff
end

lemma cont_diff.cont_diff_partial_snd_apply {Ï† : E â†’ F â†’ G} {n : â„•}
  (hF : cont_diff ğ•œ (n + 1) (uncurry Ï†)) {y : F} : cont_diff ğ•œ n â†¿(Î» x y', âˆ‚â‚‚ ğ•œ Ï† x y' y) :=
(continuous_linear_map.apply ğ•œ G y).cont_diff.comp hF.cont_diff_partial_snd

lemma cont_diff.continuous_partial_snd {Ï† : E â†’ F â†’ G} {n : â„•}
  (h : cont_diff ğ•œ ((n + 1 : â„•) : â„•âˆ) $ uncurry Ï†) : continuous â†¿(âˆ‚â‚‚ ğ•œ Ï†) :=
h.cont_diff_partial_snd.continuous

lemma cont_diff.cont_diff_top_partial_snd {Ï† : E â†’ F â†’ G} (hF : cont_diff ğ•œ âŠ¤ (uncurry Ï†)) :
  cont_diff ğ•œ âŠ¤ â†¿(âˆ‚â‚‚ ğ•œ Ï†) :=
cont_diff_top.mpr (Î» n, (cont_diff_top.mp hF (n + 1)).cont_diff_partial_snd)

end calculus

section real_calculus
open continuous_linear_map
variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
          {F : Type*} [normed_add_comm_group F] [normed_space â„ F]

lemma cont_diff.lipschitz_on_with {s : set E} {f : E â†’ F} (hf : cont_diff â„ 1 f)
  (hs : convex â„ s) (hs' : is_compact s) : âˆƒ K, lipschitz_on_with K f s :=
begin
  rcases hs'.bdd_above_norm (hf.continuous_fderiv le_rfl) with âŸ¨M, M_pos : 0 < M, hMâŸ©,
  use âŸ¨M, M_pos.leâŸ©,
  exact convex.lipschitz_on_with_of_nnnorm_fderiv_le (Î» x x_in, hf.differentiable le_rfl x) hM hs
end

end real_calculus


open real continuous_linear_map asymptotics
open_locale topological_space

lemma of_eventually_nhds {X : Type*} [topological_space X] {P : X â†’ Prop} {xâ‚€ : X}
  (h : âˆ€á¶  x in ğ“ xâ‚€, P x) : P xâ‚€ :=
mem_of_mem_nhds h

section

open asymptotics continuous_linear_map filter
open_locale filter

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
          {E : Type*}  {F : Type*} [normed_add_comm_group F]

lemma filter.eventually_le.is_O {f g h : E â†’ F} {l : filter E}
  (hfg : (Î» x, âˆ¥f xâˆ¥) â‰¤á¶ [l] Î» x, âˆ¥g xâˆ¥) (hh : g =O[l] h) : f =O[l] h :=
(is_O_iff.mpr âŸ¨1, by  simpa using hfgâŸ©).trans hh

lemma filter.eventually.is_O {f g h : E â†’ F} {l : filter E}
  (hfg : âˆ€á¶  x in l, âˆ¥f xâˆ¥ â‰¤ âˆ¥g xâˆ¥) (hh : g =O[l] h) : f =O[l] h :=
filter.eventually_le.is_O hfg hh

lemma filter.eventually.is_O' {f : E â†’ F} {g : E â†’ â„} {l : filter E}
  (hfg : âˆ€á¶  x in l, âˆ¥f xâˆ¥ â‰¤ g x) : f =O[l] g :=
begin
  rw is_O_iff,
  use 1,
  apply hfg.mono,
  intros x h,
  rwa [real.norm_eq_abs, abs_of_nonneg ((norm_nonneg $ f x).trans h), one_mul]
end

variables [normed_add_comm_group E] [normed_space ğ•œ E] [normed_space ğ•œ F]
          {G : Type*} [normed_add_comm_group G] [normed_space ğ•œ G]

lemma asymptotics.is_O.eq_zero {f : E â†’ F} {xâ‚€ : E} {n : â„•}
  (h : f =O[ğ“ xâ‚€] Î» x, âˆ¥x - xâ‚€âˆ¥^n) (hn : 0 < n) : f xâ‚€ = 0 :=
begin
  cases h.is_O_with with c hc,
  have:= mem_of_mem_nhds (is_O_with_iff.mp hc),
  simpa [zero_pow hn]
end

lemma is_o_pow_sub_pow_sub (xâ‚€ : E) {n m : â„•} (h : n < m) :
    (Î» (x : E), âˆ¥x - xâ‚€âˆ¥^m) =o[ğ“ xâ‚€] Î» (x : E), âˆ¥x - xâ‚€âˆ¥^n :=
begin
  have : tendsto (Î» x, âˆ¥x - xâ‚€âˆ¥) (ğ“ xâ‚€) (ğ“ 0),
  { apply tendsto_norm_zero.comp,
    rw â† sub_self xâ‚€,
    exact tendsto_id.sub tendsto_const_nhds },
  exact (is_o_pow_pow h).comp_tendsto this
end

lemma is_o_pow_sub_sub (xâ‚€ : E) {m : â„•} (h : 1 < m) :
    (Î» (x : E), âˆ¥x - xâ‚€âˆ¥^m) =o[ğ“ xâ‚€] Î» (x : E), x - xâ‚€ :=
by simpa only [is_o_norm_right, pow_one] using is_o_pow_sub_pow_sub xâ‚€ h

lemma asymptotics.is_O_sub_prod_left (eâ‚€ : E) (fâ‚€ : F) (l : filter $ E Ã— F) :
  (Î» p : E Ã— F, p.1 - eâ‚€) =O[l] Î» p : E Ã— F, p - (eâ‚€, fâ‚€) :=
is_O_of_le l (Î» p, le_max_left _ _)

lemma asymptotics.is_O_sub_prod_right (eâ‚€ : E) (fâ‚€ : F) (l : filter $ E Ã— F) :
  (Î» p : E Ã— F, p.2 - fâ‚€) =O[l] Î» p : E Ã— F, p - (eâ‚€, fâ‚€) :=
is_O_of_le l (Î» p, le_max_right _ _)

lemma asymptotics.is_O_pow_sub_prod_left (eâ‚€ : E) (fâ‚€ : F) (l : filter $ E Ã— F) (n : â„•) :
  (Î» p : E Ã— F, âˆ¥p.1 - eâ‚€âˆ¥^n) =O[l] Î» p : E Ã— F, âˆ¥p - (eâ‚€, fâ‚€)âˆ¥^n :=
(is_O_norm_norm.mpr $ asymptotics.is_O_sub_prod_left eâ‚€ fâ‚€ l).pow n

lemma asymptotics.is_O_pow_sub_prod_right (eâ‚€ : E) (fâ‚€ : F) (l : filter $ E Ã— F) (n : â„•) :
  (Î» p : E Ã— F, âˆ¥p.2 - fâ‚€âˆ¥^n) =O[l] Î» p : E Ã— F, âˆ¥p - (eâ‚€, fâ‚€)âˆ¥^n :=
(is_O_norm_norm.mpr $ asymptotics.is_O_sub_prod_right eâ‚€ fâ‚€ l).pow n

lemma asymptotics.is_O.comp_fst {f : E â†’ F} {n : â„•} {eâ‚€ : E} {l : filter E}
  (h : f =O[l] Î» e, âˆ¥e - eâ‚€âˆ¥^n) (gâ‚€ : G) (l' : filter G) :
  (Î» p : E Ã— G, f p.1) =O[l Ã—á¶  l'] Î» p, âˆ¥p - (eâ‚€, gâ‚€)âˆ¥^n :=
(h.comp_tendsto tendsto_fst).trans (asymptotics.is_O_pow_sub_prod_left _ _ _ _)

lemma asymptotics.is_O.comp_fst_one {f : E â†’ F} {eâ‚€ : E}  {l : filter E}
  (h : f =O[l] Î» e, âˆ¥e - eâ‚€âˆ¥) (gâ‚€ : G) (l' : filter G) :
  (Î» p : E Ã— G, f p.1) =O[l Ã—á¶  l'] Î» p, âˆ¥p - (eâ‚€, gâ‚€)âˆ¥ :=
begin
  rw show (Î» e, âˆ¥e - eâ‚€âˆ¥) = (Î» e, âˆ¥e - eâ‚€âˆ¥^1), by { ext e, simp } at h,
  simpa using h.comp_fst gâ‚€ l'
end

lemma asymptotics.is_O.comp_snd {f : G â†’ F} {n : â„•}  {gâ‚€ : G} {l' : filter G}
  (h : f =O[l'] Î» g, âˆ¥g - gâ‚€âˆ¥^n) (eâ‚€ : E) (l : filter E) :
  (Î» p : E Ã— G, f p.2) =O[l Ã—á¶  l'] Î» p, âˆ¥p - (eâ‚€, gâ‚€)âˆ¥^n :=
(h.comp_tendsto tendsto_snd).trans (asymptotics.is_O_pow_sub_prod_right _ _ _ _)

lemma asymptotics.is_O.comp_snd_one {f : G â†’ F}  {gâ‚€ : G} {l' : filter G}
  (h : f =O[l'] Î» g, âˆ¥g - gâ‚€âˆ¥) (eâ‚€ : E) (l : filter E) :
  (Î» p : E Ã— G, f p.2) =O[l Ã—á¶  l'] Î» p, âˆ¥p - (eâ‚€, gâ‚€)âˆ¥ :=
begin
  rw show (Î» g, âˆ¥g - gâ‚€âˆ¥) = (Î» g, âˆ¥g - gâ‚€âˆ¥^1), by { ext g, simp } at h,
  simpa using h.comp_snd eâ‚€ l
end

lemma asymptotics.is_O.has_fderiv_at {f : E â†’ F} {xâ‚€ : E} {n : â„•}
  (h : f =O[ğ“ xâ‚€] Î» x, âˆ¥x - xâ‚€âˆ¥^n) (hn : 1 < n) :
  has_fderiv_at f (0 : E â†’L[ğ•œ] F) xâ‚€ :=
begin
  change is_o _ _ _,
  simp only [h.eq_zero (zero_lt_one.trans hn), sub_zero, zero_apply],
  exact h.trans_is_o (is_o_pow_sub_sub xâ‚€ hn)
end

lemma has_deriv_at.is_O {f : E â†’ F} {xâ‚€ : E} {f' : E â†’L[ğ•œ] F} (h : has_fderiv_at f f' xâ‚€) :
  (Î» x, f x - f xâ‚€) =O[ğ“ xâ‚€] Î» x, x - xâ‚€ :=
by simpa using h.is_O.add (is_O_sub f' (ğ“ xâ‚€) xâ‚€)

end

section
open continuous_linear_map

lemma coprod_eq_add {Râ‚ : Type*} [semiring Râ‚] {Mâ‚ : Type*} [topological_space Mâ‚]
  [add_comm_monoid Mâ‚] {Mâ‚‚ : Type*} [topological_space Mâ‚‚] [add_comm_monoid Mâ‚‚]
  {Mâ‚ƒ : Type*} [topological_space Mâ‚ƒ] [add_comm_monoid Mâ‚ƒ] [module Râ‚ Mâ‚]
  [module Râ‚ Mâ‚‚] [module Râ‚ Mâ‚ƒ] [has_continuous_add Mâ‚ƒ]
  (f : Mâ‚ â†’L[Râ‚] Mâ‚ƒ) (g : Mâ‚‚ â†’L[Râ‚] Mâ‚ƒ) :
  f.coprod g = (f.comp $ fst Râ‚ Mâ‚ Mâ‚‚) + (g.comp $ snd Râ‚ Mâ‚ Mâ‚‚) :=
by { ext ; refl }

end

open filter

/-
The lemma below is ridiculously painful, but Patrick isn't patient enough.
-/
lemma const_mul_one_div_lt {Îµ : â„} (Îµ_pos : 0 < Îµ) (C : â„) : âˆ€á¶  (N : â„) in at_top, C*âˆ¥1/Nâˆ¥ < Îµ :=
begin
  have : tendsto (Î» N : â„, 1/N) at_top (ğ“ 0),
  { rw show (Î» N : â„, 1/N) = Î» N, N^(-(1 : â„¤)), by simp,
    exact tendsto_pow_neg_at_top one_ne_zero },
  rw tendsto_iff_norm_tendsto_zero at this,
  simp only [sub_zero] at this,
  have key := this.const_mul C,
  rw mul_zero at key,
  apply (normed_add_comm_group.tendsto_nhds_zero.mp key Îµ Îµ_pos).mono,
  intros N hN,
  cases le_or_lt (C * âˆ¥1 / Nâˆ¥) 0 with h h,
  { exact h.trans_lt Îµ_pos },
  { rwa real.norm_of_nonneg h.le at hN },
end
