import geometry.manifold.diffeomorph
import geometry.manifold.algebra.monoid
import geometry.manifold.metrizable
import to_mathlib.analysis.calculus

open bundle set function filter
open_locale manifold topology

section charted_space

variables {M H : Type*} [topological_space M] [topological_space H] [charted_space H M]
  (G : structure_groupoid H)

end charted_space

namespace model_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H]
  {M : Type*} [topological_space M] (f : local_homeomorph M H) (I : model_with_corners ğ•œ E H)

end model_with_corners

section smooth_manifold_with_corners
open smooth_manifold_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
  {M : Type*} [topological_space M] [charted_space H M]
  {M' : Type*} [topological_space M'] [charted_space H' M']
  {N : Type*} [topological_space N] [charted_space G N]
  {F' : Type*} [normed_add_comm_group F'] [normed_space ğ•œ F']
  {F'' : Type*} [normed_add_comm_group F''] [normed_space ğ•œ F'']
variables {f : M â†’ M'} {m n : â„•âˆ} {s : set M} {x : M}


namespace basic_smooth_vector_bundle_core
variables [smooth_manifold_with_corners I M] (Z : basic_smooth_vector_bundle_core I M E')

end basic_smooth_vector_bundle_core

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners I' M']
  [smooth_manifold_with_corners J N]

-- this can be useful to see where we (ab)use definitional equalities
-- local attribute [irreducible] tangent_space

/-! The two instances below deserve some further thought. For example one might not want the tangent
space at every point to carry a canonical norm.

Note that `dual_pair.update` requires `F` to be a `normed_add_comm_group` (though perhaps we could
get away with `has_continuous_smul` with sufficient extra work).

In `rel_mfld.slice` we use `dual_pair.update` applied to `tangent_space`. If we don't add these
instances, then in fact Lean still accepts the definition. What is going on is that Lean
is unfolding the definition of `tangent_space`, realizing that `tangent_space I x = E` and
`tangent_space I' y = E'` and using the `normed_add_comm_group` instances of these types.
Note that this still uses these instances but at the cost that up to reducible transparency, the
term is not type-correct (in other words: you have to unfold `tangent_space` to realize that the
term is type-correct).

This means that many tactics, like `simp`, `rw`, and `dsimp` fail to rewrite within this term,
because the result is not type correct up to reducible transparancy.

Declaring these instances avoids such problems. -/

instance {x : M} : normed_add_comm_group (tangent_space I x) := by delta_instance tangent_space
instance {x : M} : normed_space ğ•œ (tangent_space I x) := by delta_instance tangent_space

lemma tangent_bundle_core_coord_change_achart (x x' : M) (z : H) :
  (tangent_bundle_core I M).coord_change (achart H x) (achart H x') z =
  fderiv_within ğ•œ (ext_chart_at I x' âˆ˜ (ext_chart_at I x).symm) (range I) (I z) :=
rfl

variables (I)
-- used in tangent_bundle_model_space_chart_at

lemma model_with_corners.fderiv_within_comp_symm (x : H) :
  fderiv_within ğ•œ (I âˆ˜ I.symm) (range I) (I x) = continuous_linear_map.id ğ•œ E :=
begin
  have : fderiv_within ğ•œ (I âˆ˜ I.symm) (range I) (I x) = fderiv_within ğ•œ id (range I) (I x),
  { refine fderiv_within_congr I.unique_diff_at_image (Î» y hy, _) (by simp only with mfld_simps),
    exact model_with_corners.right_inv _ hy },
  rwa fderiv_within_id I.unique_diff_at_image at this
end


lemma tangent_bundle_core_coord_change_model_space (x x' : H) (z : H) :
  (tangent_bundle_core I H).coord_change (achart H x) (achart H x') z =
  continuous_linear_map.id ğ•œ E :=
begin
  simp only [tangent_bundle_core_coord_change_achart, ext_chart_at, I.fderiv_within_comp_symm] with mfld_simps,
end


lemma cont_diff_on_coord_change' {e e' : local_homeomorph M H}
  (h : e âˆˆ atlas H M) (h' : e' âˆˆ atlas H M) :
  cont_diff_on ğ•œ âŠ¤ (I âˆ˜ (e.symm â‰«â‚• e') âˆ˜ I.symm) (I.symm â»Â¹' (e.symm â‰«â‚• e').source âˆ© range I) :=
(has_groupoid.compatible (cont_diff_groupoid âŠ¤ I) h h').1

variables {I}
/-- A congruence lemma for `mfderiv`, (ab)using the fact that `tangent_space I' (f x)` is
definitionally equal to `E'`. -/
lemma mfderiv_congr_point {x' : M} (h : x = x') :
  @eq (E â†’L[ğ•œ] E') (mfderiv I I' f x) (mfderiv I I' f x') :=
by subst h

/-- A congruence lemma for `mfderiv`, (ab)using the fact that `tangent_space I' (f x)` is
definitionally equal to `E'`. -/
lemma mfderiv_congr {f' : M â†’ M'} (h : f = f') :
  @eq (E â†’L[ğ•œ] E') (mfderiv I I' f x) (mfderiv I I' f' x) :=
by subst h

/-- The derivative of the projection `M Ã— M' â†’ M` is the projection `TM Ã— TM' â†’ TM` -/
lemma mfderiv_fst (x : M Ã— M') :
  mfderiv (I.prod I') I prod.fst x = continuous_linear_map.fst ğ•œ E E' :=
begin
  simp_rw [mfderiv, if_pos smooth_at_fst.mdifferentiable_at, written_in_ext_chart_at,
    ext_chart_at_prod, function.comp, local_equiv.prod_coe, local_equiv.prod_coe_symm],
  have : unique_diff_within_at ğ•œ (range (I.prod I')) (ext_chart_at (I.prod I') x x) :=
  (I.prod I').unique_diff _ (mem_range_self _),
  refine (filter.eventually_eq.fderiv_within_eq this _ _).trans _,
  swap 3,
  { exact (ext_chart_at I x.1).right_inv ((ext_chart_at I x.1).maps_to $
      mem_ext_chart_source I x.1) },
  { refine eventually_of_mem (ext_chart_at_target_mem_nhds_within (I.prod I') x)
      (Î» y hy, local_equiv.right_inv _ _),
    rw [ext_chart_at_prod] at hy,
    exact hy.1 },
  exact fderiv_within_fst this,
end

/-- The derivative of the projection `M Ã— M' â†’ M'` is the projection `TM Ã— TM' â†’ TM'` -/
lemma mfderiv_snd (x : M Ã— M') :
  mfderiv (I.prod I') I' prod.snd x = continuous_linear_map.snd ğ•œ E E' :=
begin
  simp_rw [mfderiv, if_pos smooth_at_snd.mdifferentiable_at, written_in_ext_chart_at,
    ext_chart_at_prod, function.comp, local_equiv.prod_coe, local_equiv.prod_coe_symm],
  have : unique_diff_within_at ğ•œ (range (I.prod I')) (ext_chart_at (I.prod I') x x) :=
  (I.prod I').unique_diff _ (mem_range_self _),
  refine (filter.eventually_eq.fderiv_within_eq this _ _).trans _,
  swap 3,
  { exact (ext_chart_at I' x.2).right_inv ((ext_chart_at I' x.2).maps_to $
      mem_ext_chart_source I' x.2) },
  { refine eventually_of_mem (ext_chart_at_target_mem_nhds_within (I.prod I') x)
      (Î» y hy, local_equiv.right_inv _ _),
    rw [ext_chart_at_prod] at hy,
    exact hy.2 },
  exact fderiv_within_snd this,
end

lemma mdifferentiable_at.prod_mk {f : N â†’ M} {g : N â†’ M'} {x : N}
  (hf : mdifferentiable_at J I f x)
  (hg : mdifferentiable_at J I' g x) :
  mdifferentiable_at J (I.prod I') (Î» x, (f x, g x)) x :=
âŸ¨hf.1.prod hg.1, hf.2.prod hg.2âŸ©


-- todo: rename differentiable_at.fderiv_within_prod -> differentiable_within_at.fderiv_within_prod
lemma mdifferentiable_at.mfderiv_prod {f : N â†’ M} {g : N â†’ M'} {x : N}
  (hf : mdifferentiable_at J I f x)
  (hg : mdifferentiable_at J I' g x) :
  mfderiv J (I.prod I') (Î» x, (f x, g x)) x = (mfderiv J I f x).prod (mfderiv J I' g x) :=
begin
  classical,
  simp_rw [mfderiv, if_pos (hf.prod_mk hg), if_pos hf, if_pos hg],
  exact differentiable_at.fderiv_within_prod hf.2 hg.2 (J.unique_diff _ (mem_range_self _))
end

lemma mfderiv_prod_left {xâ‚€ : M} {yâ‚€ : M'} :
  mfderiv I (I.prod I') (Î» x, (x, yâ‚€)) xâ‚€ = continuous_linear_map.inl ğ•œ E E' :=
begin
  refine ((mdifferentiable_at_id I).mfderiv_prod (mdifferentiable_at_const I I')).trans _,
  rw [mfderiv_id, mfderiv_const],
  refl
end

lemma mfderiv_prod_right {xâ‚€ : M} {yâ‚€ : M'} :
  mfderiv I' (I.prod I') (Î» y, (xâ‚€, y)) yâ‚€ = continuous_linear_map.inr ğ•œ E E' :=
begin
  refine ((mdifferentiable_at_const I' I).mfderiv_prod (mdifferentiable_at_id I')).trans _,
  rw [mfderiv_id, mfderiv_const],
  refl
end

lemma mfderiv_prod_eq_add {f : N Ã— M â†’ M'} {p : N Ã— M}
  (hf : mdifferentiable_at (J.prod I) I' f p) :
  mfderiv (J.prod I) I' f p =
  (show F Ã— E â†’L[ğ•œ] E', from mfderiv (J.prod I) I' (Î» (z : N Ã— M), f (z.1, p.2)) p +
  mfderiv (J.prod I) I' (Î» (z : N Ã— M), f (p.1, z.2)) p) :=
begin
  dsimp only,
  rw [â† @prod.mk.eta _ _ p] at hf,
  rw [mfderiv_comp p (by apply hf) (smooth_fst.prod_mk smooth_const).mdifferentiable_at,
    mfderiv_comp p (by apply hf) (smooth_const.prod_mk smooth_snd).mdifferentiable_at,
    â† continuous_linear_map.comp_add,
    smooth_fst.mdifferentiable_at.mfderiv_prod smooth_const.mdifferentiable_at,
    smooth_const.mdifferentiable_at.mfderiv_prod smooth_snd.mdifferentiable_at,
    mfderiv_fst, mfderiv_snd, mfderiv_const, mfderiv_const],
  symmetry,
  convert continuous_linear_map.comp_id _,
  { exact continuous_linear_map.fst_prod_zero_add_zero_prod_snd },
  simp_rw [prod.mk.eta],
end

/-- When `Ï•` is a continuous linear map that changes vectors in charts around `x` to vectors
  in charts around `y`, `in_coordinates' Z Z' xâ‚€ x yâ‚€ y Ï•` is a coordinate change of this continuous
  linear map that makes sense from charts around `xâ‚€` to charts around `yâ‚€`
  by composing it with appropriate coordinate changes given by smooth vector bundles `Z` and `Z'`.
  -/
def in_coordinates' (Z : basic_smooth_vector_bundle_core I M F)
  (Z' : basic_smooth_vector_bundle_core I' M' F')
  (xâ‚€ x : M) (yâ‚€ y : M') (Ï• : F â†’L[ğ•œ] F') : F â†’L[ğ•œ] F' :=
Z'.coord_change (achart H' y) (achart H' yâ‚€) (chart_at H' y y) âˆ˜L Ï• âˆ˜L
  Z.coord_change (achart H xâ‚€) (achart H x) (chart_at H xâ‚€ x)

variables (I I')
/-- When `Ï• x` is a continuous linear map that changes vectors in charts around `f x` to vectors
  in charts around `g x`, `in_coordinates I I' f g Ï• xâ‚€ x` is a coordinate change of this continuous
  linear map that makes sense from charts around `f xâ‚€` to charts around `g xâ‚€`
  by composing it with appropriate coordinate changes. -/
noncomputable def in_coordinates (f : N â†’ M) (g : N â†’ M') (Ï• : N â†’ E â†’L[ğ•œ] E') :
  N â†’ N â†’ E â†’L[ğ•œ] E' :=
Î» xâ‚€ x, in_coordinates' (tangent_bundle_core I M) (tangent_bundle_core I' M')
  (f xâ‚€) (f x) (g xâ‚€) (g x) (Ï• x)


lemma in_coordinates'_tangent_bundle_core_model_space
  (xâ‚€ x : H) (yâ‚€ y : H') (Ï• : E â†’L[ğ•œ] E') : in_coordinates' (tangent_bundle_core I H)
    (tangent_bundle_core I' H') xâ‚€ x yâ‚€ y Ï• = Ï• :=
by simp_rw [in_coordinates', tangent_bundle_core_coord_change_model_space,
    continuous_linear_map.id_comp, continuous_linear_map.comp_id]

variables {I I'}

/-- The appropriate (more general) formulation of `cont_mdiff_at.mfderiv''`. -/
lemma cont_mdiff_at.mfderiv''' {x : N} (f : N â†’ M â†’ M') (g : N â†’ M)
  (hf : cont_mdiff_at (J.prod I) I' n (function.uncurry f) (x, g x))
  (hg : cont_mdiff_at J I m g x) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (in_coordinates I I' g (Î» x, f x (g x)) (Î» x', mfderiv I I' (f x') (g x')) x) x :=
begin
  have h4f : continuous_at (Î» x, f x (g x)) x,
  { apply continuous_at.comp (by apply hf.continuous_at) (continuous_at_id.prod hg.continuous_at) },
  have h3f := cont_mdiff_at_iff_cont_mdiff_at_nhds.mp (hf.of_le $ (self_le_add_left 1 m).trans hmn),
  have h2f : âˆ€á¶  xâ‚‚ in ğ“ x, cont_mdiff_at I I' 1 (f xâ‚‚) (g xâ‚‚),
  { refine ((continuous_at_id.prod hg.continuous_at).tendsto.eventually h3f).mono (Î» x hx, _),
    exact hx.comp (g x) (cont_mdiff_at_const.prod_mk cont_mdiff_at_id) },
  have h2g := hg.continuous_at.preimage_mem_nhds (ext_chart_at_source_mem_nhds I (g x)),
  have : cont_diff_within_at ğ•œ m (Î» x', fderiv_within ğ•œ
    (ext_chart_at I' (f x (g x)) âˆ˜ f ((ext_chart_at J x).symm x') âˆ˜ (ext_chart_at I (g x)).symm)
    (range I) (ext_chart_at I (g x) (g ((ext_chart_at J x).symm x'))))
    (range J) (ext_chart_at J x x),
  { rw [cont_mdiff_at_iff] at hf hg,
    simp_rw [function.comp, uncurry, ext_chart_at_prod, local_equiv.prod_coe_symm] at hf âŠ¢,
    refine (cont_diff_within_at_fderiv_within _
      (hg.2.mono_of_mem _) I.unique_diff hmn _ _ _ _).mono_of_mem _,
    swap 3,
    { simp_rw [function.comp, ext_chart_at_to_inv], exact hf.2 },
    { refine (ext_chart_at J x).target âˆ©
      (Î» x', (ext_chart_at J x).symm x') â»Â¹' (g â»Â¹' (ext_chart_at I (g x)).source) },
    { exact mem_of_superset self_mem_nhds_within
        ((inter_subset_left _ _).trans $ ext_chart_at_target_subset_range J x) },
    { simp_rw [mem_inter_iff, mem_preimage, ext_chart_at_to_inv],
      exact âŸ¨local_equiv.maps_to _ (mem_ext_chart_source J x), mem_ext_chart_source I (g x)âŸ© },
    { simp_rw [model_with_corners.range_prod],
      exact set.prod_mono ((inter_subset_left _ _).trans $ ext_chart_at_target_subset_range J x)
        subset_rfl },
    { refine eventually_of_forall (Î» x', mem_range_self _) },
    swap 2,
    { refine inter_mem (ext_chart_at_target_mem_nhds_within J x) _,
      refine nhds_within_le_nhds (ext_chart_at_preimage_mem_nhds' _ _ (mem_ext_chart_source J x) _),
      exact hg.1.preimage_mem_nhds (ext_chart_at_source_mem_nhds I (g x)) },
    simp_rw [function.comp, model_with_corners.range_prod, ext_chart_at_to_inv],
    refine mem_of_superset self_mem_nhds_within _,
    refine (image_subset_range _ _).trans _,
    exact range_comp_subset_range (Î» a, chart_at H (g x) $ g $ (chart_at G x).symm $ J.symm a) I },
  have : cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x', fderiv_within ğ•œ (ext_chart_at I' (f x (g x)) âˆ˜ f x' âˆ˜ (ext_chart_at I (g x)).symm)
    (range I) (ext_chart_at I (g x) (g x'))) x,
  { simp_rw [cont_mdiff_at_iff_source_of_mem_source (mem_chart_source G x),
      cont_mdiff_within_at_iff_cont_diff_within_at, function.comp],
    exact this },
  have : cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x', fderiv_within ğ•œ (ext_chart_at I' (f x (g x)) âˆ˜ (ext_chart_at I' (f x' (g x'))).symm âˆ˜
      written_in_ext_chart_at I I' (g x') (f x') âˆ˜ ext_chart_at I (g x') âˆ˜
      (ext_chart_at I (g x)).symm) (range I) (ext_chart_at I (g x) (g x'))) x,
  { refine this.congr_of_eventually_eq _,
    filter_upwards [h2g, h2f],
    intros xâ‚‚ hxâ‚‚ h2xâ‚‚,
    have : âˆ€ x' âˆˆ (ext_chart_at I (g x)).symm â»Â¹' (ext_chart_at I (g xâ‚‚)).source âˆ©
        (ext_chart_at I (g x)).symm â»Â¹' (f xâ‚‚ â»Â¹' (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).source),
      (ext_chart_at I' (f x (g x)) âˆ˜ (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).symm âˆ˜
      written_in_ext_chart_at I I' (g xâ‚‚) (f xâ‚‚) âˆ˜ ext_chart_at I (g xâ‚‚) âˆ˜
      (ext_chart_at I (g x)).symm) x' =
      ext_chart_at I' (f x (g x)) (f xâ‚‚ ((ext_chart_at I (g x)).symm x')),
    { rintro x' âŸ¨hx', h2x'âŸ©,
      simp_rw [written_in_ext_chart_at, function.comp_apply],
      rw [(ext_chart_at I (g xâ‚‚)).left_inv hx', (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).left_inv h2x'] },
    refine filter.eventually_eq.fderiv_within_eq_nhds (I.unique_diff _ $ mem_range_self _) _,
    refine eventually_of_mem (inter_mem _ _) this,
    { exact ext_chart_at_preimage_mem_nhds' _ _ hxâ‚‚ (ext_chart_at_source_mem_nhds I (g xâ‚‚)) },
    refine ext_chart_at_preimage_mem_nhds' _ _ hxâ‚‚ _,
    exact (h2xâ‚‚.continuous_at).preimage_mem_nhds (ext_chart_at_source_mem_nhds _ _) },
  /- The conclusion is the same as the following, when unfolding coord_change of
    `tangent_bundle_core` -/
  change cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x', (fderiv_within ğ•œ (ext_chart_at I' (f x (g x)) âˆ˜ (ext_chart_at I' (f x' (g x'))).symm)
        (range I') (ext_chart_at I' (f x' (g x')) (f x' (g x')))).comp
        ((mfderiv I I' (f x') (g x')).comp (fderiv_within ğ•œ (ext_chart_at I (g x') âˆ˜
        (ext_chart_at I (g x)).symm) (range I) (ext_chart_at I (g x) (g x'))))) x,
  refine this.congr_of_eventually_eq _,
  filter_upwards [h2g, h2f,
    h4f.preimage_mem_nhds (ext_chart_at_source_mem_nhds I' (f x (g x)))],
  intros xâ‚‚ hxâ‚‚ h2xâ‚‚ h3xâ‚‚,
  symmetry,
  rw [(h2xâ‚‚.mdifferentiable_at le_rfl).mfderiv],
  have hI := (cont_diff_within_at_ext_coord_change I (g xâ‚‚) (g x) $
    local_equiv.mem_symm_trans_source _ hxâ‚‚ $ mem_ext_chart_source I (g xâ‚‚))
    .differentiable_within_at le_top,
  have hI' := (cont_diff_within_at_ext_coord_change I' (f x (g x)) (f xâ‚‚ (g xâ‚‚)) $
    local_equiv.mem_symm_trans_source _
    (mem_ext_chart_source I' (f xâ‚‚ (g xâ‚‚))) h3xâ‚‚).differentiable_within_at le_top,
  have h3f := (h2xâ‚‚.mdifferentiable_at le_rfl).2,
  refine fderiv_within.compâ‚ƒ _ hI' h3f hI _ _ _ _ (I.unique_diff _ $ mem_range_self _),
  { exact Î» x _, mem_range_self _ },
  { exact Î» x _, mem_range_self _ },
  { simp_rw [written_in_ext_chart_at, function.comp_apply,
      (ext_chart_at I (g xâ‚‚)).left_inv (mem_ext_chart_source I (g xâ‚‚))] },
  { simp_rw [function.comp_apply, (ext_chart_at I (g x)).left_inv hxâ‚‚] }
end

/-- The map `D_xf(x,y)` is `C^n` as a continuous linear map, assuming that `f` is a `C^(n+1)` map
between manifolds.
We have to insert appropriate coordinate changes to make sense of this statement.
This statement is general enough to work for partial derivatives / functions with parameters. -/
lemma cont_mdiff_at.mfderiv'' (f : M â†’ M â†’ M')
  (hf : cont_mdiff_at (I.prod I) I' n (function.uncurry f) (x, x)) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (in_coordinates I I' id (Î» x, f x x) (Î» x', mfderiv I I' (f x') x') x) x :=
hf.mfderiv''' f id cont_mdiff_at_id hmn

/-- The map `mfderiv f` is `C^n` as a continuous linear map, assuming that `f` is `C^(n+1)`.
We have to insert appropriate coordinate changes to make sense of this statement. -/
lemma cont_mdiff_at.mfderiv' {f : M â†’ M'}
  (hf : cont_mdiff_at I I' n f x) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m (in_coordinates I I' id f (mfderiv I I' f) x) x :=
begin
  have : cont_mdiff_at (I.prod I) I' n (Î» x : M Ã— M, f x.2) (x, x) :=
  cont_mdiff_at.comp (x, x) hf cont_mdiff_at_snd,
  apply cont_mdiff_at.mfderiv'' (Î» x, f) this hmn
  -- apply cont_mdiff_at.mfderiv''' (Î» x, f) id this cont_mdiff_at_id hmn
end

instance has_smooth_add_self : has_smooth_add ğ“˜(ğ•œ, F) F :=
âŸ¨by { convert cont_diff_add.cont_mdiff, exact model_with_corners_self_prod.symm,
  exact charted_space_self_prod }âŸ©

end smooth_manifold_with_corners

section maps

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
{H : Type*} [topological_space H]
{H' : Type*} [topological_space H']
{G : Type*} [topological_space G]
{G' : Type*} [topological_space G']
{I : model_with_corners ğ•œ E H} {I' : model_with_corners ğ•œ E' H'}
{J : model_with_corners ğ•œ F G} {J' : model_with_corners ğ•œ F G'}

variables {M : Type*} [topological_space M] [charted_space H M]
{M' : Type*} [topological_space M'] [charted_space H' M']
{N : Type*} [topological_space N] [charted_space G N]
{N' : Type*} [topological_space N'] [charted_space G' N']
{n : â„•âˆ}
(f : C^âˆâŸ®I, M; J, NâŸ¯)

namespace cont_mdiff_map

/-- The first projection of a product, as a smooth map. -/
def fst : C^nâŸ®I.prod I', M Ã— M'; I, MâŸ¯ := âŸ¨prod.fst, cont_mdiff_fstâŸ©

/-- The second projection of a product, as a smooth map. -/
def snd : C^nâŸ®I.prod I', M Ã— M'; I', M'âŸ¯ := âŸ¨prod.snd, cont_mdiff_sndâŸ©

/-- Given two smooth maps `f` and `g`, this is the smooth map `(x, y) â†¦ (f x, g y)`. -/
def prod_mk (f : C^nâŸ®J, N; I, MâŸ¯) (g : C^nâŸ®J, N; I', M'âŸ¯) : C^nâŸ®J, N; I.prod I', M Ã— M'âŸ¯ :=
âŸ¨Î» x, (f x, g x), f.2.prod_mk g.2âŸ©

end cont_mdiff_map

namespace diffeomorph

instance : continuous_map_class (M â‰ƒâ‚˜âŸ®I, JâŸ¯ N) M N :=
{ coe := coe_fn,
  coe_injective' := coe_fn_injective,
  map_continuous := Î» f, f.continuous }

end diffeomorph

end maps

section
variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  {H : Type*} [topological_space H] (I : model_with_corners â„ E H) {M : Type*}
  [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {G : Type*} [normed_add_comm_group G] [normed_space â„ G] [finite_dimensional â„ G]
  {HG : Type*} [topological_space HG] (IG : model_with_corners â„ G HG) {N : Type*}
  [topological_space N] [charted_space HG N] [smooth_manifold_with_corners IG N]

def filter.germ.cont_mdiff_at' {x : M} (Ï† : germ (ğ“ x) N) (n : â„•âˆ) : Prop :=
quotient.lift_on' Ï† (Î» f, cont_mdiff_at I IG n f x) (Î» f g h, propext begin
  split,
  all_goals { refine Î» H, H.congr_of_eventually_eq _ },
  exacts [h.symm, h]
end)
end
