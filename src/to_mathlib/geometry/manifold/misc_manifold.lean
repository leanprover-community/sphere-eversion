import geometry.manifold.diffeomorph
import geometry.manifold.algebra.monoid
import geometry.manifold.metrizable
import to_mathlib.analysis.calculus

open bundle set function filter
open_locale manifold topology
noncomputable theory

section charted_space

variables {M H : Type*} [topological_space M] [topological_space H] [charted_space H M]
  (G : structure_groupoid H)

end charted_space

namespace model_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H]
  {M : Type*} [topological_space M] (f : local_homeomorph M H) (I : model_with_corners ğ•œ E H)

end model_with_corners


-- todo: make `vector_bundle_core.total_space` protected!
namespace vector_bundle_core

variables {ğ•œ B F : Type*} [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group F] [normed_space ğ•œ F] [topological_space B]
  {Î¹ : Type*} (Z : vector_bundle_core ğ•œ B F Î¹) {i j : Î¹}

@[simp, mfld_simps] lemma local_triv_continuous_linear_map_at {b : B} (hb : b âˆˆ Z.base_set i) :
  (Z.local_triv i).continuous_linear_map_at ğ•œ b = Z.coord_change (Z.index_at b) i b :=
begin
  ext1 v,
  rw [(Z.local_triv i).continuous_linear_map_at_apply ğ•œ, (Z.local_triv i).coe_linear_map_at_of_mem],
  exacts [rfl, hb]
end

@[simp, mfld_simps] lemma trivialization_at_continuous_linear_map_at {bâ‚€ b : B}
  (hb : b âˆˆ (trivialization_at F Z.fiber bâ‚€).base_set) :
  (trivialization_at F Z.fiber bâ‚€).continuous_linear_map_at ğ•œ b =
  Z.coord_change (Z.index_at b) (Z.index_at bâ‚€) b :=
Z.local_triv_continuous_linear_map_at hb

@[simp, mfld_simps] lemma local_triv_symmL {b : B} (hb : b âˆˆ Z.base_set i) :
  (Z.local_triv i).symmL ğ•œ b = Z.coord_change i (Z.index_at b) b :=
by { ext1 v, rw [(Z.local_triv i).symmL_apply ğ•œ, (Z.local_triv i).symm_apply], exacts [rfl, hb] }

@[simp, mfld_simps] lemma trivialization_at_symmL {bâ‚€ b : B}
  (hb : b âˆˆ (trivialization_at F Z.fiber bâ‚€).base_set) :
  (trivialization_at F Z.fiber bâ‚€).symmL ğ•œ b = Z.coord_change (Z.index_at bâ‚€) (Z.index_at b) b :=
Z.local_triv_symmL hb

@[simp, mfld_simps] lemma trivialization_at_coord_change_eq {bâ‚€ bâ‚ b : B}
  (hb : b âˆˆ (trivialization_at F Z.fiber bâ‚€).base_set âˆ© (trivialization_at F Z.fiber bâ‚).base_set)
  (v : F) :
  (trivialization_at F Z.fiber bâ‚€).coord_changeL ğ•œ (trivialization_at F Z.fiber bâ‚) b v =
  Z.coord_change (Z.index_at bâ‚€) (Z.index_at bâ‚) b v :=
Z.local_triv_coord_change_eq _ _ hb v

end vector_bundle_core

section
open smooth_manifold_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
-- declare a smooth manifold `M` over the pair `(E, H)`.
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] (I : model_with_corners ğ•œ E H)
{M : Type*} [topological_space M] [charted_space H M] [Is : smooth_manifold_with_corners I M]
-- declare a smooth manifold `M'` over the pair `(E', H')`.
{E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners ğ•œ E' H')
{M' : Type*} [topological_space M'] [charted_space H' M'] [I's : smooth_manifold_with_corners I' M']
-- declare a manifold `M''` over the pair `(E'', H'')`.
{E'' : Type*} [normed_add_comm_group E''] [normed_space ğ•œ E'']
{H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
{M'' : Type*} [topological_space M''] [charted_space H'' M'']
-- declare a smooth manifold `N` over the pair `(F, G)`.
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
{G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
{N : Type*} [topological_space N] [charted_space G N] [Js : smooth_manifold_with_corners J N]
-- declare a smooth manifold `N'` over the pair `(F', G')`.
{F' : Type*} [normed_add_comm_group F'] [normed_space ğ•œ F']
{G' : Type*} [topological_space G'] {J' : model_with_corners ğ•œ F' G'}
{N' : Type*} [topological_space N'] [charted_space G' N'] [J's : smooth_manifold_with_corners J' N']
-- Fâ‚, Fâ‚‚, Fâ‚ƒ, Fâ‚„ are normed spaces
{Fâ‚ : Type*} [normed_add_comm_group Fâ‚] [normed_space ğ•œ Fâ‚]
{Fâ‚‚ : Type*} [normed_add_comm_group Fâ‚‚] [normed_space ğ•œ Fâ‚‚]
{Fâ‚ƒ : Type*} [normed_add_comm_group Fâ‚ƒ] [normed_space ğ•œ Fâ‚ƒ]
{Fâ‚„ : Type*} [normed_add_comm_group Fâ‚„] [normed_space ğ•œ Fâ‚„]
-- declare functions, sets, points and smoothness indices
{e : local_homeomorph M H} {e' : local_homeomorph M' H'}
{f fâ‚ : M â†’ M'} {s sâ‚ t : set M} {x : M} {m n : â„•âˆ}

end

section

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
{M : Type*} [topological_space M] [charted_space H M]
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]

lemma ext_chart_at_def (x : M) : ext_chart_at I x = (chart_at H x).extend I := rfl

variables [smooth_manifold_with_corners I M]

namespace tangent_bundle

@[simp, mfld_simps] lemma trivialization_at_continuous_linear_map_at {bâ‚€ b : M}
  (hb : b âˆˆ (trivialization_at E (tangent_space I) bâ‚€).base_set) :
  (trivialization_at E (tangent_space I) bâ‚€).continuous_linear_map_at ğ•œ b =
  (tangent_bundle_core I M).coord_change (achart H b) (achart H bâ‚€) b :=
(tangent_bundle_core I M).local_triv_continuous_linear_map_at hb

@[simp, mfld_simps] lemma trivialization_at_symmL {bâ‚€ b : M}
  (hb : b âˆˆ (trivialization_at E (tangent_space I) bâ‚€).base_set) :
  (trivialization_at E (tangent_space I) bâ‚€).symmL ğ•œ b =
    (tangent_bundle_core I M).coord_change (achart H bâ‚€) (achart H b) b :=
(tangent_bundle_core I M).local_triv_symmL hb

lemma coord_change_model_space (b b' x : F) :
  (tangent_bundle_core ğ“˜(ğ•œ, F) F).coord_change (achart F b) (achart F b') x = 1 :=
by simpa only [tangent_bundle_core_coord_change] with mfld_simps using
    fderiv_within_id unique_diff_within_at_univ

lemma symmL_model_space (b b' : F) :
  (trivialization_at F (tangent_space ğ“˜(ğ•œ, F)) b).symmL ğ•œ b' = (1 : F â†’L[ğ•œ] F) :=
begin
  rw [tangent_bundle.trivialization_at_symmL, coord_change_model_space],
  apply mem_univ
end

lemma continuous_linear_map_at_model_space (b b' : F) :
  (trivialization_at F (tangent_space ğ“˜(ğ•œ, F)) b).continuous_linear_map_at ğ•œ b' =
  (1 : F â†’L[ğ•œ] F) :=
begin
  rw [tangent_bundle.trivialization_at_continuous_linear_map_at, coord_change_model_space],
  apply mem_univ
end

end tangent_bundle

end

section

variables {ğ•œ B B' F M : Type*} {E : B â†’ Type*} [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group F] [normed_space ğ•œ F]
  [topological_space (total_space E)] [âˆ€ x, topological_space (E x)]

  {EB : Type*} [normed_add_comm_group EB] [normed_space ğ•œ EB]
  {HB : Type*} [topological_space HB] (IB : model_with_corners ğ•œ EB HB)
  (E' : B â†’ Type*) [Î  x, has_zero (E' x)]
  {EM : Type*} [normed_add_comm_group EM] [normed_space ğ•œ EM]
  {HM : Type*} [topological_space HM] {IM : model_with_corners ğ•œ EM HM}
  [topological_space M] [charted_space HM M]
  {n : â„•âˆ}

variables [topological_space B] [charted_space HB B] [fiber_bundle F E]
variables {F E IB} [smooth_manifold_with_corners IB B]


end


section smooth_manifold_with_corners
open smooth_manifold_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
  {F' : Type*} [normed_add_comm_group F'] [normed_space ğ•œ F']
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
  {G' : Type*} [topological_space G'] {J' : model_with_corners ğ•œ F' G'}
  {M : Type*} [topological_space M] [charted_space H M]
  {M' : Type*} [topological_space M'] [charted_space H' M']
  {N : Type*} [topological_space N] [charted_space G N]
  {N' : Type*} [topological_space N'] [charted_space G' N']
  {F'' : Type*} [normed_add_comm_group F''] [normed_space ğ•œ F'']
  {E'' : Type*} [normed_add_comm_group E''] [normed_space ğ•œ E'']
  {H'' : Type*} [topological_space H''] {I'' : model_with_corners ğ•œ E'' H''}
  {M'' : Type*} [topological_space M''] [charted_space H'' M'']
  {e : local_homeomorph M H}
variables {f : M â†’ M'} {m n : â„•âˆ} {s : set M} {x x' : M}

-- this can be useful to see where we (ab)use definitional equalities
-- local attribute [irreducible] tangent_space

/-! The two instances below deserve some further thought. For example one might not want the tangent
space at every point to carry a canonical norm.

Note that `dual_pair.update` requires `F` to be a `normed_add_comm_group` (though perhaps we could
get away with `has_continuous_smul` with sufficient extra work).

In `rel_mfld.slice` we use `dual_pair.update` applied to `tangent_space`. If we don't add these
instances, then in fact Lean still accepts the definition. What is going on is that Lean
is unfolding the definition of `tangent_space`, realizing that `tangent_space I x = E` and
`tangent_space I' y = E'` and using the `normed_add_comm_group` instances of these types.
Note that this still uses these instances but at the cost that up to reducible transparency, the
term is not type-correct (in other words: you have to unfold `tangent_space` to realize that the
term is type-correct).

This means that many tactics, like `simp`, `rw`, and `dsimp` fail to rewrite within this term,
because the result is not type correct up to reducible transparancy.

Declaring these instances avoids such problems. -/

section

variables [smooth_manifold_with_corners I M]
instance {x : M} : normed_add_comm_group (tangent_space I x) := by delta_instance tangent_space
instance {x : M} : normed_space ğ•œ (tangent_space I x) := by delta_instance tangent_space
end
-- lemma tangent_bundle_core_coord_change_achart (x x' : M) (z : H) :
--   (tangent_bundle_core I M).coord_change (achart H x) (achart H x') z =
--   fderiv_within ğ•œ (ext_chart_at I x' âˆ˜ (ext_chart_at I x).symm) (range I) (I z) :=
-- rfl

lemma cont_mdiff_prod {f : M â†’ M' Ã— N'} :
  cont_mdiff I (I'.prod J') n f â†”
  cont_mdiff I I' n (Î» x, (f x).1) âˆ§ cont_mdiff I J' n (Î» x, (f x).2) :=
âŸ¨Î» h, âŸ¨h.fst, h.sndâŸ©, Î» h, by { convert h.1.prod_mk h.2, ext x; refl }âŸ©

lemma cont_mdiff_at_prod {f : M â†’ M' Ã— N'} {x : M} :
  cont_mdiff_at I (I'.prod J') n f x â†”
  cont_mdiff_at I I' n (Î» x, (f x).1) x âˆ§ cont_mdiff_at I J' n (Î» x, (f x).2) x :=
âŸ¨Î» h, âŸ¨h.fst, h.sndâŸ©, Î» h, by { convert h.1.prod_mk h.2, ext x; refl }âŸ©

lemma smooth_prod {f : M â†’ M' Ã— N'} :
  smooth I (I'.prod J') f â†”
  smooth I I' (Î» x, (f x).1) âˆ§ smooth I J' (Î» x, (f x).2) :=
cont_mdiff_prod

lemma smooth_at_prod {f : M â†’ M' Ã— N'} {x : M} :
  smooth_at I (I'.prod J') f x â†”
  smooth_at I I' (Î» x, (f x).1) x âˆ§ smooth_at I J' (Î» x, (f x).2) x :=
cont_mdiff_at_prod

lemma cont_mdiff_within_at.congr_of_eventually_eq_insert {f f' : M â†’ M'}
  (hf : cont_mdiff_within_at I I' n f s x)
  (h : f' =á¶ [ğ“[insert x s] x] f) : cont_mdiff_within_at I I' n f' s x :=
hf.congr_of_eventually_eq (h.filter_mono $ nhds_within_mono x $ subset_insert x s) $
  h.self_of_nhds_within (mem_insert x s)

lemma cont_mdiff_at.comp_of_eq {g : M' â†’ M''} {x : M} {y : M'}
  (hg : cont_mdiff_at I' I'' n g y) (hf : cont_mdiff_at I I' n f x) (hx : f x = y) :
  cont_mdiff_at I I'' n (g âˆ˜ f) x :=
by { subst hx, exact hg.comp x hf }

lemma cont_mdiff_within_at.comp_of_eq {t : set M'} {g : M' â†’ M''} {x : M} {y : M'}
  (hg : cont_mdiff_within_at I' I'' n g t y) (hf : cont_mdiff_within_at I I' n f s x)
  (st : maps_to f s t) (hx : f x = y) :
  cont_mdiff_within_at I I'' n (g âˆ˜ f) s x :=
by { subst hx, exact hg.comp x hf st }

variables (I)
lemma cont_mdiff_on_model_symm : cont_mdiff_on ğ“˜(ğ•œ, E) I n I.symm (range I) :=
begin
  rw [cont_mdiff_on_iff],
  refine âŸ¨I.continuous_on_symm, Î» x y, _âŸ©,
  simp only with mfld_simps,
  exact cont_diff_on_id.congr (Î» x', I.right_inv)
end
variables {I}


section
variables [smooth_manifold_with_corners I M]

lemma cont_mdiff_on_extend_symm (he : e âˆˆ maximal_atlas I M) :
  cont_mdiff_on ğ“˜(ğ•œ, E) I n (e.extend I).symm (I '' e.target) :=
begin
  have h1 := cont_mdiff_on_model_symm I,
  have h2 := cont_mdiff_on_symm_of_mem_maximal_atlas he,
  refine h2.comp (h1.mono $ image_subset_range _ _) _,
  simp_rw [image_subset_iff, local_equiv.restr_coe_symm, I.to_local_equiv_coe_symm,
    preimage_preimage, I.left_inv, preimage_id']
end

variables (I)
lemma cont_mdiff_on_ext_chart_at_symm (x : M) :
  cont_mdiff_on ğ“˜(ğ•œ, E) I n (ext_chart_at I x).symm (ext_chart_at I x).target :=
begin
  convert cont_mdiff_on_extend_symm (chart_mem_maximal_atlas I x),
  rw [ext_chart_at_target, I.image_eq]
end

end

variables (I)

lemma model_with_corners.fderiv_within_comp_symm (x : H) :
  fderiv_within ğ•œ (I âˆ˜ I.symm) (range I) (I x) = continuous_linear_map.id ğ•œ E :=
begin
  have : fderiv_within ğ•œ (I âˆ˜ I.symm) (range I) (I x) = fderiv_within ğ•œ id (range I) (I x),
  { refine fderiv_within_congr I.unique_diff_at_image (Î» y hy, _) (by simp only with mfld_simps),
    exact model_with_corners.right_inv _ hy },
  rwa fderiv_within_id I.unique_diff_at_image at this
end

lemma tangent_bundle_core_coord_change_model_space (x x' : H) (z : H) :
  (tangent_bundle_core I H).coord_change (achart H x) (achart H x') z =
  continuous_linear_map.id ğ•œ E :=
begin
  simp only [tangent_bundle_core_coord_change_achart, ext_chart_at, I.fderiv_within_comp_symm] with mfld_simps,
end

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners I' M']
  [smooth_manifold_with_corners J N]

lemma cont_diff_on_coord_change' {e e' : local_homeomorph M H}
  (h : e âˆˆ atlas H M) (h' : e' âˆˆ atlas H M) :
  cont_diff_on ğ•œ âŠ¤ (I âˆ˜ (e.symm â‰«â‚• e') âˆ˜ I.symm) (I.symm â»Â¹' (e.symm â‰«â‚• e').source âˆ© range I) :=
(has_groupoid.compatible (cont_diff_groupoid âŠ¤ I) h h').1

end smooth_manifold_with_corners

section vector_bundle

open smooth_manifold_with_corners vector_bundle_core
open_locale bundle

variables {ğ•œ B F M : Type*} {E : B â†’ Type*}
  [nontrivially_normed_field ğ•œ]
  [âˆ€ x, add_comm_monoid (E x)] [âˆ€ x, module ğ•œ (E x)]
  [normed_add_comm_group F] [normed_space ğ•œ F]
  [topological_space (total_space E)] [âˆ€ x, topological_space (E x)]
  {EB : Type*} [normed_add_comm_group EB] [normed_space ğ•œ EB]
  {HB : Type*} [topological_space HB] {IB : model_with_corners ğ•œ EB HB}
  [topological_space B] [charted_space HB B]
  {EM : Type*} [normed_add_comm_group EM] [normed_space ğ•œ EM]
  {HM : Type*} [topological_space HM] {IM : model_with_corners ğ•œ EM HM}
  [topological_space M] [charted_space HM M]
  {n : â„•âˆ}
  [fiber_bundle F E] [vector_bundle ğ•œ F E]
  {e e' : trivialization F (Ï€ E)}

theorem vector_bundle_core.smooth_at_coord_change {Î¹} (Z : vector_bundle_core ğ•œ B F Î¹)
  [Z.is_smooth IB] (i j : Î¹) {xâ‚€ : B}
  (hxâ‚€ : xâ‚€ âˆˆ Z.base_set i âˆ© Z.base_set j) :
  smooth_at IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Z.coord_change i j) xâ‚€ :=
(Z.smooth_on_coord_change IB i j).smooth_at $
  ((Z.is_open_base_set i).inter (Z.is_open_base_set j)).mem_nhds hxâ‚€

variables (IB) [smooth_manifold_with_corners IB B] [smooth_vector_bundle F E IB]
lemma smooth_at_coord_change (e e' : trivialization F (Ï€ E)) {xâ‚€ : B}
  (hxâ‚€ : xâ‚€ âˆˆ e.base_set âˆ© e'.base_set)
  [mem_trivialization_atlas e] [mem_trivialization_atlas e']  :
  smooth_at IB ğ“˜(ğ•œ, F â†’L[ğ•œ] F) (Î» b : B, (e.coord_changeL ğ•œ e' b : F â†’L[ğ•œ] F)) xâ‚€ :=
(smooth_on_coord_change e e').smooth_at $ (e.open_base_set.inter e'.open_base_set).mem_nhds hxâ‚€

variables {IB}
lemma cont_mdiff_at_coord_change_apply (e e' : trivialization F (Ï€ E)) {xâ‚€ : M}
  {f : M â†’ B} {g : M â†’ F} (hf : cont_mdiff_at IM IB n f xâ‚€)
  (hg : cont_mdiff_at IM ğ“˜(ğ•œ, F) n g xâ‚€)
  (hxâ‚€ : f xâ‚€ âˆˆ e.base_set âˆ© e'.base_set)
  [mem_trivialization_atlas e] [mem_trivialization_atlas e']  :
  cont_mdiff_at IM ğ“˜(ğ•œ, F) n (Î» x, e.coord_changeL ğ•œ e' (f x) (g x)) xâ‚€ :=
(((smooth_at_coord_change IB e e' hxâ‚€).of_le le_top).comp xâ‚€ hf).clm_apply hg

end vector_bundle


section smooth_manifold_with_corners
open smooth_manifold_with_corners

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
  {F' : Type*} [normed_add_comm_group F'] [normed_space ğ•œ F']
  {H : Type*} [topological_space H] {I : model_with_corners ğ•œ E H}
  {H' : Type*} [topological_space H'] {I' : model_with_corners ğ•œ E' H'}
  {G : Type*} [topological_space G] {J : model_with_corners ğ•œ F G}
  {G' : Type*} [topological_space G'] {J' : model_with_corners ğ•œ F' G'}
  {M : Type*} [topological_space M] [charted_space H M]
  {M' : Type*} [topological_space M'] [charted_space H' M']
  {N : Type*} [topological_space N] [charted_space G N]
  {N' : Type*} [topological_space N'] [charted_space G' N']
  {F'' : Type*} [normed_add_comm_group F''] [normed_space ğ•œ F'']
variables {f : M â†’ M'} {m n : â„•âˆ} {s : set M} {x x' : M}
-- declare some additional normed spaces, used for fibers of vector bundles
{Fâ‚ : Type*} [normed_add_comm_group Fâ‚] [normed_space ğ•œ Fâ‚]
{Fâ‚‚ : Type*} [normed_add_comm_group Fâ‚‚] [normed_space ğ•œ Fâ‚‚]

lemma mdifferentiable_at.prod_mk {f : N â†’ M} {g : N â†’ M'} {x : N}
  (hf : mdifferentiable_at J I f x)
  (hg : mdifferentiable_at J I' g x) :
  mdifferentiable_at J (I.prod I') (Î» x, (f x, g x)) x :=
âŸ¨hf.1.prod hg.1, hf.2.prod hg.2âŸ©

variables [smooth_manifold_with_corners I M] [smooth_manifold_with_corners I' M']
  [smooth_manifold_with_corners J N]

/-- A congruence lemma for `mfderiv`, (ab)using the fact that `tangent_space I' (f x)` is
definitionally equal to `E'`. -/
lemma mfderiv_congr_point {x' : M} (h : x = x') :
  @eq (E â†’L[ğ•œ] E') (mfderiv I I' f x) (mfderiv I I' f x') :=
by subst h

/-- A congruence lemma for `mfderiv`, (ab)using the fact that `tangent_space I' (f x)` is
definitionally equal to `E'`. -/
lemma mfderiv_congr {f' : M â†’ M'} (h : f = f') :
  @eq (E â†’L[ğ•œ] E') (mfderiv I I' f x) (mfderiv I I' f' x) :=
by subst h

/-- The derivative of the projection `M Ã— M' â†’ M` is the projection `TM Ã— TM' â†’ TM` -/
lemma mfderiv_fst (x : M Ã— M') :
  mfderiv (I.prod I') I prod.fst x = continuous_linear_map.fst ğ•œ E E' :=
begin
  simp_rw [mfderiv, if_pos smooth_at_fst.mdifferentiable_at, written_in_ext_chart_at,
    ext_chart_at_prod, function.comp, local_equiv.prod_coe, local_equiv.prod_coe_symm],
  have : unique_diff_within_at ğ•œ (range (I.prod I')) (ext_chart_at (I.prod I') x x) :=
  (I.prod I').unique_diff _ (mem_range_self _),
  refine (filter.eventually_eq.fderiv_within_eq this _ _).trans _,
  swap 3,
  { exact (ext_chart_at I x.1).right_inv ((ext_chart_at I x.1).maps_to $
      mem_ext_chart_source I x.1) },
  { refine eventually_of_mem (ext_chart_at_target_mem_nhds_within (I.prod I') x)
      (Î» y hy, local_equiv.right_inv _ _),
    rw [ext_chart_at_prod] at hy,
    exact hy.1 },
  exact fderiv_within_fst this,
end

/-- The derivative of the projection `M Ã— M' â†’ M'` is the projection `TM Ã— TM' â†’ TM'` -/
lemma mfderiv_snd (x : M Ã— M') :
  mfderiv (I.prod I') I' prod.snd x = continuous_linear_map.snd ğ•œ E E' :=
begin
  simp_rw [mfderiv, if_pos smooth_at_snd.mdifferentiable_at, written_in_ext_chart_at,
    ext_chart_at_prod, function.comp, local_equiv.prod_coe, local_equiv.prod_coe_symm],
  have : unique_diff_within_at ğ•œ (range (I.prod I')) (ext_chart_at (I.prod I') x x) :=
  (I.prod I').unique_diff _ (mem_range_self _),
  refine (filter.eventually_eq.fderiv_within_eq this _ _).trans _,
  swap 3,
  { exact (ext_chart_at I' x.2).right_inv ((ext_chart_at I' x.2).maps_to $
      mem_ext_chart_source I' x.2) },
  { refine eventually_of_mem (ext_chart_at_target_mem_nhds_within (I.prod I') x)
      (Î» y hy, local_equiv.right_inv _ _),
    rw [ext_chart_at_prod] at hy,
    exact hy.2 },
  exact fderiv_within_snd this,
end

-- todo: rename differentiable_at.fderiv_within_prod -> differentiable_within_at.fderiv_within_prod
lemma mdifferentiable_at.mfderiv_prod {f : N â†’ M} {g : N â†’ M'} {x : N}
  (hf : mdifferentiable_at J I f x)
  (hg : mdifferentiable_at J I' g x) :
  mfderiv J (I.prod I') (Î» x, (f x, g x)) x = (mfderiv J I f x).prod (mfderiv J I' g x) :=
begin
  classical,
  simp_rw [mfderiv, if_pos (hf.prod_mk hg), if_pos hf, if_pos hg],
  exact differentiable_at.fderiv_within_prod hf.2 hg.2 (J.unique_diff _ (mem_range_self _))
end

lemma mfderiv_prod_left {xâ‚€ : M} {yâ‚€ : M'} :
  mfderiv I (I.prod I') (Î» x, (x, yâ‚€)) xâ‚€ = continuous_linear_map.inl ğ•œ E E' :=
begin
  refine ((mdifferentiable_at_id I).mfderiv_prod (mdifferentiable_at_const I I')).trans _,
  rw [mfderiv_id, mfderiv_const],
  refl
end

lemma mfderiv_prod_right {xâ‚€ : M} {yâ‚€ : M'} :
  mfderiv I' (I.prod I') (Î» y, (xâ‚€, y)) yâ‚€ = continuous_linear_map.inr ğ•œ E E' :=
begin
  refine ((mdifferentiable_at_const I' I).mfderiv_prod (mdifferentiable_at_id I')).trans _,
  rw [mfderiv_id, mfderiv_const],
  refl
end

lemma mfderiv_prod_eq_add {f : N Ã— M â†’ M'} {p : N Ã— M}
  (hf : mdifferentiable_at (J.prod I) I' f p) :
  mfderiv (J.prod I) I' f p =
  (show F Ã— E â†’L[ğ•œ] E', from mfderiv (J.prod I) I' (Î» (z : N Ã— M), f (z.1, p.2)) p +
  mfderiv (J.prod I) I' (Î» (z : N Ã— M), f (p.1, z.2)) p) :=
begin
  dsimp only,
  rw [â† @prod.mk.eta _ _ p] at hf,
  rw [mfderiv_comp p (by apply hf) (smooth_fst.prod_mk smooth_const).mdifferentiable_at,
    mfderiv_comp p (by apply hf) (smooth_const.prod_mk smooth_snd).mdifferentiable_at,
    â† continuous_linear_map.comp_add,
    smooth_fst.mdifferentiable_at.mfderiv_prod smooth_const.mdifferentiable_at,
    smooth_const.mdifferentiable_at.mfderiv_prod smooth_snd.mdifferentiable_at,
    mfderiv_fst, mfderiv_snd, mfderiv_const, mfderiv_const],
  symmetry,
  convert continuous_linear_map.comp_id _,
  { exact continuous_linear_map.fst_prod_zero_add_zero_prod_snd },
  simp_rw [prod.mk.eta],
end

open bundle
variables
  {Z : M â†’ Type*} [topological_space (total_space Z)] [âˆ€ b, topological_space (Z b)]
  [âˆ€ b, add_comm_monoid (Z b)] [âˆ€ b, module ğ•œ (Z b)]
  [fiber_bundle Fâ‚ Z] [vector_bundle ğ•œ Fâ‚ Z] [smooth_vector_bundle Fâ‚ Z I]
  {Zâ‚‚ : M' â†’ Type*} [topological_space (total_space Zâ‚‚)] [âˆ€ b, topological_space (Zâ‚‚ b)]
  [âˆ€ b, add_comm_monoid (Zâ‚‚ b)] [âˆ€ b, module ğ•œ (Zâ‚‚ b)]
  [fiber_bundle Fâ‚‚ Zâ‚‚] [vector_bundle ğ•œ Fâ‚‚ Zâ‚‚] [smooth_vector_bundle Fâ‚‚ Zâ‚‚ I']

open_locale bundle

variables (I I' Z Zâ‚‚ Fâ‚ Fâ‚‚)

/-- When `Ï•` is a continuous linear map that changes vectors in charts around `x` to vectors
  in charts around `y`, `in_coordinates Z Zâ‚‚ xâ‚€ x yâ‚€ y Ï•` is a coordinate change of this continuous
  linear map that makes sense from charts around `xâ‚€` to charts around `yâ‚€`
  by composing it with appropriate coordinate changes given by smooth vector bundles `Z` and `Zâ‚‚`.
-/
def in_coordinates (xâ‚€ x : M) (yâ‚€ y : M') (Ï• : Z x â†’L[ğ•œ] Zâ‚‚ y) : Fâ‚ â†’L[ğ•œ] Fâ‚‚ :=
(trivialization_at Fâ‚‚ Zâ‚‚ yâ‚€).continuous_linear_map_at ğ•œ y âˆ˜L Ï• âˆ˜L
(trivialization_at Fâ‚ Z xâ‚€).symmL ğ•œ x

/-- When `Ï• x` is a continuous linear map that changes vectors in charts around `f x` to vectors
  in charts around `g x`, `in_tangent_coordinates I I' f g Ï• xâ‚€ x` is a coordinate change of
  this continuous linear map that makes sense from charts around `f xâ‚€` to charts around `g xâ‚€`
  by composing it with appropriate coordinate changes.
  Note that in the type of `Ï•` is more accurately
  `Î  x : N, tangent_space I (f x) â†’L[ğ•œ] tangent_space I' (g x)`.
  We are unfolding `tangent_space` in this type so that Lean recognizes that the type of `Ï•` doesn't
  actually depend on `f` or `g`. -/
def in_tangent_coordinates {N} (f : N â†’ M) (g : N â†’ M') (Ï• : N â†’ E â†’L[ğ•œ] E') : N â†’ N â†’ E â†’L[ğ•œ] E' :=
Î» xâ‚€ x, in_coordinates E E' (tangent_space I) (tangent_space I') (f xâ‚€) (f x) (g xâ‚€) (g x) (Ï• x)

variables {Fâ‚ Fâ‚‚}

/-- rewrite `in_coordinates` using continuous linear equivalences. -/
lemma in_coordinates_eq (xâ‚€ x : M) (yâ‚€ y : M') (Ï• : Z x â†’L[ğ•œ] Zâ‚‚ y)
  (hx : x âˆˆ (trivialization_at Fâ‚ Z xâ‚€).base_set)
  (hy : y âˆˆ (trivialization_at Fâ‚‚ Zâ‚‚ yâ‚€).base_set) :
  in_coordinates Fâ‚ Fâ‚‚ Z Zâ‚‚ xâ‚€ x yâ‚€ y Ï• =
  ((trivialization_at Fâ‚‚ Zâ‚‚ yâ‚€).continuous_linear_equiv_at ğ•œ y hy : Zâ‚‚ y â†’L[ğ•œ] Fâ‚‚) âˆ˜L Ï• âˆ˜L
  (((trivialization_at Fâ‚ Z xâ‚€).continuous_linear_equiv_at ğ•œ x hx).symm : Fâ‚ â†’L[ğ•œ] Z x) :=
begin
  ext,
  simp_rw [in_coordinates, continuous_linear_map.coe_comp', continuous_linear_equiv.coe_coe,
    trivialization.coe_continuous_linear_equiv_at_eq,
    trivialization.symm_continuous_linear_equiv_at_eq]
end

protected lemma vector_bundle_core.in_coordinates_eq {Î¹â‚ Î¹â‚‚} (Zâ‚ : vector_bundle_core ğ•œ M Fâ‚ Î¹â‚)
  (Zâ‚‚ : vector_bundle_core ğ•œ M' Fâ‚‚ Î¹â‚‚)
  {xâ‚€ x : M} {yâ‚€ y : M'} (Ï• : Fâ‚ â†’L[ğ•œ] Fâ‚‚)
  (hx : x âˆˆ Zâ‚.base_set (Zâ‚.index_at xâ‚€))
  (hy : y âˆˆ Zâ‚‚.base_set (Zâ‚‚.index_at yâ‚€)) :
    in_coordinates Fâ‚ Fâ‚‚ Zâ‚.fiber Zâ‚‚.fiber xâ‚€ x yâ‚€ y Ï• =
    Zâ‚‚.coord_change (Zâ‚‚.index_at y) (Zâ‚‚.index_at yâ‚€) y âˆ˜L Ï• âˆ˜L
    Zâ‚.coord_change (Zâ‚.index_at xâ‚€) (Zâ‚.index_at x) x :=
by simp_rw [in_coordinates, Zâ‚‚.trivialization_at_continuous_linear_map_at hy,
  Zâ‚.trivialization_at_symmL hx]

/-- The map `in_coordinates` is trivial on the model spaces -/
lemma in_coordinates_tangent_bundle_core_model_space
  (xâ‚€ x : H) (yâ‚€ y : H') (Ï• : E â†’L[ğ•œ] E') :
    in_coordinates E E' (tangent_space I) (tangent_space I') xâ‚€ x yâ‚€ y Ï• = Ï• :=
begin
  refine (vector_bundle_core.in_coordinates_eq _ _ _ _ _).trans _,
  { exact mem_univ x },
  { exact mem_univ y },
  simp_rw [tangent_bundle_core_index_at, tangent_bundle_core_coord_change_model_space,
    continuous_linear_map.id_comp, continuous_linear_map.comp_id]
end

lemma in_tangent_coordinates_model_space (f : N â†’ H) (g : N â†’ H') (Ï• : N â†’ E â†’L[ğ•œ] E') (xâ‚€ : N) :
    in_tangent_coordinates I I' f g Ï• xâ‚€ = Ï• :=
by simp_rw [in_tangent_coordinates, in_coordinates_tangent_bundle_core_model_space]

lemma in_tangent_coordinates_eq (f : N â†’ M) (g : N â†’ M') (Ï• : N â†’ E â†’L[ğ•œ] E') {xâ‚€ x : N}
  (hx : f x âˆˆ (chart_at H (f xâ‚€)).source) (hy : g x âˆˆ (chart_at H' (g xâ‚€)).source) :
  in_tangent_coordinates I I' f g Ï• xâ‚€ x =
  (tangent_bundle_core I' M').coord_change (achart H' (g x)) (achart H' (g xâ‚€)) (g x) âˆ˜L Ï• x âˆ˜L
  (tangent_bundle_core I M).coord_change (achart H (f xâ‚€)) (achart H (f x)) (f x) :=
(tangent_bundle_core I M).in_coordinates_eq (tangent_bundle_core I' M') (Ï• x) hx hy

variables {I I'}

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` is `C^n` at `xâ‚€`,
where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
`cont_mdiff_at.mfderiv_id` and `cont_mdiff_at.mfderiv_const` are special cases of this.

This lemma should be generalized to a `cont_mdiff_within_at` for `mfderiv_within`. If we do that, we
can deduce `cont_mdiff_on.cont_mdiff_on_tangent_map_within` from this.
-/
lemma cont_mdiff_at.mfderiv {xâ‚€ : N} (f : N â†’ M â†’ M') (g : N â†’ M)
  (hf : cont_mdiff_at (J.prod I) I' n (function.uncurry f) (xâ‚€, g xâ‚€))
  (hg : cont_mdiff_at J I m g xâ‚€) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (in_tangent_coordinates I I' g (Î» x, f x (g x)) (Î» x, mfderiv I I' (f x) (g x)) xâ‚€) xâ‚€ :=
begin
  have h4f : continuous_at (Î» x, f x (g x)) xâ‚€,
  { apply continuous_at.comp (by apply hf.continuous_at) (continuous_at_id.prod hg.continuous_at) },
  have h4f := h4f.preimage_mem_nhds (ext_chart_at_source_mem_nhds I' (f xâ‚€ (g xâ‚€))),
  have h3f := cont_mdiff_at_iff_cont_mdiff_at_nhds.mp (hf.of_le $ (self_le_add_left 1 m).trans hmn),
  have h2f : âˆ€á¶  xâ‚‚ in ğ“ xâ‚€, cont_mdiff_at I I' 1 (f xâ‚‚) (g xâ‚‚),
  { refine ((continuous_at_id.prod hg.continuous_at).tendsto.eventually h3f).mono (Î» x hx, _),
    exact hx.comp (g x) (cont_mdiff_at_const.prod_mk cont_mdiff_at_id) },
  have h2g := hg.continuous_at.preimage_mem_nhds (ext_chart_at_source_mem_nhds I (g xâ‚€)),
  have : cont_diff_within_at ğ•œ m (Î» x, fderiv_within ğ•œ
    (ext_chart_at I' (f xâ‚€ (g xâ‚€)) âˆ˜ f ((ext_chart_at J xâ‚€).symm x) âˆ˜ (ext_chart_at I (g xâ‚€)).symm)
    (range I) (ext_chart_at I (g xâ‚€) (g ((ext_chart_at J xâ‚€).symm x))))
    (range J) (ext_chart_at J xâ‚€ xâ‚€),
  { rw [cont_mdiff_at_iff] at hf hg,
    simp_rw [function.comp, uncurry, ext_chart_at_prod, local_equiv.prod_coe_symm] at hf âŠ¢,
    refine (cont_diff_within_at_fderiv_within _
      (hg.2.mono_of_mem _) I.unique_diff hmn _ _ _ _).mono_of_mem _,
    swap 3,
    { simp_rw [function.comp, ext_chart_at_to_inv], exact hf.2 },
    { refine (ext_chart_at J xâ‚€).target âˆ©
      (Î» x, (ext_chart_at J xâ‚€).symm x) â»Â¹' (g â»Â¹' (ext_chart_at I (g xâ‚€)).source) },
    { exact mem_of_superset self_mem_nhds_within
        ((inter_subset_left _ _).trans $ ext_chart_at_target_subset_range J xâ‚€) },
    { simp_rw [mem_inter_iff, mem_preimage, ext_chart_at_to_inv],
      exact âŸ¨local_equiv.maps_to _ (mem_ext_chart_source J xâ‚€), mem_ext_chart_source I (g xâ‚€)âŸ© },
    { simp_rw [model_with_corners.range_prod],
      exact set.prod_mono ((inter_subset_left _ _).trans $ ext_chart_at_target_subset_range J xâ‚€)
        subset_rfl },
    { refine eventually_of_forall (Î» x, mem_range_self _) },
    swap 2,
    { refine inter_mem (ext_chart_at_target_mem_nhds_within J xâ‚€) _,
      refine nhds_within_le_nhds (ext_chart_at_preimage_mem_nhds' _ _ (mem_ext_chart_source J xâ‚€) _),
      exact hg.1.preimage_mem_nhds (ext_chart_at_source_mem_nhds I (g xâ‚€)) },
    simp_rw [function.comp, ext_chart_at_to_inv],
    refine mem_of_superset self_mem_nhds_within _,
    refine (image_subset_range _ _).trans _,
    exact range_comp_subset_range (Î» a, chart_at H (g xâ‚€) $ g $ (chart_at G xâ‚€).symm $ J.symm a) I },
  have : cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x, fderiv_within ğ•œ (ext_chart_at I' (f xâ‚€ (g xâ‚€)) âˆ˜ f x âˆ˜ (ext_chart_at I (g xâ‚€)).symm)
    (range I) (ext_chart_at I (g xâ‚€) (g x))) xâ‚€,
  { simp_rw [cont_mdiff_at_iff_source_of_mem_source (mem_chart_source G xâ‚€),
      cont_mdiff_within_at_iff_cont_diff_within_at, function.comp],
    exact this },
  have : cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x, fderiv_within ğ•œ (ext_chart_at I' (f xâ‚€ (g xâ‚€)) âˆ˜ (ext_chart_at I' (f x (g x))).symm âˆ˜
      written_in_ext_chart_at I I' (g x) (f x) âˆ˜ ext_chart_at I (g x) âˆ˜
      (ext_chart_at I (g xâ‚€)).symm) (range I) (ext_chart_at I (g xâ‚€) (g x))) xâ‚€,
  { refine this.congr_of_eventually_eq _,
    filter_upwards [h2g, h2f],
    intros xâ‚‚ hxâ‚‚ h2xâ‚‚,
    have : âˆ€ x âˆˆ (ext_chart_at I (g xâ‚€)).symm â»Â¹' (ext_chart_at I (g xâ‚‚)).source âˆ©
        (ext_chart_at I (g xâ‚€)).symm â»Â¹' (f xâ‚‚ â»Â¹' (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).source),
      (ext_chart_at I' (f xâ‚€ (g xâ‚€)) âˆ˜ (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).symm âˆ˜
      written_in_ext_chart_at I I' (g xâ‚‚) (f xâ‚‚) âˆ˜ ext_chart_at I (g xâ‚‚) âˆ˜
      (ext_chart_at I (g xâ‚€)).symm) x =
      ext_chart_at I' (f xâ‚€ (g xâ‚€)) (f xâ‚‚ ((ext_chart_at I (g xâ‚€)).symm x)),
    { rintro x âŸ¨hx, h2xâŸ©,
      simp_rw [written_in_ext_chart_at, function.comp_apply],
      rw [(ext_chart_at I (g xâ‚‚)).left_inv hx, (ext_chart_at I' (f xâ‚‚ (g xâ‚‚))).left_inv h2x] },
    refine filter.eventually_eq.fderiv_within_eq_nhds (I.unique_diff _ $ mem_range_self _) _,
    refine eventually_of_mem (inter_mem _ _) this,
    { exact ext_chart_at_preimage_mem_nhds' _ _ hxâ‚‚ (ext_chart_at_source_mem_nhds I (g xâ‚‚)) },
    refine ext_chart_at_preimage_mem_nhds' _ _ hxâ‚‚ _,
    exact (h2xâ‚‚.continuous_at).preimage_mem_nhds (ext_chart_at_source_mem_nhds _ _) },
  /- The conclusion is equal to the following, when unfolding coord_change of
    `tangent_bundle_core` -/
  have : cont_mdiff_at J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (Î» x, (fderiv_within ğ•œ (ext_chart_at I' (f xâ‚€ (g xâ‚€)) âˆ˜ (ext_chart_at I' (f x (g x))).symm)
        (range I') (ext_chart_at I' (f x (g x)) (f x (g x)))).comp
        ((mfderiv I I' (f x) (g x)).comp (fderiv_within ğ•œ (ext_chart_at I (g x) âˆ˜
        (ext_chart_at I (g xâ‚€)).symm) (range I) (ext_chart_at I (g xâ‚€) (g x))))) xâ‚€,
  { refine this.congr_of_eventually_eq _,
    filter_upwards [h2g, h2f, h4f],
    intros xâ‚‚ hxâ‚‚ h2xâ‚‚ h3xâ‚‚,
    symmetry,
    rw [(h2xâ‚‚.mdifferentiable_at le_rfl).mfderiv],
    have hI := (cont_diff_within_at_ext_coord_change I (g xâ‚‚) (g xâ‚€) $
      local_equiv.mem_symm_trans_source _ hxâ‚‚ $ mem_ext_chart_source I (g xâ‚‚))
      .differentiable_within_at le_top,
    have hI' := (cont_diff_within_at_ext_coord_change I' (f xâ‚€ (g xâ‚€)) (f xâ‚‚ (g xâ‚‚)) $
      local_equiv.mem_symm_trans_source _
      (mem_ext_chart_source I' (f xâ‚‚ (g xâ‚‚))) h3xâ‚‚).differentiable_within_at le_top,
    have h3f := (h2xâ‚‚.mdifferentiable_at le_rfl).2,
    refine fderiv_within.compâ‚ƒ _ hI' h3f hI _ _ _ _ (I.unique_diff _ $ mem_range_self _),
    { exact Î» x _, mem_range_self _ },
    { exact Î» x _, mem_range_self _ },
    { simp_rw [written_in_ext_chart_at, function.comp_apply,
        (ext_chart_at I (g xâ‚‚)).left_inv (mem_ext_chart_source I (g xâ‚‚))] },
    { simp_rw [function.comp_apply, (ext_chart_at I (g xâ‚€)).left_inv hxâ‚‚] } },
  refine this.congr_of_eventually_eq _,
  filter_upwards [h2g, h4f] with x hx h2x,
  rw [in_tangent_coordinates_eq],
  { refl },
  { rwa [ext_chart_at_source] at hx },
  { rwa [ext_chart_at_source] at h2x },
end

/-- The function `x â†¦ D_yf(x,y)` is `C^n` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^(n+1)` at `(xâ‚€, xâ‚€)`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of `cont_mdiff_at.mfderiv` (with `g = id`),
and `cont_mdiff_at.mfderiv_const` is a special case of this.
-/
lemma cont_mdiff_at.mfderiv_id {xâ‚€ : M} (f : M â†’ M â†’ M')
  (hf : cont_mdiff_at (I.prod I) I' n (function.uncurry f) (xâ‚€, xâ‚€)) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m
    (in_tangent_coordinates I I' id (Î» x, f x x) (Î» x, mfderiv I I' (f x) x) xâ‚€) xâ‚€ :=
hf.mfderiv f id cont_mdiff_at_id hmn

/-- The derivative `D_yf(y)` is `C^n` at `xâ‚€`, where the derivative is taken as a continuous
linear map. We have to assume that `f` is `C^(n+1)` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `x` to make the derivative sensible.
This is a special case of See `cont_mdiff_at.mfderiv_id` where `f` does not contain any parameters.
-/
lemma cont_mdiff_at.mfderiv_const {xâ‚€ : M} {f : M â†’ M'}
  (hf : cont_mdiff_at I I' n f xâ‚€) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at I ğ“˜(ğ•œ, E â†’L[ğ•œ] E') m (in_tangent_coordinates I I' id f (mfderiv I I' f) xâ‚€) xâ‚€ :=
begin
  have : cont_mdiff_at (I.prod I) I' n (Î» x : M Ã— M, f x.2) (xâ‚€, xâ‚€) :=
  cont_mdiff_at.comp (xâ‚€, xâ‚€) hf cont_mdiff_at_snd,
  apply cont_mdiff_at.mfderiv_id (Î» x, f) this hmn
end

/-- The function that sends `x` to the `y`-derivative of `f(x,y)` at `g(x)` applied to `gâ‚‚(x)` is
`C^n` at `xâ‚€`, where the derivative is taken as a continuous linear map.
We have to assume that `f` is `C^(n+1)` at `(xâ‚€, g(xâ‚€))` and `g` is `C^n` at `xâ‚€`.
We have to insert a coordinate change from `xâ‚€` to `gâ‚(x)` to make the derivative sensible.

This is  similar to `cont_mdiff_at.mfderiv`, but where the continuous linear map is applied to a
(variable) vector.
-/
lemma cont_mdiff_at.mfderiv_apply {xâ‚€ : N'} (f : N â†’ M â†’ M') (g : N â†’ M) (gâ‚ : N' â†’ N)
  (gâ‚‚ : N' â†’ E)
  (hf : cont_mdiff_at (J.prod I) I' n (function.uncurry f) (gâ‚ xâ‚€, g (gâ‚ xâ‚€)))
  (hg : cont_mdiff_at J I m g (gâ‚ xâ‚€))
  (hgâ‚ : cont_mdiff_at J' J m gâ‚ xâ‚€)
  (hgâ‚‚ : cont_mdiff_at J' ğ“˜(ğ•œ, E) m gâ‚‚ xâ‚€) (hmn : m + 1 â‰¤ n) :
  cont_mdiff_at J' ğ“˜(ğ•œ, E') m
    (Î» x, in_tangent_coordinates I I' g (Î» x, f x (g x)) (Î» x, mfderiv I I' (f x) (g x))
      (gâ‚ xâ‚€) (gâ‚ x) (gâ‚‚ x))
    xâ‚€ :=
((hf.mfderiv f g hg hmn).comp_of_eq hgâ‚ rfl).clm_apply hgâ‚‚

instance has_smooth_add_self : has_smooth_add ğ“˜(ğ•œ, F) F :=
âŸ¨by { convert cont_diff_add.cont_mdiff, exact model_with_corners_self_prod.symm,
  exact charted_space_self_prod }âŸ©

end smooth_manifold_with_corners

section maps

variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
{E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
{E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
{F : Type*} [normed_add_comm_group F] [normed_space ğ•œ F]
{H : Type*} [topological_space H]
{H' : Type*} [topological_space H']
{G : Type*} [topological_space G]
{G' : Type*} [topological_space G']
{I : model_with_corners ğ•œ E H} {I' : model_with_corners ğ•œ E' H'}
{J : model_with_corners ğ•œ F G} {J' : model_with_corners ğ•œ F G'}

variables {M : Type*} [topological_space M] [charted_space H M]
{M' : Type*} [topological_space M'] [charted_space H' M']
{N : Type*} [topological_space N] [charted_space G N]
{N' : Type*} [topological_space N'] [charted_space G' N']
{n : â„•âˆ}
(f : C^âˆâŸ®I, M; J, NâŸ¯)

namespace cont_mdiff_map

/-- The first projection of a product, as a smooth map. -/
def fst : C^nâŸ®I.prod I', M Ã— M'; I, MâŸ¯ := âŸ¨prod.fst, cont_mdiff_fstâŸ©

/-- The second projection of a product, as a smooth map. -/
def snd : C^nâŸ®I.prod I', M Ã— M'; I', M'âŸ¯ := âŸ¨prod.snd, cont_mdiff_sndâŸ©

/-- Given two smooth maps `f` and `g`, this is the smooth map `(x, y) â†¦ (f x, g y)`. -/
def prod_mk (f : C^nâŸ®J, N; I, MâŸ¯) (g : C^nâŸ®J, N; I', M'âŸ¯) : C^nâŸ®J, N; I.prod I', M Ã— M'âŸ¯ :=
âŸ¨Î» x, (f x, g x), f.2.prod_mk g.2âŸ©

end cont_mdiff_map

namespace diffeomorph

instance : continuous_map_class (M â‰ƒâ‚˜âŸ®I, JâŸ¯ N) M N :=
{ coe := coe_fn,
  coe_injective' := coe_fn_injective,
  map_continuous := Î» f, f.continuous }

end diffeomorph

end maps

section
variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
  {H : Type*} [topological_space H] (I : model_with_corners â„ E H) {M : Type*}
  [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {G : Type*} [normed_add_comm_group G] [normed_space â„ G] [finite_dimensional â„ G]
  {HG : Type*} [topological_space HG] (IG : model_with_corners â„ G HG) {N : Type*}
  [topological_space N] [charted_space HG N] [smooth_manifold_with_corners IG N]

def filter.germ.cont_mdiff_at' {x : M} (Ï† : germ (ğ“ x) N) (n : â„•âˆ) : Prop :=
quotient.lift_on' Ï† (Î» f, cont_mdiff_at I IG n f x) (Î» f g h, propext begin
  split,
  all_goals { refine Î» H, H.congr_of_eventually_eq _ },
  exacts [h.symm, h]
end)
end
