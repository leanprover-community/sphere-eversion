import measure_theory.integral.interval_integral
import measure_theory.group.action
import measure_theory.measure.haar_lebesgue
import measure_theory.group.integration
import to_mathlib.measure_theory.parametric_interval_integral
import to_mathlib.analysis.cont_diff_bump
import to_mathlib.order.filter.small_sets
import analysis.calculus.fderiv_measurable
import analysis.calculus.specific_functions

noncomputable theory
open topological_space measure_theory function set measure_theory.measure
open finite_dimensional continuous_linear_map metric
open_locale pointwise topological_space nnreal measure_theory
open filter (hiding map_map map_id map map_id')


/-!
This file defines the convolution on two vector valued functions, with as domain a group `G` with a
Haar measure. These functions are "multiplied" in the integrand using some continuous bilinear map.

This seems to not be a common version in math (In Bourbaki and various other books on analysis the
functions are only valued in â„ or â„‚).
It doesn't seem to exist in Isabelle (some results containing the word convolution, but not
convolution of functions:
https://arxiv.org/pdf/1702.04603.pdf
https://isabelle.in.tum.de/library/HOL/HOL-Probability/document.pdf )

This version is useful and necessary if we even want to write something like
`fderiv â„ (f â‹† g) = fderiv â„ f â‹† g` (this doesn't typecheck if `f â‹† g` is only defined where `f` is
scalar-valued and `g` is vector-valued)

TODO:
* Generalize abelian groups to groups, where possible
* [maybe] generalize bilinear map to special bilinear map
* Currently the definition of `convolution` works better with measures that are right-invariant.
  Perhaps we should reverse this.

-/

namespace measure_theory

lemma ae_strongly_measurable.comp_measurable'
  {Î± Î² Î³ : Type*} [topological_space Î²]
  {mÎ³ : measurable_space Î³} {mÎ± : measurable_space Î±} {f : Î³ â†’ Î±} {g : Î± â†’ Î²}
  {Î¼ : measure Î³} {Î½ : measure Î±}
  (hg : ae_strongly_measurable g Î½) (hf : measurable f)
  (h : Î¼.map f â‰ª Î½) :
  ae_strongly_measurable (g âˆ˜ f) Î¼ :=
(hg.mono' h).comp_measurable hf

lemma ae_strongly_measurable.fst {Î± Î² Î³ : Type*} [measurable_space Î±] [measurable_space Î²]
  [topological_space Î³] {Î¼ : measure Î±} {Î½ : measure Î²}
  [sigma_finite Î½] {f : Î± â†’ Î³}
  (hf : ae_strongly_measurable f Î¼) : ae_strongly_measurable (Î» (z : Î± Ã— Î²), f z.1) (Î¼.prod Î½) :=
hf.comp_measurable' measurable_fst prod_fst_absolutely_continuous

lemma ae_strongly_measurable.snd {Î± Î² Î³ : Type*} [measurable_space Î±] [measurable_space Î²]
  [topological_space Î³] {Î¼ : measure Î±} {Î½ : measure Î²}
  [sigma_finite Î½] {f : Î² â†’ Î³}
  (hf : ae_strongly_measurable f Î½) : ae_strongly_measurable (Î» (z : Î± Ã— Î²), f z.2) (Î¼.prod Î½) :=
hf.comp_measurable' measurable_snd prod_snd_absolutely_continuous

end measure_theory

section op_norm

theorem continuous_linear_map.dist_le_op_norm {ğ•œ ğ•œâ‚‚ E F : Type*}
  [semi_normed_group E] [semi_normed_group F]
  [nondiscrete_normed_field ğ•œ] [nondiscrete_normed_field ğ•œâ‚‚] [normed_space ğ•œ E] [normed_space ğ•œâ‚‚ F]
  {Ïƒâ‚â‚‚ : ğ•œ â†’+* ğ•œâ‚‚} [ring_hom_isometric Ïƒâ‚â‚‚]
  (f : E â†’SL[Ïƒâ‚â‚‚] F) (x y : E) : dist (f x) (f y) â‰¤ âˆ¥fâˆ¥ * dist x y :=
by simp_rw [dist_eq_norm, â† map_sub, f.le_op_norm]

end op_norm



variables {ğ•œ G Gâ‚€ X Y M R E E' E'' F : Type*}

section continuous_bilinear_map

variables [nondiscrete_normed_field ğ•œ]
  [normed_group E] [normed_group E'] [normed_group E''] [normed_group F]
  [normed_space ğ•œ E] [normed_space ğ•œ E'] [normed_space ğ•œ E''] [normed_space ğ•œ F]

variables {f f' : G â†’ E} {g g' : G â†’ E'}
    {x x' : G} {y y' : E}

namespace continuous_linear_map

lemma map_add_left (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {x x' : E} {y : E'} : L (x + x') y = L x y + L x' y :=
by rw [L.map_add, continuous_linear_map.add_apply]

lemma map_add_right (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {x : E} {y y' : E'} : L x (y + y') = L x y + L x y' :=
(L x).map_add y y'

lemma map_sub_left (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {x x' : E} {y : E'} : L (x - x') y = L x y - L x' y :=
by rw [L.map_sub, continuous_linear_map.sub_apply]

lemma map_sub_right (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {x : E} {y y' : E'} : L x (y - y') = L x y - L x y' :=
(L x).map_sub y y'

lemma map_smul_left (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {c : ğ•œ} {x : E} {y : E'} : L (c â€¢ x) y = c â€¢ L x y :=
by rw [L.map_smul, smul_apply]

lemma map_smul_right (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {c : ğ•œ} {x : E} {y : E'} : L x (c â€¢ y) = c â€¢ L x y :=
(L x).map_smul c y

lemma map_zero_left (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {y : E'} : L 0 y = 0 :=
by rw [L.map_zero, zero_apply]

lemma map_zero_right (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {x : E} : L x 0 = 0 :=
(L x).map_zero

lemma continuousâ‚‚ (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) : continuous (uncurry (Î» x y, L x y)) :=
L.is_bounded_bilinear_map.continuous

lemma has_fderiv_at_const_left [normed_group X] [normed_space ğ•œ X]
  (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {f : X â†’ E'} {f' : X â†’L[ğ•œ] E'}
  (x : X) {c : E} (hf : has_fderiv_at f f' x) : has_fderiv_at (Î» x, L c (f x)) ((L c).comp f') x :=
(L c).has_fderiv_at.comp x hf

lemma has_fderiv_at_const_right [normed_group X] [normed_space ğ•œ X]
  (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {f : X â†’ E} {f' : X â†’L[ğ•œ] E}
  (x : X) {c : E'}
  (hf : has_fderiv_at f f' x) : has_fderiv_at (Î» x, L (f x) c) ((flip L c).comp f') x :=
(flip L).has_fderiv_at_const_left x hf


section

variables [measurable_space X] {Î¼ : measure X}

lemma ae_strongly_measurable_compâ‚‚ (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) {f : X â†’ E} {g : X â†’ E'}
  (hf : ae_strongly_measurable f Î¼) (hg : ae_strongly_measurable g Î¼) :
  ae_strongly_measurable (Î» x, L (f x) (g x)) Î¼ :=
L.continuousâ‚‚.comp_ae_strongly_measurable $ hf.prod_mk hg

end


variables (E'')

/--  Apply the bilinear map pointwise on the second argument -/
@[simps apply]
def precompR (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) : E â†’L[ğ•œ] (E'' â†’L[ğ•œ] E') â†’L[ğ•œ] (E'' â†’L[ğ•œ] F) :=
(continuous_linear_map.compL ğ•œ E'' E' F).comp L

/--  Apply the bilinear map pointwise on the second argument -/
def precompL (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) : (E'' â†’L[ğ•œ] E) â†’L[ğ•œ] E' â†’L[ğ•œ] (E'' â†’L[ğ•œ] F) :=
(precompR E'' (flip L)).flip

end continuous_linear_map

end continuous_bilinear_map

section general_measure
variables
  [measurable_space G] [measurable_space Gâ‚€] [measurable_space X] [measurable_space Y]
  [normed_group E] [normed_group E'] [normed_group E''] [normed_group F]
  [normed_space â„ E] [normed_space â„ E'] [normed_space â„ E'']
  {Î¼ : measure G}

namespace measure_theory

section integrable

-- variables {f : G â†’ E} {g : G} [measurable_space G] [normed_group E] [second_countable_topology E] [normed_space â„ E] [complete_space E] [measurable_space E] [borel_space E] [normed_group F] [measurable_space F] [opens_measurable_space F] [group G] [has_measurable_mul G] [has_measurable_inv G]
variables [group G] [has_measurable_mul G] [has_measurable_inv G]

variables (Î¼)
@[to_additive]
lemma integrable_comp_div_left (f : G â†’ F)
  [is_inv_invariant Î¼] [is_mul_left_invariant Î¼] (g : G) :
  integrable (Î» t, f (g / t)) Î¼ â†” integrable f Î¼ :=
begin
  refine âŸ¨Î» h, _, Î» h, h.comp_div_left gâŸ©,
  convert h.comp_inv.comp_mul_left gâ»Â¹,
  simp_rw [div_inv_eq_mul, mul_inv_cancel_left]
end

end integrable

variables [normed_space â„ F] [complete_space E]

section smul
variables [group G] [mul_action G X] [has_measurable_smul G X]

@[to_additive]
lemma integral_smul_eq_self {Î¼ : measure X} [smul_invariant_measure G X Î¼] (f : X â†’ E) {m : G} :
  âˆ« x, f (m â€¢ x) âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
begin
  have h : measurable_embedding (Î» x : X, m â€¢ x) :=
  (measurable_equiv.smul m).measurable_embedding,
  rw [â† h.integral_map, map_smul]
end

end smul


section mul

variables [group G] {A : set G}
variables {f : G â†’ E}

section has_measurable_mul
variables [has_measurable_mul G]

@[to_additive]
lemma integral_div_right_eq_self
  (f : G â†’ E) (Î¼ : measure G) [is_mul_right_invariant Î¼] (x' : G) :
  âˆ« x, f (x / x') âˆ‚Î¼ = âˆ« x, f x âˆ‚Î¼ :=
by simp_rw [div_eq_mul_inv, integral_mul_right_eq_self f x'â»Â¹]

end has_measurable_mul

section

variables [has_measurable_mulâ‚‚ G] [has_measurable_inv G]
variables (Î¼) [sigma_finite Î¼]

lemma quasi_measure_preserving.prod_of_right {Î± Î² Î³} [measurable_space Î±] [measurable_space Î²]
  [measurable_space Î³] {f : Î± Ã— Î² â†’ Î³} {Î¼ : measure Î±} {Î½ : measure Î²} {Ï„ : measure Î³}
  (hf : measurable f) [sigma_finite Î½]
  (h2f : âˆ€ x, quasi_measure_preserving (Î» y, f (x, y)) Î½ Ï„) :
  quasi_measure_preserving f (Î¼.prod Î½) Ï„ :=
begin
  refine âŸ¨hf, _âŸ©,
  refine absolutely_continuous.mk (Î» s hs h2s, _),
  simp_rw [map_apply hf hs, prod_apply (hf hs), preimage_preimage, (h2f _).preimage_null h2s,
    lintegral_zero],
end

lemma quasi_measure_preserving.prod_of_left {Î± Î² Î³} [measurable_space Î±] [measurable_space Î²]
  [measurable_space Î³] {f : Î± Ã— Î² â†’ Î³} {Î¼ : measure Î±} {Î½ : measure Î²} {Ï„ : measure Î³}
  (hf : measurable f) [sigma_finite Î¼] [sigma_finite Î½]
  (h2f : âˆ€ y, quasi_measure_preserving (Î» x, f (x, y)) Î¼ Ï„) :
  quasi_measure_preserving f (Î¼.prod Î½) Ï„ :=
begin
  refine âŸ¨hf, _âŸ©,
  refine absolutely_continuous.mk (Î» s hs h2s, _),
  simp_rw [map_apply hf hs, prod_apply_symm (hf hs), preimage_preimage, (h2f _).preimage_null h2s,
    lintegral_zero],
end

@[to_additive]
lemma quasi_measure_preserving_div [is_mul_right_invariant Î¼] :
  quasi_measure_preserving (Î» (p : G Ã— G), p.1 / p.2) (Î¼.prod Î¼) Î¼ :=
begin
  refine quasi_measure_preserving.prod_of_left measurable_div _,
  simp_rw [div_eq_mul_inv],
  refine Î» y, âŸ¨measurable_mul_const yâ»Â¹, (map_mul_right_eq_self Î¼ yâ»Â¹).absolutely_continuousâŸ©
end

variables [is_mul_left_invariant Î¼]

@[to_additive]
lemma map_inv_absolutely_continuous : map has_inv.inv Î¼ â‰ª Î¼ :=
(quasi_measure_preserving_inv Î¼).absolutely_continuous

@[to_additive]
lemma absolutely_continuous_map_inv : Î¼ â‰ª map has_inv.inv Î¼ :=
begin
  refine absolutely_continuous.mk (Î» s hs, _),
  rw [map_apply measurable_inv hs, measure_inv_null], exact id
end

@[to_additive] lemma quasi_measure_preserving_mul_right (g : G) :
  quasi_measure_preserving (Î» h : G, h * g) Î¼ Î¼ :=
begin
  refine âŸ¨measurable_mul_const g, absolutely_continuous.mk $ Î» s hs, _âŸ©,
  rw [map_apply (measurable_mul_const g) hs, measure_mul_right_null], exact id,
end

@[to_additive]
lemma map_mul_right_absolutely_continuous (g : G) : map (* g) Î¼ â‰ª Î¼ :=
(quasi_measure_preserving_mul_right Î¼ g).absolutely_continuous

@[to_additive]
lemma absolutely_continuous_map_mul_right (g : G) : Î¼ â‰ª map (* g) Î¼ :=
begin
  refine absolutely_continuous.mk (Î» s hs, _),
  rw [map_apply (measurable_mul_const g) hs, measure_mul_right_null], exact id
end

@[to_additive] lemma quasi_measure_preserving_div_left (g : G) :
  quasi_measure_preserving (Î» h : G, g / h) Î¼ Î¼ :=
begin
  refine âŸ¨measurable_const.div measurable_id, _âŸ©,
  simp_rw [div_eq_mul_inv],
  rw [â† map_map (measurable_const_mul g) measurable_inv],
  refine ((map_inv_absolutely_continuous Î¼).map $ measurable_const_mul g).trans _,
  rw [map_mul_left_eq_self],
end

@[to_additive]
lemma map_div_left_absolutely_continuous (g : G) : map (Î» h, g / h) Î¼ â‰ª Î¼ :=
(quasi_measure_preserving_div_left Î¼ g).absolutely_continuous

@[to_additive]
lemma absolutely_continuous_map_div_left (g : G) : Î¼ â‰ª map (Î» h, g / h) Î¼ :=
begin
  simp_rw [div_eq_mul_inv],
  rw [â† map_map (measurable_const_mul g) measurable_inv],
  conv_lhs { rw [â† map_mul_left_eq_self Î¼ g] },
  exact (absolutely_continuous_map_inv Î¼).map (measurable_const_mul g)
end

end

end mul

end measure_theory

lemma measurable_equiv.map_ae {Î± Î² : Type*} [measurable_space Î±] [measurable_space Î²]
  (f : Î± â‰ƒáµ Î²) {Î¼ : measure Î±} : filter.map f Î¼.ae = (map f Î¼).ae :=
by { ext s, simp_rw [mem_map, mem_ae_iff, â† preimage_compl, f.map_apply] }


variables [group G] {x : G}

@[to_additive]
lemma measurable_div_const [has_measurable_mul G] (g : G) : measurable (Î» h, h / g) :=
by simp_rw [div_eq_mul_inv, measurable_mul_const]

/-- `equiv.div_right` as a `measurable_equiv` -/
@[to_additive /-" `equiv.sub_right` as a `measurable_equiv` "-/]
def measurable_equiv.div_right [has_measurable_mul G] (g : G) : G â‰ƒáµ G :=
{ to_equiv := equiv.div_right g,
  measurable_to_fun := measurable_div_const g,
  measurable_inv_fun := measurable_mul_const g }

/-- `equiv.div_left` as a `measurable_equiv` -/
@[to_additive /-" `equiv.sub_left` as a `measurable_equiv` "-/]
def measurable_equiv.div_left [has_measurable_mul G] [has_measurable_inv G] (g : G) : G â‰ƒáµ G :=
{ to_equiv := equiv.div_left g,
  measurable_to_fun := measurable_id.const_div g,
  measurable_inv_fun := measurable_inv.mul_const g }

@[to_additive]
lemma map_mul_left_ae [has_measurable_mul G] [is_mul_left_invariant Î¼] :
  filter.map (Î» t, x * t) Î¼.ae = Î¼.ae :=
(measurable_equiv.mul_left x).map_ae.trans $ congr_arg ae $ map_mul_left_eq_self Î¼ x

@[to_additive]
lemma map_div_left_ae [has_measurable_mul G] [has_measurable_inv G] [is_mul_left_invariant Î¼]
  [is_inv_invariant Î¼] :
  filter.map (Î» t, x / t) Î¼.ae = Î¼.ae :=
(measurable_equiv.div_left x).map_ae.trans $ congr_arg ae $ map_div_left_eq_self Î¼ x

@[to_additive]
lemma tendsto_mul_left_ae_ae [has_measurable_mul G] [is_mul_left_invariant Î¼] :
  tendsto (Î» t, x * t) Î¼.ae Î¼.ae :=
map_mul_left_ae.le

@[to_additive]
lemma tendsto_div_left_ae_ae [has_measurable_mul G] [has_measurable_inv G] [is_mul_left_invariant Î¼]
  [is_inv_invariant Î¼] :
  tendsto (Î» t, x / t) Î¼.ae Î¼.ae :=
map_div_left_ae.le

end general_measure

open measure_theory

section preparation

variables [nondiscrete_normed_field ğ•œ]
variables [normed_group E] [normed_group E'] [normed_group E''] [normed_group F]
variables [normed_space ğ•œ E] [normed_space ğ•œ E'] [normed_space ğ•œ E''] [normed_space ğ•œ F]
variables {f f' : G â†’ E} {g g' : G â†’ E'}
variables {L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F}

section
variables [add_group G] [topological_space G] [has_continuous_sub G]
lemma continuous.convolution_integrand_snd (hf : continuous f) (hg : continuous g) (x : G) :
  continuous (Î» t, L (f t) (g (x - t))) :=
L.continuousâ‚‚.compâ‚‚ hf (hg.comp $ continuous_const.sub continuous_id)

lemma continuous.convolution_integrand_swap_snd (hf : continuous f) (hg : continuous g) (x : G) :
  continuous (Î» t, L (f (x - t)) (g t)) :=
L.continuousâ‚‚.compâ‚‚ (hf.comp $ continuous_const.sub continuous_id) hg
end

section
variables [measurable_space G] {Î¼ : measure G}

lemma integral_norm_bilinear_le_right (g : G â†’ E') (c : E) (hg : integrable g Î¼) :
  âˆ¥âˆ« x, âˆ¥L c (g x)âˆ¥ âˆ‚Î¼âˆ¥ â‰¤ âˆ¥Lâˆ¥ * âˆ¥câˆ¥ * âˆ« x, âˆ¥g xâˆ¥ âˆ‚Î¼ :=
begin
  simp_rw [â† integral_mul_left],
  rw [real.norm_of_nonneg],
  { exact integral_mono_of_nonneg (eventually_of_forall $ Î» t, norm_nonneg _) (hg.norm.const_mul _)
      (eventually_of_forall $ Î» t, L.le_op_normâ‚‚ _ _) },
  exact integral_nonneg (Î» x, norm_nonneg _),
end

end

end preparation

variables [normed_group E] [normed_group E'] [normed_group E''] [normed_group F]
variables {f f' : G â†’ E} {g g' : G â†’ E'} {x x' : G} {y y' : E}

section nondiscrete_normed_field

variables [nondiscrete_normed_field ğ•œ]
variables [normed_space ğ•œ E] [normed_space ğ•œ E'] [normed_space ğ•œ E''] [normed_space ğ•œ F]
variables (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)

section no_measurability

variables [add_group G] [topological_space G]

lemma has_compact_support.convolution_integrand_bound_right (hcg : has_compact_support g)
  (hg : continuous g) {x t : G} {s : set G} (hx : x âˆˆ s) :
  âˆ¥L (f t) (g (x - t))âˆ¥ â‰¤ (- tsupport g + s).indicator (Î» t, âˆ¥Lâˆ¥ * âˆ¥f tâˆ¥ * (â¨† i, âˆ¥g iâˆ¥)) t :=
begin
  refine le_indicator (Î» t ht, _) (Î» t ht, _) t,
  { refine (L.le_op_normâ‚‚ _ _).trans _,
    refine mul_le_mul_of_nonneg_left
        (le_csupr (hg.norm.bdd_above_range_of_has_compact_support hcg.norm) $ x - t)
        (mul_nonneg (norm_nonneg _) (norm_nonneg _)) },
  { have : x - t âˆ‰ support g,
    { refine mt (Î» hxt, _) ht, refine âŸ¨_, _, set.neg_mem_neg.mpr (subset_closure hxt), hx, _âŸ©,
      rw [neg_sub, sub_add_cancel] },
    rw [nmem_support.mp this, L.map_zero_right, norm_zero] }
end

lemma continuous.convolution_integrand_fst [has_continuous_sub G] (hg : continuous g) (t : G) :
  continuous (Î» x, L (f t) (g (x - t))) :=
L.continuousâ‚‚.compâ‚‚ continuous_const $ hg.comp $ continuous_id.sub continuous_const

lemma has_compact_support.convolution_integrand_bound_left (hcf : has_compact_support f)
  (hf : continuous f) {x t : G} {s : set G} (hx : x âˆˆ s) :
  âˆ¥L (f (x - t)) (g t)âˆ¥ â‰¤ (- tsupport f + s).indicator (Î» t, âˆ¥Lâˆ¥ * (â¨† i, âˆ¥f iâˆ¥) * âˆ¥g tâˆ¥) t :=
by { convert hcf.convolution_integrand_bound_right L.flip hf hx,
  simp_rw [L.op_norm_flip, mul_right_comm] }

end no_measurability

section measurability

variables [measurable_space G] [measurable_space Gâ‚€] [measurable_space X] {Î¼ : measure G}

/-- The convolution of `f` and `g` exists at `x` when the function `t â†¦ L (f t) (g (x - t))` is
  integrable. There are various conditions on `f` and `g` to prove this. -/
def convolution_exists_at [has_sub G] (f : G â†’ E) (g : G â†’ E') (x : G) (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
  (Î¼ : measure G . volume_tac) : Prop :=
integrable (Î» t, L (f t) (g (x - t))) Î¼

/-- The convolution of `f` and `g` exists when the function `t â†¦ L (f t) (g (x - t))` is integrable
  for all `x : G`. There are various conditions on `f` and `g` to prove this. -/
def convolution_exists [has_sub G] (f : G â†’ E) (g : G â†’ E') (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
  (Î¼ : measure G . volume_tac) : Prop :=
âˆ€ x : G, convolution_exists_at f g x L Î¼

section convolution_exists

variables {L}
lemma convolution_exists_at.integrable [has_sub G] {x : G} (h : convolution_exists_at f g x L Î¼) :
  integrable (Î» t, L (f t) (g (x - t))) Î¼ :=
h

variables (L)

section group

variables [add_group G]

section measurable
variables [has_measurable_addâ‚‚ G] [has_measurable_neg G]
variables [sigma_finite Î¼] [is_add_left_invariant Î¼]

lemma measure_theory.ae_strongly_measurable.convolution_integrand_snd
  (hf : ae_strongly_measurable f Î¼) (hg : ae_strongly_measurable g Î¼)
  (x : G) : ae_strongly_measurable (Î» t, L (f t) (g (x - t))) Î¼ :=
begin
  refine L.ae_strongly_measurable_compâ‚‚ hf
    (ae_strongly_measurable.comp_measurable _ $ measurable_id.const_sub x),
  exact hg.mono' (map_sub_left_absolutely_continuous Î¼ x)
end

lemma measure_theory.ae_strongly_measurable.convolution_integrand_swap_snd
  (hf : ae_strongly_measurable f Î¼) (hg : ae_strongly_measurable g Î¼)
  (x : G) : ae_strongly_measurable (Î» t, L (f (x - t)) (g t)) Î¼ :=
begin
  refine L.ae_strongly_measurable_compâ‚‚
    (ae_strongly_measurable.comp_measurable _ $ measurable_id.const_sub x) hg,
  exact hf.mono' (map_sub_left_absolutely_continuous Î¼ x)
end
end measurable



end group

section comm_group

variables [add_comm_group G]

section measurable_group

variables {L} [has_measurable_addâ‚‚ G] [has_measurable_neg G] [is_add_left_invariant Î¼]

lemma convolution_exists_at_flip  [is_neg_invariant Î¼] :
  convolution_exists_at g f x L.flip Î¼ â†” convolution_exists_at f g x L Î¼ :=
begin
  convert integrable_comp_sub_left Î¼ (Î» t, L (f t) (g (x - t))) x,
  ext t,
  simp_rw [sub_sub_cancel],
  refl,
end

lemma convolution_exists_at.integrable_swap [is_neg_invariant Î¼]
  (h : convolution_exists_at f g x L Î¼) : integrable (Î» t, L (f (x - t)) (g t)) Î¼ :=
by { convert h.comp_sub_left x, simp_rw [sub_sub_self], }

lemma convolution_exists_at_iff_integrable_swap [is_neg_invariant Î¼] :
  convolution_exists_at f g x L Î¼ â†” integrable (Î» t, L (f (x - t)) (g t)) Î¼ :=
convolution_exists_at_flip.symm

variables (L) [sigma_finite Î¼]

lemma measure_theory.ae_strongly_measurable.convolution_integrand
  (hf : ae_strongly_measurable f Î¼) (hg : ae_strongly_measurable g Î¼) :
  ae_strongly_measurable (Î» p : G Ã— G, L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î¼) :=
begin
  refine L.ae_strongly_measurable_compâ‚‚ hf.snd
    (ae_strongly_measurable.comp_measurable _ $ measurable_fst.sub measurable_snd),
  refine hg.mono' (quasi_measure_preserving_sub Î¼).absolutely_continuous,
end

lemma measure_theory.integrable.convolution_integrand (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integrable (Î» p : G Ã— G, L (f p.2) (g (p.1 - p.2))) (Î¼.prod Î¼) :=
begin
  -- We can also do this for nonabelian groups, but this exact proof doesn't work
  -- for that case (we use that `Î¼` is right invariant here)
  have h_meas : ae_strongly_measurable (Î» (p : G Ã— G), (L (f p.2)) (g (p.1 - p.2))) (Î¼.prod Î¼) :=
    hf.ae_strongly_measurable.convolution_integrand L hg.ae_strongly_measurable,
  have h2_meas : ae_strongly_measurable (Î» (y : G), âˆ« (x : G), âˆ¥(L (f y)) (g (x - y))âˆ¥ âˆ‚Î¼) Î¼ :=
    h_meas.prod_swap.norm.integral_prod_right',
  simp_rw [integrable_prod_iff'
    (hf.ae_strongly_measurable.convolution_integrand L hg.ae_strongly_measurable)],
  refine âŸ¨eventually_of_forall (Î» t, (L (f t)).integrable_comp (hg.comp_sub_right t)), _âŸ©,
  refine integrable.mono' _ h2_meas (eventually_of_forall $
    Î» t, integral_norm_bilinear_le_right (Î» x, g (x - t)) (f t) (hg.comp_sub_right t)),
  simp_rw [integral_sub_right_eq_self (Î» t, âˆ¥ g t âˆ¥) Î¼],
  exact (hf.norm.const_mul _).mul_const _,
end

lemma integrable.ae_convolution_exists (hf : integrable f Î¼) (hg : integrable g Î¼) :
  âˆ€áµ x âˆ‚Î¼, convolution_exists_at f g x L Î¼ :=
((integrable_prod_iff $ hf.ae_strongly_measurable.convolution_integrand L
  hg.ae_strongly_measurable).mp $ hf.convolution_integrand L hg).1

lemma bdd_above.convolution_exists_at {xâ‚€ : G}
  {s : set G} (hs : measurable_set s) (h2s : support (Î» t, L (f t) (g (xâ‚€ - t))) âŠ† s)
  (hf : integrable_on f s Î¼)
  (hmf : ae_strongly_measurable f Î¼)
  (hbg : bdd_above ((Î» i, âˆ¥g iâˆ¥) '' ((Î» t, xâ‚€ - t) â»Â¹' s)))
  (hmg : ae_strongly_measurable g Î¼) :
    convolution_exists_at f g xâ‚€ L Î¼ :=
begin
  simp at hbg,
  let s' := (Î» t, xâ‚€ - t) â»Â¹' s,
  have : âˆ€áµ (t : G) âˆ‚Î¼,
    âˆ¥L (f t) (g (xâ‚€ - t))âˆ¥ â‰¤ s.indicator (Î» t, âˆ¥Lâˆ¥ * âˆ¥f tâˆ¥ * â¨† i : s', âˆ¥g iâˆ¥) t,
  { refine eventually_of_forall _,
    refine le_indicator (Î» t ht, _) (Î» t ht, _),
    { refine (L.le_op_normâ‚‚ _ _).trans _,
      refine mul_le_mul_of_nonneg_left
        (le_csupr_set hbg $ mem_preimage.mpr _)
        (mul_nonneg (norm_nonneg _) (norm_nonneg _)),
      rwa sub_sub_cancel },
    { have : t âˆ‰ support (Î» t, L (f t) (g (xâ‚€ - t))) := mt (Î» h, h2s h) ht,
      rw [nmem_support.mp this, norm_zero] } },
  refine integrable.mono' _ _ this,
  { rw [integrable_indicator_iff hs], exact (hf.norm.const_mul _).mul_const _ },
  { exact (hmf.convolution_integrand_snd L hmg xâ‚€) }
end

end measurable_group

variables [topological_space G] [topological_add_group G] [borel_space G]
  [second_countable_topology G] [is_add_left_invariant Î¼]
  [sigma_compact_space G] [sigma_finite Î¼]

lemma has_compact_support.convolution_exists_at {xâ‚€ : G}
  (h : has_compact_support (Î» t, L (f t) (g (xâ‚€ - t)))) (hf : locally_integrable f Î¼)
  (hg : continuous g) : convolution_exists_at f g xâ‚€ L Î¼ :=
(((homeomorph.sub_left xâ‚€).compact_preimage.mpr h).bdd_above_image hg.norm.continuous_on)
  .convolution_exists_at L is_closed_closure.measurable_set subset_closure (hf h)
  hf.ae_strongly_measurable (hg.ae_strongly_measurable)

lemma has_compact_support.convolution_exists_right
  (hf : locally_integrable f Î¼) (hcg : has_compact_support g) (hg : continuous g) :
  convolution_exists f g L Î¼ :=
begin
  intro xâ‚€,
  refine has_compact_support.convolution_exists_at L _ hf hg,
  refine (hcg.comp_homeomorph (homeomorph.sub_left xâ‚€)).mono _,
  refine Î» t, mt (Î» ht : g (xâ‚€ - t) = 0, _),
  simp_rw [ht, L.map_zero_right]
end

lemma has_compact_support.convolution_exists_left_of_continuous_right
  (hcf : has_compact_support f) (hf : locally_integrable f Î¼) (hg : continuous g) :
  convolution_exists f g L Î¼ :=
begin
  intro xâ‚€,
  refine has_compact_support.convolution_exists_at L _ hf hg,
  refine hcf.mono _,
  refine Î» t, mt (Î» ht : f t = 0, _),
  simp_rw [ht, L.map_zero_left]
end

lemma has_compact_support.convolution_exists_left [is_neg_invariant Î¼]
  (hcf : has_compact_support f) (hf : continuous f) (hg : locally_integrable g Î¼) :
  convolution_exists f g L Î¼ :=
begin
  intro xâ‚€, rw [â† convolution_exists_at_flip],
  exact hcf.convolution_exists_right L.flip hg hf xâ‚€
end

end comm_group

end convolution_exists

variables [normed_space â„ F] [complete_space F]

/-- The convolution of two functions `f` and `g`. -/
def convolution [has_sub G] (f : G â†’ E) (g : G â†’ E') (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
  (Î¼ : measure G . volume_tac) : G â†’ F :=
Î» x, âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼

localized "notation f ` â‹†[`:67 L:67 `, ` Î¼:67 `] `:0 g:66 := convolution f g L Î¼" in convolution
localized "notation f ` â‹†[`:67 L:67 `]`:0 g:66 := convolution f g L
  measure_theory.measure_space.volume" in convolution
localized "notation f ` â‹† `:67 g:66 := convolution f g (continuous_linear_map.lsmul â„ â„)
  measure_theory.measure_space.volume" in convolution

lemma convolution_def [has_sub G] : (f â‹†[L, Î¼] g) x = âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼ := rfl

section group

variables {L} [add_group G]

lemma smul_convolution [smul_comm_class â„ ğ•œ F]
  {y : ğ•œ} : (y â€¢ f) â‹†[L, Î¼] g = y â€¢ (f â‹†[L, Î¼] g) :=
by { ext, simp only [pi.smul_apply, convolution_def, â† integral_smul, L.map_smul_left] }

lemma convolution_smul [smul_comm_class â„ ğ•œ F]
  {y : ğ•œ} : f â‹†[L, Î¼] (y â€¢ g) = y â€¢ (f â‹†[L, Î¼] g) :=
by { ext, simp only [pi.smul_apply, convolution_def, â† integral_smul, L.map_smul_right] }

lemma zero_convolution : 0 â‹†[L, Î¼] g = 0 :=
by { ext, simp_rw [convolution_def, pi.zero_apply, L.map_zero_left, integral_zero] }

lemma convolution_zero : f â‹†[L, Î¼] 0 = 0 :=
by { ext, simp_rw [convolution_def, pi.zero_apply, L.map_zero_right, integral_zero] }

lemma convolution_exists_at.distrib_add {x : G} (hfg : convolution_exists_at f g x L Î¼)
  (hfg' : convolution_exists_at f g' x L Î¼) :
  (f â‹†[L, Î¼] (g + g')) x = (f â‹†[L, Î¼] g) x + (f â‹†[L, Î¼] g') x :=
by simp only [convolution_def, L.map_add_right, pi.add_apply, integral_add hfg hfg']

lemma convolution_exists.distrib_add (hfg : convolution_exists f g L Î¼)
  (hfg' : convolution_exists f g' L Î¼) : f â‹†[L, Î¼] (g + g') = f â‹†[L, Î¼] g + f â‹†[L, Î¼] g' :=
by { ext, exact (hfg x).distrib_add (hfg' x) }

lemma convolution_exists_at.add_distrib {x : G} (hfg : convolution_exists_at f g x L Î¼)
  (hfg' : convolution_exists_at f' g x L Î¼) :
  ((f + f') â‹†[L, Î¼] g) x = (f â‹†[L, Î¼] g) x + (f' â‹†[L, Î¼] g) x :=
by simp only [convolution_def, L.map_add_left, pi.add_apply, integral_add hfg hfg']

lemma convolution_exists.add_distrib (hfg : convolution_exists f g L Î¼)
  (hfg' : convolution_exists f' g L Î¼) : (f + f') â‹†[L, Î¼] g = f â‹†[L, Î¼] g + f' â‹†[L, Î¼] g :=
by { ext, exact (hfg x).add_distrib (hfg' x) }

variables (L)

lemma convolution_congr [has_measurable_add G] [has_measurable_neg G] [is_add_left_invariant Î¼]
  [is_neg_invariant Î¼] (h1 : f =áµ[Î¼] f') (h2 : g =áµ[Î¼] g') :
  f â‹†[L, Î¼] g = f' â‹†[L, Î¼] g' :=
begin
  ext,
  apply integral_congr_ae,
  exact (h1.prod_mk $ h2.comp_tendsto map_sub_left_ae.le).fun_comp â†¿(Î» x y, L x y)
end

lemma support_convolution_subset_swap : support (f â‹†[L, Î¼] g) âŠ† support g + support f :=
begin
  intros x h2x,
  by_contra hx,
  apply h2x,
  simp_rw [set.mem_add, not_exists, not_and_distrib, nmem_support] at hx,
  rw [convolution_def],
  convert integral_zero G F,
  ext t,
  rcases hx (x - t) t with h|h|h,
  { rw [h, L.map_zero_right] },
  { rw [h, L.map_zero_left] },
  { exact (h $ sub_add_cancel x t).elim }
end

end group

section comm_group

variables [add_comm_group G]

lemma support_convolution_subset : support (f â‹†[L, Î¼] g) âŠ† support f + support g :=
(support_convolution_subset_swap L).trans (add_comm _ _).subset

variables [topological_space G]
variables [topological_add_group G]

lemma has_compact_support.convolution [t2_space G] (hcf : has_compact_support f)
  (hcg : has_compact_support g) : has_compact_support (f â‹†[L, Î¼] g) :=
compact_of_is_closed_subset (hcf.is_compact.add hcg) is_closed_closure $ closure_minimal
  ((support_convolution_subset L).trans $ add_subset_add subset_closure subset_closure)
  (hcf.is_compact.add hcg).is_closed

variables [borel_space G]
variables [is_add_left_invariant Î¼]

variable (L)
/- commutativity of convolution -/
lemma convolution_flip [is_neg_invariant Î¼] : g â‹†[L.flip, Î¼] f = f â‹†[L, Î¼] g :=
by { ext1 x, simp_rw [convolution_def], rw [â† integral_sub_left_eq_self _ Î¼ x],
  simp_rw [sub_sub_self], refl }
variable {L}

lemma convolution_eq_swap [is_neg_invariant Î¼] : (f â‹†[L, Î¼] g) x = âˆ« t, L (f (x - t)) (g t) âˆ‚Î¼ :=
by { rw [â† convolution_flip], refl }

variables (L) [second_countable_topology G] [sigma_finite Î¼]

lemma integrable.integrable_convolution (hf : integrable f Î¼) (hg : integrable g Î¼) :
  integrable (f â‹†[L, Î¼] g) Î¼ :=
(hf.convolution_integrand L hg).integral_prod_left

lemma has_compact_support.continuous_convolution_right [locally_compact_space G] [t2_space G]
  (hf : locally_integrable f Î¼) (hcg : has_compact_support g)
  (hg : continuous g) : continuous (f â‹†[L, Î¼] g) :=
begin
  rw [continuous_iff_continuous_at],
  intro xâ‚€,
  obtain âŸ¨K, hK, h2KâŸ© := exists_compact_mem_nhds xâ‚€,
  let K' := - tsupport g + K,
  have hK' : is_compact K' := hcg.neg.add hK,
  have : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ (t : G) âˆ‚Î¼,
    âˆ¥L (f t) (g (x - t))âˆ¥ â‰¤ K'.indicator (Î» t, âˆ¥Lâˆ¥ * âˆ¥f tâˆ¥ * (â¨† i, âˆ¥g iâˆ¥)) t :=
  eventually_of_mem h2K (Î» x hx, eventually_of_forall $
    Î» t, hcg.convolution_integrand_bound_right L hg hx),
  refine continuous_at_of_dominated _ this _ _,
  { exact eventually_of_forall
      (hf.ae_strongly_measurable.convolution_integrand_snd L hg.ae_strongly_measurable) },
  { rw [integrable_indicator_iff hK'.measurable_set], exact ((hf hK').norm.const_mul _).mul_const _ },
  { exact eventually_of_forall (Î» t, (L.continuousâ‚‚.compâ‚‚ continuous_const $
      hg.comp $ continuous_id.sub $ by apply continuous_const).continuous_at) }
end

lemma has_compact_support.continuous_convolution_left [locally_compact_space G] [t2_space G]
  [is_neg_invariant Î¼]
  (hcf : has_compact_support f) (hf : continuous f) (hg : locally_integrable g Î¼) :
    continuous (f â‹†[L, Î¼] g) :=
by { rw [â† convolution_flip], exact hcf.continuous_convolution_right L.flip hg hf }

end comm_group

section normed_group

variables [normed_group G]

/-- We can simplify the RHS further if we assume `f` is integrable, but also if `L = (â€¢)`. -/
lemma convolution_eq_right' {xâ‚€ : G} {R : â„}
  (hf : support f âŠ† ball (0 : G) R)
  (hg : âˆ€ x âˆˆ ball xâ‚€ R, g x = g xâ‚€) : (f â‹†[L, Î¼] g) xâ‚€ = âˆ« (t : G), (L (f t)) (g xâ‚€) âˆ‚Î¼ :=
begin
  have h2 : âˆ€ t, L (f t) (g (xâ‚€ - t)) = L (f t) (g xâ‚€),
  { intro t, by_cases ht : t âˆˆ support f,
    { have h2t := hf ht,
      rw [mem_ball_zero_iff] at h2t,
      specialize hg (xâ‚€ - t),
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, â† sub_eq_add_neg] at hg,
      rw [hg h2t] },
    { rw [nmem_support] at ht,
      simp_rw [ht, L.map_zero_left] } },
  simp_rw [convolution_def, h2],
end

variables [borel_space G] [second_countable_topology G]
variables [is_add_left_invariant Î¼] [sigma_finite Î¼]

lemma dist_convolution_le' {xâ‚€ : G} {R Îµ : â„}
  (hif : integrable f Î¼)
  (hR : 0 < R) -- todo: remove this assumption(?)
  (hf : support f âŠ† ball (0 : G) R)
  (hmg : ae_strongly_measurable g Î¼)
  (hg : âˆ€ x âˆˆ ball xâ‚€ R, dist (g x) (g xâ‚€) â‰¤ Îµ) :
  dist ((f â‹†[L, Î¼] g : G â†’ F) xâ‚€) (âˆ« (t : G), (L (f t)) (g xâ‚€) âˆ‚Î¼) â‰¤ âˆ¥Lâˆ¥ * âˆ« x, âˆ¥f xâˆ¥ âˆ‚Î¼ * Îµ :=
begin
  have hÎµ : 0 â‰¤ Îµ, { convert hg xâ‚€ (mem_ball_self hR), rw dist_self },
  have hfg : convolution_exists_at f g xâ‚€ L Î¼,
  { refine bdd_above.convolution_exists_at L measurable_set_ball (subset_trans _ hf)
      hif.integrable_on hif.ae_strongly_measurable _ hmg,
    { refine Î» t, mt (Î» ht : f t = 0, _), simp_rw [ht, L.map_zero_left] },
    rw [bdd_above_def],
    refine âŸ¨âˆ¥g xâ‚€âˆ¥ + Îµ, _âŸ©,
    rintro _ âŸ¨x, hx, rflâŸ©,
    refine norm_le_norm_add_const_of_dist_le (hg x _),
    rwa [mem_ball_iff_norm, norm_sub_rev, â† mem_ball_zero_iff] },
  have h2 : âˆ€ t, dist (L (f t) (g (xâ‚€ - t))) (L (f t) (g xâ‚€)) â‰¤ âˆ¥L (f t)âˆ¥ * Îµ,
  { intro t, by_cases ht : t âˆˆ support f,
    { have h2t := hf ht,
      rw [mem_ball_zero_iff] at h2t,
      specialize hg (xâ‚€ - t),
      rw [sub_eq_add_neg, add_mem_ball_iff_norm, norm_neg, â† sub_eq_add_neg] at hg,
      refine ((L (f t)).dist_le_op_norm _ _).trans _,
      refine mul_le_mul_of_nonneg_left (hg h2t) (norm_nonneg _) },
    { rw [nmem_support] at ht,
      simp_rw [ht, L.map_zero_left, L.map_zero, norm_zero, zero_mul, dist_self] } },
  simp_rw [convolution_def],
  simp_rw [dist_eq_norm] at h2 âŠ¢,
  rw [â† integral_sub hfg.integrable], swap, { exact (L.flip (g xâ‚€)).integrable_comp hif },
  refine (norm_integral_le_of_norm_le ((L.integrable_comp hif).norm.mul_const Îµ)
    (eventually_of_forall h2)).trans _,
  rw [integral_mul_right],
  refine mul_le_mul_of_nonneg_right _ hÎµ,
  have h3 : âˆ€ t, âˆ¥L (f t)âˆ¥ â‰¤ âˆ¥Lâˆ¥ * âˆ¥f tâˆ¥ := Î» t, L.le_op_norm (f t),
  refine (integral_mono (L.integrable_comp hif).norm (hif.norm.const_mul _) h3).trans_eq _,
  rw [integral_mul_left]
end

variables [normed_space â„ E] [normed_space â„ E'] [complete_space E']

lemma dist_convolution_le {f : G â†’ â„} {xâ‚€ : G} {R Îµ : â„}
  (hR : 0 < R) -- todo: remove this assumption(?)
  (hf : support f âŠ† ball (0 : G) R)
  (hnf : âˆ€ x, 0 â‰¤ f x)
  (hintf : âˆ« x, f x âˆ‚Î¼ = 1)
  (hmg : ae_strongly_measurable g Î¼)
  (hg : âˆ€ x âˆˆ ball xâ‚€ R, dist (g x) (g xâ‚€) â‰¤ Îµ) :
  dist ((f â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) (g xâ‚€) â‰¤ Îµ :=
begin
  have hÎµ : 0 â‰¤ Îµ, { convert hg xâ‚€ (mem_ball_self hR), rw dist_self },
  have hif : integrable f Î¼,
  { by_contra hif, exact zero_ne_one ((integral_undef hif).symm.trans hintf) },
  convert (dist_convolution_le' _ hif hR hf hmg hg).trans _,
  { simp_rw [lsmul_apply, integral_smul_const, hintf, one_smul] },
  { simp_rw [real.norm_of_nonneg (hnf _), hintf, mul_one],
    convert (mul_le_mul_of_nonneg_right op_norm_lsmul_le hÎµ).trans_eq (one_mul Îµ) }
end

lemma convolution_tendsto {Î¹} {l : filter Î¹} {Ï† : Î¹ â†’ G â†’ â„}
  (hnÏ† : âˆ€ i x, 0 â‰¤ Ï† i x)
  (hiÏ† : âˆ€ i, âˆ« s, Ï† i s âˆ‚Î¼ = 1)
  (hÏ† : tendsto (Î» n, support (Ï† n)) l (ğ“ 0).small_sets)
  (hmg : ae_strongly_measurable g Î¼) {xâ‚€ : G} (hcg : continuous_at g xâ‚€) :
  tendsto (Î» i, (Ï† i â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) l (ğ“ (g xâ‚€)) :=
begin
  simp_rw [tendsto_small_sets_iff] at hÏ†,
  rw [metric.continuous_at_iff] at hcg,
  rw [metric.tendsto_nhds],
  intros Îµ hÎµ,
  rcases hcg (Îµ / 2) (half_pos hÎµ) with âŸ¨Î´, hÎ´, hgÎ´âŸ©,
  refine (hÏ† (ball (0 : G) Î´) $ ball_mem_nhds _ hÎ´).mono (Î» i hi, _),
  exact (dist_convolution_le hÎ´ hi (hnÏ† i) (hiÏ† i) hmg (Î» x hx, (hgÎ´ hx.out).le)).trans_lt
    (half_lt_self hÎµ)
end

end normed_group

namespace cont_diff_bump_of_inner

variables {n : with_top â„•}
variables [normed_space â„ E']
variables [inner_product_space â„ G]
variables [complete_space E']
variables {a : G} {Ï† : cont_diff_bump_of_inner (0 : G)}

lemma convolution_eq_right {xâ‚€ : G}
  (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.R, g x = g xâ‚€) : (Ï† â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€ = integral Î¼ Ï† â€¢ g xâ‚€ :=
by simp_rw [convolution_eq_right' _ Ï†.support_eq.subset hg, lsmul_apply, integral_smul_const]

variables [borel_space G]
variables [is_locally_finite_measure Î¼] [is_open_pos_measure Î¼]
variables [finite_dimensional â„ G]

lemma normed_convolution_eq_right {xâ‚€ : G}
  (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.R, g x = g xâ‚€) : (Ï†.normed Î¼ â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€ = g xâ‚€ :=
by { simp_rw [convolution_eq_right' _ Ï†.support_normed_eq.subset hg, lsmul_apply],
  exact integral_normed_smul Î¼ Ï† (g xâ‚€) }

variables [is_add_left_invariant Î¼]

lemma dist_normed_convolution_le {xâ‚€ : G} {Îµ : â„}
  (hmg : ae_strongly_measurable g Î¼)
  (hg : âˆ€ x âˆˆ ball xâ‚€ Ï†.R, dist (g x) (g xâ‚€) â‰¤ Îµ) :
  dist ((Ï†.normed Î¼ â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) (g xâ‚€) â‰¤ Îµ :=
dist_convolution_le Ï†.R_pos Ï†.support_normed_eq.subset Ï†.nonneg_normed Ï†.integral_normed hmg hg

lemma convolution_tendsto' {Î¹} {Ï† : Î¹ â†’ cont_diff_bump_of_inner (0 : G)}
  {l : filter Î¹} (hÏ† : tendsto (Î» i, (Ï† i).R) l (ğ“ 0))
  (hmg : ae_strongly_measurable g Î¼) {xâ‚€ : G} (hcg : continuous_at g xâ‚€) :
  tendsto (Î» i, ((Î» x, (Ï† i).normed Î¼ x) â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) l (ğ“ (g xâ‚€)) :=
begin
  refine convolution_tendsto (Î» i, (Ï† i).nonneg_normed) (Î» i, (Ï† i).integral_normed) _ hmg hcg,
  rw [normed_group.tendsto_nhds_zero] at hÏ†,
  rw [tendsto_small_sets_iff],
  intros t ht,
  rcases metric.mem_nhds_iff.mp ht with âŸ¨Îµ, hÎµ, htâŸ©,
  refine (hÏ† Îµ hÎµ).mono (Î» i hi, subset_trans _ ht),
  simp_rw [(Ï† i).support_normed_eq],
  rw [real.norm_eq_abs, abs_eq_self.mpr (Ï† i).R_pos.le] at hi,
  exact ball_subset_ball hi.le
end

lemma convolution_tendsto {Î¹} {Ï† : Î¹ â†’ cont_diff_bump_of_inner (0 : G)}
  {l : filter Î¹} (hÏ† : tendsto (Î» i, (Ï† i).R) l (ğ“ 0))
  (hg : continuous g) (xâ‚€ : G) :
  tendsto (Î» i, ((Î» x, (Ï† i).normed Î¼ x) â‹†[lsmul â„ â„, Î¼] g : G â†’ E') xâ‚€) l (ğ“ (g xâ‚€)) :=
convolution_tendsto' hÏ† hg.ae_strongly_measurable hg.continuous_at

end cont_diff_bump_of_inner

end measurability

end nondiscrete_normed_field

open_locale convolution


section normed_space

variables [is_R_or_C ğ•œ]
variables [normed_space ğ•œ E]
variables [normed_space ğ•œ E']
variables [normed_space ğ•œ E'']
variables [normed_space â„ F] [normed_space ğ•œ F]
variables [normed_group G]
variables {n : with_top â„•}
variables [complete_space F]
variables [measurable_space G] [borel_space G] {Î¼ : measure G} [second_countable_topology G]
variables (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
variables [sigma_finite Î¼] [sigma_compact_space G]
variables [is_add_left_invariant Î¼]

lemma convolution_precompR_apply
  {g : G â†’ E'' â†’L[ğ•œ] E'}
  (hf : locally_integrable f Î¼) (hcg : has_compact_support g) (hg : continuous g)
  (xâ‚€ : G) (x : E'') : (f â‹†[L.precompR E'', Î¼] g) xâ‚€ x = (f â‹†[L, Î¼] (Î» a, g a x)) xâ‚€  :=
begin
  have := hcg.convolution_exists_right (L.precompR E'') hf hg xâ‚€,
  simp_rw [convolution_def, continuous_linear_map.integral_apply this],
  refl,
end

variables [normed_space ğ•œ G] [proper_space G]

lemma has_compact_support.has_fderiv_at_convolution_right
  (hf : locally_integrable f Î¼) (hcg : has_compact_support g) (hg : cont_diff ğ•œ 1 g)
  (xâ‚€ : G) : has_fderiv_at (f â‹†[L, Î¼] g) ((f â‹†[L.precompR G, Î¼] fderiv ğ•œ g) xâ‚€) xâ‚€ :=
begin
  set L' := L.precompR G,
  have h1 : âˆ€á¶  x in ğ“ xâ‚€, ae_strongly_measurable (Î» t, L (f t) (g (x - t))) Î¼ :=
  eventually_of_forall
    (hf.ae_strongly_measurable.convolution_integrand_snd L hg.continuous.ae_strongly_measurable),
  have h2 : âˆ€ x, ae_strongly_measurable (Î» t, L' (f t) (fderiv ğ•œ g (x - t))) Î¼,
  { exact hf.ae_strongly_measurable.convolution_integrand_snd L'
      (hg.continuous_fderiv le_rfl).ae_strongly_measurable },
  have h3 : âˆ€ x t, has_fderiv_at (Î» x, g (x - t)) (fderiv ğ•œ g (x - t)) x,
  { intros x t,
    simpa using (hg.differentiable le_rfl).differentiable_at.has_fderiv_at.comp x
      ((has_fderiv_at_id x).sub (has_fderiv_at_const t x)) },
  let K' := - tsupport (fderiv ğ•œ g) + closed_ball xâ‚€ 1,
  have hK' : is_compact K' := (hcg.fderiv ğ•œ).neg.add (is_compact_closed_ball xâ‚€ 1),
  refine has_fderiv_at_integral_of_dominated_of_fderiv_le
    zero_lt_one h1 _ (h2 xâ‚€) _ _ _,
  { exact K'.indicator (Î» t, âˆ¥L'âˆ¥ * âˆ¥f tâˆ¥ * (â¨† x, âˆ¥fderiv ğ•œ g xâˆ¥)) },
  { exact hcg.convolution_exists_right L hf hg.continuous xâ‚€ },
  { refine eventually_of_forall (Î» t x hx, _),
    exact (hcg.fderiv ğ•œ).convolution_integrand_bound_right L'
      (hg.continuous_fderiv le_rfl) (ball_subset_closed_ball hx) },
  { rw [integrable_indicator_iff hK'.measurable_set], exact ((hf hK').norm.const_mul _).mul_const _ },
  { refine eventually_of_forall (Î» t x hx, L.has_fderiv_at_const_left x (h3 x t)) },
end

lemma has_compact_support.has_fderiv_at_convolution_left
  [is_neg_invariant Î¼]
  (hcf : has_compact_support f) (hf : cont_diff ğ•œ 1 f)
  (hg : locally_integrable g Î¼) (xâ‚€ : G) :
  has_fderiv_at (f â‹†[L, Î¼] g) ((fderiv ğ•œ f â‹†[L.precompL G, Î¼] g) xâ‚€) xâ‚€ :=
begin
  simp only [â† convolution_flip] {single_pass := tt},
  exact hcf.has_fderiv_at_convolution_right L.flip hg hf xâ‚€,
end


lemma has_compact_support.cont_diff_convolution_right [complete_space ğ•œ] [finite_dimensional ğ•œ G]
  (hf : locally_integrable f Î¼) (hcg : has_compact_support g)
  (hg : cont_diff ğ•œ n g) : cont_diff ğ•œ n (f â‹†[L, Î¼] g) :=
begin
  induction n using with_top.nat_induction with n ih ih generalizing g,
  { rw [cont_diff_zero] at hg âŠ¢,
    exact hcg.continuous_convolution_right L hf hg },
  { have h : âˆ€ x, has_fderiv_at (f â‹†[L, Î¼] g) ((f â‹†[L.precompR G, Î¼] fderiv ğ•œ g) x) x :=
      hcg.has_fderiv_at_convolution_right L hf hg.one_of_succ,
    rw cont_diff_succ_iff_fderiv_apply,
    split,
    { exact Î» xâ‚€, âŸ¨_, h xâ‚€âŸ© },
    { simp_rw [fderiv_eq h, convolution_precompR_apply L hf (hcg.fderiv ğ•œ)
        (hg.continuous_fderiv (with_top.le_add_self 1 n))],
      intro x,
      refine ih _ _,
      { refine @has_compact_support.comp_left _ _ _ _ _ _ (Î» (G : _ â†’L[ğ•œ] _), G x) _
          (hcg.fderiv ğ•œ) (continuous_linear_map.zero_apply x) },
      { revert x, rw [â† cont_diff_clm_apply],
        exact (cont_diff_succ_iff_fderiv.mp hg).2 } } },
  { rw [cont_diff_top] at hg âŠ¢, exact Î» n, ih n hcg (hg n) }
end

lemma has_compact_support.cont_diff_convolution_left [complete_space ğ•œ] [finite_dimensional ğ•œ G]
  [is_neg_invariant Î¼]
  (hcf : has_compact_support f) (hf : cont_diff ğ•œ n f)
  (hg : locally_integrable g Î¼) :
  cont_diff ğ•œ n (f â‹†[L, Î¼] g) :=
begin
  rw [â† convolution_flip],
  exact hcf.cont_diff_convolution_right L.flip hg hf,
end

end normed_space

section real
/-! The one-variable case -/

variables [is_R_or_C ğ•œ] --[measurable_space ğ•œ] [proper_space ğ•œ]
variables [normed_space ğ•œ E]
variables [normed_space ğ•œ E']
variables [normed_space â„ F] [normed_space ğ•œ F]
variables {fâ‚€ : ğ•œ â†’ E} {gâ‚€ : ğ•œ â†’ E'}
variables {n : with_top â„•}
variables (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F)
variables [complete_space F]
variables {Î¼ : measure ğ•œ}
variables [is_add_left_invariant Î¼] [sigma_finite Î¼]

lemma has_compact_support.has_deriv_at_convolution_right
  (hf : locally_integrable fâ‚€ Î¼) (hcg : has_compact_support gâ‚€) (hg : cont_diff ğ•œ 1 gâ‚€)
  (xâ‚€ : ğ•œ) :
  has_deriv_at (fâ‚€ â‹†[L, Î¼] gâ‚€) ((fâ‚€ â‹†[L, Î¼] deriv gâ‚€) xâ‚€) xâ‚€ :=
begin
  convert (hcg.has_fderiv_at_convolution_right L hf hg xâ‚€).has_deriv_at,
  rw [convolution_precompR_apply L hf (hcg.fderiv ğ•œ) (hg.continuous_fderiv le_rfl)],
  refl,
end

lemma has_compact_support.has_deriv_at_convolution_left [is_neg_invariant Î¼]
  (hcf : has_compact_support fâ‚€) (hf : cont_diff ğ•œ 1 fâ‚€)
  (hg : locally_integrable gâ‚€ Î¼) (xâ‚€ : ğ•œ) :
  has_deriv_at (fâ‚€ â‹†[L, Î¼] gâ‚€) ((deriv fâ‚€ â‹†[L, Î¼] gâ‚€) xâ‚€) xâ‚€ :=
begin
  simp only [â† convolution_flip] {single_pass := tt},
  exact hcf.has_deriv_at_convolution_right L.flip hg hf xâ‚€,
end

end real

/-
TODO:
is_R_or_C -> nondiscrete_normed_field ?
one proof has a comment to remove commutativity

-/
