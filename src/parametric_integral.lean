import measure_theory.interval_integral
import analysis.calculus.mean_value
import analysis.normed_space.finite_dimension

noncomputable theory

open topological_space measure_theory filter first_countable_topology metric
open_locale topological_space filter nnreal big_operators

@[simp]
lemma real.nnabs_of_nonneg {x : â„} (h : 0 â‰¤ x) : real.nnabs x = nnreal.of_real x :=
by { ext, simp [nnreal.coe_of_real x h, abs_of_nonneg h] }

lemma nnreal.coe_of_real_le (x : â„) : (nnreal.of_real x : â„) â‰¤ abs x :=
begin
  by_cases h : 0 â‰¤ x,
  { simp [h, nnreal.coe_of_real x h, le_abs_self] },
  { simp [nnreal.of_real, h, le_abs_self, abs_nonneg] }
end



variables {Î± : Type*} [measurable_space Î±] {Î¼ : measure Î±}


/-! # Integral with parameters -/

section

variables {E : Type*} [normed_group E] [second_countable_topology E] [normed_space â„ E]
  [complete_space E] [measurable_space E] [borel_space E]

variables {X : Type*} [topological_space X] [first_countable_topology X]

lemma continuous_at_of_dominated {F : X â†’ Î± â†’ E} {xâ‚€ : X} {bound : Î± â†’ â„}
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼) (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ a âˆ‚Î¼, âˆ¥F x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous_at (Î» x, F x a) xâ‚€) :
  continuous_at (Î»n, âˆ« a, F n a âˆ‚Î¼) xâ‚€ :=
tendsto_integral_filter_of_dominated_convergence bound
  (first_countable_topology.nhds_generated_countable xâ‚€) â€¹_â€º (mem_of_nhds hF_meas : _) â€¹_â€º â€¹_â€º â€¹_â€º

lemma continuous_of_dominated {F : X â†’ Î± â†’ E} {bound : Î± â†’ â„}
  (hF_meas : âˆ€ x, ae_measurable (F x) Î¼) (h_bound : âˆ€ x, âˆ€áµ a âˆ‚Î¼, âˆ¥F x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼) (h_cont : âˆ€áµ a âˆ‚Î¼, continuous (Î» x, F x a)) :
  continuous (Î»n, âˆ« a, F n a âˆ‚Î¼) :=
continuous_iff_continuous_at.mpr (Î» xâ‚€, continuous_at_of_dominated (eventually_of_forall hF_meas)
  (eventually_of_forall h_bound) â€¹_â€º $ h_cont.mono $ Î» _, continuous.continuous_at)

end

variables {E : Type*} [normed_group E] [measurable_space E] [borel_space E]

lemma integrable_of_norm_sub_le {fâ‚€ fâ‚ : Î± â†’ E} {g : Î± â†’ â„}
  (hfâ‚_m : ae_measurable fâ‚ Î¼)
  (hfâ‚€_i : integrable fâ‚€ Î¼)
  (hg_i : integrable g Î¼)
  (h : âˆ€áµ a âˆ‚Î¼, âˆ¥fâ‚€ a - fâ‚ aâˆ¥ â‰¤ g a) :
  integrable fâ‚ Î¼ :=
begin
  have : âˆ€áµ a âˆ‚Î¼, âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a,
  { apply h.mono,
    intros a ha,
    calc âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + âˆ¥fâ‚€ a - fâ‚ aâˆ¥ : norm_le_insert _ _
    ... â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a : add_le_add_left ha _ },
  exact integrable.mono' (hfâ‚€_i.norm.add hg_i) hfâ‚_m this
end

section
variables [normed_space â„ E] {H : Type*} [normed_group H] [normed_space â„ H]

lemma measurable.apply_continuous_linear_map {Ï† : Î± â†’ H â†’L[â„] E} (hÏ† : measurable Ï†) (v : H) :
  measurable (Î» a, Ï† a v) :=
(continuous_linear_map.apply â„ E v).measurable.comp hÏ†

lemma ae_measurable.apply_continuous_linear_map {Ï† : Î± â†’ H â†’L[â„] E} (hÏ† : ae_measurable Ï† Î¼) (v : H) :
  ae_measurable (Î» a, Ï† a v) Î¼ :=
(continuous_linear_map.apply â„ E v).measurable.comp_ae_measurable hÏ†
end

variables [normed_space â„ E] {H : Type*} [normed_group H] [normed_space â„ H] [borel_space $ H â†’L[â„] E]

lemma measure_theory.integrable.apply_continuous_linear_map {Ï† : Î± â†’ H â†’L[â„] E}
  (Ï†_int : integrable Ï† Î¼) (v : H) : integrable (Î» a, Ï† a v) Î¼ :=
(Ï†_int.norm.mul_const âˆ¥vâˆ¥).mono' (Ï†_int.ae_measurable.apply_continuous_linear_map v)
  (eventually_of_forall $ Î» a, (Ï† a).le_op_norm v)

variables [second_countable_topology E] [complete_space E] [second_countable_topology $ H â†’L[â„] E] 

lemma continuous_linear_map.apply_integral {Ï† : Î± â†’ H â†’L[â„] E}
  (Ï†_int : integrable Ï† Î¼) (v : H) : âˆ« a, Ï† a v âˆ‚Î¼ = (âˆ« a, Ï† a âˆ‚Î¼) v :=
(continuous_linear_map.apply â„ E v).integral_comp_comm Ï†_int

lemma measurable_abs : measurable (abs : â„ â†’ â„) :=
continuous_abs.measurable

lemma has_fderiv_at_of_dominated_loc_of_lip' {F : H â†’ Î± â†’ E} {F' : Î± â†’ (H â†’L[â„] E)} {xâ‚€ : H}
  {bound : Î± â†’ â„}
  {Îµ : â„}
  (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€ x âˆˆ ball xâ‚€ Îµ, ae_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_measurable F' Î¼)
  (h_lipsch : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Îµ))
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' a) xâ‚€) :
  integrable F' Î¼ âˆ§ has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have nneg : âˆ€ x, 0 â‰¤ âˆ¥x - xâ‚€âˆ¥â»Â¹ := Î» x, inv_nonneg.mpr (norm_nonneg _) ,
  set b : Î± â†’ â„ := Î» a, abs (bound a),
  --have b_meas : measurable b :=  measurable_abs.comp bound_measurable,
  have b_int : integrable b Î¼ := bound_integrable.norm,
  have b_nonneg : âˆ€ a, 0 â‰¤ b a := Î» a, abs_nonneg _,
  have hF_int' : âˆ€ x âˆˆ ball xâ‚€ Îµ, integrable (F x) Î¼,
  { intros x x_in,
    have : âˆ€áµ a âˆ‚Î¼, âˆ¥F xâ‚€ a - F x aâˆ¥ â‰¤ Îµ * âˆ¥(bound a : â„)âˆ¥,
    { apply h_lipsch.mono,
      intros a ha,
      rw lipschitz_on_with_iff_norm_sub_le at ha,
      apply (ha xâ‚€ xâ‚€_in x x_in).trans,
      rw [mul_comm, nnreal.coe_nnabs, real.norm_eq_abs],
      rw [mem_ball, dist_eq_norm, norm_sub_rev] at x_in,
      exact mul_le_mul_of_nonneg_right (le_of_lt x_in) (abs_nonneg  _) },
    exact integrable_of_norm_sub_le (hF_meas x x_in) hF_int
      (integrable.const_mul bound_integrable.norm Îµ) this },
  have hF'_int : integrable F' Î¼,
  { have : âˆ€áµ a âˆ‚Î¼, âˆ¥F' aâˆ¥ â‰¤ b a,
    { apply (h_diff.and h_lipsch).mono,
      rintros a âŸ¨ha_diff, ha_lipâŸ©,
      exact ha_diff.le_of_lip (ball_mem_nhds _ Îµ_pos) ha_lip },
    exact b_int.mono' hF'_meas this },
  refine âŸ¨hF'_int, _âŸ©,
  have h_ball: ball xâ‚€ Îµ âˆˆ ğ“ xâ‚€ := ball_mem_nhds xâ‚€ Îµ_pos,
  have : âˆ€á¶  x in ğ“ xâ‚€,
      âˆ¥x - xâ‚€âˆ¥â»Â¹ * âˆ¥âˆ« a, F x a âˆ‚Î¼ - âˆ« a, F xâ‚€ a âˆ‚Î¼ - (âˆ« a, F' a âˆ‚Î¼) (x - xâ‚€)âˆ¥ =
       âˆ¥âˆ« a, âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a  - F' a (x - xâ‚€)) âˆ‚Î¼âˆ¥,
  { apply mem_sets_of_superset (ball_mem_nhds _ Îµ_pos),
    intros x x_in,
    rw [set.mem_set_of_eq, â† norm_smul_of_nonneg (nneg _), integral_smul,
        integral_sub, integral_sub, continuous_linear_map.apply_integral hF'_int],
    exacts [hF_int' x x_in, hF_int, (hF_int' x x_in).sub hF_int,
            hF'_int.apply_continuous_linear_map _] },
  rw [has_fderiv_at_iff_tendsto, tendsto_congr' this, â† tendsto_zero_iff_norm_tendsto_zero,
      â† show âˆ« (a : Î±), âˆ¥xâ‚€ - xâ‚€âˆ¥â»Â¹ â€¢ (F xâ‚€ a - F xâ‚€ a - (F' a) (xâ‚€ - xâ‚€)) âˆ‚Î¼ = 0, by simp],
  apply tendsto_integral_filter_of_dominated_convergence,
  { apply is_countably_generated_nhds },
  { filter_upwards [h_ball],
    intros x x_in,
    apply ae_measurable.const_smul,
    exact ((hF_meas _ x_in).sub (hF_meas _ xâ‚€_in)).sub (hF'_meas.apply_continuous_linear_map _) },
  { simp [measurable_const] },
  { apply mem_sets_of_superset h_ball,
    intros x hx,
    apply (h_diff.and h_lipsch).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    show âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))âˆ¥ â‰¤ b a + âˆ¥F' aâˆ¥,
    replace ha_bound : âˆ¥F x a - F xâ‚€ aâˆ¥ â‰¤ b a * âˆ¥x - xâ‚€âˆ¥,
    { rw lipschitz_on_with_iff_norm_sub_le at ha_bound,
      exact ha_bound _ hx _ xâ‚€_in },
    calc âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))âˆ¥
    = âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a) - âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ F' a (x - xâ‚€)âˆ¥ : by rw smul_sub
    ... â‰¤  âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a)âˆ¥ + âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ F' a (x - xâ‚€)âˆ¥ : norm_sub_le _ _
    ... =  âˆ¥x - xâ‚€âˆ¥â»Â¹ * âˆ¥F x a - F xâ‚€ aâˆ¥ + âˆ¥x - xâ‚€âˆ¥â»Â¹ * âˆ¥F' a (x - xâ‚€)âˆ¥ : by { rw [norm_smul_of_nonneg, norm_smul_of_nonneg] ; exact nneg _}
    ... â‰¤  âˆ¥x - xâ‚€âˆ¥â»Â¹ * (b a * âˆ¥x - xâ‚€âˆ¥) + âˆ¥x - xâ‚€âˆ¥â»Â¹ * (âˆ¥F' aâˆ¥ * âˆ¥x - xâ‚€âˆ¥) : add_le_add _ _
    ... â‰¤ b a + âˆ¥F' aâˆ¥ : _,
    exact mul_le_mul_of_nonneg_left ha_bound (nneg _),
    apply mul_le_mul_of_nonneg_left ((F' a).le_op_norm _) (nneg _),
    by_cases h : âˆ¥x - xâ‚€âˆ¥ = 0,
    { simpa [h] using add_nonneg (b_nonneg a) (norm_nonneg (F' a)) },
    { field_simp [h] } },
  { exact b_int.add hF'_int.norm },
  { apply h_diff.mono,
    intros a ha,
    suffices : tendsto (Î» x, âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))) (ğ“ xâ‚€) (ğ“ 0),
    by simpa,
    rw tendsto_zero_iff_norm_tendsto_zero,
    have : (Î» x, âˆ¥x - xâ‚€âˆ¥â»Â¹ * âˆ¥F x a - F xâ‚€ a - F' a (x - xâ‚€)âˆ¥) = Î» x, âˆ¥âˆ¥x - xâ‚€âˆ¥â»Â¹ â€¢ (F x a - F xâ‚€ a - F' a (x - xâ‚€))âˆ¥,
    { ext x,
      rw norm_smul_of_nonneg (nneg _) },
    rwa [has_fderiv_at_iff_tendsto, this] at ha },
end

lemma has_fderiv_at_of_dominated_loc_of_lip {F : H â†’ Î± â†’ E} {F' : Î± â†’ (H â†’L[â„] E)} {xâ‚€ : H}
  {bound : Î± â†’ â„}
  {Îµ : â„}
  (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_measurable F' Î¼)
  (h_lip : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Îµ))
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' a) xâ‚€) :
  integrable F' Î¼ âˆ§ has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  obtain âŸ¨Îµ', Îµ'_pos, h'âŸ© : âˆƒ Îµ' > 0, âˆ€ x âˆˆ ball xâ‚€ Îµ', ae_measurable (F x) Î¼,
  by simpa using nhds_basis_ball.eventually_iff.mp hF_meas,
  set Î´ := min Îµ Îµ',
  have Î´_pos : 0 < Î´ := lt_min Îµ_pos Îµ'_pos,
  replace h' : âˆ€ x, x âˆˆ ball xâ‚€ Î´ â†’ ae_measurable (F x) Î¼,
  { intros x x_in,
    exact h' _ (ball_subset_ball (min_le_right Îµ Îµ') x_in) },
  replace h_lip : âˆ€áµ (a : Î±) âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Î´),
  { apply h_lip.mono,
    intros a lip,
    exact lip.mono (ball_subset_ball $ min_le_left Îµ Îµ') },
  apply has_fderiv_at_of_dominated_loc_of_lip' Î´_pos ; assumption
end


lemma has_fderiv_at_of_dominated_of_fderiv_le {F : H â†’ Î± â†’ E} {F' : H â†’ Î± â†’ (H â†’L[â„] E)} {xâ‚€ : H}
  {bound : Î± â†’ â„}
  {Îµ : â„}
  (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_measurable (F' xâ‚€) Î¼)
  (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, âˆ¥F' x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, has_fderiv_at (Î» x, F x a) (F' x a) x) :
  has_fderiv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ :=
begin
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, has_fderiv_at (Î» x, F x a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono (Î» a ha, ha xâ‚€ xâ‚€_in),
  have : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs (bound a)) (Î» x, F x a) (ball xâ‚€ Îµ),
  { apply (h_diff.and h_bound).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    have bound_nonneg : 0 â‰¤ bound a := (norm_nonneg (F' xâ‚€ a)).trans (ha_bound xâ‚€ xâ‚€_in),
    rw show real.nnabs (bound a) = nnreal.of_real (bound a), by simp [bound_nonneg],
    apply convex.lipschitz_on_with_of_norm_has_fderiv_within_le _ ha_bound (convex_ball _ _),
    intros x x_in,
    exact (ha_deriv x x_in).has_fderiv_within_at, },
  exact (has_fderiv_at_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int
                                               hF'_meas this bound_integrable diff_xâ‚€).2
end

open set


lemma continuous.ae_measurable {Î± Î³ : Type*} [topological_space Î±] [measurable_space Î±]
  [opens_measurable_space Î±] [topological_space Î³] [measurable_space Î³]
  [borel_space Î³] {f : Î± â†’ Î³} (h : continuous f) (Î¼ : measure Î±): ae_measurable f Î¼ :=
âŸ¨f, h.measurable, eventually_eq.refl _ _âŸ©

lemma has_deriv_at_of_dominated_loc_of_lip {F : â„ â†’ Î± â†’ E} {F' : Î± â†’ E} {xâ‚€ : â„} {bound : Î± â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_measurable F' Î¼)
  (h_lipsch : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs $ bound a) (Î» x, F x a) (ball xâ‚€ Îµ))
  (bound_integrable : integrable (bound : Î± â†’ â„) Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, has_deriv_at (Î» x, F x a) (F' a) xâ‚€) :
  (integrable F' Î¼) âˆ§ has_deriv_at (Î» x, âˆ« a, F x a âˆ‚Î¼) (âˆ« a, F' a âˆ‚Î¼) xâ‚€ :=
begin
  have hm := (continuous_linear_map.smul_rightL â„ â„ E 1).continuous.measurable.comp_ae_measurable
             hF'_meas,
  cases has_fderiv_at_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hm h_lipsch bound_integrable
    h_diff with hF'_int key,
  replace hF'_int : integrable F' Î¼,
  { rw [â† integrable_norm_iff hm] at hF'_int,
    simpa [integrable_norm_iff, hF'_meas] using hF'_int },
  refine âŸ¨hF'_int, _âŸ©,
  simp_rw has_deriv_at_iff_has_fderiv_at at h_diff âŠ¢,
  rwa continuous_linear_map.integral_comp_comm _ hF'_int at key,
end

lemma has_deriv_at_of_dominated_loc_of_deriv_le {F : â„ â†’ Î± â†’ E} {F' : â„ â†’ Î± â†’ E} {xâ‚€ : â„} {bound : Î± â†’ â„} {Îµ : â„}
  (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) Î¼)
  (hF_int : integrable (F xâ‚€) Î¼)
  (hF'_meas : ae_measurable (F' xâ‚€) Î¼)
  (h_bound : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, âˆ¥F' x aâˆ¥ â‰¤ bound a)
  (bound_integrable : integrable bound Î¼)
  (h_diff : âˆ€áµ a âˆ‚Î¼, âˆ€ x âˆˆ ball xâ‚€ Îµ, has_deriv_at (Î» x, F x a) (F' x a) x) :
  (integrable (F' xâ‚€) Î¼) âˆ§ has_deriv_at (Î»n, âˆ« a, F n a âˆ‚Î¼) (âˆ« a, F' xâ‚€ a âˆ‚Î¼) xâ‚€ :=
begin
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have diff_xâ‚€ : âˆ€áµ a âˆ‚Î¼, has_deriv_at (Î» x, F x a) (F' xâ‚€ a) xâ‚€ :=
    h_diff.mono (Î» a ha, ha xâ‚€ xâ‚€_in),
  have : âˆ€áµ a âˆ‚Î¼, lipschitz_on_with (real.nnabs (bound a)) (Î» (x : â„), F x a) (ball xâ‚€ Îµ),
  { apply (h_diff.and h_bound).mono,
    rintros a âŸ¨ha_deriv, ha_boundâŸ©,
    have bound_nonneg : 0 â‰¤ bound a := (norm_nonneg (F' xâ‚€ a)).trans (ha_bound xâ‚€ xâ‚€_in),
    rw show real.nnabs (bound a) = nnreal.of_real (bound a), by simp [bound_nonneg],
    apply convex.lipschitz_on_with_of_norm_has_deriv_within_le (convex_ball _ _)
    (Î» x x_in, (ha_deriv x x_in).has_deriv_within_at) ha_bound },
  exact has_deriv_at_of_dominated_loc_of_lip Îµ_pos hF_meas hF_int hF'_meas this
        bound_integrable diff_xâ‚€
end

