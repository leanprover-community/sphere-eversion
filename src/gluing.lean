import topology.instances.real

/-!
# Gluing continuous functions

These are preliminaries about gluing continuous functions that should be in mathlib
in some form.
I also let a couple of lemmas that I ended up not using but should still be somewhere.
-/

noncomputable theory
open_locale classical topological_space filter
open filter set 

-- filter.basic
@[simp]
lemma tendsto_bot {Î± Î² : Type*} (f : Î± â†’ Î²) (F : filter Î²) : tendsto f âŠ¥ F :=
begin
  rw [tendsto, map_bot],
  exact bot_le,
end


lemma tendsto_nhds_within_of_tendsto_of_subset {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] {s : set Î±} {t : set Î²} 
{f : Î± â†’ Î²} {x : Î±} {y : Î²}  (h : tendsto f (ğ“ x) (ğ“ y)) (h' : s âŠ† f â»Â¹' t) :
  tendsto f (nhds_within x s) (nhds_within y t) :=
begin
  erw tendsto_inf,
  split,
  { exact tendsto_nhds_within_of_tendsto_nhds h },
  { apply tendsto_inf_right,
    rwa tendsto_principal_principal },
end


lemma tendsto_nhds_within_of_not_in_closure {Î± Î² : Type*} [topological_space Î±] {s : set Î±} 
{f : Î± â†’ Î²} {x : Î±} {F : filter Î²}  (h : x âˆ‰ closure s) :
  tendsto f (nhds_within x s) F :=
begin
  rw mem_closure_iff_nhds_within_ne_bot at h,
  push_neg at h,
  simp [h],
end

section
variables {Î± : Type*} [topological_space Î±] [linear_order Î±] [order_topology Î±] [densely_ordered Î±] [no_top_order Î±] 

@[simp]
lemma frontier_Iic (x : Î±) : frontier (Iic x) = {x} :=
begin
  unfold frontier,
  rw [interior_Iic, closure_eq_of_is_closed (is_closed_Iic)],
  { ext y,
    suffices : y â‰¤ x âˆ§ x â‰¤ y â†” y = x, by simpa,
    split ; intros h,
    { exact le_antisymm h.1 h.2 },
    { simp [h] } },
  apply_instance,
end
end

lemma Icc_inter_Icc_subset {Î± : Type*} [preorder Î±] (a b c : Î±) : Icc a b âˆ© Iic c âŠ† Icc a c :=
begin
  rintros x âŸ¨âŸ¨xa, xbâŸ©, hâŸ©,
  split ; assumption,
end

lemma Icc_inter_Icc {a b c : â„} : Icc a b âˆ© Iic c = Icc a (b âŠ“ c) :=
begin
  ext x,
  simp [and_assoc],
end

lemma Icc_inter_Ici_subset {Î± : Type*} [preorder Î±] (a b c : Î±) : Icc a b âˆ© Ici c âŠ† Icc c b :=
begin
  rintros x âŸ¨âŸ¨ax, xbâŸ©, xcâŸ©,
  split ; assumption,
end

lemma Icc_inter_Ici {a b c : â„} : Icc a b âˆ© Ici c = Icc (a âŠ” c) b :=
begin
  ext x,
  change (a â‰¤ x âˆ§ x â‰¤ b) âˆ§ c â‰¤ x â†” a âŠ” c â‰¤ x âˆ§ x â‰¤ b,
  simp,
  tauto
end

lemma and_iff_and_of_imp_iff {p q r : Prop} (h : r â†’ (p â†” q)) : (p âˆ§ r) â†” (q âˆ§ r) :=
by tauto

lemma closure_eq_interior_union_frontier {Î± : Type*} [topological_space Î±] (s : set Î±) :
  closure s = interior s âˆª frontier s :=
(union_diff_cancel  interior_subset_closure).symm

lemma closure_eq_self_union_frontier {Î± : Type*} [topological_space Î±] (s : set Î±) :
  closure s = s âˆª frontier s :=
begin
  have : s âˆª closure (-s) = univ,
  { apply eq_univ_of_subset _ (union_compl_self s),
    exact union_subset_union (subset.refl s) (subset_closure : -s âŠ† closure (-s)) },
  rw [frontier_eq_closure_inter_closure, union_inter_distrib_left, this, inter_univ,
      union_eq_self_of_subset_left subset_closure],
end

local notation `cl` := closure

lemma continuous_on_if {Î± Î² : Type*} [topological_space Î±] [topological_space Î²] {p : Î± â†’ Prop} {s : set Î±}
  {f g : Î± â†’ Î²} 
  (hp : âˆ€ a âˆˆ s âˆ© frontier {a | p a}, f a = g a) (hf : continuous_on f $ s âˆ© closure {a | p a})
  (hg : continuous_on g $ s âˆ© closure {a | Â¬ p a}) :
  continuous_on (Î»a, if p a then f a else g a) s :=
begin
  set Ï† := (Î»a, if p a then f a else g a),
  set A := {a | p a},
  set B := {a | Â¬ p a},
  rw continuous_on_iff_is_closed at *,
  intros t t_closed,
  rcases hf t t_closed with âŸ¨u, u_closed, huâŸ©,
  rcases hg t t_closed with âŸ¨v, v_closed, hvâŸ©,
  use [(u âˆ© cl A) âˆª (v âˆ© cl B),
       is_closed_union (is_closed_inter u_closed is_closed_closure) 
                       (is_closed_inter v_closed  is_closed_closure)],
  have factA : Ï† â»Â¹' t âˆ© s âˆ© cl A = f â»Â¹' t âˆ© s âˆ© cl A,
  { have : âˆ€ x âˆˆ s âˆ© cl A, Ï† x = f x,
    { rintros x âŸ¨xs, xAâŸ©,
      rw closure_eq_self_union_frontier A at xA,
      cases xA,
      { change p x at xA,
        simp [Ï†, if_pos xA] },
      { specialize hp x âŸ¨xs, xAâŸ©,
        dsimp [Ï†],
        split_ifs ; tauto } },
      ext x,
    rw [inter_assoc, mem_inter_iff],
    conv_rhs { rw [inter_assoc, mem_inter_iff] },
    apply and_iff_and_of_imp_iff,
    intro x_in,
    change Ï† x âˆˆ _ â†” f x âˆˆ _,
    rw this x x_in, },
  have factB : Ï† â»Â¹' t âˆ© s âˆ© cl B = g â»Â¹' t âˆ© s âˆ© cl B,
  { have : âˆ€ x âˆˆ s âˆ© cl B, Ï† x = g x,
    { rintros x âŸ¨xs, xBâŸ©,
      rw closure_eq_self_union_frontier B at xB,
      cases xB,
      { change Â¬ p x at xB,
        simp [Ï†, if_neg xB] },
      { rw â† frontier_compl at hp,
        specialize hp x âŸ¨xs, xBâŸ©,
        dsimp [Ï†],
        split_ifs ; tauto } },
      ext x,
    rw [inter_assoc, mem_inter_iff],
    conv_rhs { rw [inter_assoc, mem_inter_iff] },
    apply and_iff_and_of_imp_iff,
    intro x_in,
    change Ï† x âˆˆ _ â†” g x âˆˆ _,
    rw this x x_in },
  have cl_cl : cl A âˆª cl B = univ,
  { apply eq_univ_of_subset _ (union_compl_self $ set_of p),
    exact union_subset_union subset_closure subset_closure },
  calc Ï† â»Â¹' t âˆ© s = (Ï† â»Â¹' t âˆ© s) âˆ© (cl A âˆª cl B) : by simp [cl_cl]
  ... = Ï† â»Â¹' t âˆ© s âˆ© cl A âˆª Ï† â»Â¹' t âˆ© s âˆ© cl B  : by rw inter_union_distrib_left
  ... = f â»Â¹' t âˆ© s âˆ© cl A âˆª g â»Â¹' t âˆ© s âˆ© cl B  : by rw [factA, factB]
  ... = (u âˆ© s âˆ© cl A) âˆª (v âˆ© s âˆ© cl B) : by assoc_rewrite [hu, hv]
  ... =  (u âˆ© cl A âˆª v âˆ© cl B) âˆ© s : by rw [inter_right_comm, inter_right_comm v, union_inter_distrib_right],
end

lemma continuous_on_if_Icc {Î± Î² : Type*} [topological_space Î±] [linear_order Î±] [order_topology Î±] [densely_ordered Î±] [no_top_order Î±] [topological_space Î²] {a b c : Î±} {f g : Î± â†’ Î²} 
  (hf : continuous_on f $ Icc a b) (hg : continuous_on g $ Icc b c) (hb : f b = g b) :
  continuous_on (Î» x, if x â‰¤ b then f x else g x) (Icc a c) :=
begin
  apply continuous_on_if,
  { erw [frontier_Iic b],
    rintros x âŸ¨_, x_inâŸ©,
    convert hb },
  { erw [closure_eq_of_is_closed is_closed_Iic],
    exact continuous_on.mono hf (Icc_inter_Icc_subset _ _ _),
    apply_instance },
  { push_neg,
    erw closure_Ioi,
    exact continuous_on.mono hg (Icc_inter_Ici_subset _ _ _) }
end
