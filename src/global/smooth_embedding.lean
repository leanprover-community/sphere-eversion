import topology.metric_space.hausdorff_distance
import topology.uniform_space.compact_separated
import geometry.manifold.cont_mdiff
import global.indexing
import to_mathlib.topology.paracompact
import to_mathlib.topology.local_homeomorph
import to_mathlib.geometry.manifold.charted_space
import to_mathlib.analysis.normed_space.misc

noncomputable theory

open set equiv
open_locale manifold topological_space

/-- A variant of `is_compact.exists_forall_le` for real-valued functions that does not require the
assumption `s.nonempty`.

TODO Move -/
lemma is_compact.exists_forall_le' {Œ≤ : Type*} [topological_space Œ≤]
  {s : set Œ≤} (hs : is_compact s)
  {f : Œ≤ ‚Üí ‚Ñù} (hf : continuous_on f s) {a : ‚Ñù} (hf' : ‚àÄ b ‚àà s, a < f b) :
  ‚àÉ a', a < a' ‚àß ‚àÄ b ‚àà s, a' ‚â§ f b :=
begin
  rcases s.eq_empty_or_nonempty with rfl | hs',
  { exact ‚ü®a + 1, by simp only [lt_add_iff_pos_right, zero_lt_one], Œª b hb, by simpa using hb‚ü©, },
  { obtain ‚ü®x, hx, hx'‚ü© := hs.exists_forall_le hs' hf,
    exact ‚ü®f x, hf' x hx, hx'‚ü©, },
end

section general
variables {ùïú : Type*} [nontrivially_normed_field ùïú]
  {E : Type*} [normed_add_comm_group E] [normed_space ùïú E]
  {H : Type*} [topological_space H]
  (I : model_with_corners ùïú E H)
  (M : Type*) [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ùïú E']
  {H' : Type*} [topological_space H']
  (I' : model_with_corners ùïú E' H')
  (M' : Type*) [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']

structure open_smooth_embedding  :=
(to_fun : M ‚Üí M')
(inv_fun : M' ‚Üí M)
(left_inv'   : ‚àÄ{x}, inv_fun (to_fun x) = x)
(right_inv'  : ‚àÄ{x}, x ‚àà range to_fun ‚Üí to_fun (inv_fun x) = x)
(open_map : is_open_map to_fun)
(smooth_to : smooth I I' to_fun)
(smooth_inv : smooth_on I' I inv_fun (range to_fun))

instance : has_coe_to_fun (open_smooth_embedding I M I' M') (Œª _, M ‚Üí M') :=
‚ü®open_smooth_embedding.to_fun‚ü©

namespace open_smooth_embedding

variables {I I' M M'} (f : open_smooth_embedding I M I' M')

@[simp] lemma coe_mk (f g h‚ÇÅ h‚ÇÇ h‚ÇÉ h‚ÇÑ h‚ÇÖ) :
  ‚áë(‚ü®f, g, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, h‚ÇÑ, h‚ÇÖ‚ü© : open_smooth_embedding I M I' M') = f :=
rfl

@[simp] lemma left_inv (x : M) : f.inv_fun (f x) = x := by apply f.left_inv'

@[simp] lemma inv_fun_comp_coe : f.inv_fun ‚àò f = id := funext f.left_inv

@[simp] lemma right_inv {y : M'} (hy : y ‚àà range f) : f (f.inv_fun y) = y := f.right_inv' hy

lemma coe_comp_inv_fun_eventually_eq (x : M) : f ‚àò f.inv_fun =·∂†[ùìù (f x)] id :=
filter.eventually_of_mem (f.open_map.range_mem_nhds x) $ Œª y hy, f.right_inv' hy

protected lemma continuous : continuous f := f.smooth_to.continuous

lemma is_open_range : is_open (range f) :=
f.open_map.is_open_range

lemma smooth_at_inv {y : M'} (hy : y ‚àà range f) : smooth_at I' I f.inv_fun y :=
(f.smooth_inv y hy).cont_mdiff_at $ f.is_open_range.mem_nhds hy

/- Note that we are slightly abusing the fact that `tangent_space I x` and
`tangent_space I (f.inv_fun (f x))` are both definitionally `E` below. -/
def fderiv (x : M) : tangent_space I x ‚âÉL[ùïú] tangent_space I' (f x) :=
have h‚ÇÅ : mdifferentiable_at I' I f.inv_fun (f x) := ((f.smooth_inv (f x) (mem_range_self x)
  ).mdifferentiable_within_at le_top).mdifferentiable_at (f.open_map.range_mem_nhds x),
have h‚ÇÇ : mdifferentiable_at I I' f x := f.smooth_to.cont_mdiff.mdifferentiable le_top _,
continuous_linear_equiv.equiv_of_inverse
  (mfderiv I I' f x)
  (mfderiv I' I f.inv_fun (f x))
begin
  intros v,
  rw [‚Üê continuous_linear_map.comp_apply, ‚Üê mfderiv_comp x h‚ÇÅ h‚ÇÇ, f.inv_fun_comp_coe, mfderiv_id,
    continuous_linear_map.coe_id', id.def],
end
begin
  intros v,
  have hx : x = f.inv_fun (f x), { rw f.left_inv, },
  have hx' : f (f.inv_fun (f x)) = f x, { rw f.left_inv, },
  rw hx at h‚ÇÇ,
  rw [hx, hx', ‚Üê continuous_linear_map.comp_apply, ‚Üê mfderiv_comp (f x) h‚ÇÇ h‚ÇÅ, ((has_mfderiv_at_id
    I' (f x)).congr_of_eventually_eq (f.coe_comp_inv_fun_eventually_eq x)).mfderiv,
    continuous_linear_map.coe_id', id.def],
end

@[simp] lemma fderiv_coe (x : M) :
  (f.fderiv x : tangent_space I x ‚ÜíL[ùïú] tangent_space I' (f x)) = mfderiv I I' f x :=
by { ext, refl }

@[simp] lemma fderiv_symm_coe (x : M) :
  ((f.fderiv x).symm : tangent_space I' (f x) ‚ÜíL[ùïú] tangent_space I x) =
  mfderiv I' I f.inv_fun (f x) :=
by { ext, refl }

lemma fderiv_symm_coe' {x : M'} (hx : x ‚àà range f) :
  ((f.fderiv (f.inv_fun x)).symm : tangent_space I' (f (f.inv_fun x)) ‚ÜíL[ùïú]
    tangent_space I (f.inv_fun x)) =
  (mfderiv I' I f.inv_fun x : tangent_space I' x ‚ÜíL[ùïú] tangent_space I (f.inv_fun x)) :=
by rw [fderiv_symm_coe, f.right_inv hx]

variables (I M)

/-- The identity map is a smooth open embedding. -/
@[simps] def id : open_smooth_embedding I M I M :=
{ to_fun := id,
  inv_fun := id,
  left_inv' := Œª x, rfl,
  right_inv' := Œª x hx, rfl,
  open_map := is_open_map.id,
  smooth_to := smooth_id,
  smooth_inv := smooth_on_id }

variables {I M I' M'}

@[simps] def comp
  {E'' : Type*} [normed_add_comm_group E''] [normed_space ùïú E'']
  {H'' : Type*} [topological_space H'']
  {I'' : model_with_corners ùïú E'' H''}
  {M'' : Type*} [topological_space M''] [charted_space H'' M''] [smooth_manifold_with_corners I'' M'']
  (g : open_smooth_embedding I' M' I'' M'') (f : open_smooth_embedding I M I' M') :
  open_smooth_embedding I M I'' M'' :=
{ to_fun := g ‚àò f,
  inv_fun := f.inv_fun ‚àò g.inv_fun,
  left_inv' := Œª x, by simp only [function.comp_app, left_inv],
  right_inv' := Œª x hx, by { obtain ‚ü®y, rfl‚ü© := hx, simp only [function.comp_app, left_inv], },
  open_map := g.open_map.comp f.open_map,
  smooth_to := g.smooth_to.comp f.smooth_to,
  smooth_inv := (f.smooth_inv.comp' g.smooth_inv).mono
  begin
    change range (g ‚àò f) ‚äÜ range g ‚à© g.inv_fun ‚Åª¬π' range f,
    refine subset_inter_iff.mpr ‚ü®range_comp_subset_range f g, _‚ü©,
    rintros x' ‚ü®x, rfl‚ü©,
    exact ‚ü®x, by simp only [left_inv]‚ü©,
  end, }

/-
Note: the only indended use of the following definition is the case where `f = (id : ‚Ñù ‚Üí ‚Ñù)`,
but the proof shouldn't be hard anyway.
-/
@[simps] def prod
  {F : Type*} [normed_add_comm_group F] [normed_space ùïú F]
  {G : Type*} [topological_space G]
  {J : model_with_corners ùïú F G}
  {N : Type*} [topological_space N] [charted_space G N] [smooth_manifold_with_corners J N]
  {F' : Type*} [normed_add_comm_group F'] [normed_space ùïú F']
  {G' : Type*} [topological_space G']
  {J' : model_with_corners ùïú F' G'}
  {N' : Type*} [topological_space N'] [charted_space G' N'] [smooth_manifold_with_corners J' N']
  (f : open_smooth_embedding I M J N)
  (f' : open_smooth_embedding I' M' J' N') :
  open_smooth_embedding (I.prod I') (M √ó M') (J.prod J') (N √ó N') :=
{ to_fun := prod.map f f',
  inv_fun := prod.map f.inv_fun f'.inv_fun,
  left_inv' := sorry,
  right_inv' := sorry,
  open_map := sorry,
  smooth_to := sorry,
  smooth_inv := sorry }

end open_smooth_embedding

namespace continuous_linear_equiv

variables (e : E ‚âÉL[ùïú] E') [complete_space E] [complete_space E']

@[simp] lemma is_open_map : is_open_map e := (e : E ‚ÜíL[ùïú] E').is_open_map e.surjective

@[simps] def to_open_smooth_embedding :
  open_smooth_embedding ùìò(ùïú, E) E ùìò(ùïú, E') E' :=
{ to_fun := e,
  inv_fun := e.symm,
  left_inv' := e.symm_apply_apply,
  right_inv' := Œª x hx, e.apply_symm_apply x,
  open_map := e.is_open_map,
  smooth_to := (e : E ‚ÜíL[ùïú] E').cont_mdiff,
  smooth_inv := (e.symm : E' ‚ÜíL[ùïú] E).cont_mdiff.cont_mdiff_on }

end continuous_linear_equiv

end general

section without_boundary

open metric (hiding mem_nhds_iff) function

universe u

section general_nonsense

variables {ùïú E H M : Type*} [nontrivially_normed_field ùïú]
  [normed_add_comm_group E] [normed_space ùïú E]
  [topological_space H] {I : model_with_corners ùïú E H}
  [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {x : M} {n : ‚Ñï‚àû}

lemma ext_chart_at_target_eq_image_chart_target :
  (ext_chart_at I x).target = I '' (chart_at H x).target :=
begin
  erw [(chart_at H x).to_local_equiv.trans_target'' I.to_local_equiv, I.source_eq, univ_inter],
  refl,
end

@[simp] lemma model_with_corners_self.ext_chart_at {e : E} :
  ext_chart_at ùìò(ùïú, E) e = local_equiv.refl E :=
by simp

lemma cont_mdiff_on_ext_chart_symm :
  cont_mdiff_on ùìò(ùïú, E) I n (ext_chart_at I x).symm (ext_chart_at I x).target :=
begin
  -- TODO: find a sane proof
  have hs : (ext_chart_at I x).target ‚äÜ (chart_at E (ext_chart_at I x x)).source, { simp, },
  have h2s : maps_to (ext_chart_at I x).symm (ext_chart_at I x).target (chart_at H x).source,
  { rw ‚Üê ext_chart_at_source I, exact (ext_chart_at I x).symm_maps_to, },
  refine (cont_mdiff_on_iff_of_subset_source hs h2s).mpr ‚ü®_, _‚ü©,
  { rw ext_chart_at_target_eq_image_chart_target,
    apply (chart_at H x).symm.continuous_to_fun.comp I.continuous_inv_fun.continuous_on,
    simpa using maps_to_id _, },
  { simp only [model_with_corners_self.ext_chart_at, local_equiv.refl_symm, local_equiv.refl_coe,
      comp.right_id, id.def, image_id'],
    exact (cont_diff_on_congr (Œª y hy, (ext_chart_at I x).right_inv hy)).mpr cont_diff_on_id, },
end

end general_nonsense

variables
  {F H : Type*} (M : Type u)
  [normed_add_comm_group F] [normed_space ‚Ñù F]
  [topological_space H] [topological_space M] [charted_space H M]
  [t2_space M] [locally_compact_space M] [sigma_compact_space M]
  (IF : model_with_corners ‚Ñù F H) [smooth_manifold_with_corners IF M]

/- Clearly should be generalised. Maybe what we really want is a theory of local diffeomorphisms.

Note that the input `f` is morally an `open_smooth_embedding` but stated in terms of `cont_diff`
instead of `cont_mdiff`. This is more convenient for our purposes. -/
def open_smooth_emb_of_diffeo_subset_chart_target (x : M) {f : local_homeomorph F F}
  (hf‚ÇÅ : f.source = univ)
  (hf‚ÇÇ : cont_diff ‚Ñù ‚àû f)
  (hf‚ÇÉ : cont_diff_on ‚Ñù ‚àû f.symm f.target)
  (hf‚ÇÑ : range f ‚äÜ IF '' (chart_at H x).target) :
  open_smooth_embedding ùìò(‚Ñù, F) F IF M :=
{ to_fun := (ext_chart_at IF x).symm ‚àò f,
  inv_fun := f.inv_fun ‚àò (ext_chart_at IF x),
  left_inv' := Œª y,
  begin
    obtain ‚ü®z, hz, hz'‚ü© := hf‚ÇÑ (mem_range_self y),
    have aux : f.symm (IF z) = y, { rw hz', exact f.left_inv (hf‚ÇÅ.symm ‚ñ∏ mem_univ _), },
    simp only [‚Üê hz', (chart_at H x).right_inv hz, aux, ext_chart_at, local_equiv.coe_trans,
      local_homeomorph.inv_fun_eq_coe, model_with_corners.to_local_equiv_coe,
      local_homeomorph.coe_coe, local_equiv.coe_trans_symm, local_homeomorph.coe_coe_symm,
      model_with_corners.left_inv, model_with_corners.to_local_equiv_coe_symm, comp_app, aux],
  end,
  right_inv' :=
  begin
    rintros - ‚ü®y, rfl‚ü©,
    obtain ‚ü®z, hz, hz'‚ü© := hf‚ÇÑ (mem_range_self y),
    have aux : f.symm (IF z) = y, { rw hz', exact f.left_inv (hf‚ÇÅ.symm ‚ñ∏ mem_univ _), },
    simp only [‚Üê hz', (chart_at H x).right_inv hz, aux, ext_chart_at, local_equiv.coe_trans,
      local_homeomorph.inv_fun_eq_coe, model_with_corners.to_local_equiv_coe,
      local_homeomorph.coe_coe, local_equiv.coe_trans_symm, local_homeomorph.coe_coe_symm,
      model_with_corners.left_inv, model_with_corners.to_local_equiv_coe_symm, comp_app, aux],
  end,
  open_map := Œª u hu,
  begin
    have aux : is_open (f '' u) := f.image_open_of_open hu (hf‚ÇÅ.symm ‚ñ∏ subset_univ u),
    convert ext_chart_preimage_open_of_open' IF x aux,
    rw image_comp,
    refine (ext_chart_at IF x).symm_image_eq_source_inter_preimage
      ((image_subset_range f u).trans _),
    rw ext_chart_at_target_eq_image_chart_target,
    exact hf‚ÇÑ,
  end,
  smooth_to :=
  begin
    refine cont_mdiff_on_ext_chart_symm.comp_cont_mdiff hf‚ÇÇ.cont_mdiff (Œª y, _),
    rw ext_chart_at_target_eq_image_chart_target,
    exact hf‚ÇÑ (mem_range_self y),
  end,
  smooth_inv :=
  begin
    rw ‚Üê ext_chart_at_target_eq_image_chart_target at hf‚ÇÑ,
    have hf' : range ((ext_chart_at IF x).symm ‚àò f) ‚äÜ (ext_chart_at IF x) ‚Åª¬π' f.target,
    { rw [range_comp, ‚Üê image_subset_iff, ‚Üê f.image_source_eq_target, hf‚ÇÅ, image_univ],
      exact (local_equiv.image_symm_image_of_subset_target _ hf‚ÇÑ).subset, },
    have hf'' : range ((ext_chart_at IF x).symm ‚àò f) ‚äÜ (chart_at H x).source,
    { rw [‚Üê ext_chart_at_source IF, range_comp, ‚Üê local_equiv.symm_image_target_eq_source],
      exact (monotone_image hf‚ÇÑ).trans subset.rfl, },
    exact hf‚ÇÉ.cont_mdiff_on.comp (cont_mdiff_on_ext_chart_at.mono hf'') hf',
  end}

@[simp] lemma coe_open_smooth_emb_of_diffeo_subset_chart_target
  (x : M) {f : local_homeomorph F F}
  (hf‚ÇÅ : f.source = univ)
  (hf‚ÇÇ : cont_diff ‚Ñù ‚àû f)
  (hf‚ÇÉ : cont_diff_on ‚Ñù ‚àû f.symm f.target)
  (hf‚ÇÑ : range f ‚äÜ IF '' (chart_at H x).target) :
  (open_smooth_emb_of_diffeo_subset_chart_target M IF x hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ hf‚ÇÑ : F ‚Üí M) =
  (ext_chart_at IF x).symm ‚àò f :=
by simp [open_smooth_emb_of_diffeo_subset_chart_target]

lemma range_open_smooth_emb_of_diffeo_subset_chart_target
  (x : M) {f : local_homeomorph F F}
  (hf‚ÇÅ : f.source = univ)
  (hf‚ÇÇ : cont_diff ‚Ñù ‚àû f)
  (hf‚ÇÉ : cont_diff_on ‚Ñù ‚àû f.symm f.target)
  (hf‚ÇÑ : range f ‚äÜ IF '' (chart_at H x).target) :
  range (open_smooth_emb_of_diffeo_subset_chart_target M IF x hf‚ÇÅ hf‚ÇÇ hf‚ÇÉ hf‚ÇÑ) =
  (ext_chart_at IF x).symm '' (range f) :=
by rw [coe_open_smooth_emb_of_diffeo_subset_chart_target, range_comp]

variables {M} (F) [model_with_corners.boundaryless IF] [finite_dimensional ‚Ñù F]

lemma nice_atlas'
  {Œπ : Type*} {s : Œπ ‚Üí set M} (s_op : ‚àÄ j, is_open $ s j) (cov : (‚ãÉ j, s j) = univ)
  (U : set F) (hU‚ÇÅ : (0 : F) ‚àà U) (hU‚ÇÇ : is_open U) :
  ‚àÉ (Œπ' : Type u) (t : set Œπ') (œÜ : t ‚Üí open_smooth_embedding ùìò(‚Ñù, F) F IF M),
  t.countable ‚àß
  (‚àÄ i, ‚àÉ j, range (œÜ i) ‚äÜ s j) ‚àß
  locally_finite (Œª i, range (œÜ i)) ‚àß
  (‚ãÉ i, œÜ i '' U) = univ :=
begin
  let W : M ‚Üí ‚Ñù ‚Üí set M := Œª x r,
    (ext_chart_at IF x).symm ‚àò diffeomorph_to_nhd (ext_chart_at IF x x) r '' U,
  let B : M ‚Üí ‚Ñù ‚Üí set M := charted_space.ball IF,
  let p : M ‚Üí ‚Ñù ‚Üí Prop :=
    Œª x r, 0 < r ‚àß ball (ext_chart_at IF x x) r ‚äÜ (ext_chart_at IF x).target ‚àß ‚àÉ j, B x r ‚äÜ s j,
  have hW‚ÇÄ : ‚àÄ x r, p x r ‚Üí x ‚àà W x r := Œª x r h, ‚ü®0, hU‚ÇÅ, by simp [h.1]‚ü©,
  have hW‚ÇÅ : ‚àÄ x r, p x r ‚Üí is_open (W x r),
  { rintros x r ‚ü®h‚ÇÅ, h‚ÇÇ, -, -‚ü©,
    simp only [W],
    rw image_comp,
    let V := diffeomorph_to_nhd (ext_chart_at IF x x) r '' U,
    change is_open ((ext_chart_at IF x).symm '' V),
    have hV‚ÇÅ : is_open V := ((diffeomorph_to_nhd
      (ext_chart_at IF x x) r).is_open_image_iff_of_subset_source (by simp)).mp hU‚ÇÇ,
    have hV‚ÇÇ : V ‚äÜ (ext_chart_at IF x).target :=
      subset.trans ((image_subset_range _ _).trans (by simp [h‚ÇÅ])) h‚ÇÇ,
    rw (ext_chart_at IF x).symm_image_eq_source_inter_preimage hV‚ÇÇ,
    exact ext_chart_preimage_open_of_open' IF x hV‚ÇÅ, },
  have hB : ‚àÄ x, (ùìù x).has_basis (p x) (B x) :=
    Œª x, charted_space.nhds_has_basis_balls_of_open_cov IF x s_op cov,
  have hp : ‚àÄ i r, p i r ‚Üí 0 < r := Œª i r h, h.1,
  obtain ‚ü®t, ht‚ÇÅ, ht‚ÇÇ, ht‚ÇÉ, ht‚ÇÑ‚ü© :=
    exists_countable_locally_finite_cover surjective_id hp hW‚ÇÄ hW‚ÇÅ hB,
  let g : M √ó ‚Ñù ‚Üí local_homeomorph F F := Œª z, diffeomorph_to_nhd (ext_chart_at IF z.1 z.1) z.2,
  have hg‚ÇÅ : ‚àÄ z, (g z).source = univ, { simp, },
  have hg‚ÇÇ : ‚àÄ z, cont_diff ‚Ñù ‚àû (g z), { simp, },
  have hg‚ÇÉ : ‚àÄ z, cont_diff_on ‚Ñù ‚àû (g z).symm (g z).target, { simp, },
  refine ‚ü®M √ó ‚Ñù, t,
    Œª z, open_smooth_emb_of_diffeo_subset_chart_target M IF z.1.1 (hg‚ÇÅ z.1) (hg‚ÇÇ z.1) (hg‚ÇÉ z.1) _,
    ht‚ÇÅ, Œª z, _, _, _‚ü©,
  { obtain ‚ü®‚ü®x, r‚ü©, hxr‚ü© := z,
    obtain ‚ü®hr : 0 < r, hr' : ball (ext_chart_at IF x x) r ‚äÜ _, -‚ü© := ht‚ÇÇ _ hxr,
    rw ‚Üê ext_chart_at_target_eq_image_chart_target,
    exact (range_diffeomorph_to_nhd_subset_ball (ext_chart_at IF x x) hr).trans hr', },
  { obtain ‚ü®‚ü®x, r‚ü©, hxr‚ü© := z,
    obtain ‚ü®hr : 0 < r, -, j, hj : B x r ‚äÜ s j‚ü© := ht‚ÇÇ _ hxr,
    simp_rw range_open_smooth_emb_of_diffeo_subset_chart_target,
    exact ‚ü®j, subset_trans (monotone_image (range_diffeomorph_to_nhd_subset_ball _ hr)) hj‚ü©, },
  { simp_rw range_open_smooth_emb_of_diffeo_subset_chart_target,
    refine ht‚ÇÑ.subset _,
    rintros ‚ü®‚ü®x, r‚ü©, hxr‚ü©,
    obtain ‚ü®hr : 0 < r, -, -‚ü© := ht‚ÇÇ _ hxr,
    exact monotone_image (range_diffeomorph_to_nhd_subset_ball _ hr), },
  { simpa only [Union_coe_set] using ht‚ÇÉ, },
end

variables [nonempty M]

lemma nice_atlas {Œπ : Type*} {s : Œπ ‚Üí set M} (s_op : ‚àÄ j, is_open $ s j) (cov : (‚ãÉ j, s j) = univ) :
  ‚àÉ n, ‚àÉ œÜ : index_type n ‚Üí open_smooth_embedding ùìò(‚Ñù, F) F IF M,
  (‚àÄ i, ‚àÉ j, range (œÜ i) ‚äÜ s j) ‚àß
  locally_finite (Œª i, range (œÜ i)) ‚àß
  (‚ãÉ i, œÜ i '' ball 0 1) = univ :=
begin
  obtain ‚ü®Œπ', t, œÜ, h‚ÇÅ, h‚ÇÇ, h‚ÇÉ, h‚ÇÑ‚ü© := nice_atlas' F IF s_op cov (ball 0 1) (by simp) is_open_ball,
  have htne : t.nonempty,
  { by_contra contra,
    simp only [not_nonempty_iff_eq_empty.mp contra, Union_false, Union_coe_set, Union_empty,
      @eq_comm _ _ univ, univ_eq_empty_iff] at h‚ÇÑ,
    exact not_is_empty_of_nonempty M h‚ÇÑ, },
  obtain ‚ü®n, ‚ü®fn‚ü©‚ü© := (set.countable_iff_exists_nonempty_index_type_equiv htne).mp h‚ÇÅ,
  refine ‚ü®n, œÜ ‚àò fn, Œª i, h‚ÇÇ (fn i), h‚ÇÉ.comp_injective fn.injective, _‚ü©,
  rwa fn.surjective.Union_comp (Œª i, œÜ i '' ball 0 1),
end

end without_boundary

namespace open_smooth_embedding

section updating

variables {ùïú EX EM EY EN X M Y N : Type*} [nontrivially_normed_field ùïú]
  [normed_add_comm_group EX] [normed_space ùïú EX]
  [normed_add_comm_group EM] [normed_space ùïú EM]
  [normed_add_comm_group EY] [normed_space ùïú EY]
  [normed_add_comm_group EN] [normed_space ùïú EN]
  {HX : Type*} [topological_space HX] {IX : model_with_corners ùïú EX HX}
  {HY : Type*} [topological_space HY] {IY : model_with_corners ùïú EY HY}
  {HM : Type*} [topological_space HM] {IM : model_with_corners ùïú EM HM}
  {HN : Type*} [topological_space HN] {IN : model_with_corners ùïú EN HN}
  [topological_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]
  [topological_space M] [charted_space HM M] [smooth_manifold_with_corners IM M] [t2_space M]
  [metric_space Y]      [charted_space HY Y] [smooth_manifold_with_corners IY Y] [proper_space Y]
  [metric_space N]      [charted_space HN N] [smooth_manifold_with_corners IN N]
  (œÜ : open_smooth_embedding IX X IM M)
  (œà : open_smooth_embedding IY Y IN N)
  (f : M ‚Üí N) (g : X ‚Üí Y)
  [decidable_pred (‚àà range œÜ)]

/-- This is definition `def:update` in the blueprint. -/
def update (m : M) : N := if m ‚àà range œÜ then œà (g (œÜ.inv_fun m)) else f m

@[simp] lemma update_of_nmem_range {m : M} (hm : m ‚àâ range œÜ) :
  update œÜ œà f g m = f m :=
by simp [update, hm]

@[simp] lemma update_of_mem_range {m : M} (hm : m ‚àà range œÜ) :
  update œÜ œà f g m = œà (g (œÜ.inv_fun m)) :=
by simp [update, hm]

@[simp] lemma update_apply_embedding (x : X) :
  update œÜ œà f g (œÜ x) = œà (g x) :=
by simp [update]

/- This small auxiliry result is used in the next two lemmas. -/
lemma nice_update_of_eq_outside_compact_aux {K : set X} (g : X ‚Üí Y)
  (hg : ‚àÄ (x : X), x ‚àâ K ‚Üí f (œÜ x) = œà (g x)) {m : M} (hm : m ‚àâ ‚áëœÜ '' K) :
  œÜ.update œà f g m = f m :=
begin
  by_cases hm' : m ‚àà range œÜ,
  { obtain ‚ü®x, rfl‚ü© := hm',
    replace hm : x ‚àâ K, { contrapose! hm, exact mem_image_of_mem œÜ hm, },
    simp [hg x hm] },
  { simp [hm'] }
end


/- FIXME: the blueprint statement corresponding to the next two lemmas has very confusing
quantifiers status.
-/

/-
In the next lemma, it is better to assume directly that `œÜ '' K` is closed. This
will hold whe `œÜ = Id.prod œà` and `K = ‚Ñù √ó H` with `H` compact.
-/

/-- This is half of lemma `lem:updating` in the blueprint. -/
lemma nice_update_of_eq_outside_compact
  {K : set X} (hK : is_compact K) (hf : smooth IM IN f) (hg : smooth IX IY g)
  (hg' : ‚àÄ x, x ‚àâ K ‚Üí f (œÜ x) = œà (g x)) : smooth IM IN (update œÜ œà f g) :=
begin
  have hK' : ‚àÄ m ‚àâ œÜ '' K, update œÜ œà f g m = f m := Œª m hm, by
    from nice_update_of_eq_outside_compact_aux œÜ œà f g hg' hm,
  refine cont_mdiff_of_locally_cont_mdiff_on (Œª m, _),
  let U := range œÜ,
  let V := (œÜ '' K)·∂ú,
  have h‚ÇÇ : is_open V := is_open_compl_iff.mpr (hK.image œÜ.continuous).is_closed,
  have h‚ÇÉ : V ‚à™ U = univ,
  { rw [‚Üê compl_subset_iff_union, compl_compl], exact image_subset_range œÜ K, },
  have h‚ÇÑ : ‚àÄ m ‚àà U, update œÜ œà f g m = (œà ‚àò g ‚àò œÜ.inv_fun) m := Œª m hm, by simp [hm],
  by_cases hm : m ‚àà U,
  { exact ‚ü®U, œÜ.is_open_range, hm, (cont_mdiff_on_congr h‚ÇÑ).mpr $
      œà.smooth_to.comp_cont_mdiff_on $ hg.comp_cont_mdiff_on œÜ.smooth_inv‚ü©, },
  { refine ‚ü®V, h‚ÇÇ, _, (cont_mdiff_on_congr hK').mpr hf.cont_mdiff_on‚ü©,
    simpa [hm] using set.ext_iff.mp h‚ÇÉ m }
end

/-
The next lemma probably isn't quite enough. We want to apply it to
`K = [0, 1] √ó Ball 0 2` but the condition `f (œÜ x) = œà (g x)` doesn't hold on
`{1} √ó Ball 0 2`. However we also don't care about what happens when `t` isn't in `[0, 1]`
so we should probably weaken the conclusion.
We could even use a reflection trick to reduce to a case where `K = [0, 2] √ó Ball 0 2`
and the whole boundary is ok.
-/

/-- This is half of lemma `lem:updating` in the blueprint. -/
lemma nice_update_of_eq_outside_compact' {K : set X} (hK : is_compact K) (hf : smooth IM IN f)
  (hf' : f '' range œÜ ‚äÜ range œà) {Œµ : M ‚Üí ‚Ñù} (hŒµ : ‚àÄ m, 0 < Œµ m) (hŒµ' : continuous Œµ) :
  ‚àÉ (Œ∑ > (0 : ‚Ñù)), ‚àÄ g : X ‚Üí Y,
    (‚àÄ x, x ‚àâ K ‚Üí f (œÜ x) = œà (g x)) ‚Üí
    (‚àÄ x, dist (g x) (œà.inv_fun (f (œÜ x))) < Œ∑) ‚Üí
      ‚àÄ m, dist (update œÜ œà f g m) (f m) < Œµ m :=
begin
  let K‚ÇÅ := metric.cthickening 1 ((œà.inv_fun ‚àò f ‚àò œÜ) '' K),
  have hK‚ÇÅ : is_compact K‚ÇÅ,
  { refine metric.is_compact_of_is_closed_bounded metric.is_closed_cthickening
      (metric.bounded.cthickening $ is_compact.bounded $ hK.image _),
    replace hf' : ‚àÄ x, f (œÜ x) ‚àà range œà := Œª x, hf' ‚ü®œÜ x, mem_range_self x, rfl‚ü©,
    exact œà.smooth_inv.continuous_on.comp_continuous
      (hf.continuous.comp œÜ.continuous) hf', },
  have h‚ÇÅ : uniform_continuous_on œà K‚ÇÅ :=
    hK‚ÇÅ.uniform_continuous_on_of_continuous œà.continuous.continuous_on,
  have hŒµœÜ : ‚àÄ x ‚àà K, 0 < (Œµ ‚àò œÜ) x := Œª x hx, hŒµ _,
  obtain ‚ü®Œµ‚ÇÄ, hŒµ‚ÇÄ, hŒµ‚ÇÄ'‚ü© :=
    hK.exists_forall_le' (hŒµ'.comp œÜ.continuous).continuous_on hŒµœÜ,
  obtain ‚ü®œÑ, hœÑ : 0 < œÑ, hœÑ'‚ü© := metric.uniform_continuous_on_iff.mp h‚ÇÅ Œµ‚ÇÄ hŒµ‚ÇÄ,
  refine ‚ü®min œÑ 1, by simp [hœÑ], Œª g hg hŒ∑ m,  _‚ü©,
  have hK' : ‚àÄ m ‚àâ œÜ '' K, update œÜ œà f g m = f m := Œª m hm, by
    from nice_update_of_eq_outside_compact_aux œÜ œà f g hg hm,
  by_cases hm : m ‚àà œÜ '' K, swap, { simp [hK', hm, hŒµ m], },
  obtain ‚ü®x, hx, rfl‚ü© := hm,
  refine lt_of_lt_of_le _ (hŒµ‚ÇÄ' x hx),
  simp only [update_apply_embedding],
  have h‚ÇÅ : g x ‚àà K‚ÇÅ :=
    metric.mem_cthickening_of_dist_le _ _ _ _ ‚ü®x, hx, rfl‚ü© (lt_min_iff.mp (hŒ∑ x)).2.le,
  have h‚ÇÇ : f (œÜ x) ‚àà range œà := hf' ‚ü®œÜ x, mem_range_self x, rfl‚ü©,
  rw ‚Üê œà.right_inv h‚ÇÇ,
  exact hœÑ' _ h‚ÇÅ _ (metric.self_subset_cthickening _ ‚ü®x, hx, rfl‚ü©) (lt_min_iff.mp (hŒ∑ x)).1,
end

end updating

end open_smooth_embedding
