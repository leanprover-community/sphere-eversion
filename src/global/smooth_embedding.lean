import topology.metric_space.hausdorff_distance
import topology.uniform_space.separation
import geometry.manifold.cont_mdiff_mfderiv
import indexing
import to_mathlib.topology.paracompact
import to_mathlib.topology.algebra.order.compact
import to_mathlib.topology.nhds_set
import to_mathlib.topology.misc
import to_mathlib.geometry.manifold.charted_space
import to_mathlib.geometry.manifold.smooth_manifold_with_corners
import to_mathlib.analysis.normed_space.misc

import interactive_expr
set_option trace.filter_inst_type true

noncomputable theory

open set equiv
open_locale manifold topology

section general
variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H)
  (M : Type*) [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H']
  (I' : model_with_corners ğ•œ E' H')
  (M' : Type*) [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']

structure open_smooth_embedding  :=
(to_fun : M â†’ M')
(inv_fun : M' â†’ M)
(left_inv' : âˆ€{x}, inv_fun (to_fun x) = x)
(is_open_range : is_open (range to_fun))
(smooth_to : smooth I I' to_fun)
(smooth_inv : smooth_on I' I inv_fun (range to_fun))

instance : has_coe_to_fun (open_smooth_embedding I M I' M') (Î» _, M â†’ M') :=
âŸ¨open_smooth_embedding.to_funâŸ©

namespace open_smooth_embedding

variables {I I' M M'} (f : open_smooth_embedding I M I' M')

@[simp] lemma coe_mk (f g hâ‚ hâ‚‚ hâ‚ƒ hâ‚„) :
  â‡‘(âŸ¨f, g, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ© : open_smooth_embedding I M I' M') = f :=
rfl

@[simp] lemma left_inv (x : M) : f.inv_fun (f x) = x := by apply f.left_inv'

@[simp] lemma inv_fun_comp_coe : f.inv_fun âˆ˜ f = id := funext f.left_inv

@[simp] lemma right_inv {y : M'} (hy : y âˆˆ range f) : f (f.inv_fun y) = y :=
by { obtain âŸ¨x, rflâŸ© := hy, rw [f.left_inv] }

lemma smooth_at_inv {y : M'} (hy : y âˆˆ range f) : smooth_at I' I f.inv_fun y :=
(f.smooth_inv y hy).cont_mdiff_at $ f.is_open_range.mem_nhds hy

lemma smooth_at_inv' {x : M} : smooth_at I' I f.inv_fun (f x) :=
f.smooth_at_inv $ mem_range_self x

lemma left_inverse : function.left_inverse f.inv_fun f :=
left_inv f

lemma injective : function.injective f :=
f.left_inverse.injective

protected lemma continuous : continuous f := f.smooth_to.continuous

lemma open_map : is_open_map f :=
f.left_inverse.is_open_map f.is_open_range f.smooth_inv.continuous_on

lemma coe_comp_inv_fun_eventually_eq (x : M) : f âˆ˜ f.inv_fun =á¶ [ğ“ (f x)] id :=
filter.eventually_of_mem (f.open_map.range_mem_nhds x) $ Î» y hy, f.right_inv hy

/- Note that we are slightly abusing the fact that `tangent_space I x` and
`tangent_space I (f.inv_fun (f x))` are both definitionally `E` below. -/
def fderiv (x : M) : tangent_space I x â‰ƒL[ğ•œ] tangent_space I' (f x) :=
have hâ‚ : mdifferentiable_at I' I f.inv_fun (f x) := ((f.smooth_inv (f x) (mem_range_self x)
  ).mdifferentiable_within_at le_top).mdifferentiable_at (f.open_map.range_mem_nhds x),
have hâ‚‚ : mdifferentiable_at I I' f x := f.smooth_to.cont_mdiff.mdifferentiable le_top _,
continuous_linear_equiv.equiv_of_inverse
  (mfderiv I I' f x)
  (mfderiv I' I f.inv_fun (f x))
begin
  intros v,
  rw [â† continuous_linear_map.comp_apply, â† mfderiv_comp x hâ‚ hâ‚‚, f.inv_fun_comp_coe, mfderiv_id,
    continuous_linear_map.coe_id', id.def],
end
begin
  intros v,
  have hx : x = f.inv_fun (f x), { rw f.left_inv, },
  have hx' : f (f.inv_fun (f x)) = f x, { rw f.left_inv, },
  rw hx at hâ‚‚,
  rw [hx, hx', â† continuous_linear_map.comp_apply, â† mfderiv_comp (f x) hâ‚‚ hâ‚, ((has_mfderiv_at_id
    I' (f x)).congr_of_eventually_eq (f.coe_comp_inv_fun_eventually_eq x)).mfderiv,
    continuous_linear_map.coe_id', id.def],
end

@[simp] lemma fderiv_coe (x : M) :
  (f.fderiv x : tangent_space I x â†’L[ğ•œ] tangent_space I' (f x)) = mfderiv I I' f x :=
by { ext, refl }

@[simp] lemma fderiv_symm_coe (x : M) :
  ((f.fderiv x).symm : tangent_space I' (f x) â†’L[ğ•œ] tangent_space I x) =
  mfderiv I' I f.inv_fun (f x) :=
by { ext, refl }

lemma fderiv_symm_coe' {x : M'} (hx : x âˆˆ range f) :
  ((f.fderiv (f.inv_fun x)).symm : tangent_space I' (f (f.inv_fun x)) â†’L[ğ•œ]
    tangent_space I (f.inv_fun x)) =
  (mfderiv I' I f.inv_fun x : tangent_space I' x â†’L[ğ•œ] tangent_space I (f.inv_fun x)) :=
by rw [fderiv_symm_coe, f.right_inv hx]

open filter

lemma open_embedding : open_embedding f :=
open_embedding_of_continuous_injective_open f.continuous f.injective f.open_map

lemma inducing : inducing f := f.open_embedding.to_inducing

-- `âˆ€á¶  x near s, p x` means property `p` holds at every point in a neighborhood of the set `s`.
notation `âˆ€á¶ ` binders ` near ` s `, ` r:(scoped p, filter.eventually p $ ğ“Ë¢ s) := r

lemma forall_near' {P : M â†’ Prop} {A : set M'} (h : âˆ€á¶  m near f â»Â¹' A, P m) :
  âˆ€á¶  m' near A âˆ© range f, âˆ€ m, m' = f m â†’ P m :=
begin
  rw eventually_nhds_set_iff at h âŠ¢,
  rintros _ âŸ¨hfmâ‚€, mâ‚€, rflâŸ©,
  have : âˆ€ U âˆˆ ğ“ mâ‚€, âˆ€á¶  m' in ğ“ (f mâ‚€), m' âˆˆ f '' U,
  { intros U U_in,
    exact f.open_map.image_mem_nhds U_in },
  apply (this _ $ h mâ‚€ hfmâ‚€).mono,
  rintros _ âŸ¨mâ‚€, hmâ‚€, hmâ‚€'âŸ© mâ‚ rfl,
  rwa â† f.injective hmâ‚€'
end

lemma eventually_nhds_set_mono {Î± : Type*} [topological_space Î±] {s t : set Î±} {P : Î± â†’ Prop}
  (h : âˆ€á¶  x near t, P x) (h' : s âŠ† t) : âˆ€á¶  x near s, P x :=
h.filter_mono (nhds_set_mono h')

-- TODO: optimize this proof which is probably more complicated than it needs to be
lemma forall_near [t2_space M'] {P : M â†’ Prop} {P' : M' â†’ Prop} {K : set M} (hK : is_compact K)
  {A : set M'} (hP : âˆ€á¶  m near f â»Â¹' A, P m) (hP' : âˆ€á¶  m' near A, m' âˆ‰ f '' K â†’ P' m')
  (hPP' : âˆ€ m, P m â†’ P' (f m)) :
  âˆ€á¶  m' near A, P' m' :=
begin
  rw show A = (A âˆ© range f) âˆª (A âˆ© (range f)á¶œ), by simp,
  apply filter.eventually.union,
  { have : âˆ€á¶  m' near A âˆ© range f, m' âˆˆ range f,
      from f.is_open_range.forall_near_mem_of_subset (inter_subset_right _ _),
    apply (this.and $ f.forall_near' hP).mono,
    rintros _ âŸ¨âŸ¨m, rflâŸ©, hmâŸ©,
    exact hPP' _ (hm _ rfl) },
  { have op : is_open (f '' K)á¶œ,
    { rw is_open_compl_iff,
      exact (hK.image f.continuous).is_closed },
    have : A âˆ© (range f)á¶œ âŠ† A âˆ© (f '' K)á¶œ,
    { exact inter_subset_inter_right _ (compl_subset_compl.mpr (image_subset_range f K)) },
    apply eventually_nhds_set_mono _ this,
    rw eventually_nhds_set_iff at hP' âŠ¢,
    rintros x âŸ¨hx, hx'âŸ©,
    have hx' : âˆ€á¶  y in ğ“ x, y âˆˆ (f '' K)á¶œ,
      from is_open_iff_eventually.mp op x hx',
    apply ((hP' x hx).and hx').mono,
    rintros y âŸ¨hy, hy'âŸ©,
    exact hy hy' },
end

variables (I M)

/-- The identity map is a smooth open embedding. -/
-- unused
@[simps] def id : open_smooth_embedding I M I M :=
{ to_fun := id,
  inv_fun := id,
  left_inv' := Î» x, rfl,
  is_open_range := is_open_map.id.is_open_range,
  smooth_to := smooth_id,
  smooth_inv := smooth_on_id }

variables {I M I' M'}

-- unused
@[simps] def comp
  {E'' : Type*} [normed_add_comm_group E''] [normed_space ğ•œ E'']
  {H'' : Type*} [topological_space H'']
  {I'' : model_with_corners ğ•œ E'' H''}
  {M'' : Type*} [topological_space M''] [charted_space H'' M''] [smooth_manifold_with_corners I'' M'']
  (g : open_smooth_embedding I' M' I'' M'') (f : open_smooth_embedding I M I' M') :
  open_smooth_embedding I M I'' M'' :=
{ to_fun := g âˆ˜ f,
  inv_fun := f.inv_fun âˆ˜ g.inv_fun,
  left_inv' := Î» x, by simp only [function.comp_app, left_inv],
  is_open_range := (g.open_map.comp f.open_map).is_open_range,
  smooth_to := g.smooth_to.comp f.smooth_to,
  smooth_inv := (f.smooth_inv.comp' g.smooth_inv).mono
  begin
    change range (g âˆ˜ f) âŠ† range g âˆ© g.inv_fun â»Â¹' range f,
    refine subset_inter_iff.mpr âŸ¨range_comp_subset_range f g, _âŸ©,
    rintros x' âŸ¨x, rflâŸ©,
    exact âŸ¨x, by simp only [left_inv]âŸ©,
  end, }

end open_smooth_embedding

namespace continuous_linear_equiv

variables (e : E â‰ƒL[ğ•œ] E') [complete_space E] [complete_space E']

@[simp] lemma is_open_map : is_open_map e := (e : E â†’L[ğ•œ] E').is_open_map e.surjective

-- unused
@[simps] def to_open_smooth_embedding :
  open_smooth_embedding ğ“˜(ğ•œ, E) E ğ“˜(ğ•œ, E') E' :=
{ to_fun := e,
  inv_fun := e.symm,
  left_inv' := e.symm_apply_apply,
  is_open_range := e.is_open_map.is_open_range,
  smooth_to := (e : E â†’L[ğ•œ] E').cont_mdiff,
  smooth_inv := (e.symm : E' â†’L[ğ•œ] E).cont_mdiff.cont_mdiff_on }

end continuous_linear_equiv

end general

section without_boundary

open metric (hiding mem_nhds_iff) function

universe u

section general_nonsense

variables {ğ•œ E H M : Type*} [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group E] [normed_space ğ•œ E]
  [topological_space H] {I : model_with_corners ğ•œ E H}
  [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {x : M} {n : â„•âˆ}

lemma ext_chart_at_target_eq_image_chart_target :
  (ext_chart_at I x).target = I '' (chart_at H x).target :=
begin
  erw [(chart_at H x).to_local_equiv.trans_target'' I.to_local_equiv, I.source_eq, univ_inter],
  refl,
end

@[simp] lemma model_with_corners_self.ext_chart_at {e : E} :
  ext_chart_at ğ“˜(ğ•œ, E) e = local_equiv.refl E :=
by simp

lemma cont_mdiff_on_ext_chart_symm :
  cont_mdiff_on ğ“˜(ğ•œ, E) I n (ext_chart_at I x).symm (ext_chart_at I x).target :=
begin
  have hs : (ext_chart_at I x).target âŠ† (chart_at E (ext_chart_at I x x)).source,
  { simp only [subset_univ] with mfld_simps },
  have h2s : maps_to (ext_chart_at I x).symm (ext_chart_at I x).target (chart_at H x).source,
  { rw â† ext_chart_at_source I, exact (ext_chart_at I x).symm_maps_to, },
  refine (cont_mdiff_on_iff_of_subset_source hs h2s).mpr âŸ¨continuous_on_ext_chart_at_symm I x, _âŸ©,
  simp_rw [model_with_corners_self.ext_chart_at, local_equiv.refl_symm, local_equiv.refl_coe,
    comp.right_id, id.def, image_id'],
  exact (cont_diff_on_congr (Î» y hy, (ext_chart_at I x).right_inv hy)).mpr cont_diff_on_id
end

end general_nonsense

variables
  {F H : Type*} (M : Type u)
  [normed_add_comm_group F] [normed_space â„ F]
  [topological_space H] [topological_space M] [charted_space H M]
  [t2_space M] [locally_compact_space M] [sigma_compact_space M]
  (IF : model_with_corners â„ F H) [smooth_manifold_with_corners IF M]

/- Clearly should be generalised. Maybe what we really want is a theory of local diffeomorphisms.

Note that the input `f` is morally an `open_smooth_embedding` but stated in terms of `cont_diff`
instead of `cont_mdiff`. This is more convenient for our purposes. -/
def open_smooth_emb_of_diffeo_subset_chart_target (x : M) {f : local_homeomorph F F}
  (hfâ‚ : f.source = univ)
  (hfâ‚‚ : cont_diff â„ âˆ f)
  (hfâ‚ƒ : cont_diff_on â„ âˆ f.symm f.target)
  (hfâ‚„ : range f âŠ† IF '' (chart_at H x).target) :
  open_smooth_embedding ğ“˜(â„, F) F IF M :=
{ to_fun := (ext_chart_at IF x).symm âˆ˜ f,
  inv_fun := f.inv_fun âˆ˜ (ext_chart_at IF x),
  left_inv' := Î» y,
  begin
    obtain âŸ¨z, hz, hz'âŸ© := hfâ‚„ (mem_range_self y),
    have aux : f.symm (IF z) = y, { rw hz', exact f.left_inv (hfâ‚.symm â–¸ mem_univ _), },
    simp only [â† hz', (chart_at H x).right_inv hz, aux, ext_chart_at,
      local_homeomorph.extend, local_equiv.coe_trans,
      local_homeomorph.inv_fun_eq_coe, model_with_corners.to_local_equiv_coe,
      local_homeomorph.coe_coe, local_equiv.coe_trans_symm, local_homeomorph.coe_coe_symm,
      model_with_corners.left_inv, model_with_corners.to_local_equiv_coe_symm, comp_app, aux],
  end,
  is_open_range := is_open_map.is_open_range $ Î» u hu,
  begin
    have aux : is_open (f '' u) := f.image_open_of_open hu (hfâ‚.symm â–¸ subset_univ u),
    convert is_open_ext_chart_at_preimage' IF x aux,
    rw image_comp,
    refine (ext_chart_at IF x).symm_image_eq_source_inter_preimage
      ((image_subset_range f u).trans _),
    rw ext_chart_at_target_eq_image_chart_target,
    exact hfâ‚„,
  end,
  smooth_to :=
  begin
    refine cont_mdiff_on_ext_chart_symm.comp_cont_mdiff hfâ‚‚.cont_mdiff (Î» y, _),
    rw ext_chart_at_target_eq_image_chart_target,
    exact hfâ‚„ (mem_range_self y),
  end,
  smooth_inv :=
  begin
    rw â† ext_chart_at_target_eq_image_chart_target at hfâ‚„,
    have hf' : range ((ext_chart_at IF x).symm âˆ˜ f) âŠ† (ext_chart_at IF x) â»Â¹' f.target,
    { rw [range_comp, â† image_subset_iff, â† f.image_source_eq_target, hfâ‚, image_univ],
      exact (local_equiv.image_symm_image_of_subset_target _ hfâ‚„).subset, },
    have hf'' : range ((ext_chart_at IF x).symm âˆ˜ f) âŠ† (chart_at H x).source,
    { rw [â† ext_chart_at_source IF, range_comp, â† local_equiv.symm_image_target_eq_source],
      exact (monotone_image hfâ‚„).trans subset.rfl, },
    exact hfâ‚ƒ.cont_mdiff_on.comp (cont_mdiff_on_ext_chart_at.mono hf'') hf',
  end}

@[simp] lemma coe_open_smooth_emb_of_diffeo_subset_chart_target
  (x : M) {f : local_homeomorph F F}
  (hfâ‚ : f.source = univ)
  (hfâ‚‚ : cont_diff â„ âˆ f)
  (hfâ‚ƒ : cont_diff_on â„ âˆ f.symm f.target)
  (hfâ‚„ : range f âŠ† IF '' (chart_at H x).target) :
  (open_smooth_emb_of_diffeo_subset_chart_target M IF x hfâ‚ hfâ‚‚ hfâ‚ƒ hfâ‚„ : F â†’ M) =
  (ext_chart_at IF x).symm âˆ˜ f :=
by simp [open_smooth_emb_of_diffeo_subset_chart_target]

lemma range_open_smooth_emb_of_diffeo_subset_chart_target
  (x : M) {f : local_homeomorph F F}
  (hfâ‚ : f.source = univ)
  (hfâ‚‚ : cont_diff â„ âˆ f)
  (hfâ‚ƒ : cont_diff_on â„ âˆ f.symm f.target)
  (hfâ‚„ : range f âŠ† IF '' (chart_at H x).target) :
  range (open_smooth_emb_of_diffeo_subset_chart_target M IF x hfâ‚ hfâ‚‚ hfâ‚ƒ hfâ‚„) =
  (ext_chart_at IF x).symm '' (range f) :=
by rw [coe_open_smooth_emb_of_diffeo_subset_chart_target, range_comp]

variables {M} (F) [model_with_corners.boundaryless IF] [finite_dimensional â„ F]

lemma nice_atlas'
  {Î¹ : Type*} {s : Î¹ â†’ set M} (s_op : âˆ€ j, is_open $ s j) (cov : (â‹ƒ j, s j) = univ)
  (U : set F) (hUâ‚ : (0 : F) âˆˆ U) (hUâ‚‚ : is_open U) :
  âˆƒ (Î¹' : Type u) (t : set Î¹') (Ï† : t â†’ open_smooth_embedding ğ“˜(â„, F) F IF M),
  t.countable âˆ§
  (âˆ€ i, âˆƒ j, range (Ï† i) âŠ† s j) âˆ§
  locally_finite (Î» i, range (Ï† i)) âˆ§
  (â‹ƒ i, Ï† i '' U) = univ :=
begin
  let W : M â†’ â„ â†’ set M := Î» x r,
    (ext_chart_at IF x).symm âˆ˜ diffeomorph_to_nhd (ext_chart_at IF x x) r '' U,
  let B : M â†’ â„ â†’ set M := charted_space.ball IF,
  let p : M â†’ â„ â†’ Prop :=
    Î» x r, 0 < r âˆ§ ball (ext_chart_at IF x x) r âŠ† (ext_chart_at IF x).target âˆ§ âˆƒ j, B x r âŠ† s j,
  have hWâ‚€ : âˆ€ x r, p x r â†’ x âˆˆ W x r := Î» x r h, âŸ¨0, hUâ‚, by simp [h.1]âŸ©,
  have hWâ‚ : âˆ€ x r, p x r â†’ is_open (W x r),
  { rintros x r âŸ¨hâ‚, hâ‚‚, -, -âŸ©,
    simp only [W],
    rw image_comp,
    let V := diffeomorph_to_nhd (ext_chart_at IF x x) r '' U,
    change is_open ((ext_chart_at IF x).symm '' V),
    have hVâ‚ : is_open V := ((diffeomorph_to_nhd
      (ext_chart_at IF x x) r).is_open_image_iff_of_subset_source (by simp)).mp hUâ‚‚,
    have hVâ‚‚ : V âŠ† (ext_chart_at IF x).target :=
      subset.trans ((image_subset_range _ _).trans (by simp [hâ‚])) hâ‚‚,
    rw (ext_chart_at IF x).symm_image_eq_source_inter_preimage hVâ‚‚,
    exact is_open_ext_chart_at_preimage' IF x hVâ‚, },
  have hB : âˆ€ x, (ğ“ x).has_basis (p x) (B x) :=
    Î» x, charted_space.nhds_has_basis_balls_of_open_cov IF x s_op cov,
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒ, htâ‚„âŸ© :=
    exists_countable_locally_finite_cover surjective_id hWâ‚€ hWâ‚ hB,
  let g : M Ã— â„ â†’ local_homeomorph F F := Î» z, diffeomorph_to_nhd (ext_chart_at IF z.1 z.1) z.2,
  have hgâ‚ : âˆ€ z, (g z).source = univ, { simp, },
  have hgâ‚‚ : âˆ€ z, cont_diff â„ âˆ (g z), { simp, },
  have hgâ‚ƒ : âˆ€ z, cont_diff_on â„ âˆ (g z).symm (g z).target, { simp, },
  refine âŸ¨M Ã— â„, t,
    Î» z, open_smooth_emb_of_diffeo_subset_chart_target M IF z.1.1 (hgâ‚ z.1) (hgâ‚‚ z.1) (hgâ‚ƒ z.1) _,
    htâ‚, Î» z, _, _, _âŸ©,
  { obtain âŸ¨âŸ¨x, râŸ©, hxrâŸ© := z,
    obtain âŸ¨hr : 0 < r, hr' : ball (ext_chart_at IF x x) r âŠ† _, -âŸ© := htâ‚‚ _ hxr,
    rw â† ext_chart_at_target_eq_image_chart_target,
    exact (range_diffeomorph_to_nhd_subset_ball (ext_chart_at IF x x) hr).trans hr', },
  { obtain âŸ¨âŸ¨x, râŸ©, hxrâŸ© := z,
    obtain âŸ¨hr : 0 < r, -, j, hj : B x r âŠ† s jâŸ© := htâ‚‚ _ hxr,
    simp_rw range_open_smooth_emb_of_diffeo_subset_chart_target,
    exact âŸ¨j, (monotone_image (range_diffeomorph_to_nhd_subset_ball _ hr)).trans hjâŸ©, },
  { simp_rw range_open_smooth_emb_of_diffeo_subset_chart_target,
    refine htâ‚„.subset _,
    rintros âŸ¨âŸ¨x, râŸ©, hxrâŸ©,
    obtain âŸ¨hr : 0 < r, -, -âŸ© := htâ‚‚ _ hxr,
    exact monotone_image (range_diffeomorph_to_nhd_subset_ball _ hr), },
  { simpa only [Union_coe_set] using htâ‚ƒ, },
end

variables [nonempty M]

lemma nice_atlas {Î¹ : Type*} {s : Î¹ â†’ set M} (s_op : âˆ€ j, is_open $ s j) (cov : (â‹ƒ j, s j) = univ) :
  âˆƒ n, âˆƒ Ï† : index_type n â†’ open_smooth_embedding ğ“˜(â„, F) F IF M,
  (âˆ€ i, âˆƒ j, range (Ï† i) âŠ† s j) âˆ§
  locally_finite (Î» i, range (Ï† i)) âˆ§
  (â‹ƒ i, Ï† i '' ball 0 1) = univ :=
begin
  obtain âŸ¨Î¹', t, Ï†, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ© := nice_atlas' F IF s_op cov (ball 0 1) (by simp) is_open_ball,
  have htne : t.nonempty,
  { by_contra contra,
    simp only [not_nonempty_iff_eq_empty.mp contra, Union_false, Union_coe_set, Union_empty,
      @eq_comm _ _ univ, univ_eq_empty_iff] at hâ‚„,
    exact not_is_empty_of_nonempty M hâ‚„, },
  obtain âŸ¨n, âŸ¨fnâŸ©âŸ© := (set.countable_iff_exists_nonempty_index_type_equiv htne).mp hâ‚,
  refine âŸ¨n, Ï† âˆ˜ fn, Î» i, hâ‚‚ (fn i), hâ‚ƒ.comp_injective fn.injective, _âŸ©,
  rwa fn.surjective.Union_comp (Î» i, Ï† i '' ball 0 1),
end

end without_boundary

namespace open_smooth_embedding

section updating

variables {ğ•œ EX EM EY EN EM' X M Y N M' : Type*} [nontrivially_normed_field ğ•œ]
  [normed_add_comm_group EX] [normed_space ğ•œ EX]
  [normed_add_comm_group EM] [normed_space ğ•œ EM]
  [normed_add_comm_group EM'] [normed_space ğ•œ EM']
  [normed_add_comm_group EY] [normed_space ğ•œ EY]
  [normed_add_comm_group EN] [normed_space ğ•œ EN]
  {HX : Type*} [topological_space HX] {IX : model_with_corners ğ•œ EX HX}
  {HY : Type*} [topological_space HY] {IY : model_with_corners ğ•œ EY HY}
  {HM : Type*} [topological_space HM] {IM : model_with_corners ğ•œ EM HM}
  {HM' : Type*} [topological_space HM'] {IM' : model_with_corners ğ•œ EM' HM'}
  {HN : Type*} [topological_space HN] {IN : model_with_corners ğ•œ EN HN}
  [topological_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]
  [topological_space M] [charted_space HM M] [smooth_manifold_with_corners IM M]
  [topological_space M'] [charted_space HM' M']

section non_metric
variables
  [topological_space Y]      [charted_space HY Y] [smooth_manifold_with_corners IY Y]
  [topological_space N]      [charted_space HN N] [smooth_manifold_with_corners IN N]
  (Ï† : open_smooth_embedding IX X IM M)
  (Ïˆ : open_smooth_embedding IY Y IN N)
  (f : M â†’ N) (g : X â†’ Y)

section
local attribute [instance] classical.dec
/-- This is definition `def:update` in the blueprint. -/
def update (m : M) : N := if m âˆˆ range Ï† then Ïˆ (g (Ï†.inv_fun m)) else f m
end

@[simp] lemma update_of_nmem_range {m : M} (hm : m âˆ‰ range Ï†) :
  update Ï† Ïˆ f g m = f m :=
by simp [update, hm]

@[simp] lemma update_of_mem_range {m : M} (hm : m âˆˆ range Ï†) :
  update Ï† Ïˆ f g m = Ïˆ (g (Ï†.inv_fun m)) :=
by simp [update, hm]

@[simp] lemma update_apply_embedding (x : X) :
  update Ï† Ïˆ f g (Ï† x) = Ïˆ (g x) :=
by simp [update]

/- This small auxiliry result is used in the next two lemmas. -/
lemma nice_update_of_eq_outside_compact_aux {K : set X} (g : X â†’ Y)
  (hg : âˆ€ (x : X), x âˆ‰ K â†’ f (Ï† x) = Ïˆ (g x)) {m : M} (hm : m âˆ‰ Ï† '' K) :
  Ï†.update Ïˆ f g m = f m :=
begin
  by_cases hm' : m âˆˆ range Ï†,
  { obtain âŸ¨x, rflâŸ© := hm',
    replace hm : x âˆ‰ K, { contrapose! hm, exact mem_image_of_mem Ï† hm, },
    simp [hg x hm] },
  { simp [hm'] }
end

open function

/-- This is lemma `lem:smooth_updating` in the blueprint. -/
lemma smooth_update
  (f : M' â†’ M â†’ N) (g : M' â†’ X â†’ Y)
  {k : M' â†’ M}
  {K : set X} (hK : is_closed (Ï† '' K)) (hf : smooth (IM'.prod IM) IN (uncurry f))
  (hg : smooth (IM'.prod IX) IY (uncurry g))
  (hk : smooth IM' IM k)
  (hg' : âˆ€ y x, x âˆ‰ K â†’ f y (Ï† x) = Ïˆ (g y x)) :
  smooth IM' IN (Î» x, update Ï† Ïˆ (f x) (g x) (k x)) :=
begin
  have hK' : âˆ€ x, k x âˆ‰ Ï† '' K â†’ update Ï† Ïˆ (f x) (g x) (k x) = f x (k x) :=
    Î» x hx, nice_update_of_eq_outside_compact_aux Ï† Ïˆ (f x) (g x) (hg' x) hx,
  refine cont_mdiff_of_locally_cont_mdiff_on (Î» x, _),
  let U := range Ï†,
  let V := (Ï† '' K)á¶œ,
  have hâ‚‚ : is_open (k â»Â¹' V) := hK.is_open_compl.preimage hk.continuous,
  have hâ‚ƒ : V âˆª U = univ,
  { rw [â† compl_subset_iff_union, compl_compl], exact image_subset_range Ï† K, },
  have hâ‚„ : âˆ€ x, k x âˆˆ U â†’ update Ï† Ïˆ (f x) (g x) (k x) = (Ïˆ âˆ˜ g x âˆ˜ Ï†.inv_fun) (k x) :=
    Î» m hm, by simp [hm],
  by_cases hx : k x âˆˆ U,
  { refine âŸ¨k â»Â¹' U, Ï†.is_open_range.preimage hk.continuous, hx,
    (cont_mdiff_on_congr hâ‚„).mpr $ Ïˆ.smooth_to.comp_cont_mdiff_on $
      hg.comp_cont_mdiff_on (smooth_on_id.prod_mk $ Ï†.smooth_inv.comp hk.smooth_on subset_rfl)âŸ© },
  { refine âŸ¨k â»Â¹' V, hâ‚‚, _, (cont_mdiff_on_congr hK').mpr
      (hf.comp (smooth_id.prod_mk hk)).cont_mdiff_onâŸ©,
    simpa [hx] using set.ext_iff.mp hâ‚ƒ (k x) }
end
end non_metric

section metric
variables
  [metric_space Y]      [charted_space HY Y] [smooth_manifold_with_corners IY Y]
  [metric_space N]      [charted_space HN N] [smooth_manifold_with_corners IN N]
  (Ï† : open_smooth_embedding IX X IM M)
  (Ïˆ : open_smooth_embedding IY Y IN N)
  (f : M â†’ N) (g : X â†’ Y)



/-- This is `lem:dist_updating` in the blueprint. -/
lemma dist_update [proper_space Y] {K : set X} (hK : is_compact K)
  {P : Type*} [metric_space P] {KP : set P} (hKP : is_compact KP)
  (f : P â†’ M â†’ N) (hf : continuous â†¿f)
  (hf' : âˆ€ p, (f p) '' range Ï† âŠ† range Ïˆ) {Îµ : M â†’ â„} (hÎµ : âˆ€ m, 0 < Îµ m) (hÎµ' : continuous Îµ) :
  âˆƒ Î· > (0 : â„), âˆ€ g : P â†’ X â†’ Y,
    (âˆ€ (p âˆˆ KP) (p' âˆˆ KP) (x âˆˆ K), dist (g p' x) (Ïˆ.inv_fun (f p (Ï† x))) < Î· â†’
      dist (update Ï† Ïˆ (f p') (g p') $ Ï† x) (f p $ Ï† x) < Îµ (Ï† x)) :=
begin
  let F : P Ã— X â†’ Y := Î» q, (Ïˆ.inv_fun âˆ˜ (f q.1) âˆ˜ Ï†) q.2,
  let Kâ‚ := metric.cthickening 1 (F '' KP.prod K),
  have hKâ‚ : is_compact Kâ‚,
  { refine metric.is_compact_of_is_closed_bounded metric.is_closed_cthickening
      (metric.bounded.cthickening $ is_compact.bounded $ _),
    apply (hKP.prod hK).image,
    exact Ïˆ.smooth_inv.continuous_on.comp_continuous
      (hf.comp $ continuous_fst.prod_mk $ Ï†.continuous.comp continuous_snd)
      (Î» q, hf' q.1 âŸ¨Ï† q.2, mem_range_self _, rflâŸ©) },
  have hâ‚ : uniform_continuous_on Ïˆ Kâ‚ :=
    hKâ‚.uniform_continuous_on_of_continuous Ïˆ.continuous.continuous_on,
  have hÎµÏ† : âˆ€ x âˆˆ K, 0 < (Îµ âˆ˜ Ï†) x := Î» x hx, hÎµ _,
  obtain âŸ¨Îµâ‚€, hÎµâ‚€, hÎµâ‚€'âŸ© :=
    hK.exists_forall_le' (hÎµ'.comp Ï†.continuous).continuous_on hÎµÏ†,
  obtain âŸ¨Ï„, hÏ„ : 0 < Ï„, hÏ„'âŸ© := metric.uniform_continuous_on_iff.mp hâ‚ Îµâ‚€ hÎµâ‚€,
  refine âŸ¨min Ï„ 1, by simp [hÏ„], Î» g p hp p' hp' x hx hÎ·,  _âŸ©,
  cases lt_min_iff.mp hÎ· with H H',
  specialize hÎµÏ† x hx,
  apply lt_of_lt_of_le _ (hÎµâ‚€' x hx), clear hÎµâ‚€',
  simp only [update_apply_embedding],
  have hâ‚ : g p' x âˆˆ Kâ‚,
    from metric.mem_cthickening_of_dist_le (g p' x) (F (p, x)) 1 _ âŸ¨(p, x), âŸ¨hp, hxâŸ©, rflâŸ© H'.le,
  have hâ‚‚ : f p (Ï† x) âˆˆ range Ïˆ,
    from hf' p âŸ¨Ï† x, mem_range_self _, rflâŸ©,
  rw â† Ïˆ.right_inv hâ‚‚,
  exact hÏ„' _ hâ‚ _ (metric.self_subset_cthickening _ âŸ¨(p, x), âŸ¨hp, hxâŸ©, rflâŸ©) H,
end

end metric

end updating

end open_smooth_embedding
