import to_mathlib.data.set.prod
import to_mathlib.data.nat.basic
import to_mathlib.geometry.manifold.metrizable
import to_mathlib.topology.constructions
import global.parametricity_for_free
import global.localized_construction
import global.localisation_data
/-!
# Gromov's theorem

We prove the h-principle for open and ample first order differential relations.
-/

noncomputable theory

open set filter model_with_corners metric
open_locale topological_space manifold

variables
{EM : Type*} [normed_add_comm_group EM] [normed_space â„ EM] [finite_dimensional â„ EM]
{HM : Type*} [topological_space HM] {IM : model_with_corners â„ EM HM} [boundaryless IM]
{M : Type*} [topological_space M] [charted_space HM M] [smooth_manifold_with_corners IM M]
[t2_space M] [sigma_compact_space M]

{EX : Type*} [normed_add_comm_group EX] [normed_space â„ EX] [finite_dimensional â„ EX]
{HX : Type*} [topological_space HX] {IX : model_with_corners â„ EX HX} [model_with_corners.boundaryless IX]
-- note: X is a metric space
{X : Type*} [metric_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]
[sigma_compact_space X]

{R : rel_mfld IM M IX X}
{A : set M} {Î´ : M â†’ â„}

set_option trace.filter_inst_type true
/--
Main inductive construction for the non-parametric version of Gromov's theorem.
-/
lemma rel_mfld.ample.satisfies_h_principle_core
  [nonempty M] [nonempty X]
  (hRample : R.ample) (hRopen : is_open R)
  (hA : is_closed A)
  (hÎ´_pos : âˆ€ (x : M), 0 < Î´ x)
  (hÎ´_cont : continuous Î´)
  (Fâ‚€ : formal_sol R)
  (hFâ‚€A : âˆ€á¶  x near A, Fâ‚€.is_holonomic_at x)
  (L : localisation_data IM IX Fâ‚€.bs) :
  âˆƒ F : â„• â†’ htpy_formal_sol R, âˆ€ n : â„•,
    ((âˆ€á¶  t near Iic (0 : â„), F n t = Fâ‚€) âˆ§
    (âˆ€á¶  t near Ici (1 : â„), F n t = F n 1) âˆ§
    (âˆ€á¶  x near A, âˆ€ t, F n t x = Fâ‚€ x) âˆ§
    (âˆ€ t x, dist ((F n t).bs x) (Fâ‚€.bs x) < Î´ x) âˆ§
    (âˆ€á¶  x near â‹ƒ i â‰¤ L.index n, (L.Ï† i) '' metric.closed_ball 0 1,
      ((F n) 1).to_one_jet_sec.is_holonomic_at x)) âˆ§
    ((L.index (n + 1)  = L.index n â†’ F (n + 1) = F n) âˆ§
     âˆ€ t (x âˆ‰ range (L.Ï† $ L.index $ n+1)), F (n + 1) t x = F n t x) :=
begin
  classical,
  borelize EX,
  haveI := locally_compact_manifold IM M,
  haveI := locally_compact_manifold IX X,
  let P : â„• â†’ htpy_formal_sol R â†’ Prop := Î» n Fn,
    (âˆ€á¶  t near Iic (0 : â„), Fn t = Fâ‚€) âˆ§
    (âˆ€á¶  t near Ici (1 : â„), Fn t = Fn 1) âˆ§
    (âˆ€á¶  x near A, âˆ€ t, Fn t x = Fâ‚€ x) âˆ§
    (âˆ€ t x, dist ((Fn t).bs x) (Fâ‚€.bs x) < Î´ x) âˆ§
    (âˆ€ t x, dist ((Fn t).bs x) (Fâ‚€.bs x) < L.Îµ x) âˆ§
    (âˆ€á¶  x near â‹ƒ i â‰¤ L.index n, (L.Ï† i) '' metric.closed_ball 0 1,
      (Fn 1).is_holonomic_at x),
  let Q : â„• â†’ htpy_formal_sol R â†’ htpy_formal_sol R â†’ Prop := Î» n Fn Fnp1,
    (L.index (n + 1)  = L.index n â†’ Fnp1 = Fn) âˆ§
     âˆ€ t, âˆ€ x âˆ‰ range (L.Ï† $ L.index $ n+1), Fnp1 t x = Fn t x,
  suffices : âˆƒ F : â„• â†’ htpy_formal_sol R, âˆ€ n, P n (F n) âˆ§ Q n (F n) (F $ n+1),
  { rcases this with âŸ¨F, hFâŸ©,
    use F,
    intro n,
    cases hF n,
    tauto },
  let Kâ‚€ : set EM := closed_ball 0 1,
  have hKâ‚€ : is_compact Kâ‚€, from is_compact_closed_ball 0 1,
  let Kâ‚ : set EM := closed_ball 0 2,
  have hKâ‚ : is_compact Kâ‚, from is_compact_closed_ball 0 2,
  have hKâ‚€Kâ‚ : Kâ‚€ âŠ† interior Kâ‚,
  { dsimp [Kâ‚€, Kâ‚],
    rw interior_closed_ball (0 : EM) (by norm_num : (2 : â„) â‰  0),
    exact closed_ball_subset_ball (by norm_num) },
  let Ï„ := Î» x : M, min (Î´ x) (L.Îµ x),
  have Ï„_pos : âˆ€ x, 0 < Ï„ x, from Î» x, lt_min (hÎ´_pos x) (L.Îµ_pos x),
  have Ï„_cont : continuous Ï„, from hÎ´_cont.min L.Îµ_cont,
  apply exists_by_induction' P Q,
  { dsimp only [P], clear P Q,
    let F := Fâ‚€.const_htpy,
    have hFâ‚€ : âˆ€á¶  (t : â„) near Iic 0, F t = F 0,
    { apply eventually_of_forall _,
      simp [Fâ‚€.const_htpy_eq] },
    have hFâ‚ : âˆ€á¶  (t : â„) near Ici 1, F t = F 1,
    { apply eventually_of_forall _,
      simp [Fâ‚€.const_htpy_eq] },
    have hFâ‚€A : âˆ€á¶  x near A, (F 0).is_holonomic_at x,
    { simp only [Fâ‚€.const_htpy_eq, hFâ‚€A] },
    have hFFâ‚€Ï„ : âˆ€ t x, dist ((F t).bs x) ((F 0).bs x) < Ï„ x,
    { simp only [Fâ‚€.const_htpy_eq, dist_self, Ï„_pos, forall_const] },
    have hFÏ†Ïˆ : âˆ€ t, (F t).bs '' (range $ L.Ï† 0) âŠ† range (L.Ïˆj 0),
    { simp only [Fâ‚€.const_htpy_eq, forall_const, â† range_comp, L.rg_subset_rg] },
    have hFA : âˆ€á¶  x near A, âˆ€ t, F t x = F 0 x,
    { simp only [Fâ‚€.const_htpy_eq, eq_self_iff_true, eventually_true, forall_const] },
    have hFC : âˆ€á¶  x near âˆ…, (F 1).is_holonomic_at x,
    { simp only [nhds_set_empty] },
    rcases (L.Ï† 0).improve_htpy_formal_sol (L.Ïˆj 0) hRample hRopen hA is_closed_empty
      Ï„_pos Ï„_cont hFâ‚€ hFâ‚ hFâ‚€A hFFâ‚€Ï„ hFÏ†Ïˆ hFA hFC hKâ‚€ hKâ‚ hKâ‚€Kâ‚ with âŸ¨F', hF'â‚€, hF'â‚, hF'Fâ‚€Ï„, hF'Kâ‚, hF'Ï„, hF'Kâ‚€âŸ©,
    rw [nhds_set_union, eventually_sup] at hF'Kâ‚€,
    refine âŸ¨F', _, _, _, _, _, _âŸ©,
    { apply hF'â‚€.mono,
      intros t ht,
      rw [ht, Fâ‚€.const_htpy_eq] },
    { exact hF'â‚ },
    { exact hF'Fâ‚€Ï„ },
    { exact Î» t x, lt_of_lt_of_le (hF'Ï„ t x) (min_le_left _ _) },
    { exact Î» t x, lt_of_lt_of_le (hF'Ï„ t x) (min_le_right _ _) },
    { rw L.Union_le_zero,
      simpa using hF'Kâ‚€.2 } },
  { rintros n F âŸ¨hFâ‚€, hFâ‚, hFA, hFÎ´, hFÎµ, hFholâŸ©,
    by_cases hn : L.index (n+1) = L.index n,
    { refine âŸ¨F, âŸ¨hFâ‚€, hFâ‚, hFA, hFÎ´, hFÎµ, _âŸ©, Î» _, rfl, Î» _ _ _, rflâŸ© ; clear P Q,
      rw hn,
      exact hFhol },
    { dsimp only [P, Q], clear P Q,
      have hFâ‚€â‚€ := hFâ‚€.on_set 0 right_mem_Iic,
      simp only [â† hFâ‚€.on_set 0 right_mem_Iic] at hFâ‚€ hFâ‚€A hFÎ´ hFÎµ hFA âŠ¢,
      have hFÏ„ : âˆ€ t x, dist ((F t).bs x) ((F 0).bs x) <  Ï„ x,
      { exact Î» t x, lt_min (hFÎ´ t x) (hFÎµ t x) },
      rcases (L.Ï† $ L.index $ n+1).improve_htpy_formal_sol (L.Ïˆj $ L.index $ n+1) hRample hRopen
        hA _ Ï„_pos Ï„_cont hFâ‚€ hFâ‚ hFâ‚€A hFÏ„ _ hFA hFhol hKâ‚€ hKâ‚ hKâ‚€Kâ‚  with
        âŸ¨F', hF'â‚€, hF'â‚, hF'A, hF'Kâ‚, hF'Ï„, hF'Kâ‚€âŸ©,
      rw [nhds_set_union, eventually_sup] at hF'Kâ‚€,
      refine âŸ¨F', âŸ¨hF'â‚€, hF'â‚, _, _, _, _âŸ©, _, _âŸ© ; clear hRample hRopen hÎ´_pos hÎ´_cont hKâ‚€ hKâ‚ hKâ‚€Kâ‚,
      { exact hF'A },
      { exact Î» t x, lt_of_lt_of_le (hF'Ï„ t x) (min_le_left _ _) },
      { exact Î» t x, lt_of_lt_of_le (hF'Ï„ t x) (min_le_right _ _) },
      { rw L.Union_succ,
        exact hF'Kâ‚€.2, },
      { exact Î» hn', (hn hn').elim },
      { exact Î» t x hx, hF'Kâ‚ t x (Î» hx', hx $ mem_range_of_mem_image _ _ hx') },
      { exact L.is_closed_Union hKâ‚€ n },
      { intro t,
        rw â† range_comp,
        apply L.Îµ_spec,
        simp only [â† hFâ‚€â‚€],
        apply hFÎµ } } },
end

/-- The non-parametric version of Gromov's theorem -/
lemma rel_mfld.ample.satisfies_h_principle (hRample : R.ample) (hRopen : is_open R)
  (hA : is_closed A)
  (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : continuous Î´) :
  R.satisfies_h_principle A Î´ :=
begin
  /- We first get rid of edge cases where `M` or `X` is empty.
  This is necessary because the localization data existence lemma has nonemptyness assumptions. -/
  haveI := locally_compact_manifold IM M,
  haveI := locally_compact_manifold IX X,
  refine rel_mfld.satisfies_h_principle_of_weak hA _,
  unfreezingI { clear_dependent A },
  intros A hA ğ“•â‚€ hğ“•â‚€,
  casesI is_empty_or_nonempty M with hM hM,
  { refine  âŸ¨empty_htpy_formal_sol R, _, _, _, _âŸ©,
    all_goals { try { apply eventually_of_forall _ } },
    all_goals { try { intro } },
    all_goals { try { intro } },
    all_goals { apply empty_htpy_formal_sol_eq <|> apply (is_empty.false â€¹Mâ€º).elim } },
  casesI is_empty_or_nonempty X with hX hX,
  { exfalso,
    inhabit M,
    exact (is_empty.false $ ğ“•â‚€.bs default).elim },
  /- We now start the main proof under the assumption that `M` and `X` are nonempty. -/
  have cont : continuous ğ“•â‚€.bs, from ğ“•â‚€.smooth_bs.continuous,
  let L : localisation_data IM IX ğ“•â‚€.bs := std_localisation_data EM IM EX IX cont,
  let Ï€ := L.index,

  suffices : âˆƒ F : â„• â†’ htpy_formal_sol R, âˆ€ n,
    ((F n 0 = ğ“•â‚€) âˆ§
    (âˆ€ t, âˆ€á¶  x near A, F n t x = ğ“•â‚€ x) âˆ§
    (âˆ€ t x, dist ((F n t).bs x) (ğ“•â‚€.bs x) < Î´ x) âˆ§

    (âˆ€ x âˆˆ â‹ƒ i â‰¤ Ï€ n, L.Ï† i '' metric.closed_ball (0 : EM) 1,
             (F n 1).is_holonomic_at x)) âˆ§
    ((Ï€ (n+1) = Ï€ n â†’ F (n+1) = F n) âˆ§
     (âˆ€ t, âˆ€ x âˆ‰ range (L.Ï† $ Ï€ (n+1)), F (n+1) t x = F n t x)),
  { /- Here we must build the homotopy of formal solution from the sequence
    given by the inductive construction.
    **TODO**:
    This uses `locally_finite.exists_forall_eventually_of_indexing` which partially duplicates
    use `locally_eventually_constant_on` from `to_mathlib.order.filter.eventually_constant`.
    Maybe this can be unified. -/
    clear_dependent hRample hRopen,
    simp_rw [and_assoc, forall_and_distrib] at this,
    rcases this with âŸ¨F, hFâ‚€, hfA, hFÎ´, hFhol, hFÏ€, hFultimâŸ©,
    /- The sequence F seen as a sequence of plain functions. -/
    let FF := Î» n : â„•, Î» p : â„ Ã— M, F n p.1 p.2,
    have h : âˆ€ n : â„•, âˆ€ x âˆ‰ (univ : set â„) Ã—Ë¢ range (L.Ï† $ Ï€ $ n+1), FF (n+1) x = FF n x,
    { rintros n âŸ¨t, xâŸ© H,
      exact hFultim _ _ _ (Î» hx, H âŸ¨trivial, hxâŸ©) },
    have h' : âˆ€ (n : â„•), Ï€ (n + 1) = Ï€ n â†’ FF (n + 1) = FF n,
    { intros n hn,
      ext1 âŸ¨t, xâŸ©,
      dsimp [FF],
      rw hFÏ€ n hn },
    have loc_fin : locally_finite (Î» i, (univ Ã—Ë¢ range (L.Ï† i) : set $ â„ Ã— M)),
    { apply L.lf_Ï†.prod_left },
    have : âˆ€ x : â„ Ã— M, âˆ€á¶  n in at_top, x.2 âˆˆ â‹ƒ i â‰¤ Ï€ n, (L.Ï† i) '' metric.ball 0 1,
    { rintros âŸ¨t, xâŸ©,
      exact L.eventually_mem_Union x },
    /- Now get the limit function. -/
    cases loc_fin.exists_forall_eventually_of_indexing h h' with G hG, clear h h' loc_fin,
    choose n hn' hn using Î» x, eventually_at_top.mp ((this x).and (hG x)), clear hG this,
    have G_eq : âˆ€ t x, G (t, x) = F (n (t, x)) t x,
    { exact Î» t x, ((hn (t, x) _ le_rfl).eq_of_nhds).symm },
    have hG11 : âˆ€ t x, (G (t, x)).1.1 = x,
    { intros t x,
      rw G_eq,
      refl },
    /- The limit function is a homotopy of formal solutions. -/
    let ğ“• : htpy_formal_sol R := {
      bs := Î» t x, (G (t, x)).1.2,
      Ï• := Î» t x, (G (t, x)).2,
      smooth' := begin
        intro x,
        apply ((F (n x)).smooth' x).congr_of_eventually_eq,
        apply (hn x _ le_rfl).mono,
        intros p hp,
        dsimp only,
        rw [show (p.1, p.2) = p, from prod.ext rfl rfl, â† hp],
        refl
      end,
      is_sol' := begin
        intros t x,
        change one_jet_bundle.mk x (G (t, x)).1.2 (G (t, x)).2 âˆˆ R,
        rw â† (hn (t, x) _ le_rfl).eq_of_nhds,
        exact (F (n (t, x))).is_sol' t x,
      end },
    refine âŸ¨ğ“•, _, _, _, _âŸ©,
    { clear_dependent Î´ hfA hFhol hFÏ€ hFultim,
      intro x,
      ext1,
      { refl },
      { change (G (0, x)).1.2 = _,
        rw [G_eq, hFâ‚€] },
      { change (G (0, x)).2 = _,
        rw [G_eq, hFâ‚€] } },
    { clear_dependent Î´ hFâ‚€ hfA hFÏ€ hFultim,
      intro x,
      have : x âˆˆ â‹ƒ i â‰¤ Ï€ (n (1, x)), (L.Ï† i) '' metric.closed_ball 0 1,
      { have : x âˆˆ _ := hn' (1, x) _ le_rfl,
        apply set.bUnion_mono subset_rfl _ this,
        rintros i -,
        exact image_subset _ metric.ball_subset_closed_ball, },
      apply (hFhol (n (1, x)) x this).congr, clear this,
      have : F (n (1, x)) 1 =á¶ [ğ“ x] (Î» x, G (1, x)),
      { exact (hn (1, x) (n(1, x)) le_rfl).slice },
      apply this.mono, clear this,
      rintros y (hy : F (n (1, x)) 1 y = G (1, y)),
      change F (n (1, x)) 1 y = ğ“• 1 y,
      rw hy,
      change G (1, y) = ğ“• 1 y,
      ext ; try { refl },
      rw hG11,
      refl },
    { clear_dependent Î´ hFâ‚€ hFhol hFÏ€ hFultim,
      intros x x_in t,
      rw [â† (hfA (n (t, x)) t).nhds_set_forall_mem x x_in, â† G_eq],
      ext ; try { refl },
      rw hG11, refl, },
    { clear_dependent hFâ‚€ hFhol hFÏ€ hFultim hfA,
      intros t x,
      apply le_of_lt,
      change dist (G (t, x)).1.2 (ğ“•â‚€.bs x) < Î´ x,
      rw â† (hn (t, x) _ le_rfl).eq_of_nhds,
      exact hFÎ´ (n (t, x)) t x } },
  -- The next six lines work around the fact that the statement of `satisfies_h_principle_core`
  -- is now slightly too strong. This should be aligned at some point.
  rcases hRample.satisfies_h_principle_core hRopen hA hÎ´_pos hÎ´_cont ğ“•â‚€ hğ“•â‚€ L with âŸ¨F, hâŸ©,
  refine âŸ¨F, Î» n, _âŸ©,
  rcases h n with âŸ¨âŸ¨hâ‚€, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ©, hâ‚…, hâ‚†âŸ©,
  refine âŸ¨âŸ¨_, _, _, _âŸ©, _, _âŸ©,
  all_goals { try { assumption} },
  exact hâ‚€.on_set 0 right_mem_Iic,
  exact hâ‚‚.forall,
  exact hâ‚„.on_set,
end

variables
{EP : Type*} [normed_add_comm_group EP] [normed_space â„ EP]  [finite_dimensional â„ EP]
{HP : Type*} [topological_space HP] {IP : model_with_corners â„ EP HP} [boundaryless IP]
{P : Type*} [topological_space P] [charted_space HP P] [smooth_manifold_with_corners IP P]
[sigma_compact_space P]
[t2_space P]
{C : set (P Ã— M)}

/-
We now deduce the parametric case from the unparametric one using
`rel_mfld.satisfies_h_principle.satisfies_h_principle_with` which reduces the parametric
`h`-principle to the non-parametric one for a different relation and `rel_mafld.ample.relativize`
which ensures the ampleness assumption survives this reduction.
-/

/-- **Gromov's Theorem** -/
theorem rel_mfld.ample.satisfies_h_principle_with (hRample : R.ample) (hRopen : is_open R)
  (hC : is_closed C)
  (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : continuous Î´) :
  R.satisfies_h_principle_with IP C Î´ :=
begin
  have hÎ´_pos' : âˆ€ (x : P Ã— M), 0 < Î´ x.2 := Î» (x : P Ã— M), hÎ´_pos x.snd,
  have hÎ´_cont' : continuous (Î» (x : P Ã— M), Î´ x.2) := hÎ´_cont.comp continuous_snd,
  have is_op : is_open (rel_mfld.relativize IP P R) := R.is_open_relativize hRopen,
  apply rel_mfld.satisfies_h_principle.satisfies_h_principle_with,
  exact (hRample.relativize IP P).satisfies_h_principle is_op hC hÎ´_pos' hÎ´_cont',
end

variables
{E' : Type*} [normed_add_comm_group E'] [normed_space â„ E'] [finite_dimensional â„ E']
{H' : Type*} [topological_space H'] {I' : model_with_corners â„ E' H'} [model_with_corners.boundaryless I']
{M' : Type*} [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']
[sigma_compact_space M'] [t2_space M']

include IP

/-
Since every (sigma-compact) manifold is metrizable, the metric space assumption can be removed.
-/

/-- Gromov's Theorem without metric space assumption -/
theorem rel_mfld.ample.satisfies_h_principle_with' {R : rel_mfld IM M I' M'}
  (hRample : R.ample) (hRopen : is_open R) (hC : is_closed C)
  (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : continuous Î´) :
  by letI := manifold_metric I' M' ; exact
  R.satisfies_h_principle_with IP C Î´ :=
by apply rel_mfld.ample.satisfies_h_principle_with; assumption
