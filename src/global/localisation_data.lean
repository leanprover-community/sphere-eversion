import geometry.manifold.algebra.smooth_functions
import geometry.manifold.instances.real
import topology.metric_space.partition_of_unity
import global.smooth_embedding

noncomputable theory

open_locale manifold
open set metric

section
variables {ğ•œ : Type*} [nontrivially_normed_field ğ•œ]
  {E : Type*} [normed_add_comm_group E] [normed_space ğ•œ E]
  {H : Type*} [topological_space H]
  (I : model_with_corners ğ•œ E H)
  {M : Type*} [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
  {E' : Type*} [normed_add_comm_group E'] [normed_space ğ•œ E']
  {H' : Type*} [topological_space H']
  (I' : model_with_corners ğ•œ E' H')
  {M' : Type*} [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']

/-- Definition `def:localisation_data`. -/
structure localisation_data (f : M â†’ M') :=
(Î¹ Î¹' : Type*)
(hÎ¹ : encodable Î¹)
(Ï† : Î¹ â†’ open_smooth_embedding (model_with_corners_self ğ•œ E) E I M)
(Ïˆ : Î¹' â†’ open_smooth_embedding (model_with_corners_self ğ•œ E') E' I' M')
(j : Î¹ â†’ Î¹')
(hâ‚ : (â‹ƒ i, (Ï† i) '' (ball (0:E) 1)) = univ)
(hâ‚‚ : (â‹ƒ i', (Ïˆ i') '' (ball (0:E') 1)) = univ)
(hâ‚ƒ : âˆ€ i, range (f âˆ˜ (Ï† i)) âŠ† (Ïˆ (j i)) '' (ball (0:E') 1))
(hâ‚„ : locally_finite $ Î» i', range (Ïˆ i'))

namespace localisation_data

variables {f : M â†’ M'} {I I'} (ld : localisation_data I I' f)

abbreviation Ïˆj := ld.Ïˆ âˆ˜ ld.j

end localisation_data

end

section
variables
  {E : Type*} [normed_add_comm_group E] [normed_space â„ E] [finite_dimensional â„ E]
  {M : Type*} [topological_space M] [sigma_compact_space M] [locally_compact_space M] [t2_space M]
  {H : Type*} [topological_space H] (I : model_with_corners â„ E H) [model_with_corners.boundaryless I]
  [nonempty M] [charted_space H M] [smooth_manifold_with_corners I M]
  (E' : Type*) [normed_add_comm_group E'] [normed_space â„ E'] [finite_dimensional â„ E']
  {H' : Type*} [topological_space H'] (I' : model_with_corners â„ E' H') [model_with_corners.boundaryless I']
  {M' : Type*} [metric_space M'] [sigma_compact_space M'] [locally_compact_space M']
  [nonempty M'] [charted_space H' M']
  [smooth_manifold_with_corners I' M']

variables (M')

lemma nice_atlas_target :
  âˆƒ n, âˆƒ Ïˆ : index_type n â†’ open_smooth_embedding ğ“˜(â„, E') E' I' M',
  locally_finite (Î» i', range (Ïˆ i')) âˆ§
  (â‹ƒ i', Ïˆ i' '' ball 0 1) = univ :=
let h := (nice_atlas E' I' (Î» j : punit, @is_open_univ M' _) (by simp [eq_univ_iff_forall])) in
âŸ¨h.some, h.some_spec.some, h.some_spec.some_spec.2âŸ©

/-- A collection of charts on a manifold `M'` which are smooth open embeddings with domain the whole
model space, and which cover the manifold when restricted in each case to the unit ball. -/
def target_charts (i' : index_type (nice_atlas_target E' I' M').some) :
  open_smooth_embedding ğ“˜(â„, E') E' I'  M' :=
(nice_atlas_target E' I' M').some_spec.some i'

lemma target_charts_cover : (â‹ƒ i', (target_charts E' I' M' i') '' (ball (0:E') 1)) = univ :=
(nice_atlas_target E' I' M').some_spec.some_spec.2

variables (E) {M'} {f : M â†’ M'} (hf : continuous f)

lemma nice_atlas_domain :
  âˆƒ n, âˆƒ Ï† : index_type n â†’ open_smooth_embedding ğ“˜(â„, E) E I M,
  (âˆ€ i, âˆƒ i', (range (Ï† i)) âŠ† f â»Â¹' (â‡‘(target_charts E' I' M' i') '' (ball (0:E') 1))) âˆ§
  locally_finite (Î» i, range (Ï† i)) âˆ§
  (â‹ƒ i, Ï† i '' ball 0 1) = univ :=
nice_atlas E I
  (Î» i', ((target_charts E' I' M' i').open_map (ball 0 1) is_open_ball).preimage hf)
  (by rw [â† preimage_Union, target_charts_cover, preimage_univ])

/-- Lemma `lem:ex_localisation`
  Any continuous map between manifolds has some localisation data. -/
def std_localisation_data : localisation_data I I' f :=
{ Î¹ := index_type (nice_atlas_domain E I E' I' hf).some,
  Î¹' := index_type (nice_atlas_target E' I' M').some,
  hÎ¹ := index_type_encodable _,
  Ï† := (nice_atlas_domain E I E' I' hf).some_spec.some,
  Ïˆ := target_charts E' I' M',
  j := Î» i, ((nice_atlas_domain E I E' I' hf).some_spec.some_spec.1 i).some,
  hâ‚ := (nice_atlas_domain E I E' I' hf).some_spec.some_spec.2.2,
  hâ‚‚ := target_charts_cover E' I' M',
  hâ‚ƒ := Î» i, begin
    rw range_comp,
    rintros - âŸ¨y, hy, rflâŸ©,
    exact ((nice_atlas_domain E I E' I' hf).some_spec.some_spec.1 i).some_spec hy,
  end,
  hâ‚„ := (nice_atlas_target E' I' M').some_spec.some_spec.1 }

/-- Lemma `lem:localisation_stability`. -/
lemma localisation_stability {f : M â†’ M'} (hf : continuous f)
  (ld : localisation_data I I' f) :
  âˆƒ (Îµ : M â†’ â„) (hÎµ : âˆ€ m, 0 < Îµ m) (hÎµ' : continuous Îµ),
    âˆ€ (g : M â†’ M') (hg : âˆ€ m, dist (g m) (f m) < Îµ m) i, range (g âˆ˜ ld.Ï† i) âŠ† range (ld.Ïˆj i) :=
begin
  let K : ld.Î¹' â†’ set M' := Î» i, ld.Ïˆ i '' closed_ball 0 1,
  let U : ld.Î¹' â†’ set M' := Î» i, range $ ld.Ïˆ i,
  have hK : âˆ€ i, is_closed (K i) := Î» i, is_compact.is_closed
    (is_compact.image (is_compact_closed_ball 0 1) (ld.Ïˆ i).continuous),
  have hK' : locally_finite K := ld.hâ‚„.subset (Î» i, image_subset_range (ld.Ïˆ i) (closed_ball 0 1)),
  have hU : âˆ€ i, is_open (U i) := Î» i, (ld.Ïˆ i).is_open_range,
  have hKU : âˆ€ i, K i âŠ† U i := Î» i, image_subset_range _ _,
  obtain âŸ¨Î´, hÎ´â‚€, hÎ´â‚âŸ© := exists_continuous_real_forall_closed_ball_subset hK hU hKU hK',
  refine âŸ¨Î´ âˆ˜ f, Î» m, hÎ´â‚€ (f m), by continuity, Î» g hg i, _âŸ©,
  rintros - âŸ¨e, rflâŸ©,
  have hi : f (ld.Ï† i e) âˆˆ K (ld.j i) :=
    image_subset _ ball_subset_closed_ball (ld.hâ‚ƒ i (mem_range_self e)),
  exact hÎ´â‚ (ld.j i) (f $ ld.Ï† i e) hi (le_of_lt (hg _)),
end

end
