/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import to_mathlib.analysis.cont_diff
import analysis.special_functions.trigonometric.deriv

/-! # Rotation about an axis, considered as a function in that axis -/

noncomputable theory

variables (E : Type*) [inner_product_space â„ E] [finite_dimensional â„ E]

/-- The identification of a finite-dimensional inner product space with its algebraic dual. -/
def to_dual : E â‰ƒâ‚—[â„] (E â†’â‚—[â„] â„) :=
(inner_product_space.to_dual â„ E).to_linear_equiv â‰ªâ‰«â‚— linear_map.to_continuous_linear_map.symm

variables {E} (Î© : alternating_map â„ E â„ (fin 3))
include E Î©

/-- Linear map from `E` to `E â†’â‚—[â„] E` constructed from a 3-form `Î©` on `E` and an identification of
`E` with its dual.  Effectively, the Hodge star operation.  (Under appropriate hypotheses it turns
out that the image of this map is in `ğ”°ğ”¬(E)`, the skew-symmetric operators, which can be identified
with `Î›Â²E`.) -/
def A : E â†’â‚—[â„] (E â†’â‚—[â„] E) :=
begin
  let z : alternating_map â„ E â„ (fin 0) â‰ƒâ‚—[â„] â„ :=
    alternating_map.const_linear_equiv_of_is_empty.symm,
  let y : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E â†’â‚—[â„] â„ :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 0)) â„ z) âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  let y' : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E :=
    (linear_map.llcomp â„ (alternating_map â„ E â„ (fin 1)) (E â†’â‚—[â„] â„) E (to_dual E).symm) y,
  let x : alternating_map â„ E â„ (fin 2) â†’â‚—[â„] E â†’â‚—[â„] E :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 1)) _ y') âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  exact x âˆ˜â‚— Î©.curry_left_linear_map,
end

lemma A_apply_self (v : E) : A Î© v v = 0 := by simp [A]

attribute [irreducible] A

/-- The map `A`, upgraded from linear to continuous-linear; useful for calculus. -/
def A' : E â†’L[â„] (E â†’L[â„] E) :=
(â†‘(linear_map.to_continuous_linear_map : (E â†’â‚—[â„] E) â‰ƒâ‚—[â„] (E â†’L[â„] E))
  âˆ˜â‚— (A Î©)).to_continuous_linear_map

@[simp] lemma A'_apply (v : E) : A' Î© v = (A Î© v).to_continuous_linear_map := rfl

/-- A family of endomorphisms of `E`, parametrized by `E Ã— â„`. The idea is that for nonzero `v : E`
and `t : â„` this endomorphism should be the rotation by the angle `t` about the axis spanned by `v`,
although this definition does not itself impose enough conditions to ensure that meaning. -/
def rot (p : E Ã— â„) : E â†’L[â„] E :=
(â„ âˆ™ p.1).subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.1) : E â†’L[â„] (â„ âˆ™ p.1))
  + real.cos p.2 â€¢ (â„ âˆ™ p.1)á—®.subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.1)á—® : E â†’L[â„] (â„ âˆ™ p.1)á—®)
  + real.sin p.2 â€¢ (A Î© p.1).to_continuous_linear_map

/-- Alternative form of the construction `rot`, convenient for the smoothness calculation. -/
def rot_aux (p : E Ã— â„) : E â†’L[â„] E :=
real.cos p.2 â€¢ continuous_linear_map.id â„ E +
  ((1 - real.cos p.2) â€¢ (â„ âˆ™ p.1).subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.1) : E â†’L[â„] (â„ âˆ™ p.1))
    + real.sin p.2 â€¢ (A' Î© p.1))

lemma rot_eq_aux : rot Î© = rot_aux Î© :=
begin
  ext1 p,
  dsimp [rot, rot_aux],
  rw id_eq_sum_orthogonal_projection_self_orthogonal_complement (â„ âˆ™ p.1),
  simp only [smul_add, sub_smul, one_smul],
  abel,
end

/-- The map `rot` is smooth on `(E \ {0}) Ã— â„`. -/
lemma cont_diff_rot {p : E Ã— â„} (hp : p.1 â‰  0) : cont_diff_at â„ âŠ¤ (rot Î©) p :=
begin
  simp only [rot_eq_aux],
  refine (cont_diff_at_snd.cos.smul cont_diff_at_const).add _,
  refine ((cont_diff_at_const.sub cont_diff_at_snd.cos).smul _).add
    (cont_diff_at_snd.sin.smul _),
  { exact (cont_diff_at_orthogonal_projection_singleton hp).comp _ cont_diff_at_fst },
  { exact (A' Î©).cont_diff.cont_diff_at.comp _ cont_diff_at_fst },
end

/-- The map `rot` sends `E Ã— {0}` to the identity. -/
lemma rot_zero (v : E) : rot Î© (v, 0) = continuous_linear_map.id â„ E :=
begin
  ext w,
  simpa [rot] using (eq_sum_orthogonal_projection_self_orthogonal_complement (â„ âˆ™ v) w).symm,
end

/-- The map `rot` sends `(v, Ï€)` to a transformation which on `(â„ âˆ™ v)á—®` acts as the negation. -/
lemma rot_pi (v : E) {w : E} (hw : w âˆˆ (â„ âˆ™ v)á—®) : rot Î© (v, real.pi) w = - w :=
by simp [rot, orthogonal_projection_eq_self_iff.mpr hw,
  orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hw]

/-- The map `rot` sends `(v, t)` to a transformation preserving `v`. -/
lemma rot_self (p : E Ã— â„) : rot Î© p p.1 = p.1 :=
begin
  have H : â†‘(orthogonal_projection (â„ âˆ™ p.1) p.1) = p.1 :=
    orthogonal_projection_eq_self_iff.mpr (submodule.mem_span_singleton_self p.1),
  simp [rot, A_apply_self, orthogonal_projection_orthogonal_complement_singleton_eq_zero, H],
end
