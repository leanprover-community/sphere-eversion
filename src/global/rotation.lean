/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth
-/
import to_mathlib.analysis.cont_diff
import to_mathlib.analysis.inner_product_space
import to_mathlib.linear_algebra.finrank
import analysis.inner_product_space.orientation
import analysis.special_functions.trigonometric.deriv

/-! # Rotation about an axis, considered as a function in that axis -/

noncomputable theory

open_locale real_inner_product_space
open finite_dimensional

variables (E : Type*) [inner_product_space â„ E]

section
variables [finite_dimensional â„ E]

/-- The identification of a finite-dimensional inner product space with its algebraic dual. -/
def to_dual : E â‰ƒâ‚—[â„] (E â†’â‚—[â„] â„) :=
(inner_product_space.to_dual â„ E).to_linear_equiv â‰ªâ‰«â‚— linear_map.to_continuous_linear_map.symm

variables {E} (Î© : alternating_map â„ E â„ (fin 3))
include E Î©

/-- Linear map from `E` to `E â†’â‚—[â„] E` constructed from a 3-form `Î©` on `E` and an identification of
`E` with its dual.  Effectively, the Hodge star operation.  (Under appropriate hypotheses it turns
out that the image of this map is in `ğ”°ğ”¬(E)`, the skew-symmetric operators, which can be identified
with `Î›Â²E`.) -/
def A : E â†’â‚—[â„] (E â†’â‚—[â„] E) :=
begin
  let z : alternating_map â„ E â„ (fin 0) â‰ƒâ‚—[â„] â„ :=
    alternating_map.const_linear_equiv_of_is_empty.symm,
  let y : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E â†’â‚—[â„] â„ :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 0)) â„ z) âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  let y' : alternating_map â„ E â„ (fin 1) â†’â‚—[â„] E :=
    (linear_map.llcomp â„ (alternating_map â„ E â„ (fin 1)) (E â†’â‚—[â„] â„) E (to_dual E).symm) y,
  let x : alternating_map â„ E â„ (fin 2) â†’â‚—[â„] E â†’â‚—[â„] E :=
    (linear_map.llcomp â„ E (alternating_map â„ E â„ (fin 1)) _ y') âˆ˜â‚—
      alternating_map.curry_left_linear_map,
  exact x âˆ˜â‚— Î©.curry_left_linear_map,
end

lemma A_apply_self (v : E) : A Î© v v = 0 := by simp [A]

lemma inner_A_apply (u v w : E) : âŸªA Î© u v, wâŸ« = Î© ![u, v, w] :=
by simp only [A, to_dual, linear_equiv.trans_symm, linear_equiv.symm_symm,
  linear_isometry_equiv.to_linear_equiv_symm, alternating_map.curry_left_linear_map_apply,
  linear_map.coe_comp, function.comp_app, linear_map.llcomp_apply,
  linear_equiv.coe_coe, linear_equiv.trans_apply, linear_isometry_equiv.coe_to_linear_equiv,
  linear_isometry_equiv.norm_map, submodule.coe_norm,
  inner_product_space.to_dual_symm_apply, alternating_map.curry_left_apply_apply,
  alternating_map.const_linear_equiv_of_is_empty_symm_apply,
  eq_self_iff_true, linear_map.coe_to_continuous_linear_map', matrix.zero_empty]

lemma inner_A_apply_self (x : E) (v : (â„ âˆ™ x)á—®) : âŸªA Î© x v, xâŸ« = 0 :=
begin
  rw inner_A_apply Î© x v x,
  refine Î©.map_eq_zero_of_eq ![x, v, x] _ (_ : (0 : fin 3) â‰  2),
  { simp },
  { norm_num }
end

lemma inner_A_apply_apply_self (x : E) (v : (â„ âˆ™ x)á—®) : âŸªA Î© x v, vâŸ« = 0 :=
begin
  rw inner_A_apply Î© x v v,
  refine Î©.map_eq_zero_of_eq ![x, v, v] _ (_ : (1 : fin 3) â‰  2),
  { simp },
  { norm_num }
end

attribute [irreducible] A

/-- The map `A`, upgraded from linear to continuous-linear; useful for calculus. -/
def A' : E â†’L[â„] (E â†’L[â„] E) :=
(â†‘(linear_map.to_continuous_linear_map : (E â†’â‚—[â„] E) â‰ƒâ‚—[â„] (E â†’L[â„] E))
  âˆ˜â‚— (A Î©)).to_continuous_linear_map

@[simp] lemma A'_apply (v : E) : A' Î© v = (A Î© v).to_continuous_linear_map := rfl

/-- A family of endomorphisms of `E`, parametrized by `â„ Ã— E`. The idea is that for nonzero `v : E`
and `t : â„` this endomorphism should be the rotation by the angle `t` about the axis spanned by `v`,
although this definition does not itself impose enough conditions to ensure that meaning. -/
def rot (p : â„ Ã— E) : E â†’L[â„] E :=
(â„ âˆ™ p.2).subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.2) : E â†’L[â„] (â„ âˆ™ p.2))
  + real.cos (p.1 * real.pi) â€¢ (â„ âˆ™ p.2)á—®.subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.2)á—® : E â†’L[â„] (â„ âˆ™ p.2)á—®)
  + real.sin (p.1 * real.pi) â€¢ (A Î© p.2).to_continuous_linear_map

/-- Alternative form of the construction `rot`, convenient for the smoothness calculation. -/
def rot_aux (p : â„ Ã— E) : E â†’L[â„] E :=
real.cos (p.1 * real.pi) â€¢ continuous_linear_map.id â„ E +
  ((1 - real.cos (p.1 * real.pi)) â€¢ (â„ âˆ™ p.2).subtypeL âˆ˜L (orthogonal_projection (â„ âˆ™ p.2) : E â†’L[â„] (â„ âˆ™ p.2))
    + real.sin (p.1 * real.pi) â€¢ (A' Î© p.2))

lemma rot_eq_aux : rot Î© = rot_aux Î© :=
begin
  ext1 p,
  dsimp [rot, rot_aux],
  rw id_eq_sum_orthogonal_projection_self_orthogonal_complement (â„ âˆ™ p.2),
  simp only [smul_add, sub_smul, one_smul],
  abel,
end

/-- The map `rot` is smooth on `â„ Ã— (E \ {0})`. -/
lemma cont_diff_rot {p : â„ Ã— E} (hp : p.2 â‰  0) : cont_diff_at â„ âŠ¤ (rot Î©) p :=
begin
  simp only [rot_eq_aux],
  refine (cont_diff_at_fst.mul_const.cos.smul cont_diff_at_const).add _,
  refine ((cont_diff_at_const.sub cont_diff_at_fst.mul_const.cos).smul _).add
    (cont_diff_at_fst.mul_const.sin.smul _),
  { exact (cont_diff_at_orthogonal_projection_singleton hp).comp _ cont_diff_at_snd },
  { exact (A' Î©).cont_diff.cont_diff_at.comp _ cont_diff_at_snd },
end

/-- The map `rot` sends `{0} Ã— E` to the identity. -/
lemma rot_zero (v : E) : rot Î© (0, v) = continuous_linear_map.id â„ E :=
begin
  ext w,
  simpa [rot] using (eq_sum_orthogonal_projection_self_orthogonal_complement (â„ âˆ™ v) w).symm,
end

/-- The map `rot` sends `(1, v)` to a transformation which on `(â„ âˆ™ v)á—®` acts as the negation. -/
lemma rot_one (v : E) {w : E} (hw : w âˆˆ (â„ âˆ™ v)á—®) : rot Î© (1, v) w = - w :=
by simp [rot, orthogonal_projection_eq_self_iff.mpr hw,
  orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hw]

/-- The map `rot` sends `(v, t)` to a transformation fixing `v`. -/
lemma rot_self (p : â„ Ã— E) : rot Î© p p.2 = p.2 :=
begin
  have H : â†‘(orthogonal_projection (â„ âˆ™ p.2) p.2) = p.2 :=
    orthogonal_projection_eq_self_iff.mpr (submodule.mem_span_singleton_self p.2),
  simp [rot, A_apply_self, orthogonal_projection_orthogonal_complement_singleton_eq_zero, H],
end

/-- The map `rot` sends `(v, t)` to a transformation preserving the subspace `(â„ âˆ™ v)á—®`. -/
lemma inner_rot_apply_self (p : â„ Ã— E) (w : E) (hw : w âˆˆ (â„ âˆ™ p.2)á—®) : âŸªrot Î© p w, p.2âŸ« = 0 :=
begin
  have Hâ‚ : orthogonal_projection (â„ âˆ™ p.2) w = 0 :=
    orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero hw,
  have Hâ‚‚ : (orthogonal_projection (â„ âˆ™ p.2)á—® w : E) = w :=
    congr_arg (coe : (â„ âˆ™ p.2)á—® â†’ E) (orthogonal_projection_mem_subspace_eq_self âŸ¨w, hwâŸ©),
  have Hâ‚ƒ : âŸªw, p.2âŸ« = 0 :=
    by simpa only [real_inner_comm] using hw p.2 (submodule.mem_span_singleton_self _),
  have Hâ‚„ : âŸªA Î© p.2 w, p.2âŸ« = 0 := inner_A_apply_self Î© p.2 âŸ¨w, hwâŸ©,
  simp [rot, Hâ‚, Hâ‚‚, Hâ‚ƒ, Hâ‚„, inner_smul_left, inner_add_left],
end

end

namespace orientation
variables {E} [fact (finrank â„ E = 3)] (Ï‰ : orientation â„ E (fin 3))

local attribute [instance] fact_finite_dimensional_of_finrank_eq_succ

lemma norm_A (x : E) (v : (â„ âˆ™ x)á—®) : âˆ¥A Ï‰.volume_form x vâˆ¥ = âˆ¥xâˆ¥ * âˆ¥vâˆ¥ :=
begin
  classical,
  refine le_antisymm _ _,
  { cases eq_or_lt_of_le (norm_nonneg (A Ï‰.volume_form x v)) with h h,
    { rw â† h,
      positivity },
    refine le_of_mul_le_mul_right' _ h,
    rw â† real_inner_self_eq_norm_mul_norm,
    simpa only [inner_A_apply, fin.mk_zero, fin.prod_univ_succ, finset.card_singleton,
      finset.prod_const, fintype.univ_of_subsingleton, matrix.cons_val_fin_one, matrix.cons_val_succ,
      matrix.cons_val_zero, mul_assoc, nat.nat_zero_eq_zero, pow_one, submodule.coe_norm]
      using Ï‰.volume_form_apply_le ![x, â†‘v, A Ï‰.volume_form x v] },
  let K : submodule â„ E := submodule.span â„ ({x, v} : set E),
  haveI : nontrivial Ká—®,
  { apply @finite_dimensional.nontrivial_of_finrank_pos â„,
    have : finrank â„ K â‰¤ _ := finrank_span_insert_le {(v:E)} x,
    have : finrank â„ _ â‰¤ 1 := finrank_span_singleton_le (v:E),
    have : finrank â„ K + finrank â„ Ká—® = finrank â„ E := K.finrank_add_finrank_orthogonal,
    have : finrank â„ E = 3 := fact.out _,
    linarith },
  obtain âŸ¨w, hwâŸ© : âˆƒ w : Ká—®, w â‰  0 := exists_ne 0,
  have hw' : (w:E) â‰  0 := Î» h, hw (submodule.coe_eq_zero.mp h),
  have H : pairwise (Î» i j, âŸª![x, v, â†‘w] i, ![x, v, â†‘w] jâŸ« = 0),
  { intros i j hij,
    have h1 : âŸªx, vâŸ« = 0 := v.2 _ (submodule.mem_span_singleton_self _),
    have h2 : âŸª(v:E), wâŸ« = 0 := w.2 _ (submodule.subset_span (by simp)),
    have h3 : âŸªx, wâŸ« = 0 := w.2 _ (submodule.subset_span (by simp)),
    fin_cases i; fin_cases j; norm_num at hij; simp [h1, h2, h3]; rw real_inner_comm; assumption },
  refine le_of_mul_le_mul_right' _ (by rwa norm_pos_iff : 0 < âˆ¥wâˆ¥),
  -- Cauchy-Schwarz inequality for `A Ï‰.volume_form x v` and `w`
  simpa only [inner_A_apply, Ï‰.abs_volume_form_apply_of_pairwise_orthogonal H, inner_A_apply,
    fin.mk_zero, fin.prod_univ_succ, finset.card_singleton, finset.prod_const,
    fintype.univ_of_subsingleton, matrix.cons_val_fin_one, matrix.cons_val_succ, matrix.cons_val_zero,
    nat.nat_zero_eq_zero, pow_one, mul_assoc]
    using abs_real_inner_le_norm (A Ï‰.volume_form x v) w,
end

lemma isometry_on_A (x : metric.sphere (0:E) 1) (v : (â„ âˆ™ (x:E))á—®) : âˆ¥A Ï‰.volume_form x vâˆ¥ = âˆ¥vâˆ¥ :=
by simp [norm_A]

lemma isometry_on_rot (t : â„) (x : metric.sphere (0:E) 1) (v : (â„ âˆ™ (x:E))á—®) :
  âˆ¥rot Ï‰.volume_form (t, x) vâˆ¥ = âˆ¥(v:E)âˆ¥ :=
begin
  have h1 : âŸªA Ï‰.volume_form x v, A Ï‰.volume_form x vâŸ« = âŸªv, vâŸ«,
  { simp only [inner_self_eq_norm_sq_to_K, Ï‰.isometry_on_A x v] },
  have h2 : âŸªA Ï‰.volume_form x v, vâŸ« = 0 := inner_A_apply_apply_self Ï‰.volume_form x v,
  have h3 : âŸª(v:E), A Ï‰.volume_form x vâŸ« = 0 := by rwa real_inner_comm,
  have : âˆ¥real.cos (t * real.pi) â€¢ (v:E) + real.sin (t * real.pi) â€¢ A Ï‰.volume_form x vâˆ¥ = âˆ¥(v:E)âˆ¥,
  { simp only [norm_eq_sqrt_inner],
    congr' 2,
    simp only [inner_add_left, inner_add_right, inner_smul_left, inner_smul_right, h1, h2, h3,
      is_R_or_C.conj_to_real, submodule.coe_inner],
    linear_combination âŸª(v:E), vâŸ« * real.cos_sq_add_sin_sq (t * real.pi) },
  simp [rot, orthogonal_projection_mem_subspace_orthogonal_complement_eq_zero v.prop, this],
end

lemma isometry_rot (t : â„) (x : metric.sphere (0:E) 1) :
  isometry (rot Ï‰.volume_form (t, x)) :=
begin
  rw add_monoid_hom_class.isometry_iff_norm,
  intros v,
  obtain âŸ¨a, ha, w, hw, rflâŸ© := (â„ âˆ™ (x:E)).exists_sum_mem_mem_orthogonal v,
  rw submodule.mem_span_singleton at ha,
  obtain âŸ¨s, rflâŸ© := ha,
  rw [â† sq_eq_sq (norm_nonneg _) (norm_nonneg _), sq, sq, map_add,
    norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero,
    norm_add_sq_eq_norm_sq_add_norm_sq_of_inner_eq_zero],
  { have hxw : âˆ¥rot Ï‰.volume_form (t, x) wâˆ¥ = âˆ¥wâˆ¥ := by simpa only using Ï‰.isometry_on_rot t x âŸ¨w, hwâŸ©,
    simp [hxw, rot_self] },
  { simp [inner_smul_left, hw x (submodule.mem_span_singleton_self _)] },
  rw real_inner_comm,
  simp [rot_self, inner_smul_right, inner_rot_apply_self Ï‰.volume_form (t, x) w hw],
end

end orientation
