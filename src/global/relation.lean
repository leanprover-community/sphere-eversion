import local.relation
import global.one_jet_sec
import global.smooth_embedding
import to_mathlib.topology.algebra.module
import interactive_expr
set_option trace.filter_inst_type true

/-!
# First order partial differential relations for maps between manifolds

This file contains fundamental definitions about first order partial differential relations
for maps between manifolds and relating them to the local story of first order partial differential
relations for maps between vector spaces.

Given manifolds `M` and `M'` modelled on `I` and `I'`, a first order partial differential relation
for maps from `M` to `M'` is a set in the 1-jet bundle JÂ¹(M, M'), also known as
`one_jet_bundle I M I' M'`.
-/

noncomputable theory

open set function filter (hiding map_smul) charted_space smooth_manifold_with_corners
open_locale topological_space manifold

section defs
/-! ## Fundamental definitions -/

variables
{E : Type*} [normed_add_comm_group E] [normed_space â„ E]
{H : Type*} [topological_space H] (I : model_with_corners â„ E H)
(M : Type*) [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
{E' : Type*} [normed_add_comm_group E'] [normed_space â„ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners â„ E' H')
(M' : Type*) [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']
{F : Type*} [normed_add_comm_group F] [normed_space â„ F]
{G : Type*} [topological_space G] (J : model_with_corners â„ F G)
(N : Type*) [topological_space N] [charted_space G N] [smooth_manifold_with_corners J N]
{F' : Type*} [normed_add_comm_group F'] [normed_space â„ F']
{G' : Type*} [topological_space G'] (J' : model_with_corners â„ F' G')
(N' : Type*) [topological_space N'] [charted_space G' N'] [smooth_manifold_with_corners J' N']
{EP : Type*} [normed_add_comm_group EP] [normed_space â„ EP]
{HP : Type*} [topological_space HP] (IP : model_with_corners â„ EP HP)
(P : Type*) [topological_space P] [charted_space HP P] [smooth_manifold_with_corners IP P]
{EX : Type*} [normed_add_comm_group EX] [normed_space â„ EX]
{HX : Type*} [topological_space HX] {IX : model_with_corners â„ EX HX}
-- note: X is a metric space
{X : Type*} [metric_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]

local notation `TM` := tangent_space I
local notation `TM'` := tangent_space I'

/-- A first-order differential relation for maps from `M` to `N` is a subset of the 1-jet bundle. -/
@[reducible] def rel_mfld := set (one_jet_bundle I M I' M')

variables {I M I' M'} {R : rel_mfld I M I' M'}

/-- A solution to a relation `R`. -/
@[ext] structure sol (R : rel_mfld I M I' M') :=
(f : M â†’ M')
(f_diff : cont_mdiff I I' âŠ¤ f)
(is_sol : âˆ€ x, one_jet_ext I I' f x âˆˆ R)

/-- A formal solution to a local relation `R` over a set `U`. -/
@[ext] structure formal_sol (R : rel_mfld I M I' M') extends
  to_one_jet_sec : one_jet_sec I M I' M' :=
(is_sol' : âˆ€ x : M, to_one_jet_sec x âˆˆ R)

instance (R : rel_mfld I M I' M') :
  has_coe_to_fun (formal_sol R) (Î» S, M â†’ one_jet_bundle I M I' M') :=
âŸ¨Î» F, F.to_one_jet_secâŸ©

namespace formal_sol

@[simp]
lemma coe_mk {S : one_jet_sec I M I' M'} {h : âˆ€ x, S x âˆˆ R} {x : M} : formal_sol.mk S h x = S x :=
rfl

@[simp]
lemma to_one_jet_sec_coe (S : formal_sol R) {x : M} : S.to_one_jet_sec x = S x :=
rfl

lemma is_sol (F : formal_sol R) : âˆ€ x, F x âˆˆ R :=
F.is_sol'

lemma coe_apply (F : formal_sol R) (x : M) : F x = âŸ¨(x, F.bs x), (F.Ï• x)âŸ© := rfl
lemma fst_eq (F : formal_sol R) (x : M) : (F x).1 = (x, F.bs x) := rfl
lemma snd_eq (F : formal_sol R) (x : M) : (F x).2 = F.Ï• x := rfl
lemma is_sec (F : formal_sol R) (x : M) : (F x).1.1 = x := rfl
lemma bs_eq (F : formal_sol R) (x : M) : F.bs x = (F x).1.2 := rfl

end formal_sol

/-- part of the construction of the slice `R(Ïƒ,p)`. -/
def rel_mfld.slice (R : rel_mfld I M I' M') (Ïƒ : one_jet_bundle I M I' M')
  (p : dual_pair' $ TM Ïƒ.1.1) : set (TM' Ïƒ.1.2) :=
{w : TM' Ïƒ.1.2 | one_jet_bundle.mk Ïƒ.1.1 Ïƒ.1.2 (p.update Ïƒ.2 w) âˆˆ R}

/-- For some reason `rw [mem_set_of_eq]` fails after unfolding `slice`,
but rewriting with this lemma works. -/
lemma mem_slice {R : rel_mfld I M I' M'} {Ïƒ : one_jet_bundle I M I' M'}
  {p : dual_pair' $ TM Ïƒ.1.1} {w : TM' Ïƒ.1.2} :
  w âˆˆ R.slice Ïƒ p â†” one_jet_bundle.mk Ïƒ.1.1 Ïƒ.1.2 (p.update Ïƒ.2 w) âˆˆ R :=
iff.rfl

@[simp] lemma jet_apply_v_mem_slice
  {R : rel_mfld I M I' M'} {Ïƒ : one_jet_bundle I M I' M'} (p : dual_pair' $ TM Ïƒ.1.1) :
  Ïƒ.2 p.v âˆˆ R.slice Ïƒ p â†” Ïƒ âˆˆ R :=
by { rcases Ïƒ with âŸ¨âŸ¨m, m'âŸ©, Ï†âŸ©, simp [mem_slice], }

lemma slice_mk_update {R : rel_mfld I M I' M'} {Ïƒ : one_jet_bundle I M I' M'}
  {p : dual_pair' $ TM Ïƒ.1.1} (x : E') :
  R.slice (one_jet_bundle.mk Ïƒ.1.1 Ïƒ.1.2 (p.update Ïƒ.2 x)) p = (R.slice Ïƒ p : set E') :=
begin
  ext1 w,
  dsimp only [mem_slice],
  congr' 3,
  simp_rw [one_jet_bundle_mk_snd, p.update_update],
end

def rel_mfld.ample (R : rel_mfld I M I' M') : Prop :=
âˆ€ â¦ƒÏƒ : one_jet_bundle I M I' M'â¦„ (p : dual_pair' $ TM Ïƒ.1.1), ample_set (R.slice Ïƒ p)

lemma rel_mfld.ample_iff (R : rel_mfld I M I' M') : R.ample â†”
  âˆ€ â¦ƒÏƒ : one_jet_bundle I M I' M'â¦„ (p : dual_pair' $ TM Ïƒ.1.1), Ïƒ âˆˆ R â†’ ample_set (R.slice Ïƒ p) :=
begin
  simp_rw [rel_mfld.ample],
  refine âŸ¨Î» h Ïƒ p _, h p, Î» h Ïƒ p x hx, _âŸ©,
  have := @h (one_jet_bundle.mk Ïƒ.1.1 Ïƒ.1.2 (p.update Ïƒ.2 x)) p hx,
  rw [slice_mk_update] at this,
  exact this x hx
end

/-- A family of formal solutions indexed by manifold `N` is a function from `N` into formal
  solutions in such a way that the function is smooth as a function of all arguments. -/
@[ext] structure family_formal_sol (R : rel_mfld I M I' M') extends
  to_family_one_jet_sec : family_one_jet_sec I M I' M' J N :=
(is_sol' : âˆ€ (t : N) (x : M), to_family_one_jet_sec t x âˆˆ R)

instance : has_coe_to_fun (family_formal_sol J N R) (Î» S, N â†’ formal_sol R) :=
âŸ¨Î» S t, âŸ¨S.to_family_one_jet_sec t, S.is_sol' tâŸ©âŸ©

namespace family_formal_sol

variables {J N J' N'}

@[simp]
lemma coe_mk {S : family_one_jet_sec I M I' M' J N} {h : âˆ€ t x, S t x âˆˆ R} {t : N} {x : M} :
  family_formal_sol.mk S h t x = S t x :=
rfl

lemma coe_mk_to_one_jet_sec {S : family_one_jet_sec I M I' M' J N} {h : âˆ€ t x, S t x âˆˆ R} {t : N} :
  (family_formal_sol.mk S h t).to_one_jet_sec = S t :=
rfl

@[simp]
lemma to_family_one_jet_sec_coe (S : family_formal_sol J N R) {t : N} {x : M} :
  S.to_family_one_jet_sec t x = S t x :=
rfl

@[simp]
lemma to_family_one_jet_sec_eq (S : family_formal_sol J N R) {t : N} :
  S.to_family_one_jet_sec t = (S t).to_one_jet_sec :=
rfl

lemma is_sol (S : family_formal_sol J N R) {t : N} {x : M} : S t x âˆˆ R :=
S.is_sol' t x

/-- Reindex a family along a smooth function `f`. -/
def reindex (S : family_formal_sol J' N' R) (f : C^âˆâŸ®J, N; J', N'âŸ¯) :
  family_formal_sol J N R :=
âŸ¨S.to_family_one_jet_sec.reindex f, Î» t, S.is_sol' (f t)âŸ©

end family_formal_sol

/-- A homotopy of formal solutions is a family indexed by `â„` -/
@[reducible] def htpy_formal_sol (R : rel_mfld I M I' M') := family_formal_sol ğ“˜(â„, â„) â„ R

def formal_sol.const_htpy {R : rel_mfld I M I' M'} (F : formal_sol R) : htpy_formal_sol R :=
{ bs := Î» t, F.bs,
  Ï• := Î» t, F.Ï•,
  smooth' := sorry,
  is_sol' := Î» t, F.is_sol' }

/-- The relation `ğ“¡ ^ P` -/
def rel_mfld.relativize (R : rel_mfld I M I' M') : rel_mfld (IP.prod I) (P Ã— M) I' M' :=
bundle_snd â»Â¹' R

lemma mem_relativize (R : rel_mfld I M I' M') (w : one_jet_bundle (IP.prod I) (P Ã— M) I' M') :
 w âˆˆ R.relativize IP P â†”
  (one_jet_bundle.mk w.1.1.2 w.1.2 (w.2.comp (continuous_linear_map.inr â„ EP E)) :
    one_jet_bundle I M I' M') âˆˆ R :=
iff.rfl

lemma rel_mfld.is_open_relativize (R : rel_mfld I M I' M') (h2 : is_open R) :
  is_open (R.relativize IP P) :=
h2.preimage smooth_bundle_snd.continuous

variables {P}

/-- A relation `R` satisfies the (non-parametric) relative Câ°-dense h-principle w.r.t. a subset
`C` of the domain if for every formal solution `ğ“•â‚€` that is holonomic near `C`
there is a homotopy between `ğ“•â‚€` and a holonomic solution that is constant near `C` and
`Îµ`-close to `ğ“•â‚€`. This is a temporary version with a slightly weaker conclusion. -/
def rel_mfld.satisfies_h_principle_weak (R : rel_mfld I M IX X) (C : set M) (Îµ : M â†’ â„) : Prop :=
âˆ€ ğ“•â‚€ : formal_sol R, (âˆ€á¶  x in ğ“Ë¢ C, ğ“•â‚€.to_one_jet_sec.is_holonomic_at x) â†’
âˆƒ ğ“• : htpy_formal_sol R, (âˆ€ x : M, ğ“• 0 x = ğ“•â‚€ x) âˆ§
  (ğ“• 1).to_one_jet_sec.is_holonomic âˆ§
  (âˆ€ x âˆˆ C, âˆ€ t : â„, ğ“• t x = ğ“•â‚€ x) âˆ§
  (âˆ€ (t : â„) (x : M), dist ((ğ“• t).bs x) (ğ“•â‚€.bs x) â‰¤ Îµ x)

/-- A relation `R` satisfies the (non-parametric) relative Câ°-dense h-principle w.r.t. a subset
`C` of the domain if for every formal solution `ğ“•â‚€` that is holonomic near `C`
there is a homotopy between `ğ“•â‚€` and a holonomic solution that is constant near `C` and
`Îµ`-close to `ğ“•â‚€`. -/
def rel_mfld.satisfies_h_principle (R : rel_mfld I M IX X) (C : set M) (Îµ : M â†’ â„) : Prop :=
âˆ€ ğ“•â‚€ : formal_sol R, (âˆ€á¶  x in ğ“Ë¢ C, ğ“•â‚€.to_one_jet_sec.is_holonomic_at x) â†’
âˆƒ ğ“• : htpy_formal_sol R, (âˆ€ x : M, ğ“• 0 x = ğ“•â‚€ x) âˆ§
  (ğ“• 1).to_one_jet_sec.is_holonomic âˆ§
  (âˆ€á¶  x in ğ“Ë¢ C, âˆ€ t : â„, ğ“• t x = ğ“•â‚€ x) âˆ§
  (âˆ€ (t : â„) (x : M), dist ((ğ“• t).bs x) (ğ“•â‚€.bs x) â‰¤ Îµ x)

lemma rel_mfld.satisfies_h_principle_of_weak {R : rel_mfld I M IX X} {Îµ : M â†’ â„}
  {C : set M} (hC : is_closed C)
  (h : âˆ€ A : set M, is_closed A â†’ R.satisfies_h_principle_weak A Îµ)  : R.satisfies_h_principle C Îµ :=
sorry

/-- A relation `R` satisfies the parametric relative Câ°-dense h-principle w.r.t. manifold `P`,
`C âŠ† P Ã— M` and `Îµ : M â†’ â„` if for every family of
formal solutions `ğ“•â‚€` indexed by a manifold with boundary `P` that is holonomic near `C`,
there is a homotopy `ğ“•` between `ğ“•â‚€` and a holonomic solution,
in such a way that `ğ“•` is constant near `C` and `Îµ`-close to `ğ“•â‚€`.
Note: `Îµ`-closeness is measured using an arbitrary distance function obtained from the metrizability
of `JÂ¹(M, M')`. Potentially we prefer to have this w.r.t. an arbitrary compatible metric.
-/
def rel_mfld.satisfies_h_principle_with (R : rel_mfld I M IX X) (C : set (P Ã— M)) (Îµ : M â†’ â„) :
  Prop :=
âˆ€ ğ“•â‚€ : family_formal_sol IP P R, -- given a family of formal solutions with parameters in `P`
(âˆ€á¶  (p : P Ã— M) in ğ“Ë¢ C, (ğ“•â‚€ p.1).to_one_jet_sec.is_holonomic_at p.2) â†’ -- holonomic near `C`
âˆƒ ğ“• : family_formal_sol (ğ“˜(â„, â„).prod IP) (â„ Ã— P) R, -- then there is a homotopy of such families
  (âˆ€ (s : P) (x : M), ğ“• (0, s) x = ğ“•â‚€ s x) âˆ§ -- that agrees on `t = 0`
  (âˆ€ (s : P), (ğ“• (1, s)).to_one_jet_sec.is_holonomic) âˆ§ -- is holonomic everywhere for `t = 1`
  (âˆ€á¶  (p : P Ã— M) in ğ“Ë¢ C, âˆ€ t : â„, ğ“• (t, p.1) p.2 = ğ“•â‚€ p.1 p.2) âˆ§ -- and agrees near `C`
  (âˆ€ (t : â„) (s : P) (x : M), dist ((ğ“• (t, s)).bs x) ((ğ“•â‚€ s).bs x) â‰¤ Îµ x) -- and close to `ğ“•â‚€`.


variables {IP}

/-- If a relation satisfies the parametric relative Câ°-dense h-principle wrt some data
then we can forget the homotopy and get a family of solutions from every
family of formal solutions. -/
lemma rel_mfld.satisfies_h_principle_with.bs {R : rel_mfld I M IX X} {C : set (P Ã— M)}
  {Îµ : M â†’ â„} (h : R.satisfies_h_principle_with IP C Îµ) (ğ“•â‚€ : family_formal_sol IP P R)
  (h2 : âˆ€á¶  (p : P Ã— M) in ğ“Ë¢ C, (ğ“•â‚€ p.1).to_one_jet_sec.is_holonomic_at p.2) :
  âˆƒ f : P â†’ M â†’ X,
    (smooth (IP.prod I) IX $ uncurry f) âˆ§
    (âˆ€á¶  (p : P Ã— M) in ğ“Ë¢ C, f p.1 p.2 = ğ“•â‚€.bs p.1 p.2) âˆ§
    (âˆ€ p m, dist (f p m) ((ğ“•â‚€ p).bs m) â‰¤ Îµ m) âˆ§
    (âˆ€ p m, one_jet_ext I IX (f p) m âˆˆ R) :=
begin
  rcases h ğ“•â‚€ h2  with âŸ¨ğ“•, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ©,
  refine âŸ¨Î» s, (ğ“• (1, s)).bs, _, _, _, _âŸ©,
  { have := ğ“•.to_family_one_jet_sec.smooth,
    let j : C^âˆâŸ®IP, P ; ğ“˜(â„, â„).prod IP, â„ Ã— PâŸ¯ := âŸ¨Î» p, (1, p),
                                                    smooth.prod_mk smooth_const smooth_idâŸ©,
    rw show uncurry (Î» s, (ğ“• (1, s)).bs) = prod.snd âˆ˜ (one_jet_bundle.proj _ _ _ _) âˆ˜
                                            (Î» (p : P Ã— M), ğ“•.reindex j p.1 p.2),
    by { ext, refl },
    exact smooth_snd.comp ((basic_smooth_vector_bundle_core.smooth_proj _).comp
                           ((ğ“•.reindex j).to_family_one_jet_sec.smooth)) },
  { apply hâ‚ƒ.mono,
    intros x hx,
    simp_rw [one_jet_sec.bs_eq, formal_sol.to_one_jet_sec_coe, hx, family_one_jet_sec.bs_eq,
      ğ“•â‚€.to_family_one_jet_sec_coe] },
  { intros p m,
    apply hâ‚„ },
  { intros p m,
    suffices : one_jet_ext I IX (ğ“• (1, p)).bs m = ((ğ“•.to_family_one_jet_sec) (1, p)) m,
    { rw this,
      exact ğ“•.is_sol' (1, p) m },
    exact one_jet_sec.is_holonomic_at_iff.mp (hâ‚‚ p m) },
end

end defs

section smooth_open_embedding
/-! ## Localisation of one jet sections

In order to use the local story of convex integration, we need a way to turn a
one jet section into local ones, then apply the local story to build a homotopy of one jets section
and transfer back to the original manifolds. There is a dissymetry here. First we use
maps from whole vector spaces to open sets in manifold. And also one jet sections are carried
from manifold to vector spaces one at a time, but then the return journey is about a homotopy
of one jet sections.

The global manifolds are called `M` and `N'`. We don't assume the local ones are vector spaces,
there are manifolds `X` and `Y` that will be vector spaces in the next section.

Note: Patrick doesn't know whether we really need to allow different `E`, `H` and `I` for
manifolds `X` and `M` (and for `Y` and `N`). We use maximal generality just in case.
-/
variables
  {EX : Type*} [normed_add_comm_group EX] [normed_space â„ EX]
  {HX : Type*} [topological_space HX] {IX : model_with_corners â„ EX HX}
  {X : Type*} [topological_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]

  {EM : Type*} [normed_add_comm_group EM] [normed_space â„ EM]
  {HM : Type*} [topological_space HM] {IM : model_with_corners â„ EM HM}
  {M : Type*} [topological_space M] [charted_space HM M] [smooth_manifold_with_corners IM M]

  {EY : Type*} [normed_add_comm_group EY] [normed_space â„ EY]
  {HY : Type*} [topological_space HY] {IY : model_with_corners â„ EY HY}
  {Y : Type*} [topological_space Y] [charted_space HY Y] [smooth_manifold_with_corners IY Y]

  {EN : Type*} [normed_add_comm_group EN] [normed_space â„ EN]
  {HN : Type*} [topological_space HN] {IN : model_with_corners â„ EN HN}
  {N : Type*} [topological_space N] [charted_space HN N] [smooth_manifold_with_corners IN N]

  (F : one_jet_sec IM M IN N)
  (g : open_smooth_embedding IY Y IN N) (h : open_smooth_embedding IX X IM M)
  {R : rel_mfld IM M IN N}

local notation `TM` := tangent_space IM
local notation `TN` := tangent_space IN
local notation `TX` := tangent_space IX
local notation `TY` := tangent_space IY

/-- Transfer map between one jet bundles induced by open smooth embedding into the source and
targets. -/
@[simps fst_fst fst_snd]
def one_jet_bundle.transfer : one_jet_bundle IX X IY Y â†’ one_jet_bundle IM M IN N :=
Î» Ïƒ, âŸ¨âŸ¨h Ïƒ.1.1, g Ïƒ.1.2âŸ©,
      ((g.fderiv Ïƒ.1.2 : TY Ïƒ.1.2 â†’L[â„] TN (g Ïƒ.1.2)).comp Ïƒ.2).comp
        ((h.fderiv Ïƒ.1.1).symm : TM (h Ïƒ.1.1) â†’L[â„] TX Ïƒ.1.1)âŸ©

lemma one_jet_bundle.continuous_transfer : continuous (one_jet_bundle.transfer g h) :=
sorry

-- do we need this?
lemma one_jet_bundle.smooth_transfer : smooth ((IX.prod IY).prod ğ“˜(â„, EX â†’L[â„] EY))
  ((IM.prod IN).prod ğ“˜(â„, EM â†’L[â„] EN)) (one_jet_bundle.transfer g h) :=
sorry

/-- localize a relation -/
def rel_mfld.localize (R : rel_mfld IM M IN N) : rel_mfld IX X IY Y :=
one_jet_bundle.transfer g h â»Â¹' R

open basic_smooth_vector_bundle_core

/-- Localize a one-jet section in two open embeddings.
  It maps `x` to `(x, y, (D_y(g))â»Â¹ âˆ˜ F_Ï†(h x) âˆ˜ D_x(h))` where `y : M := gâ»Â¹(F_{bs}(h x))`. -/
@[simps] def one_jet_sec.localize (hF : range (F.bs âˆ˜ h) âŠ† range g) :
  one_jet_sec IX X IY Y :=
{ bs := Î» x, g.inv_fun (F.bs $ h x),
  Ï• := Î» x, let y := g.inv_fun (F.bs $ h x) in
  (â†‘(g.fderiv y).symm : TN (g y) â†’L[â„] TY y) âˆ˜L ((F $ h x).2 âˆ˜L (h.fderiv x : TX x â†’L[â„] TM (h x))),
  smooth' := begin
    simp_rw [h.fderiv_coe, g.fderiv_symm_coe,
      mfderiv_congr_point (g.right_inv (hF $ mem_range_self _))],
    refine smooth.one_jet_comp IN (Î» x', F.bs (h x')) _ _,
    { exact Î» x, (g.smooth_at_inv $ hF $ mem_range_self x).one_jet_ext.comp _
        (F.smooth_bs.comp h.smooth_to).cont_mdiff_at },
    apply smooth.one_jet_comp IM h (F.smooth_eta.comp h.smooth_to) h.smooth_to.one_jet_ext
  end }

lemma transfer_localize (hF : range (F.bs âˆ˜ h) âŠ† range g) (x : X) :
  (F.localize g h hF x).transfer g h = F (h x) :=
begin
  rw [one_jet_sec.coe_apply, one_jet_sec.localize_bs, one_jet_sec.localize_Ï•,
    one_jet_bundle.transfer],
  dsimp only,
  ext,
  { refl },
  { simp_rw [g.right_inv (hF $ mem_range_self x), function.comp_apply, F.bs_eq] },
  { apply heq_of_eq, dsimp only, ext v,
    simp_rw [continuous_linear_map.comp_apply, continuous_linear_equiv.coe_coe,
      continuous_linear_equiv.apply_symm_apply] },
end

lemma one_jet_sec.localize_bs_fun (hF : range (F.bs âˆ˜ h) âŠ† range g) :
  (F.localize g h hF).bs = g.inv_fun âˆ˜ F.bs âˆ˜ h :=
rfl

lemma one_jet_sec.localize_mem_iff (hF : range (F.bs âˆ˜ h) âŠ† range g) {x : X} :
  F.localize g h hF x âˆˆ R.localize g h â†” F (h x) âˆˆ R :=
by rw [rel_mfld.localize, mem_preimage, transfer_localize F g h hF]

lemma rel_mfld.ample.localize (hR : R.ample) : (R.localize g h).ample :=
begin
  intros x p,
  have : (rel_mfld.localize g h R).slice x p =
    (g.fderiv x.1.2).symm '' R.slice (x.transfer g h) (p.map (h.fderiv x.1.1)),
  { ext v,
    simp_rw [rel_mfld.localize, continuous_linear_equiv.image_symm_eq_preimage, mem_preimage,
      mem_slice, mem_preimage],
    dsimp only [one_jet_bundle.transfer, one_jet_bundle_mk_fst, one_jet_bundle_mk_snd],
    simp_rw [p.map_update_comp_right, â† p.update_comp_left, continuous_linear_equiv.coe_coe,
      one_jet_bundle.mk] },
  rw [this],
  exact (hR _).image (g.fderiv x.1.2).symm
end

lemma is_holonomic_at_localize_iff (hF : range (F.bs âˆ˜ h) âŠ† range g) (x : X) :
  (F.localize g h hF).is_holonomic_at x â†” F.is_holonomic_at (h x)  :=
begin
  have : mfderiv IX IY (g.inv_fun âˆ˜ F.bs âˆ˜ h) x =
    (g.fderiv (g.inv_fun (F.bs (h x)))).symm.to_continuous_linear_map.comp
    ((mfderiv IM IN F.bs (h x)).comp (h.fderiv x).to_continuous_linear_map),
  { have h1 : mdifferentiable_at IN IY g.inv_fun (F.bs (h x)) :=
      (g.smooth_at_inv $ hF $ mem_range_self _).mdifferentiable_at,
    have h2 : mdifferentiable_at IM IN F.bs (h x) := F.smooth_bs.mdifferentiable_at,
    have h3 : mdifferentiable_at IX IM h x := h.smooth_to.mdifferentiable_at,
    rw [mfderiv_comp x h1 (h2.comp x h3), mfderiv_comp x h2 h3,
      â† g.fderiv_symm_coe' (hF $ mem_range_self _)],
    refl, },
  simp_rw [one_jet_sec.is_holonomic_at],
  rw [mfderiv_congr (F.localize_bs_fun g h hF), one_jet_sec.snd_eq, F.localize_Ï•, this],
  simp_rw [â† continuous_linear_equiv.coe_def_rev,
    continuous_linear_equiv.cancel_left, continuous_linear_equiv.cancel_right]
end

/-- Un-localize a homotopy of one-jet sections from two open embeddings. -/
-- Note(F): this is only well-defined on `univ Ã— range h`, right?
def htpy_one_jet_sec.unlocalize (F : htpy_one_jet_sec IX X IY Y) : htpy_one_jet_sec IM M IN N :=
{ bs := Î» t m , g $ (F t).bs (h.inv_fun m),
  Ï• := Î» t m, (g.fderiv $ (F t).bs (h.inv_fun m)).to_continuous_linear_map âˆ˜L
      ((F t $ h.inv_fun m).2 âˆ˜L (h.fderiv $ h.inv_fun m).symm.to_continuous_linear_map),
  smooth' := sorry }

lemma one_jet_sec.unlocalize_localize (G : htpy_one_jet_sec IX X IY Y)
  (hF : range (F.bs âˆ˜ h) âŠ† range g)
  (hFG : G 0 = F.localize g h hF) : G.unlocalize g h 0 = F :=
sorry

/-- Localize a formal solution. -/
def transfer (hF : range (F.bs âˆ˜ h) âŠ† range g) (h2F : âˆ€ x, F (h x) âˆˆ R) :
  formal_sol (R.localize g h) :=
âŸ¨F.localize g h hF, Î» x, (F.localize_mem_iff g h hF).mpr $ h2F xâŸ©

end smooth_open_embedding

section parameter_space

variables
{E : Type*} [normed_add_comm_group E] [normed_space â„ E]
{H : Type*} [topological_space H] (I : model_with_corners â„ E H)
(M : Type*) [topological_space M] [charted_space H M] [smooth_manifold_with_corners I M]
{E' : Type*} [normed_add_comm_group E'] [normed_space â„ E']
{H' : Type*} [topological_space H'] (I' : model_with_corners â„ E' H')
(M' : Type*) [topological_space M'] [charted_space H' M'] [smooth_manifold_with_corners I' M']
{EP : Type*} [normed_add_comm_group EP] [normed_space â„ EP]
{HP : Type*} [topological_space HP] {IP : model_with_corners â„ EP HP}
{P : Type*} [topological_space P] [charted_space HP P] [smooth_manifold_with_corners IP P]
{F : Type*} [normed_add_comm_group F] [normed_space â„ F]
{G : Type*} [topological_space G] {J : model_with_corners â„ F G}
{N : Type*} [topological_space N] [charted_space G N] [smooth_manifold_with_corners J N]
{EX : Type*} [normed_add_comm_group EX] [normed_space â„ EX]
{HX : Type*} [topological_space HX] {IX : model_with_corners â„ EX HX}
-- note: X is a metric space
{X : Type*} [metric_space X] [charted_space HX X] [smooth_manifold_with_corners IX X]
variables {I M I' M'} {R : rel_mfld I M I' M'}

open_locale pointwise

lemma prod.zero_mk_add_zero_mk {M N : Type*} [add_monoid M] [has_add N] (bâ‚ bâ‚‚ : N) :
  ((0 : M), bâ‚) + (0, bâ‚‚) = (0, bâ‚ + bâ‚‚) :=
by rw [prod.mk_add_mk, add_zero]

lemma prod.smul_zero_mk {M Î± Î² : Type*} [monoid M] [add_monoid Î±] [distrib_mul_action M Î±]
  [has_smul M Î²] (a : M) (c : Î²) :
  a â€¢ ((0 : Î±), c) = (0, a â€¢ c) :=
by rw [prod.smul_mk, smul_zero]

lemma relativize_slice {Ïƒ : one_jet_bundle (IP.prod I) (P Ã— M) I' M'}
  {p : dual_pair' $ tangent_space (IP.prod I) Ïƒ.1.1}
  (q : dual_pair' $ tangent_space I Ïƒ.1.1.2)
  (hpq : p.Ï€.comp (continuous_linear_map.inr â„ EP E) = q.Ï€) :
  (rel_mfld.relativize IP P R).slice Ïƒ p =
  Ïƒ.2 (p.v - (0, q.v)) +áµ¥ R.slice (bundle_snd Ïƒ) q :=
begin
  have h2pq : âˆ€ x : E, p.Ï€ ((0 : EP), x) = q.Ï€ x := Î» x, congr_arg (Î» f : E â†’L[â„] â„, f x) hpq,
  ext1 w,
  have h1 : (p.update Ïƒ.2 w).comp (continuous_linear_map.inr â„ EP E) =
    q.update (bundle_snd Ïƒ).2 (-Ïƒ.2 (p.v - (0, q.v)) +áµ¥ w),
  { ext1 x,
    simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
      â† continuous_linear_map.map_neg, neg_sub],
    obtain âŸ¨u, hu, t, rflâŸ© := q.decomp x,
    have hv : (0, q.v) - p.v âˆˆ p.Ï€.ker,
    { rw [continuous_linear_map.mem_ker, map_sub, p.pairing, h2pq, q.pairing, sub_self] },
    have hup : ((0 : EP), u) âˆˆ p.Ï€.ker := (h2pq u).trans hu,
    rw [q.update_apply _ hu, â† prod.zero_mk_add_zero_mk, map_add, p.update_ker_pi _ _ hup,
      â† prod.smul_zero_mk, map_smul, vadd_eq_add],
    nth_rewrite 0 [â† sub_add_cancel (0, q.v) p.v],
    rw [map_add, p.update_ker_pi _ _ hv, p.update_v],
    refl },
  have := preimage_vadd_neg (show E', from Ïƒ.2 (p.v - (0, q.v)))
    (show set E', from (R.slice (bundle_snd Ïƒ) q)),
  dsimp only at this,
  simp_rw [â† this, mem_preimage, mem_slice, mem_relativize],
  dsimp only [one_jet_bundle_mk_fst, one_jet_bundle_mk_snd],
  congr'
end

lemma relativize_slice_eq_univ {Ïƒ : one_jet_bundle (IP.prod I) (P Ã— M) I' M'}
  {p : dual_pair' $ tangent_space (IP.prod I) Ïƒ.1.1}
  (hp : p.Ï€.comp (continuous_linear_map.inr â„ EP E) = 0) :
  ((rel_mfld.relativize IP P R).slice Ïƒ p).nonempty â†”
  (rel_mfld.relativize IP P R).slice Ïƒ p = univ :=
begin
  have h2p : âˆ€ x : E, p.Ï€ ((0 : EP), x) = 0 := Î» x, congr_arg (Î» f : E â†’L[â„] â„, f x) hp,
  have : âˆ€ y : E', (p.update Ïƒ.snd y).comp (continuous_linear_map.inr â„ EP E) =
    Ïƒ.snd.comp (continuous_linear_map.inr â„ EP E),
  { intro y,
    ext1 x,
    simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
      p.update_ker_pi _ _ (h2p x)] },
  simp_rw [set.nonempty, eq_univ_iff_forall, mem_slice, mem_relativize],
  dsimp only [one_jet_bundle_mk_fst, one_jet_bundle_mk_snd],
  simp_rw [this, exists_const, forall_const]
end

variables (IP P)

lemma rel_mfld.ample.relativize (hR : R.ample) : (R.relativize IP P).ample :=
begin
  intros Ïƒ p,
  let p2 := p.Ï€.comp (continuous_linear_map.inr â„ EP E),
  rcases eq_or_ne p2 0 with h|h,
  { intros w hw,
    rw [(relativize_slice_eq_univ h).mp âŸ¨w, hwâŸ©, connected_component_in_univ,
      preconnected_space.connected_component_eq_univ, convex_hull_univ] },
  obtain âŸ¨u', hu'âŸ© := continuous_linear_map.exists_ne_zero h,
  let u := (p2 u')â»Â¹ â€¢ u',
  let q : dual_pair' (tangent_space I Ïƒ.1.1.2) :=
  âŸ¨p2, u, by rw [p2.map_smul, smul_eq_mul, inv_mul_cancel hu']âŸ©,
  rw [relativize_slice q rfl],
  refine (hR q).vadd,
end

variables {IP P}

lemma family_one_jet_sec.uncurry_mem_relativize (S : family_one_jet_sec I M I' M' IP P) {s : P}
  {x : M} : S.uncurry (s, x) âˆˆ R.relativize IP P â†” S s x âˆˆ R :=
begin
  simp_rw [rel_mfld.relativize, mem_preimage, bundle_snd, one_jet_sec.coe_apply,
    map_left],
  congr',
  ext v,
  simp_rw [S.uncurry_Ï•', continuous_linear_map.comp_apply, continuous_linear_map.add_apply,
    continuous_linear_map.comp_apply, continuous_linear_map.inr_apply,
    continuous_linear_map.coe_fst', continuous_linear_map.coe_snd',
    continuous_linear_map.map_zero, zero_add, S.coe_Ï•]
end

def family_formal_sol.uncurry
  (S : family_formal_sol IP P R) : formal_sol (R.relativize IP P) :=
begin
  refine âŸ¨S.to_family_one_jet_sec.uncurry, _âŸ©,
  rintro âŸ¨s, xâŸ©,
  exact S.to_family_one_jet_sec.uncurry_mem_relativize.mpr (S.is_sol' s x)
end

lemma family_formal_sol.uncurry_Ï•' (S : family_formal_sol IP P R) (p : P Ã— M) :
  S.uncurry.Ï• p = mfderiv IP I' (Î» z, S.bs z p.2) p.1 âˆ˜L continuous_linear_map.fst â„ EP E +
  S.Ï• p.1 p.2 âˆ˜L continuous_linear_map.snd â„ EP E :=
S.to_family_one_jet_sec.uncurry_Ï•' p

def family_one_jet_sec.curry (S : family_one_jet_sec (IP.prod I) (P Ã— M) I' M' J N) :
  family_one_jet_sec I M I' M' (J.prod IP) (N Ã— P) :=
{ bs := Î» p x, (S p.1).bs (p.2, x),
  Ï• := Î» p x, (S p.1).Ï• (p.2, x) âˆ˜L mfderiv I (IP.prod I) (Î» x, (p.2, x)) x,
  smooth' := begin
    sorry
  end }

lemma family_one_jet_sec.curry_bs (S : family_one_jet_sec (IP.prod I) (P Ã— M) I' M' J N) (p : N Ã— P)
  (x : M) : (S.curry p).bs x = (S p.1).bs (p.2, x) :=
rfl

lemma family_one_jet_sec.curry_Ï• (S : family_one_jet_sec (IP.prod I) (P Ã— M) I' M' J N) (p : N Ã— P)
  (x : M) : (S.curry p).Ï• x = (S p.1).Ï• (p.2, x) âˆ˜L mfderiv I (IP.prod I) (Î» x, (p.2, x)) x :=
rfl

lemma family_one_jet_sec.curry_Ï•' (S : family_one_jet_sec (IP.prod I) (P Ã— M) I' M' J N) (p : N Ã— P)
  (x : M) : (S.curry p).Ï• x = (S p.1).Ï• (p.2, x) âˆ˜L continuous_linear_map.inr â„ EP E :=
begin
  rw [S.curry_Ï•],
  congr' 1,
  refine ((mdifferentiable_at_const I IP).mfderiv_prod smooth_id.mdifferentiable_at).trans _,
  rw [mfderiv_id, mfderiv_const],
  refl,
end

lemma formal_sol.eq_iff {Fâ‚ Fâ‚‚ : formal_sol R} {x : M} :
  Fâ‚ x = Fâ‚‚ x â†” Fâ‚.bs x = Fâ‚‚.bs x âˆ§ Fâ‚.Ï• x = by apply Fâ‚‚.Ï• x :=
by { simp_rw [sigma.ext_iff, formal_sol.fst_eq, heq_iff_eq, prod.ext_iff, eq_self_iff_true,
  true_and], refl }

lemma family_one_jet_sec.is_holonomic_at_curry (S : family_one_jet_sec (IP.prod I) (P Ã— M) I' M' J N)
  {t : N} {s : P} {x : M} (hS : (S t).is_holonomic_at (s, x)) :
  (S.curry (t, s)).is_holonomic_at x :=
begin
  simp_rw [one_jet_sec.is_holonomic_at, (S.curry _).snd_eq, S.curry_Ï•] at hS âŠ¢,
  dsimp only,
  rw [show (S.curry (t, s)).bs = Î» x, (S.curry (t, s)).bs x, from rfl, funext (S.curry_bs _)],
  dsimp only,
  refine (mfderiv_comp x (S t).smooth_bs.mdifferentiable_at
    ((mdifferentiable_at_const I IP).prod_mk smooth_id.mdifferentiable_at)).trans _,
  rw [id, hS],
  refl,
end

def family_formal_sol.curry (S : family_formal_sol J N (R.relativize IP P)) :
  family_formal_sol (J.prod IP) (N Ã— P) R :=
âŸ¨S.to_family_one_jet_sec.curry, sorryâŸ©

lemma family_formal_sol.curry_Ï• (S : family_formal_sol J N (R.relativize IP P)) (p : N Ã— P)
  (x : M) : (S.curry p).Ï• x = (S p.1).Ï• (p.2, x) âˆ˜L mfderiv I (IP.prod I) (Î» x, (p.2, x)) x :=
rfl

lemma family_formal_sol.curry_Ï•' (S : family_formal_sol J N (R.relativize IP P)) (p : N Ã— P)
  (x : M) : (S.curry p).Ï• x = (S p.1).Ï• (p.2, x) âˆ˜L continuous_linear_map.inr â„ EP E :=
S.to_family_one_jet_sec.curry_Ï•' p x

lemma curry_eq_iff_eq_uncurry {ğ“• : family_formal_sol J N (R.relativize IP P)}
  {ğ“•â‚€ : family_formal_sol IP P R} {t : N} {x : M} {s : P}
  (h : ğ“• t (s, x) = ğ“•â‚€.uncurry (s, x)) :
  (ğ“•.curry (t, s)) x = ğ“•â‚€ s x :=
begin
  simp_rw [formal_sol.eq_iff] at h âŠ¢,
  refine âŸ¨h.1, _âŸ©,
  simp_rw [ğ“•.curry_Ï•', h.2, ğ“•â‚€.uncurry_Ï•'],
  ext v,
  simp_rw [continuous_linear_map.comp_apply, continuous_linear_map.add_apply,
    continuous_linear_map.comp_apply,
    continuous_linear_map.inr_apply, continuous_linear_map.coe_fst',
    continuous_linear_map.coe_snd', continuous_linear_map.map_zero, zero_add],
  refl
end

/-- This might need some additional assumptions or other modifications. -/
lemma rel_mfld.satisfies_h_principle.satisfies_h_principle_with
  (R : rel_mfld I M IX X) {C : set (P Ã— M)}
  (Îµ : M â†’ â„) (h : (R.relativize IP P).satisfies_h_principle C (Î» x, Îµ x.2)) :
  R.satisfies_h_principle_with IP C Îµ :=
begin
  intros ğ“•â‚€ hğ“•â‚€,
  obtain âŸ¨ğ“•, h1ğ“•, h2ğ“•, h3ğ“•, h4ğ“•âŸ© := h ğ“•â‚€.uncurry _,
  swap,
  { refine hğ“•â‚€.mono (Î» p hp, ğ“•â‚€.to_family_one_jet_sec.is_holonomic_uncurry.mpr hp) },
  refine âŸ¨ğ“•.curry, _, _, _, _âŸ©,
  { intros s x, exact curry_eq_iff_eq_uncurry (h1ğ“• (s, x)) },
  { intros s x, exact ğ“•.to_family_one_jet_sec.is_holonomic_at_curry (h2ğ“• (s, x)) },
  { refine h3ğ“•.mono _, rintro âŸ¨s, xâŸ© hp t, exact curry_eq_iff_eq_uncurry (hp t) },
  { intros t s x, exact (h4ğ“• t (s, x)) },
end

end parameter_space

section loc
/-! ## Link with the local story

Now we really bridge the gap all the way to vector spaces.
-/

variables {E : Type*} [normed_add_comm_group E] [normed_space â„ E]
variables {E' : Type*} [normed_add_comm_group E'] [normed_space â„ E']

/-- For maps between vector spaces, `one_jet_ext` is the obvious thing. -/
@[simp] theorem one_jet_ext_eq_fderiv {f : E â†’ E'} {x : E} :
  one_jet_ext ğ“˜(â„, E) ğ“˜(â„, E') f x = âŸ¨(x, f x), fderiv â„ f xâŸ© :=
by { rw â† mfderiv_eq_fderiv, refl }

/-- Convert a 1-jet section between vector spaces seen as manifold to a 1-jet section
between those vector spaces. -/
def one_jet_sec.loc (F : one_jet_sec ğ“˜(â„, E) E ğ“˜(â„, E') E') : rel_loc.jet_sec E E' :=
{ f := F.bs,
  f_diff := sorry,
  Ï† := Î» x, (F x).2,
  Ï†_diff := sorry }

lemma one_jet_sec.loc_hol_at_iff (F : one_jet_sec ğ“˜(â„, E) E ğ“˜(â„, E') E') (x : E) :
F.loc.is_holonomic_at x â†” F.is_holonomic_at x :=
begin
  dsimp only [one_jet_sec.is_holonomic_at],
  rw mfderiv_eq_fderiv,
  exact iff.rfl
end

/-- Turns a relation between `E` and `E'` seen as manifolds into a relation between them
seen as vector spaces. One annoying bit is `equiv.prod_assoc E E' $ E â†’L[â„] E'` that is needed
to reassociate a product of types. -/
def rel_mfld.rel_loc (R : rel_mfld ğ“˜(â„, E) E ğ“˜(â„, E') E') : rel_loc E E' :=
(equiv.prod_assoc _ _ _) '' ((one_jet_bundle_model_space_homeomorph E ğ“˜(â„, E) E' ğ“˜(â„, E')) '' R)

lemma ample_of_ample (R : rel_mfld ğ“˜(â„, E) E ğ“˜(â„, E') E') (hR : R.ample) :
  R.rel_loc.is_ample :=
sorry

lemma is_open_of_is_open (R : rel_mfld ğ“˜(â„, E) E ğ“˜(â„, E') E') (hR : is_open R) :
  is_open R.rel_loc :=
sorry

end loc
