import measure_theory.integral.interval_integral
import analysis.calculus.parametric_integral
import algebra.module.ulift

import to_mathlib.calculus
import to_mathlib.measure_theory
import to_mathlib.topology.metric_space
import to_mathlib.misc

noncomputable theory

open topological_space measure_theory filter first_countable_topology metric set function
open_locale topological_space filter nnreal big_operators interval

namespace continuous_linear_map

open interval_integral
variables {Î± ğ•œ E H F : Type*}
variables [measurable_space Î±] [is_R_or_C ğ•œ] {Î¼ : measure Î±}
variables [measurable_space E] [normed_group E] [normed_space ğ•œ E] [borel_space E]
variables [second_countable_topology E] [complete_space E]
variables [normed_space â„ E] [is_scalar_tower â„ ğ•œ E]
variables [measurable_space F] [normed_group F] [normed_space ğ•œ F] [borel_space F]
variables [second_countable_topology F] [complete_space F]
variables [normed_space â„ F] [is_scalar_tower â„ ğ•œ F]
variables [normed_group H] [normed_space ğ•œ H] [second_countable_topology (H â†’L[ğ•œ] E)]

variables [linear_order Î±]

lemma interval_integral_apply {a b : Î±} {Ï† : Î± â†’ H â†’L[ğ•œ] E} (Ï†_int : interval_integrable Ï† Î¼ a b)
  (v : H) : (âˆ« a in a..b, Ï† a âˆ‚Î¼) v = âˆ« a in a..b, Ï† a v âˆ‚Î¼ :=
by simp_rw [interval_integral_eq_integral_interval_oc, â† integral_apply Ï†_int.def v,
  continuous_linear_map.coe_smul', pi.smul_apply]

end continuous_linear_map

section
variables {E : Type*} [normed_group E] [second_countable_topology E] [normed_space â„ E]
  [complete_space E] [measurable_space E] [borel_space E]
  {H : Type*} [normed_group H] [normed_space â„ H]
  [second_countable_topology $ H â†’L[â„] E]
  [borel_space $ H â†’L[â„] E]
  (Î½ : measure â„)

/-- Interval version of `has_fderiv_at_of_dominated_of_fderiv_le` -/
lemma has_fderiv_at_of_dominated_of_fderiv_le'' {F : H â†’ â„ â†’ E} {F' : H â†’ â„ â†’ (H â†’L[â„] E)} {xâ‚€ : H}
  {a b : â„}
  {bound : â„ â†’ â„}
  {Îµ : â„}
  (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) $ Î½.restrict (Î™ a b))
  (hF_int : interval_integrable (F xâ‚€) Î½ a b)
  (hF'_meas : ae_measurable (F' xâ‚€) $ Î½.restrict (Î™ a b))
  (h_bound : âˆ€áµ t âˆ‚Î½, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, âˆ¥F' x tâˆ¥ â‰¤ bound t)
  (bound_integrable : interval_integrable bound Î½ a b)
  (h_diff : âˆ€áµ t âˆ‚Î½, t âˆˆ Î™ a b â†’ âˆ€ x âˆˆ ball xâ‚€ Îµ, has_fderiv_at (Î» x, F x t) (F' x t) x) :
  has_fderiv_at (Î» x, âˆ« t in a..b, F x t âˆ‚Î½) (âˆ« t in a..b, F' xâ‚€ t âˆ‚Î½) xâ‚€ :=
begin
  erw ae_interval_oc_iff' at h_diff h_bound,
  simp_rw [ae_measurable_interval_oc_iff, eventually_and] at hF_meas hF'_meas,
  exact (has_fderiv_at_integral_of_dominated_of_fderiv_le Îµ_pos hF_meas.1 hF_int.1 hF'_meas.1 h_bound.1
         bound_integrable.1 h_diff.1).sub
        (has_fderiv_at_integral_of_dominated_of_fderiv_le Îµ_pos hF_meas.2 hF_int.2 hF'_meas.2 h_bound.2
         bound_integrable.2 h_diff.2)
end



/-- Interval version of `has_fderiv_at_of_dominated_loc_of_lip` -/
lemma has_fderiv_at_of_dominated_loc_of_lip_interval {F : H â†’ â„ â†’ E} {F' : â„ â†’ (H â†’L[â„] E)} {xâ‚€ : H}
  {a b : â„}
  {bound : â„ â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  (hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) $ Î½.restrict (Î™ a b))
  (hF_int : interval_integrable (F xâ‚€) Î½ a b)
  (hF'_meas : ae_measurable F' $ Î½.restrict (Î™ a b))
  (h_lip : âˆ€áµ t âˆ‚(Î½.restrict (Î™ a b)),
    lipschitz_on_with (real.nnabs $ bound t) (Î» x, F x t) (ball xâ‚€ Îµ))
  (bound_integrable : interval_integrable bound Î½ a b)
  (h_diff : âˆ€áµ t âˆ‚(Î½.restrict (Î™ a b)), has_fderiv_at (Î» x, F x t) (F' t) xâ‚€) :
  interval_integrable F' Î½ a b âˆ§
  has_fderiv_at (Î» x, âˆ« t in a..b, F x t âˆ‚Î½) (âˆ« t in a..b, F' t âˆ‚Î½) xâ‚€ :=
begin
  simp_rw [ae_measurable_interval_oc_iff, eventually_and] at hF_meas hF'_meas,
  rw ae_interval_oc at h_lip h_diff,
  have Hâ‚ := has_fderiv_at_integral_of_dominated_loc_of_lip Îµ_pos hF_meas.1 hF_int.1 hF'_meas.1
    h_lip.1 bound_integrable.1 h_diff.1,
  have Hâ‚‚ := has_fderiv_at_integral_of_dominated_loc_of_lip Îµ_pos hF_meas.2 hF_int.2 hF'_meas.2
    h_lip.2 bound_integrable.2 h_diff.2,
  exact âŸ¨âŸ¨Hâ‚.1, Hâ‚‚.1âŸ©, Hâ‚.2.sub Hâ‚‚.2âŸ©
end

end

section

open function

theorem continuous_parametric_integral_of_continuous
  {E : Type*} [normed_group E] [topological_space.second_countable_topology E] [normed_space â„ E]
  [complete_space E] [measurable_space E] [borel_space E]
  {Î± : Type*} [topological_space Î±] [measurable_space Î±] [opens_measurable_space Î±]
  {Î¼ : measure_theory.measure Î±} [is_locally_finite_measure Î¼]
  {X : Type*} [topological_space X] [first_countable_topology X] [locally_compact_space X]
  {F : X â†’ Î± â†’ E} (hF : continuous (Î» p : X Ã— Î±, F p.1 p.2))
  {s : set Î±} (hs : is_compact s) (hs' : measurable_set s):
  continuous (Î» x, âˆ« a in s, F x a âˆ‚Î¼) :=
begin
  rw continuous_iff_continuous_at,
  intros xâ‚€,
  rcases exists_compact_mem_nhds xâ‚€ with âŸ¨U, U_cpct, U_nhdsâŸ©,
  rcases (U_cpct.prod hs).bdd_above_norm hF with âŸ¨M, M_pos, hMâŸ©,
  apply continuous_at_of_dominated,
  { exact eventually_of_forall (Î» x, (hF.comp (continuous.prod.mk x)).ae_measurable _) },
  { apply eventually.mono U_nhds (Î» x x_in, _),
    apply ae_restrict_of_forall_mem hs',
    intros t t_in,
    exact hM (x, t) (set.mk_mem_prod x_in t_in) },
  { apply hs.integrable_const },
  { apply ae_of_all,
    intros a,
    apply (hF.comp $ continuous_id.prod_mk continuous_const).continuous_at }
end

end

section

variables {Î± E : Type*} [linear_order Î±] [measurable_space Î±]
  [measurable_space E] [normed_group E] [opens_measurable_space E]

lemma interval_integrable_norm_iff {f : Î± â†’ E} {Î¼ : measure Î±} {a b : Î±}
  (hf : ae_measurable f (Î¼.restrict (Î™ a b))) :
  interval_integrable (Î» t, âˆ¥f tâˆ¥) Î¼ a b â†” interval_integrable f Î¼ a b :=
begin
  simp_rw [interval_integrable_iff, integrable_on],
  exact integrable_norm_iff hf
end

lemma interval_oc_comm {Î± : Type*} [linear_order Î±] (a b : Î±) : Î™ a b = Î™ b a :=
begin
  dsimp [interval_oc],
  rw [min_comm, max_comm]
end

lemma interval_integrable_of_nonneg_of_le {f g : Î± â†’ â„} {Î¼ : measure Î±} {a b : Î±}
  (hf : ae_measurable f $ Î¼.restrict (Î™ a b))
  (h : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), 0 â‰¤ f t âˆ§ f t â‰¤ g t)
  (hg : interval_integrable g Î¼ a b) :
  interval_integrable f Î¼ a b :=
begin
  rw interval_integrable_iff at *,
  apply integrable.mono' hg hf (h.mono _),
  rintro t âŸ¨H, H'âŸ©,
  change abs ( f t) â‰¤ _,
  rwa abs_of_nonneg H
end

lemma interval_integrable_of_norm_le {f : Î± â†’ E} {bound : Î± â†’ â„} {Î¼ : measure Î±} {a b : Î±}
  (hf : ae_measurable f $ Î¼.restrict (Î™ a b))
  (h : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), âˆ¥f tâˆ¥ â‰¤ bound t) (hbound : interval_integrable bound Î¼ a b) :
  interval_integrable f Î¼ a b :=
begin
  rw â† interval_integrable_norm_iff hf,
  apply interval_integrable_of_nonneg_of_le hf.norm (h.mono _) hbound,
  simp,
end

variables [second_countable_topology E]
  [complete_space E] [normed_space â„ E] [borel_space E] {a b : Î±} {f : Î± â†’ E} {bound : Î± â†’ â„}
  {Î¼ : measure Î±}

namespace interval_integral

lemma integral_mono_of_le {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f g : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (hab : a â‰¤ b)
  (hf : interval_integrable f Î¼ a b)
  (hg : interval_integrable g Î¼ a b)
  (hfg : f â‰¤áµ[Î¼.restrict (Î™ a b)] g) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
begin
  rw interval_oc_of_le hab at hfg,
  let H := hfg.filter_mono (ae_mono le_rfl),
  simpa only [integral_of_le hab] using set_integral_mono_ae_restrict hf.1 hg.1 H
end

lemma integral_mono_of_le_of_nonneg {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f g : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (hab : a â‰¤ b)
  (hf : ae_measurable f $ Î¼.restrict (Î™ a b))
  (hfnonneg : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), 0 â‰¤ f t)
  (hg : interval_integrable g Î¼ a b)
  (hfg : f â‰¤áµ[Î¼.restrict (Î™ a b)] g) :
  âˆ« u in a..b, f u âˆ‚Î¼ â‰¤ âˆ« u in a..b, g u âˆ‚Î¼ :=
begin
  apply integral_mono_of_le hab _ hg hfg,
  have : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), 0 â‰¤ f t âˆ§ f t â‰¤ g t,
  from hfnonneg.and hfg,
  apply interval_integrable_of_nonneg_of_le hf this hg,
end

lemma integral_antimono_of_le {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f g : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (hab : b â‰¤ a)
  (hf : interval_integrable f Î¼ a b)
  (hg : interval_integrable g Î¼ a b)
  (hfg : f â‰¤áµ[Î¼.restrict (Î™ a b)] g) :
  âˆ« u in a..b, g u âˆ‚Î¼ â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ :=
begin
  cases eq_or_lt_of_le hab with hab hab,
  { simp [hab] },
  { rw interval_oc_of_lt hab at hfg,
    rw integral_symm b a,
    rw integral_symm b a,
    apply neg_le_neg,
    apply integral_mono_of_le hab.le hf.symm hg.symm,
    rwa [interval_oc_comm, interval_oc_of_lt hab] }
end

lemma integral_antimono_of_le_of_nonneg {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f g : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (hab : b â‰¤ a)
  (hf : ae_measurable f $ Î¼.restrict (Î™ a b))
  (hfnonneg : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), 0 â‰¤ f t)
  (hg : interval_integrable g Î¼ a b)
  (hfg : f â‰¤áµ[Î¼.restrict (Î™ a b)] g) :
  âˆ« u in a..b, g u âˆ‚Î¼ â‰¤ âˆ« u in a..b, f u âˆ‚Î¼ :=
begin
  apply integral_antimono_of_le hab _ hg hfg,
  have : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), 0 â‰¤ f t âˆ§ f t â‰¤ g t,
  from hfnonneg.and hfg,
  apply interval_integrable_of_nonneg_of_le hf this hg,
end
end interval_integral

/- This should replace interval_integrable.mono_set in mathlib -/
lemma interval_integrable.mono_set' {Î± E : Type*} [linear_order Î±] [measurable_space Î±]
  [measurable_space E] [normed_group E] {f : Î± â†’ E} {a b c d : Î±} {Î¼ : measure Î±}
  (hf : interval_integrable f Î¼ a b) (hsub : Î™ c d âŠ† Î™ a b) : interval_integrable f Î¼ c d :=
interval_integrable_iff.mpr (hf.def.mono hsub le_rfl)

lemma interval_integrable.const_mul {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±}
  (hf : interval_integrable f Î¼ a b) (c : â„) : interval_integrable (Î» x, c*f x) Î¼ a b :=
begin
  rw interval_integrable_iff at *,
  exact hf.const_mul c
end

lemma interval_integrable.mul_const {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±}
  (hf : interval_integrable f Î¼ a b) (c : â„) : interval_integrable (Î» x, (f x)*c) Î¼ a b :=
begin
  rw interval_integrable_iff at *,
  exact hf.mul_const c
end

lemma interval_integral.const_mul {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (c : â„) : âˆ« x in a..b, c*f x âˆ‚Î¼ = c*âˆ« x in a..b, f x âˆ‚Î¼ :=
begin
  erw mul_sub,
  simpa only [â† integral_mul_left]
end

lemma interval_integral.mul_const {Î± : Type*} [linear_order Î±] [measurable_space Î±]
  {f : Î± â†’ â„} {a b : Î±} {Î¼ : measure Î±} (c : â„) :
  âˆ« x in a..b, f x * c âˆ‚Î¼ = (âˆ« x in a..b, f x âˆ‚Î¼) * c :=
by simp_rw [mul_comm, â† interval_integral.const_mul]


lemma interval_integral.norm_integral_le_of_norm_le
  (h : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), âˆ¥f tâˆ¥ â‰¤ bound t)
  (hf : ae_measurable f (Î¼.restrict (Î™ a b)) )
  (hbound : interval_integrable bound Î¼ a b) :
  âˆ¥âˆ« t in a..b, f t âˆ‚Î¼âˆ¥ â‰¤ |âˆ« t in a..b, bound t âˆ‚Î¼| :=
begin
  apply interval_integral.norm_integral_le_abs_integral_norm.trans,
  cases le_total a b with hab hab,
  { apply abs_le_abs_of_nonneg,
    { apply interval_integral.integral_nonneg_of_forall hab,
      exact Î» t, norm_nonneg _ },
    apply interval_integral.integral_mono_of_le_of_nonneg hab hf.norm _ hbound h,
    simp },
  { apply abs_le_abs_of_nonpos,
    { rw [â† neg_nonneg, â† interval_integral.integral_symm],
      apply interval_integral.integral_nonneg_of_forall hab,
      exact Î» t, norm_nonneg _ },
    { apply interval_integral.integral_antimono_of_le_of_nonneg hab hf.norm _ hbound h,
      simp } }
end

lemma interval_integrable_of_norm_sub_le {Î² : Type*} [normed_group Î²] [measurable_space Î²]
  [opens_measurable_space Î²]
  {fâ‚€ fâ‚ : Î± â†’ Î²} {g : Î± â†’ â„}
  {a b : Î±}
  (hfâ‚_m : ae_measurable fâ‚ (Î¼.restrict $ Î™ a b))
  (hfâ‚€_i : interval_integrable fâ‚€ Î¼ a b)
  (hg_i : interval_integrable g Î¼ a b)
  (h : âˆ€áµ a âˆ‚(Î¼.restrict $ Î™ a b), âˆ¥fâ‚€ a - fâ‚ aâˆ¥ â‰¤ g a) :
  interval_integrable fâ‚ Î¼ a b :=
begin
  have : âˆ€áµ a âˆ‚(Î¼.restrict $ Î™ a b), âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a,
  { apply h.mono,
    intros a ha,
    calc âˆ¥fâ‚ aâˆ¥ â‰¤ âˆ¥fâ‚€ aâˆ¥ + âˆ¥fâ‚€ a - fâ‚ aâˆ¥ : norm_le_insert _ _
    ... â‰¤ âˆ¥fâ‚€ aâˆ¥ + g a : add_le_add_left ha _ },
  exact interval_integrable_of_norm_le hfâ‚_m this (hfâ‚€_i.norm.add hg_i)
end

end

section

open measure_theory

variables {Î± : Type*} [linear_order Î±] [measurable_space Î±] [topological_space Î±]
          [order_topology Î±] [opens_measurable_space Î±] [first_countable_topology Î±] {Î¼ : measure Î±}
          {X : Type*} [topological_space X] [first_countable_topology X]
          {E : Type*} [measurable_space E] [normed_group E] [normed_space â„ E] [borel_space E]
          [second_countable_topology E] [complete_space E]

lemma continuous_at_parametric_primitive_of_dominated
  {F : X â†’ Î± â†’ E} (bound : Î± â†’ â„) (a b : Î±) {aâ‚€ bâ‚€ : Î±} {xâ‚€ : X}
  (hF_meas : âˆ€ x, ae_measurable (F x) (Î¼.restrict $ Î™ a b))
  (h_bound : âˆ€á¶  x in ğ“ xâ‚€, âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), âˆ¥F x tâˆ¥ â‰¤ bound t)
  (bound_integrable : interval_integrable bound Î¼ a b)
  (h_cont : âˆ€áµ t âˆ‚(Î¼.restrict $ Î™ a b), continuous_at (Î» x, F x t) xâ‚€)
  (haâ‚€ : aâ‚€ âˆˆ Ioo a b) (hbâ‚€ : bâ‚€ âˆˆ Ioo a b) (hÎ¼bâ‚€ : Î¼ {bâ‚€} = 0) :
  continuous_at (Î» p : X Ã— Î±, âˆ« (t : Î±) in aâ‚€..p.2, F p.1 t âˆ‚Î¼) (xâ‚€, bâ‚€) :=
begin
  have hsubâ‚€ : Î™ aâ‚€ bâ‚€ âŠ† Î™ a b, from
    interval_oc_subset_of_mem_Ioc (mem_Ioc_of_Ioo haâ‚€) (mem_Ioc_of_Ioo hbâ‚€),
  have Ioo_nhds : Ioo a b âˆˆ ğ“ bâ‚€, from Ioo_mem_nhds hbâ‚€.1 hbâ‚€.2,
  have Icc_nhds : Icc a b âˆˆ ğ“ bâ‚€, from Icc_mem_nhds hbâ‚€.1 hbâ‚€.2,
  have hxâ‚€ : âˆ€áµ (t : Î±) âˆ‚Î¼.restrict (Î™ a b), âˆ¥F xâ‚€ tâˆ¥ â‰¤ bound t := (mem_of_mem_nhds h_bound : _),
  have : âˆ€á¶  (p : X Ã— Î±) in ğ“ (xâ‚€, bâ‚€),
    âˆ« s in aâ‚€..p.2, F p.1 s âˆ‚Î¼ = âˆ« s in aâ‚€..bâ‚€, F p.1 s âˆ‚Î¼ + âˆ« s in bâ‚€..p.2, F xâ‚€ s âˆ‚Î¼ +
                                 âˆ« s in bâ‚€..p.2, (F p.1 s - F xâ‚€ s) âˆ‚Î¼,
  { rw nhds_prod_eq,
    apply mem_of_superset (prod_mem_prod h_bound Ioo_nhds),
    rintros âŸ¨x, tâŸ© âŸ¨hx : âˆ€áµ (t : Î±) âˆ‚Î¼.restrict (Î™ a b), âˆ¥F x tâˆ¥ â‰¤ bound t, ht : t âˆˆ Ioo a bâŸ©,
    dsimp {eta := ff},
    rw [interval_integral.integral_sub, add_assoc, add_sub_cancel'_right,
        interval_integral.integral_add_adjacent_intervals],
    { exact interval_integrable_of_norm_le ((hF_meas x).mono_set hsubâ‚€)
            (ae_restrict_of_ae_restrict_of_subset hsubâ‚€ hx)
            (bound_integrable.mono_set' hsubâ‚€) },
    all_goals {
      have hsub : Î™ bâ‚€ t âŠ† Î™ a b, from
        interval_oc_subset_of_mem_Ioc (mem_Ioc_of_Ioo hbâ‚€) (mem_Ioc_of_Ioo ht),
      exact interval_integrable_of_norm_le ((hF_meas _).mono_set hsub)
            (ae_restrict_of_ae_restrict_of_subset hsub â€¹_â€º) (bound_integrable.mono_set' hsub) } },
  rw continuous_at_congr this, clear this,
  refine continuous_at.add (continuous_at.add _ _) _,
  { change continuous_at ((Î» x, âˆ« (s : Î±) in aâ‚€..bâ‚€, F x s âˆ‚Î¼) âˆ˜ prod.fst) (xâ‚€, bâ‚€),
    apply continuous_at.comp _ continuous_at_fst,
    apply interval_integral.continuous_at_of_dominated_interval
            (eventually_of_forall $ Î» x, (hF_meas x).mono_set hsubâ‚€)
            (h_bound.mono $ Î»  x, ae_mem_imp_of_ae_restrict_of_subset hsubâ‚€)
            (bound_integrable.mono_set' hsubâ‚€)
            (ae_mem_imp_of_ae_restrict_of_subset hsubâ‚€ h_cont) },
  { change continuous_at ((Î» t, âˆ« (s : Î±) in bâ‚€..t, F xâ‚€ s âˆ‚Î¼) âˆ˜ prod.snd) (xâ‚€, bâ‚€),
    apply continuous_at.comp _ continuous_at_snd,
    apply continuous_within_at.continuous_at _ (Icc_mem_nhds hbâ‚€.1 hbâ‚€.2),
    apply interval_integral.continuous_within_at_primitive hÎ¼bâ‚€,
    rw [min_eq_right hbâ‚€.1.le, max_eq_right hbâ‚€.2.le],
    exact interval_integrable_of_norm_le (hF_meas xâ‚€) hxâ‚€ bound_integrable },
  { suffices : tendsto (Î» (x : X Ã— Î±), âˆ« s in bâ‚€..x.2, F x.1 s - F xâ‚€ s âˆ‚Î¼) (ğ“ (xâ‚€, bâ‚€)) (ğ“ 0),
      by simpa [continuous_at],
    have : âˆ€á¶  p : X Ã— Î± in ğ“ (xâ‚€, bâ‚€),
      âˆ¥âˆ« s in bâ‚€..p.2, F p.1 s - F xâ‚€ s âˆ‚Î¼âˆ¥ â‰¤ |âˆ« (s : Î±) in bâ‚€..p.2, 2* bound s âˆ‚Î¼|,
    { rw nhds_prod_eq,
      apply mem_of_superset (prod_mem_prod h_bound Ioo_nhds),
      rintros âŸ¨x, tâŸ© âŸ¨hx : âˆ€áµ t âˆ‚Î¼.restrict (Î™ a b), âˆ¥F x tâˆ¥ â‰¤ bound t, ht : t âˆˆ Ioo a bâŸ©,
      have hsub : Î™ bâ‚€ t âŠ† Î™ a b, from
        interval_oc_subset_of_mem_Ioc (mem_Ioc_of_Ioo hbâ‚€) (mem_Ioc_of_Ioo ht),
      have H : âˆ€áµ (t : Î±) âˆ‚Î¼.restrict (Î™ bâ‚€ t), âˆ¥F x t - F xâ‚€ tâˆ¥ â‰¤ 2*bound t,
      { apply (ae_restrict_of_ae_restrict_of_subset hsub (hx.and hxâ‚€)).mono,
        rintros s âŸ¨hsâ‚, hsâ‚‚âŸ©,
        calc âˆ¥F x s - F xâ‚€ sâˆ¥ â‰¤ âˆ¥F x sâˆ¥ + âˆ¥F xâ‚€ sâˆ¥ : norm_sub_le _ _
        ... â‰¤ 2 * bound s : by linarith only [hsâ‚, hsâ‚‚] },
      exact interval_integral.norm_integral_le_of_norm_le H
        (((hF_meas x).mono_set hsub).sub ((hF_meas xâ‚€).mono_set hsub))
        ((bound_integrable.mono_set' hsub).const_mul 2) },
    apply squeeze_zero_norm' this,
    have : tendsto (Î» t, âˆ« (s : Î±) in bâ‚€..t, 2 * bound s âˆ‚Î¼) (ğ“ bâ‚€) (ğ“ 0),
    { suffices : continuous_at (Î» t, âˆ« (s : Î±) in bâ‚€..t, 2 * bound s âˆ‚Î¼) bâ‚€,
      { convert this,
        simp },
      apply continuous_within_at.continuous_at _ Icc_nhds,
      apply interval_integral.continuous_within_at_primitive hÎ¼bâ‚€,
      apply interval_integrable.const_mul,
      apply bound_integrable.mono_set',
      rw [min_eq_right hbâ‚€.1.le, max_eq_right hbâ‚€.2.le] },
    change tendsto (abs âˆ˜ (Î» t, âˆ« (s : Î±) in bâ‚€..t, 2*bound s âˆ‚Î¼) âˆ˜ prod.snd) (ğ“ (xâ‚€, bâ‚€)) _,
    have lim_abs : tendsto abs (ğ“ (0 : â„)) (ğ“ 0),
    { conv { congr, skip, skip, rw â† abs_zero },
      exact continuous_abs.continuous_at },
    apply lim_abs.comp (this.comp _),
    rw nhds_prod_eq, apply tendsto_snd },
end
end

section
variables {Î± : Type*} [conditionally_complete_linear_order Î±] [no_bot_order Î±] [no_top_order Î±]
          [measurable_space Î±] [topological_space Î±]
          [order_topology Î±] [opens_measurable_space Î±] [first_countable_topology Î±] {Î¼ : measure Î±}
          [is_locally_finite_measure Î¼] [has_no_atoms Î¼]
          {X : Type*} [topological_space X] [first_countable_topology X]
          {E : Type*} [measurable_space E] [normed_group E] [normed_space â„ E] [borel_space E]
          [second_countable_topology E] [complete_space E]

lemma continuous_parametric_primitive_of_continuous
  [locally_compact_space X]
  {F : X â†’ Î± â†’ E} {aâ‚€ : Î±}
  (hF : continuous (Î» p : X Ã— Î±, F p.1 p.2)) :
  continuous (Î» p : X Ã— Î±, âˆ« t in aâ‚€..p.2, F p.1 t âˆ‚Î¼) :=
begin
  rw continuous_iff_continuous_at,
  rintros âŸ¨xâ‚€, bâ‚€âŸ©,
  rcases exists_compact_mem_nhds xâ‚€ with âŸ¨U, U_cpct, U_nhdsâŸ©,
  cases no_bot (min aâ‚€ bâ‚€) with a a_lt,
  cases no_top (max aâ‚€ bâ‚€) with b lt_b,
  rw lt_min_iff at a_lt,
  rw max_lt_iff at lt_b,
  have aâ‚€_in : aâ‚€ âˆˆ Ioo a b := âŸ¨a_lt.1, lt_b.1âŸ©,
  have bâ‚€_in : bâ‚€ âˆˆ Ioo a b := âŸ¨a_lt.2, lt_b.2âŸ©,
  obtain âŸ¨M : â„, M_pos : M > 0,
          hM : âˆ€ (x : X Ã— Î±), x âˆˆ U.prod (Icc a b) â†’ âˆ¥(Î» (p : X Ã— Î±), F p.fst p.snd) xâˆ¥ â‰¤ MâŸ© :=
    (U_cpct.prod (is_compact_Icc : is_compact $ Icc a b)).bdd_above_norm hF,
  refine continuous_at_parametric_primitive_of_dominated (Î» t, M) a b _ _ _ _ aâ‚€_in bâ‚€_in
    (measure_singleton bâ‚€),
  { intro x,
    apply (hF.comp (continuous.prod.mk x)).ae_measurable _ },
  { apply eventually.mono U_nhds (Î» x (x_in : x âˆˆ U), _),
    refine ae_restrict_of_forall_mem measurable_set_Ioc _,
    intros t t_in,
    refine hM (x, t) âŸ¨x_in, (_ : t âˆˆ Icc a b)âŸ©,
    rw interval_oc_of_le (a_lt.1.trans lt_b.1).le at t_in,
    exact mem_Icc_of_Ioc t_in },
  { apply interval_integrable_const },
  { apply ae_of_all,
    intros a,
    apply (hF.comp $ continuous_id.prod_mk continuous_const).continuous_at }
end

theorem continuous_parametric_interval_integral_of_continuous
  [locally_compact_space X] {aâ‚€ : Î±}
  {F : X â†’ Î± â†’ E} (hF : continuous (Î» p : X Ã— Î±, F p.1 p.2))
  {s : X â†’ Î±} (hs : continuous s) :
  continuous (Î» x, âˆ« t in aâ‚€..s x, F x t âˆ‚Î¼) :=
show continuous ((Î» p : X Ã— Î±, âˆ« t in aâ‚€..p.2, F p.1 t âˆ‚Î¼) âˆ˜ (Î» x, (x, s x))),
from (continuous_parametric_primitive_of_continuous hF).comp (continuous_id.prod_mk hs)

theorem continuous_parametric_interval_integral_of_continuous'
  [locally_compact_space X]
  {F : X â†’ Î± â†’ E} (hF : continuous (Î» p : X Ã— Î±, F p.1 p.2)) (aâ‚€ bâ‚€ : Î±) :
  continuous (Î» x, âˆ« t in aâ‚€..bâ‚€, F x t âˆ‚Î¼) :=
continuous_parametric_interval_integral_of_continuous hF continuous_const


end


section
variables {E : Type*} [normed_group E] [normed_space â„ E]
          [complete_space E] [second_countable_topology E]
          [measurable_space E] [borel_space E]
          {H : Type*} [normed_group H] [normed_space â„ H]
          [finite_dimensional â„ H]
/-!
We could weaken `finite_dimensional â„ H` with `second_countable (H â†’L[â„] E)` if needed,
but that is less convenient to work with.
-/

open real continuous_linear_map asymptotics

/--
This statement is a new version using the continuity note in mathlib.
See commit `39e3f3f` for an older version
Maybe todo: let `a` depend on `x`.
-/
lemma has_fderiv_at_parametric_primitive_of_lip' (F : H â†’ â„ â†’ E) (F' : â„ â†’ (H â†’L[â„] E)) {xâ‚€ : H}
  {G' : H â†’L[â„] E}
  {bound : â„ â†’ â„}
  {s : H â†’ â„}
  {Îµ : â„} (Îµ_pos : 0 < Îµ)
  {a aâ‚€ bâ‚€ : â„}
  {s' : H â†’L[â„] â„}
  (ha :  a âˆˆ Ioo aâ‚€ bâ‚€)
  (hsxâ‚€ : s xâ‚€ âˆˆ Ioo aâ‚€ bâ‚€)
  (hF_meas : âˆ€ x âˆˆ ball xâ‚€ Îµ, ae_measurable (F x) (volume.restrict (Ioo aâ‚€ bâ‚€)))
  (hF_int : integrable_on (F xâ‚€) (Ioo aâ‚€ bâ‚€))
  (hF_cont : continuous_at (F xâ‚€) (s xâ‚€))
  (hF'_meas : ae_measurable F' (volume.restrict $ Î™ a (s xâ‚€)))
  (h_lipsch : âˆ€áµ t âˆ‚(volume.restrict $ Ioo aâ‚€ bâ‚€),
    lipschitz_on_with (nnabs $ bound t) (Î» x, F x t) (ball xâ‚€ Îµ))
  (bound_integrable : integrable_on bound (Ioo aâ‚€ bâ‚€))
  (bound_cont : continuous_at bound (s xâ‚€))
  (bound_nonneg : âˆ€ t, 0 â‰¤ bound t) -- this is not really needed, but much more convenient
  (h_diff : âˆ€áµ a âˆ‚volume.restrict (Î™ a (s xâ‚€)), has_fderiv_at (Î» x, F x a) (F' a) xâ‚€)
  (s_diff : has_fderiv_at s s' xâ‚€)
  (hG' : (âˆ« t in a..s xâ‚€, F' t) + (to_span_singleton â„ (F xâ‚€ (s xâ‚€))).comp s' = G') :
  interval_integrable F' volume a (s xâ‚€) âˆ§
  has_fderiv_at (Î» x : H, âˆ« t in a..s x, F x t) G' xâ‚€ :=
begin
  subst hG',
  let Ï† : H â†’ â„ â†’ E := Î» x t, âˆ« s in a..t, F x s,
  let Ïˆ : H â†’L[â„] E := âˆ« t in a..s xâ‚€, F' t,
  have Ioo_nhds : Ioo aâ‚€ bâ‚€ âˆˆ ğ“ (s xâ‚€) := Ioo_mem_nhds hsxâ‚€.1 hsxâ‚€.2,
  have bound_int : âˆ€ {s u}, s âˆˆ Ioo aâ‚€ bâ‚€ â†’ u âˆˆ Ioo aâ‚€ bâ‚€ â†’ interval_integrable bound volume s u,
  { intros s u hs hu,
    exact (bound_integrable.mono_set $ interval_subset_Ioo hs hu).interval_integrable },
  have mem_nhds : ball xâ‚€ Îµ âˆ© (s â»Â¹' Ioo aâ‚€ bâ‚€) âˆˆ ğ“ xâ‚€ :=
  filter.inter_mem (ball_mem_nhds xâ‚€ Îµ_pos) (s_diff.continuous_at.preimage_mem_nhds Ioo_nhds),
  have xâ‚€_in : xâ‚€ âˆˆ ball xâ‚€ Îµ := mem_ball_self Îµ_pos,
  have hF_meas_ball : âˆ€ {x}, x âˆˆ ball xâ‚€ Îµ â†’ âˆ€ {s u}, s âˆˆ Ioo aâ‚€ bâ‚€ â†’ u âˆˆ Ioo aâ‚€ bâ‚€ â†’
    ae_measurable (F x) (volume.restrict $ Î™ s u),
  { intros x hx s u hs hu,
    exact (hF_meas x hx).mono_set (interval_oc_subset_Ioo hs hu) },
  have hF_int_ball : âˆ€ x âˆˆ ball xâ‚€ Îµ, âˆ€ {s u}, s âˆˆ Ioo aâ‚€ bâ‚€ â†’ u âˆˆ Ioo aâ‚€ bâ‚€ â†’
    interval_integrable (F x) volume s u,
  { intros x hx s u hs hu,
    have : integrable_on (F x) (Ioo aâ‚€ bâ‚€),
    { apply integrable_of_norm_sub_le (hF_meas x hx) hF_int (bound_integrable.mul_const (âˆ¥x - xâ‚€âˆ¥)),
      apply h_lipsch.mono,
      intros t ht,
      rw norm_sub_rev,
      rw lipschitz_on_with_iff_norm_sub_le at ht,
      simpa [bound_nonneg t] using ht x hx xâ‚€ xâ‚€_in },
    exact (this.mono_set $ interval_subset_Ioo hs hu).interval_integrable },
  split,
  { apply interval_integrable_of_norm_le hF'_meas _ (bound_int ha hsxâ‚€),
    replace h_lipsch : âˆ€áµ t âˆ‚volume.restrict (Î™ a (s xâ‚€)),
      lipschitz_on_with (nnabs (bound t)) (Î» (x : H), F x t) (ball xâ‚€ Îµ),
      from ae_restrict_of_ae_restrict_of_subset (interval_oc_subset_Ioo ha hsxâ‚€) h_lipsch,
    filter_upwards [h_lipsch, h_diff],
    intros t ht_lip ht_diff,
    rw show bound t = nnabs (bound t), by simp [bound_nonneg t],
    exact ht_diff.le_of_lip (ball_mem_nhds xâ‚€ Îµ_pos) ht_lip },
  { have Dâ‚ : has_fderiv_at (Î» x, Ï† x (s xâ‚€)) (âˆ« t in a..s xâ‚€, F' t) xâ‚€,
    { replace hF_meas : âˆ€á¶  x in ğ“ xâ‚€, ae_measurable (F x) (volume.restrict (Î™ a (s xâ‚€))),
        from eventually.mono (ball_mem_nhds xâ‚€ Îµ_pos) (Î» x hx, hF_meas_ball hx ha hsxâ‚€),
      replace hF_int : interval_integrable (F xâ‚€) volume a (s xâ‚€), from hF_int_ball xâ‚€ xâ‚€_in ha hsxâ‚€,
      exact (has_fderiv_at_of_dominated_loc_of_lip_interval _ Îµ_pos hF_meas hF_int hF'_meas
              (ae_restrict_of_ae_restrict_of_subset (interval_oc_subset_Ioo ha hsxâ‚€) h_lipsch)
              (bound_int ha hsxâ‚€) h_diff).2 },
    have Dâ‚‚ : has_fderiv_at (Î» x, Ï† xâ‚€ (s x)) ((to_span_singleton â„ (F xâ‚€ (s xâ‚€))).comp s') xâ‚€,
    { refine has_fderiv_at.comp _ _ s_diff,
      rw [has_fderiv_at_iff_has_deriv_at, to_span_singleton_apply, one_smul],
      exact interval_integral.integral_has_deriv_at_right (hF_int_ball xâ‚€ xâ‚€_in ha hsxâ‚€)
        âŸ¨Ioo aâ‚€ bâ‚€, Ioo_nhds, (hF_meas xâ‚€ xâ‚€_in)âŸ© hF_cont },
    have Dâ‚ƒ : has_fderiv_at (Î» x, âˆ« t in s xâ‚€..s x, F x t - F xâ‚€ t) 0 xâ‚€,
    { apply is_O.has_fderiv_at _ one_lt_two,
      have Oâ‚ : is_O (Î» x, âˆ« s in s xâ‚€..s x, bound s) (Î» x, âˆ¥x - xâ‚€âˆ¥) (ğ“ xâ‚€),
      { have : is_O (Î» x, s x - s xâ‚€) (Î» x, âˆ¥x - xâ‚€âˆ¥) (ğ“ xâ‚€) := s_diff.is_O_sub.norm_right,
        refine is_O.trans _ this,
        show is_O ((Î» t, âˆ« s in s xâ‚€..t, bound s) âˆ˜ s) ((Î» t, t - s xâ‚€) âˆ˜ s) (ğ“ xâ‚€),
        refine is_O.comp_tendsto _ s_diff.continuous_at,
        have M : measurable_at_filter bound (ğ“ (s xâ‚€)) volume,
        { use [Ioo aâ‚€ bâ‚€, Ioo_nhds, bound_integrable.1] },
        apply is_O.congr' _ eventually_eq.rfl
          (interval_integral.integral_has_deriv_at_right (bound_int ha hsxâ‚€) M bound_cont).is_O,
        apply eventually.mono Ioo_nhds,
        rintros t ht,
        dsimp only {eta := false},
        rw interval_integral.integral_interval_sub_left (bound_int ha ht) (bound_int ha hsxâ‚€) },
      have Oâ‚‚ : is_O (Î» x, âˆ¥x - xâ‚€âˆ¥) (Î» x, âˆ¥x - xâ‚€âˆ¥) (ğ“ xâ‚€), from is_O_refl _ _,
      have Oâ‚ƒ : is_O (Î» x, âˆ« (t : â„) in s xâ‚€..s x, F x t - F xâ‚€ t)
             (Î» x, (âˆ« t' in s xâ‚€..s x, bound t') * âˆ¥x - xâ‚€âˆ¥)
             (ğ“ xâ‚€),
      { have bdd : âˆ€á¶  x in ğ“ xâ‚€,
          âˆ¥âˆ« s in s xâ‚€..s x, F x s - F xâ‚€ sâˆ¥ â‰¤ |âˆ« s in s xâ‚€..s x, bound s |* âˆ¥x - xâ‚€âˆ¥,
        { apply eventually.mono mem_nhds,
          rintros x âŸ¨hx : x âˆˆ ball xâ‚€ Îµ, hsx : s x âˆˆ Ioo aâ‚€ bâ‚€âŸ©,
          rw [â† abs_of_nonneg (norm_nonneg $ x - xâ‚€), â† abs_mul, â† interval_integral.mul_const],
          apply interval_integral.norm_integral_le_of_norm_le _ ((hF_meas_ball hx hsxâ‚€ hsx).sub
            (hF_meas_ball xâ‚€_in hsxâ‚€ hsx))
            ((bound_int hsxâ‚€ hsx).mul_const _),
          apply ae_restrict_of_ae_restrict_of_subset (interval_oc_subset_Ioo hsxâ‚€ hsx),
          apply h_lipsch.mono,
          intros t ht,
          rw lipschitz_on_with_iff_norm_sub_le at ht,
          simp only [coe_nnabs] at ht,
          rw â† abs_of_nonneg (bound_nonneg t),
          exact ht x hx xâ‚€ (mem_ball_self Îµ_pos) },
        rw â† is_O_norm_right,
        simp only [norm_eq_abs, abs_mul, abs_norm_eq_norm],
        exact bdd.is_O' },
      simp_rw pow_two,
      exact Oâ‚ƒ.trans (Oâ‚.mul Oâ‚‚) },
    have : âˆ€á¶  x in ğ“ xâ‚€, âˆ« t in a..s x, F x t =
      Ï† x (s xâ‚€) + Ï† xâ‚€ (s x) + (âˆ« t in s xâ‚€..s x, F x t - F xâ‚€ t) - Ï† xâ‚€ (s xâ‚€),
    { apply eventually.mono mem_nhds,
      rintros x âŸ¨hx : x âˆˆ ball xâ‚€ Îµ, hsx : s x âˆˆ Ioo aâ‚€ bâ‚€âŸ©,
      have intâ‚ : interval_integrable (F xâ‚€) volume a (s xâ‚€) := hF_int_ball xâ‚€ xâ‚€_in ha hsxâ‚€,
      have intâ‚‚ : interval_integrable (F xâ‚€) volume (s xâ‚€) (s x) := hF_int_ball xâ‚€ xâ‚€_in hsxâ‚€ hsx,
      have intâ‚ƒ : interval_integrable (F x) volume a (s xâ‚€) := hF_int_ball x hx ha hsxâ‚€,
      have intâ‚„ : interval_integrable (F x) volume (s xâ‚€) (s x) := hF_int_ball x hx hsxâ‚€ hsx,
      dsimp [Ï†] {eta := ff},
      rw [interval_integral.integral_sub intâ‚„ intâ‚‚, add_sub,
          add_right_comm, sub_sub, interval_integral.integral_add_adjacent_intervals intâ‚ƒ intâ‚„],
      conv_rhs { congr, skip, rw add_comm },
      rw interval_integral.integral_add_adjacent_intervals intâ‚ intâ‚‚,
      abel },
    apply has_fderiv_at.congr_of_eventually_eq _ this,
    simpa using ((Dâ‚.add Dâ‚‚).add Dâ‚ƒ).sub (has_fderiv_at_const (Ï† xâ‚€ (s xâ‚€)) xâ‚€) }
end



local notation `D` := fderiv â„
local notation u ` â¬ `:70 Ï† :=  continuous_linear_map.comp (continuous_linear_map.to_span_singleton â„ u) Ï†
local notation `âˆ‚â‚` := partial_fderiv_fst â„

/-
A version of the above lemma using Floris' style statement. This does not reuse the above lemma, but copies the proof.
-/

lemma has_fderiv_at_parametric_primitive_of_times_cont_diff' {F : H â†’ â„ â†’ E} (hF : times_cont_diff â„ 1 â†¿F)
  {s : H â†’ â„} (hs : times_cont_diff â„ 1 s)
  (xâ‚€ : H) (a : â„) :
  (interval_integrable (Î» t, (fderiv â„ $ Î» x, F x t) xâ‚€) volume a $ s xâ‚€) âˆ§
  has_fderiv_at (Î» x : H, âˆ« t in a..s x, F x t)
    ((âˆ« t in a..s xâ‚€, âˆ‚â‚F xâ‚€ t) + (F xâ‚€ (s xâ‚€)) â¬ (D s xâ‚€))
    xâ‚€ :=
begin
  set aâ‚€ :=  min a (s xâ‚€) - 1,
  set bâ‚€ :=  max a (s xâ‚€) + 1,
  have ha : a âˆˆ Ioo aâ‚€ bâ‚€,
  { dsimp [aâ‚€, bâ‚€],
    split,
    linarith [min_le_left a (s xâ‚€)],
    linarith [le_max_left a (s xâ‚€)] },
  have htâ‚€ : s xâ‚€ âˆˆ Ioo aâ‚€ bâ‚€,
  { dsimp [aâ‚€, bâ‚€],
    split,
    linarith [min_le_right a (s xâ‚€)],
    linarith [le_max_right a (s xâ‚€)] },
  have cpct : is_compact ((closed_ball xâ‚€ 1).prod $ Icc aâ‚€ bâ‚€),
      from (proper_space.is_compact_closed_ball xâ‚€ 1).prod is_compact_Icc,
  obtain âŸ¨M, M_nonneg, F_boundâŸ© : âˆƒ M : â„, 0 â‰¤ M âˆ§ âˆ€ x âˆˆ ball xâ‚€ 1, âˆ€ t âˆˆ Ioo aâ‚€ bâ‚€, âˆ¥F x tâˆ¥ â‰¤ M,
  { rcases cpct.bdd_above_norm hF.continuous with âŸ¨M, M_pos : 0 < M, hMâŸ©,
    use [M, M_pos.le],
    exact Î» x x_in t t_in, hM (x, t) âŸ¨ball_subset_closed_ball x_in, mem_Icc_of_Ioo t_inâŸ© },
  obtain âŸ¨K, F_lipâŸ© : âˆƒ K, âˆ€ t âˆˆ Ioo aâ‚€ bâ‚€, lipschitz_on_with K (Î» x, F x t) (ball xâ‚€ 1),
  { have conv : convex â„ ((closed_ball xâ‚€ 1).prod $ Icc  aâ‚€ bâ‚€),
      from (convex_closed_ball xâ‚€ 1).prod (convex_Icc aâ‚€ bâ‚€),
    rcases hF.lipschitz_on_with conv cpct with âŸ¨K, hKâŸ©,
    use K,
    intros t t_in,
    rw [show (Î» (x : H), F x t) = (uncurry F) âˆ˜ (Î» x : H, (x, t)), by { ext, simp }, â† mul_one K],
    apply hK.comp ((lipschitz_with_prod_mk_right t).lipschitz_on_with $ ball xâ‚€ 1),
    rintros âŸ¨x, sâŸ© âŸ¨x', hx, hâŸ©, cases h,
    refine âŸ¨ball_subset_closed_ball hx, mem_Icc_of_Ioo t_inâŸ© },
  have cont_x : âˆ€ x, continuous (F x),
    from Î» x, hF.continuous.comp (continuous.prod.mk x),
  have int_Icc : âˆ€ x, integrable_on (F x) (Icc aâ‚€ bâ‚€),
    from Î» x, (cont_x x).integrable_on_compact is_compact_Icc,
  have int_Ioo : âˆ€ x, integrable_on (F x) (Ioo aâ‚€ bâ‚€),
    from Î» x, (int_Icc x).mono_set Ioo_subset_Icc_self,
  apply has_fderiv_at_parametric_primitive_of_lip' _ _ zero_lt_one ha htâ‚€
    (Î» x hx, (cont_x x).ae_measurable _) (int_Ioo xâ‚€) (cont_x xâ‚€).continuous_at
    _ _ _ (continuous_at_const : continuous_at (Î» (t : â„), (K : â„)) $ s xâ‚€) (Î» t, nnreal.coe_nonneg K),
  { apply ae_of_all,
    intro t,
    apply (times_cont_diff.has_strict_fderiv_at _ le_rfl).has_fderiv_at,
    rw show (Î» x, F x t) = (uncurry F) âˆ˜ (Î» x, (x, t)), by { ext, simp },
    exact hF.comp ((times_cont_diff_prod_left t).of_le le_top) },
  { exact (times_cont_diff.has_strict_fderiv_at hs le_rfl).has_fderiv_at },
  { refl },
  { apply continuous.ae_measurable,
    have : (Î» t, fderiv â„ (Î» (x : H), F x t) xâ‚€) =
      ((Î» Ï† : H Ã— â„ â†’L[â„] E, Ï†.comp (inl â„ H â„)) âˆ˜ (fderiv â„ $ uncurry F) âˆ˜ (Î» t, (xâ‚€, t))),
    { ext t,
      have : has_fderiv_at (Î» e, F e t) ((fderiv â„ (uncurry F) (xâ‚€, t)).comp (inl â„ H â„)) xâ‚€,
        from has_fderiv_at.partial_fst (hF.has_strict_fderiv_at le_rfl).has_fderiv_at,
      rw [this.fderiv] },
    rw this, clear this,
    exact (inl â„ H â„).comp_rightL.continuous.comp ((hF.continuous_fderiv le_rfl).comp $
      continuous.prod.mk xâ‚€) },
  { refine ae_restrict_of_forall_mem measurable_set_Ioo _,
    swap,
    intros t t_in,
    rw nnabs_coe K,
    exact F_lip t t_in },
  { exact integrable_on_const.mpr (or.inr measure_Ioo_lt_top) }
end

end

section
universe variables u v
variables {E : Type (max u v)} [normed_group E] [normed_space â„ E]
          [complete_space E] [second_countable_topology E]
          [measurable_space E] [borel_space E]
          {H : Type u} [normed_group H] [normed_space â„ H]
          [finite_dimensional â„ H]

open real continuous_linear_map asymptotics

local notation `D` := fderiv â„
local notation u ` â¬ `:70 Ï† :=  continuous_linear_map.comp (continuous_linear_map.to_span_singleton â„ u) Ï†
local notation `âˆ‚â‚` := partial_fderiv_fst â„

/-- In this version the universe levels have a restriction.
Use `times_cont_diff_parametric_primitive_of_times_cont_diff'` instead. -/
lemma times_cont_diff_parametric_primitive_of_times_cont_diff'' {F : H â†’ â„ â†’ E} {n : â„•}
  (hF : times_cont_diff â„ n â†¿F)
  {s : H â†’ â„} (hs : times_cont_diff â„ n s)
  (a : â„) :
  times_cont_diff â„ n (Î» x : H, âˆ« t in a..s x, F x t)  :=
begin
  tactic.unfreeze_local_instances,
  revert E F,
  induction n with n ih; introsI E F iâ‚ iâ‚‚ iâ‚ƒ iâ‚„ iâ‚… iâ‚† hF,
  { rw [with_top.coe_zero, times_cont_diff_zero] at *,
    exact continuous_parametric_interval_integral_of_continuous hF hs },
  { have hFâ‚ : times_cont_diff â„ 1 (â†¿F), from hF.one_of_succ,
    have hsâ‚ : times_cont_diff â„ 1 s, from hs.one_of_succ,
    rw times_cont_diff_succ_iff_fderiv,
    split,
    { exact Î» xâ‚€, âŸ¨_, (has_fderiv_at_parametric_primitive_of_times_cont_diff' hFâ‚ hsâ‚ xâ‚€ a).2âŸ© },
    { rw funext (Î» x, (has_fderiv_at_parametric_primitive_of_times_cont_diff' hFâ‚ hsâ‚ x a).2.fderiv),
      apply times_cont_diff.add,
      { apply ih hs.of_succ,
        apply times_cont_diff.times_cont_diff_partial_fst,
        exact hF },
      { exact is_bounded_bilinear_map_smul_right.times_cont_diff.comp
          ((times_cont_diff_succ_iff_fderiv.mp hs).2.prod $ hF.of_succ.comp $ times_cont_diff_id.prod hs.of_succ) } } }
end

end

section

universe variables v u

variables {E : Type u}

-- set_option pp.universes true
-- note: we can almost certainly remove all `.{v}` below
open ulift

@[to_additive, simps apply] def monoid_hom.up [mul_one_class E] : E â†’* ulift E :=
âŸ¨up, rfl, Î» x y, rflâŸ©
@[to_additive, simps] def monoid_hom.down [mul_one_class E] : ulift E â†’* E :=
âŸ¨down, rfl, Î» x y, rflâŸ©

instance [normed_group E] : normed_group (ulift.{v} E) :=
normed_group.induced add_monoid_hom.down equiv.ulift.injective

instance {F} [normed_field F] [normed_group E] [normed_space F E] : normed_space F (ulift.{v} E) :=
{ norm_smul_le := by { rintro x âŸ¨yâŸ©, exact normed_space.norm_smul_le x y },
  ..ulift.module' }

instance {X} [topological_space X] [second_countable_topology X] :
  second_countable_topology (ulift.{v} X) :=
homeomorph.ulift.second_countable_topology

instance {X} [uniform_space X] : uniform_space (ulift.{v} X) :=
uniform_space.comap down â€¹_â€º

lemma uniformity.ulift {X} [uniform_space X] :
  uniformity (ulift X) = comap (prod.map down down) (uniformity X) :=
uniformity_comap rfl

lemma uniform_continuous.ulift {X} [uniform_space X] :
  uniform_continuous (@homeomorph.ulift X _) :=
by { rw [uniform_continuous, uniformity.ulift], exact tendsto_comap }

lemma homeomorph.complete_space {X Y} [uniform_space X] [uniform_space Y] [complete_space Y]
  (Ï† : X â‰ƒâ‚œ Y) (hÏ† : uniform_continuous Ï†) : complete_space X :=
begin
  constructor,
  intros f hf,
  obtain âŸ¨y, hyâŸ© := complete_space.complete (hf.map hÏ†),
  refine âŸ¨Ï†.symm y, _âŸ©,
  rw [â† Ï†.symm.map_nhds_eq],
  rw [map_le_iff_le_comap] at hy,
  convert hy,
  -- better lemma here would be useful
  exact map_eq_comap_of_inverse (funext Ï†.left_inv) (funext Ï†.right_inv)
end

instance {X} [uniform_space X] [complete_space X] : complete_space (ulift.{v} X) :=
homeomorph.complete_space homeomorph.ulift uniform_continuous.ulift

instance {E} [measurable_space E] : measurable_space (ulift.{v} E) :=
measurable_space.comap down â€¹_â€º

instance {X} [measurable_space X] [topological_space X] [borel_space X] :
  borel_space (ulift.{v} X) :=
âŸ¨by { rw [â† borel_comap.symm, (borel_space.measurable_eq.symm : borel X = _)], refl }âŸ©

attribute [simps] mul_equiv.ulift

/-- `ulift M â†’ M` is a linear equivalence. -/
@[simps {simp_rhs := tt}] def linear_equiv.ulift (R M : Type*)
  [semiring R] [add_comm_monoid M] [module R M] : ulift.{v} M â‰ƒâ‚—[R] M :=
{ map_smul' := Î» x m, rfl, ..add_equiv.ulift }

/-- `ulift M â†’ M` is a continuous linear equivalence -/
@[simps apply symm_apply {simp_rhs := tt}]
def continuous_linear_equiv.ulift (R M : Type*) [semiring R] [topological_space M]
  [add_comm_monoid M] [module R M] : ulift.{v} M â‰ƒL[R] M :=
{ ..linear_equiv.ulift R M, ..homeomorph.ulift }

lemma times_cont_diff_up {F X : Type*} [nondiscrete_normed_field F] [normed_group X]
  [normed_space F X] {n : with_top â„•} : times_cont_diff F n (@up X) :=
(continuous_linear_equiv.ulift F X).symm.times_cont_diff

lemma times_cont_diff_down {F X : Type*} [nondiscrete_normed_field F] [normed_group X]
  [normed_space F X] {n : with_top â„•} : times_cont_diff F n (@down X) :=
(continuous_linear_equiv.ulift F X).times_cont_diff

lemma times_cont_diff_up_iff {F X Y : Type*} [nondiscrete_normed_field F] [normed_group X]
  [normed_space F X] [normed_group Y] [normed_space F Y] {n : with_top â„•} (f : X â†’ Y) :
  times_cont_diff F n (Î» x, up (f x)) â†” times_cont_diff F n f :=
(continuous_linear_equiv.ulift F Y).symm.comp_times_cont_diff_iff

variables [normed_group E] [normed_space â„ E]
          [complete_space E] [second_countable_topology E]
          [measurable_space E] [borel_space E]
          {H : Type v} [normed_group H] [normed_space â„ H]
          [finite_dimensional â„ H]

lemma times_cont_diff_parametric_primitive_of_times_cont_diff'
  {F : H â†’ â„ â†’ E} {n : â„•} (hF : times_cont_diff â„ n â†¿F)
  {s : H â†’ â„} (hs : times_cont_diff â„ n s)
  (a : â„) :
  times_cont_diff â„ n (Î» x : H, âˆ« t in a..s x, F x t) :=
begin
  have : times_cont_diff â„ n (Î» x : H, âˆ« t in a..s x, up.{v} (F x t)) :=
    times_cont_diff_parametric_primitive_of_times_cont_diff''.{v u} (times_cont_diff_up.comp hF)
      hs a,
  change times_cont_diff â„ n (Î» x : H, âˆ« t in a..s x,
    (continuous_linear_equiv.ulift â„ E).symm.to_continuous_linear_map (F x t)) at this,
  have hFi : âˆ€ x, interval_integrable (F x) volume a (s x),
    from Î» x, continuous.interval_integrable (hF.continuous.comp $ continuous.prod.mk x) _ _,
  simp_rw [continuous_linear_map.interval_integral_comp_comm
    (continuous_linear_equiv.ulift â„ E).symm.to_continuous_linear_map (hFi _)] at this,
  simpa [times_cont_diff_up_iff] using this,
end

/- Should we directly prove the version below?-/

lemma times_cont_diff_parametric_primitive_of_times_cont_diff
  {F : H â†’ â„ â†’ E} {n : with_top â„•} (hF : times_cont_diff â„ n â†¿F)
  {s : H â†’ â„} (hs : times_cont_diff â„ n s)
  (a : â„) :
  times_cont_diff â„ n (Î» x : H, âˆ« t in a..s x, F x t) :=
begin
  induction n using with_top.rec_top_coe,
  { rw times_cont_diff_top at *,
    exact Î» n, times_cont_diff_parametric_primitive_of_times_cont_diff' (hF n) (hs n) a },
  { exact times_cont_diff_parametric_primitive_of_times_cont_diff' hF hs a },
end

local notation `âˆ‚â‚` := partial_fderiv_fst â„

lemma times_cont_diff_parametric_integral_of_times_cont_diff
  {F : H â†’ â„ â†’ E} {n : with_top â„•} (hF : times_cont_diff â„ n â†¿F)
  (a b : â„) :
  times_cont_diff â„ n (Î» x : H, âˆ« t in a..b, F x t) :=
times_cont_diff_parametric_primitive_of_times_cont_diff hF times_cont_diff_const a

lemma times_cont_diff.fderiv_parametric_integral
  {F : H â†’ â„ â†’ E} (hF : times_cont_diff â„ 1 â†¿F)
  (a b : â„) :
  fderiv â„ (Î» x : H, âˆ« t in a..b, F x t) = Î» x : H, (âˆ« t in a..b, âˆ‚â‚F x t) :=
begin
  ext xâ‚€,
  cases has_fderiv_at_parametric_primitive_of_times_cont_diff' hF times_cont_diff_const xâ‚€ a with int h,
  rw [h.fderiv, fderiv_const],
  simp only [continuous_linear_map.comp_zero, add_zero, pi.zero_apply]
end

end

section

variables {E : Type*} [measurable_space E] [normed_group E] [normed_space â„ E] [borel_space E]
          [second_countable_topology E] [complete_space E]

open interval_integral

lemma integral_comp_add_right' {f : â„ â†’ E} (a b : â„) :
  âˆ« t in a..(b + a), f t = âˆ« t in 0..b, f (t + a) :=
by simp [â† integral_comp_add_right]


lemma integral_comp_add_left' {f : â„ â†’ E} (a b : â„) :
  âˆ« t in a..(a + b), f t = âˆ« t in 0..b, f (t + a) :=
by simp [â† integral_comp_add_left, add_comm]

/- TODO : in interval_integral.integral_add_adjacent_intervals, turn the middle point into an
  explicit parameter so that we don't have to state integrability before rewriting.

  In the next lemma, the assumption on `f` is a bit lazy but we will need it only for continuous
  functions anyway.
  -/

lemma interval_integral_periodic {f : â„ â†’ E} {T : â„} (hf_per : periodic f T)
  (hf : âˆ€ s t, interval_integrable f volume s t)
  (a : â„) : âˆ« t in a..(a + T), f t = âˆ« t in 0..T, f t :=
begin
  rw [â† interval_integral.integral_add_adjacent_intervals (hf a 0) (hf 0 $ a + T),
      â† interval_integral.integral_add_adjacent_intervals (hf 0 T) (hf T $ a+T),
      integral_comp_add_right',
      interval_integral.integral_symm, funext (Î» t, hf_per t)],
  abel
end
end
