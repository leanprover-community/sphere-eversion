import Mathlib.Topology.Separation.Hausdorff
import Mathlib.Data.Real.Basic
import Mathlib.Order.Interval.Finset.Nat

open scoped Topology

open Set Function

/-- We could generalise and replace `Î¹ Ã— â„` with a dependent family of types but it doesn't seem
worth it. Proof partly based on `refinement_of_locallyCompact_sigmaCompact_of_nhds_basis_set`. -/
theorem exists_countable_locallyFinite_cover {Î¹ X : Type*} [TopologicalSpace X] [T2Space X]
    [LocallyCompactSpace X] [SigmaCompactSpace X] {c : Î¹ â†’ X} {W : Î¹ â†’ â„ â†’ Set X}
    {B : Î¹ â†’ â„ â†’ Set X} {p : Î¹ â†’ â„ â†’ Prop} (hc : Surjective c) (hWâ‚€ : âˆ€ i r, p i r â†’ c i âˆˆ W i r)
    (hWâ‚ : âˆ€ i r, p i r â†’ IsOpen (W i r)) (hB : âˆ€ i, (ğ“ (c i)).HasBasis (p i) (B i)) :
    âˆƒ s : Set (Î¹ Ã— â„), s.Countable âˆ§ (âˆ€ z âˆˆ s, (â†¿p) z) âˆ§ (â‹ƒ z âˆˆ s, (â†¿W) z) = univ âˆ§
      LocallyFinite (â†¿B âˆ˜ ((â†‘) : s â†’ Î¹ Ã— â„)) := by
  let K' := CompactExhaustion.choice X
  let K := K'.shiftr.shiftr
  let C : â„• â†’ Set X := fun n â†¦ K (n + 2) \ interior (K (n + 1))
  let U : â„• â†’ Set X := fun n â†¦ interior (K (n + 3)) \ K n
  have hCU : âˆ€ n, C n âŠ† U n := fun n x hx â†¦
    âŸ¨K.subset_interior_succ _ hx.1, mt (fun hxâ‚ƒ â†¦ K.subset_interior_succ _ hxâ‚ƒ) hx.2âŸ©
  have hC : âˆ€ n, IsCompact (C n) := fun n â†¦ (K.isCompact _).diff isOpen_interior
  have hC' : (â‹ƒ n, C n) = univ := by
    refine Set.univ_subset_iff.mp fun x _ â†¦ mem_iUnion.mpr âŸ¨K'.find x, ?_âŸ©
    simpa only [K'.find_shiftr] using
      diff_subset_diff_right interior_subset (K'.shiftr.mem_diff_shiftr_find x)
  have hU : âˆ€ n, IsOpen (U n) := fun n â†¦
    isOpen_interior.sdiff <| IsCompact.isClosed <| K.isCompact _
  have hU' : âˆ€ n, {m | (U m âˆ© U n).Nonempty}.Finite := fun n â†¦ by
    suffices {m | (U m âˆ© U n).Nonempty} âŠ† Icc (n - 2) (n + 2) by exact (finite_Icc _ _).subset this
    rintro m âŸ¨x, âŸ¨âŸ¨hxâ‚, hxâ‚‚âŸ©, âŸ¨hxâ‚ƒ, hxâ‚„âŸ©âŸ©âŸ©
    simp only [mem_Icc, tsub_le_iff_right]
    suffices âˆ€ {a b : â„•}, x âˆ‰ K a â†’ x âˆˆ interior (K b.succ) â†’ a â‰¤ b by
      exact âŸ¨this hxâ‚„ hxâ‚, this hxâ‚‚ hxâ‚ƒâŸ©
    intro a b ha hb
    by_contra hab
    replace hab : b + 1 â‰¤ a := by simpa using hab
    exact Set.Nonempty.ne_empty (âŸ¨x, interior_subset hb, haâŸ© : (K b.succ \ K a).Nonempty)
      (Set.diff_eq_empty.mpr (K.subset hab))
  have hU'' : âˆ€ n x, x âˆˆ C n â†’ U n âˆˆ ğ“ x := fun n x hx â†¦
    mem_nhds_iff.mpr âŸ¨U n, Subset.rfl, hU n, hCU n hxâŸ©
  have : âˆ€ (n) (x : C n), âˆƒ i r, â†‘x âˆˆ W i r âˆ§ B i r âŠ† U n âˆ§ p i r := fun n âŸ¨x, hxâŸ© â†¦ by
    obtain âŸ¨i, rflâŸ© := hc x
    obtain âŸ¨r, hrâ‚, hrâ‚‚âŸ© := (hB i).mem_iff.mp (hU'' n _ hx)
    exact âŸ¨i, r, hWâ‚€ i r hrâ‚, hrâ‚‚, hrâ‚âŸ©
  choose i r hâ‚ hâ‚‚ hâ‚ƒ using fun n â†¦ this n
  let V : âˆ€ n, C n â†’ Set X := fun n x â†¦ W (i n x) (r n x)
  have hVâ‚ : âˆ€ n x, IsOpen (V n x) := fun n x â†¦ hWâ‚ _ _ (hâ‚ƒ n x)
  have hVâ‚‚ : âˆ€ n, C n âŠ† â‹ƒ x : C n, V n x := fun n x hx â†¦ mem_iUnion.mpr âŸ¨âŸ¨x, hxâŸ©, hâ‚ _ _âŸ©
  choose f hf using fun n â†¦ (hC n).elim_finite_subcover (V n) (hVâ‚ n) (hVâ‚‚ n)
  classical
  let s : Set (Î¹ Ã— â„) := â‹ƒ n, (f n).image (Pi.prod (i n) (r n))
  refine âŸ¨s, countable_iUnion fun n â†¦ Finset.countable_toSet _, fun z hz â†¦ ?_,
    Set.univ_subset_iff.mp fun x _ â†¦ ?_, fun x â†¦ ?_âŸ©
  Â· simp only [Pi.prod, mem_iUnion, Finset.coe_image, mem_image, Finset.mem_coe,
      SetCoe.exists, s] at hz
    obtain âŸ¨n, x, hx, -, rflâŸ© := hz
    apply hâ‚ƒ
  Â· obtain âŸ¨n, hnâŸ© := iUnion_eq_univ_iff.mp hC' x
    specialize hf n hn
    simp only [iUnion_coe_set, mem_iUnion, exists_prop] at hf
    obtain âŸ¨y, hyâ‚, hyâ‚‚, hyâ‚ƒâŸ© := hf
    simp only [Pi.prod, mem_iUnion, Finset.mem_coe, Finset.mem_image, exists_prop, SetCoe.exists,
      iUnion_exists, exists_and_right, Prod.exists, Prod.mk_inj, s]
    exact âŸ¨i n âŸ¨y, hyâ‚âŸ©, r n âŸ¨y, hyâ‚âŸ©, âŸ¨n, y, hyâ‚, hyâ‚‚, rfl, rflâŸ©, hyâ‚ƒâŸ©
  Â· obtain âŸ¨n, hnâŸ© := iUnion_eq_univ_iff.mp hC' x
    refine âŸ¨U n, hU'' n x hn, ?_âŸ©
    let P : Î¹ Ã— â„ â†’ Prop := fun z â†¦ ((â†¿B) (z : Î¹ Ã— â„) âˆ© U n).Nonempty
    erw [(Equiv.Set.sep s P).symm.set_finite_iff]
    simp only [Set.iUnion_inter, â† inter_setOf_eq_sep, s]
    refine  (hU' n).iUnion (fun m _ â†¦ Set.toFinite _) fun m hm â†¦ ?_
    rw [Set.eq_empty_iff_forall_notMem]
    intro z
    simp only [Pi.prod, Finset.coe_image, mem_inter_iff, mem_image, Finset.mem_coe, SetCoe.exists,
      mem_setOf_eq, not_and, existsâ‚‚_imp, and_imp]
    rintro x hxâ‚ - rfl
    rw [Set.not_nonempty_iff_eq_empty]
    have := Set.inter_subset_inter_left (U n) (hâ‚‚ m âŸ¨x, hxâ‚âŸ©)
    rwa [Set.not_nonempty_iff_eq_empty.mp hm, Set.subset_empty_iff] at this
