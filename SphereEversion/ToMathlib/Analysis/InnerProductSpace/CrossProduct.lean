/-
Copyright (c) 2022 Heather Macbeth. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Heather Macbeth

! This file was ported from Lean 3 source module to_mathlib.analysis.inner_product_space.cross_product
-/
import Mathlib.Analysis.InnerProductSpace.Dual
import Mathlib.Analysis.InnerProductSpace.Orientation
import Mathlib.LinearAlgebra.Alternating.Curry

/-! # The cross-product on an oriented real inner product space of dimension three -/

noncomputable section

open scoped RealInnerProductSpace

open Module

set_option synthInstance.checkSynthOrder false
attribute [local instance] FiniteDimensional.of_fact_finrank_eq_succ
set_option synthInstance.checkSynthOrder true

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E]

/-- The identification of a finite-dimensional inner product space with its algebraic dual. -/
private def to_dual [FiniteDimensional â„ E] : E â‰ƒâ‚—[â„] E â†’â‚—[â„] â„ :=
  (InnerProductSpace.toDual â„ E).toLinearEquiv â‰ªâ‰«â‚— LinearMap.toContinuousLinearMap.symm

namespace Orientation

variable {E}
variable [Fact (finrank â„ E = 3)] (Ï‰ : Orientation â„ E (Fin 3))

/-- Linear map from `E` to `E â†’â‚—[â„] E` constructed from a 3-form `Î©` on `E` and an identification of
`E` with its dual.  Effectively, the Hodge star operation.  (Under appropriate hypotheses it turns
out that the image of this map is in `ğ”°ğ”¬(E)`, the skew-symmetric operators, which can be identified
with `Î›Â²E`.) -/
def crossProduct : E â†’â‚—[â„] E â†’â‚—[â„] E := by
  let z : AlternatingMap â„ E â„ (Fin 0) â‰ƒâ‚—[â„] â„ :=
    AlternatingMap.constLinearEquivOfIsEmpty.symm
  let y : AlternatingMap â„ E â„ (Fin 1) â†’â‚—[â„] E â†’â‚—[â„] â„ :=
    LinearMap.llcomp â„ E (AlternatingMap â„ E â„ (Fin 0)) â„ z âˆ˜â‚— AlternatingMap.curryLeftLinearMap
  let y' : AlternatingMap â„ E â„ (Fin 1) â†’â‚—[â„] E :=
    (LinearMap.llcomp â„ (AlternatingMap â„ E â„ (Fin 1)) (E â†’â‚—[â„] â„) E (to_dual E).symm) y
  let u : AlternatingMap â„ E â„ (Fin 2) â†’â‚—[â„] E â†’â‚—[â„] E :=
    LinearMap.llcomp â„ E (AlternatingMap â„ E â„ (Fin 1)) _ y' âˆ˜â‚— AlternatingMap.curryLeftLinearMap
  exact u âˆ˜â‚— AlternatingMap.curryLeftLinearMap (n := 2) Ï‰.volumeForm

local infixl:100 "Ã—â‚ƒ" => Ï‰.crossProduct

theorem crossProduct_apply_self (v : E) : vÃ—â‚ƒv = 0 := by simp [crossProduct]

theorem inner_crossProduct_apply (u v w : E) : âŸªuÃ—â‚ƒv, wâŸ« = Ï‰.volumeForm ![u, v, w] := by
  simp only [crossProduct, to_dual, LinearEquiv.trans_symm, LinearEquiv.symm_symm,
    Nat.succ_eq_add_one, Nat.reduceAdd, AlternatingMap.curryLeftLinearMap_apply, LinearMap.coe_comp,
    Function.comp_apply, LinearMap.llcomp_apply, LinearEquiv.coe_coe, LinearEquiv.trans_apply,
    LinearIsometryEquiv.coe_symm_toLinearEquiv]
  rw [InnerProductSpace.toDual_symm_apply]
  simp

theorem inner_crossProduct_apply_self (u : E) (v : (â„ âˆ™ u)á—®) : âŸªuÃ—â‚ƒv, uâŸ« = 0 := by
  rw [Ï‰.inner_crossProduct_apply u v u]
  exact Ï‰.volumeForm.map_eq_zero_of_eq ![u, v, u] (by simp) (by norm_num; decide : (0 : Fin 3) â‰  2)

theorem inner_crossProduct_apply_apply_self (u : E) (v : (â„ âˆ™ u)á—®) : âŸªuÃ—â‚ƒv, vâŸ« = 0 := by
  rw [Ï‰.inner_crossProduct_apply u v v]
  exact Ï‰.volumeForm.map_eq_zero_of_eq ![u, v, v] (by simp) (by norm_num; decide : (1 : Fin 3) â‰  2)

/-- The map `crossProduct`, upgraded from linear to continuous-linear; useful for calculus. -/
def crossProduct' : E â†’L[â„] E â†’L[â„] E :=
  LinearMap.toContinuousLinearMap
    (â†‘(LinearMap.toContinuousLinearMap : (E â†’â‚—[â„] E) â‰ƒâ‚—[â„] E â†’L[â„] E) âˆ˜â‚— Ï‰.crossProduct)

@[simp]
theorem crossProduct'_apply (v : E) :
    Ï‰.crossProduct' v = LinearMap.toContinuousLinearMap (Ï‰.crossProduct v) :=
  rfl

theorem norm_crossProduct (u : E) (v : (â„ âˆ™ u)á—®) : â€–uÃ—â‚ƒvâ€– = â€–uâ€– * â€–vâ€– := by
  classical
  refine le_antisymm ?_ ?_
  Â· obtain (h | h) := eq_or_lt_of_le (norm_nonneg (uÃ—â‚ƒv))
    Â· rw [â† h]
      positivity
    Â· refine le_of_mul_le_mul_right ?_ h
      rw [â† real_inner_self_eq_norm_mul_norm]
      simpa [inner_crossProduct_apply, Fin.mk_zero, Fin.prod_univ_succ, Matrix.cons_val_zero,
        mul_assoc] using Ï‰.volumeForm_apply_le ![u, v, uÃ—â‚ƒv]
  let K : Submodule â„ E := Submodule.span â„ ({u, â†‘v} : Set E)
  have : Nontrivial Ká—® := by
    apply Module.nontrivial_of_finrank_pos (R := â„)
    have : finrank â„ K â‰¤ Finset.card {u, (v : E)} := by
      simpa [Set.toFinset_singleton] using finrank_span_le_card ({u, â†‘v} : Set E)
    have : Finset.card {u, (v : E)} â‰¤ Finset.card {(v : E)} + 1 := Finset.card_insert_le u {â†‘v}
    have : Finset.card {(v : E)} = 1 := Finset.card_singleton (v : E)
    have : finrank â„ K + finrank â„ Ká—® = finrank â„ E := K.finrank_add_finrank_orthogonal
    have : finrank â„ E = 3 := Fact.out
    linarith
  obtain âŸ¨w, hwâŸ© : âˆƒ w : Ká—®, w â‰  0 := exists_ne 0
  have H : Pairwise fun i j â†¦ âŸª![u, v, w] i, ![u, v, w] jâŸ« = 0 := by
    intro i j hij
    have h1 : âŸªu, vâŸ« = 0 := v.2 _ (Submodule.mem_span_singleton_self _)
    have h2 : âŸª(v : E), wâŸ« = 0 := w.2 _ (Submodule.subset_span (by simp))
    have h3 : âŸªu, wâŸ« = 0 := w.2 _ (Submodule.subset_span (by simp))
    fin_cases i <;> fin_cases j <;> norm_num at hij <;> simp [h1, h2, h3] <;>
        rw [real_inner_comm] <;>
      assumption
  refine le_of_mul_le_mul_right ?_ (by rwa [norm_pos_iff] : 0 < â€–wâ€–)
  -- Cauchy-Schwarz inequality for `u Ã—â‚ƒ v` and `w`
  simpa [inner_crossProduct_apply, Ï‰.abs_volumeForm_apply_of_pairwise_orthogonal H,
    Fin.prod_univ_succ, Fintype.univ_ofSubsingleton, Matrix.cons_val_fin_one, Matrix.cons_val_succ,
    mul_assoc] using abs_real_inner_le_norm (uÃ—â‚ƒv) w

theorem isometry_on_crossProduct (u : Metric.sphere (0 : E) 1) (v : (â„ âˆ™ (u : E))á—®) :
    â€–uÃ—â‚ƒvâ€– = â€–vâ€– := by simp [norm_crossProduct]

end Orientation
