import Mathlib.Analysis.InnerProductSpace.Projection

noncomputable section

open scoped RealInnerProductSpace Topology

open Submodule Function Set Filter

section GeneralStuff

-- Things in this section go to other files
@[simp]
theorem forall_mem_span_singleton {R M : Type*} [CommRing R] [AddCommGroup M] [Module R M]
    (P : M â†’ Prop) (u : M) : (âˆ€ x âˆˆ span R ({u} : Set M), P x) â†” âˆ€ t : R, P (t â€¢ u) := by
  simp [mem_span_singleton]

open scoped Pointwise

@[simp]
theorem Field.exists_unit {ğ•œ : Type*} [Field ğ•œ] (P : ğ•œ â†’ Prop) :
    (âˆƒ u : ğ•œË£, P u) â†” âˆƒ u : ğ•œ, u â‰  0 âˆ§ P u := by
  constructor
  Â· rintro âŸ¨u, huâŸ©
    exact âŸ¨u, u.ne_zero, huâŸ©
  Â· rintro âŸ¨u, u_ne, huâŸ©
    exact âŸ¨Units.mk0 u u_ne, huâŸ©

theorem span_singleton_eq_span_singleton_of_ne {ğ•œ : Type*} [Field ğ•œ] {M : Type*} [AddCommGroup M]
    [Module ğ•œ M] {u v : M} (hu : u â‰  0) (hu' : u âˆˆ span ğ•œ ({v} : Set M)) :
    span ğ•œ ({u} : Set M) = span ğ•œ ({v} : Set M) := by
  rcases mem_span_singleton.mp hu' with âŸ¨a, rflâŸ©
  by_cases hv : v = 0
  Â· subst hv
    simp
  have : a â‰  0 := by
    rintro rfl
    exact hu (zero_smul ğ•œ v)
  symm
  erw [Submodule.span_singleton_eq_span_singleton, Field.exists_unit fun z : ğ•œ â†¦ z â€¢ v = a â€¢ v]
  use a, this

end GeneralStuff

variable {E : Type*} [NormedAddCommGroup E] [InnerProductSpace â„ E] --[CompleteSpace E]

theorem LinearIsometryEquiv.apply_ne_zero {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
    {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] (Ï† : E â‰ƒâ‚—áµ¢â‹†[â„] F)
    {x : E} (hx : x â‰  0) : Ï† x â‰  0 := by
  refine fun H â†¦ hx ?_
  rw [â† Ï†.symm_apply_apply x, H, Ï†.symm.map_zero]

/-- The line (one-dimensional submodule of `E`) spanned by `x : E`. -/
@[reducible] def spanLine (x : E) : Submodule â„ E := Submodule.span â„ ({x} : Set E)

@[inherit_doc] local notation "Î”" => spanLine

/-- The orthogonal complement of the line spanned by `x : E`. -/
@[reducible] def spanOrthogonal (x : E) : Submodule â„ E := (Î” x)á—®

/-- The orthogonal projection to the complement of `span x`. -/
@[reducible] def projSpanOrthogonal (x : E) :=
  orthogonalProjection (Submodule.span â„ ({x} : Set E))á—®

@[inherit_doc] local notation "{." x "}á—®" => spanOrthogonal x

@[inherit_doc] local notation "pr[" x "]á—®" => projSpanOrthogonal x

variable (u v : E)

theorem orthogonal_line_inf {u v : E} : {.u}á—® âŠ“ {.v}á—® = {.(pr[v]á—® u : E)}á—® âŠ“ {.v}á—® := by
  rw [inf_orthogonal, inf_orthogonal]
  refine congr_arg _ (le_antisymm (sup_le ?_ le_sup_right) (sup_le ?_ le_sup_right)) <;>
    rw [span_singleton_le_iff_mem]
  Â· nth_rw 2 [â† orthogonalProjection_add_orthogonalProjection_orthogonal (Î” v) u]
    exact add_mem (mem_sup_right <| coe_mem _) (mem_sup_left <| mem_span_singleton_self _)
  Â· rw [projSpanOrthogonal, orthogonalProjection_orthogonal]
    exact sub_mem (mem_sup_left <| mem_span_singleton_self _) (mem_sup_right <| coe_mem _)

theorem orthogonal_line_inf_sup_line (u v : E) : {.u}á—® âŠ“ {.v}á—® âŠ” Î” (pr[v]á—® u : E) = {.v}á—® := by
  rw [orthogonal_line_inf, sup_comm, sup_orthogonal_inf_of_completeSpace]
  rw [span_singleton_le_iff_mem]
  exact coe_mem _

theorem orthogonalProjection_eq_zero_of_mem {F : Submodule â„ E} [CompleteSpace F] {x : E}
    (h : x âˆˆ Fá—®) : orthogonalProjection F x = 0 := by
  refine Subtype.coe_injective (eq_orthogonalProjection_of_mem_of_inner_eq_zero F.zero_mem ?_)
  simp only [coe_zero, sub_zero]
  exact (mem_orthogonal' F x).mp h

theorem inner_projection_self_eq_zero_iff {F : Submodule â„ E} [CompleteSpace F] {x : E} :
    âŸªx, orthogonalProjection F xâŸ« = 0 â†” x âˆˆ Fá—® := by
  obtain âŸ¨y, hy, z, hz, rflâŸ© := F.exists_add_mem_mem_orthogonal x
  rw [inner_add_left, map_add, coe_add, inner_add_right, inner_add_right]
  suffices y = 0 â†” y + z âˆˆ Fá—® by
    simpa [orthogonalProjection_eq_zero_of_mem hz, orthogonalProjection_eq_self_iff.mpr hy,
      inner_eq_zero_symm.mp (hz y hy)]
  rw [add_mem_cancel_right hz]
  constructor
  Â· rintro rfl
    exact Fá—®.zero_mem
  Â· exact Submodule.disjoint_def.mp (orthogonal_disjoint F) _ hy

variable {xâ‚€ x : E}

@[simp]
theorem mem_orthogonal_span_singleton_iff {xâ‚€ x : E} : x âˆˆ {.xâ‚€}á—® â†” âŸªxâ‚€, xâŸ« = 0 := by
  simp only [mem_orthogonal, forall_mem_span_singleton, inner_smul_left, RCLike.conj_to_real,
    mul_eq_zero]
  refine âŸ¨fun h â†¦ by simpa using h 1, fun h t â†¦ Or.inr hâŸ©

@[simp]
theorem orthogonalProjection_orthogonal_singleton {x y : E} :
    pr[x]á—® y =
      âŸ¨y - (âŸªx, yâŸ« / âŸªx, xâŸ«) â€¢ x, by
        rcases eq_or_ne x 0 with (rfl | hx)
        Â· simp
        simp [mem_orthogonal_span_singleton_iff]
        rw [inner_sub_right, inner_smul_right]
        field_simp [inner_self_ne_zero.mpr hx]âŸ© := by
  apply Subtype.ext
  have := orthogonalProjection_add_orthogonalProjection_orthogonal (span â„ ({x} : Set E)) y
  simp [eq_sub_of_add_eq' this, orthogonalProjection_singleton, real_inner_self_eq_norm_sq]

theorem coe_orthogonalProjection_orthogonal_singleton {x y : E} :
    (pr[x]á—® y : E) = y - (âŸªx, yâŸ« / âŸªx, xâŸ«) â€¢ x := by
  rw [orthogonalProjection_orthogonal_singleton]

theorem foo {xâ‚€ x : E} (h : âŸªxâ‚€, xâŸ« â‰  0) (y : E) (hy : y âˆˆ {.xâ‚€}á—®) :
    (pr[x]á—® y : E) - (âŸªxâ‚€, pr[x]á—® yâŸ« / âŸªxâ‚€, xâŸ«) â€¢ x = y :=  by
  conv_rhs => rw [â† orthogonalProjection_add_orthogonalProjection_orthogonal (Î” x) y]
  rw [orthogonalProjection_singleton, sub_eq_add_neg, add_comm, â† neg_smul]
  congr 2
  have := orthogonalProjection_add_orthogonalProjection_orthogonal (Î” x) y
  rw [orthogonalProjection_singleton] at this
  apply_fun fun z â†¦ âŸªxâ‚€, zâŸ« at this
  rw [mem_orthogonal_span_singleton_iff.mp hy, inner_add_right, inner_smul_right] at this
  apply (eq_of_sub_eq_zero _).symm
  rw [sub_neg_eq_add]
  apply mul_left_injectiveâ‚€ h
  dsimp only
  rwa [add_mul, MulZeroClass.zero_mul, div_mul_cancelâ‚€ _ h]

attribute [fun_prop] Continuous.inner

/-- Given two non-orthogonal vectors in an inner product space,
`orthogonal_projection_orthogonal_line_iso` is the continuous linear equivalence between their
orthogonal complements obtained from orthogonal projection. -/
def orthogonalProjectionOrthogonalLineIso {xâ‚€ x : E} (h : âŸªxâ‚€, xâŸ« â‰  0) : {.xâ‚€}á—® â‰ƒL[â„] {.x}á—® :=
  {
    pr[x]á—®.comp (subtypeL {.xâ‚€}á—®) with
    invFun := fun y â†¦
      âŸ¨y - (âŸªxâ‚€, yâŸ« / âŸªxâ‚€, xâŸ«) â€¢ x, by
        rw [mem_orthogonal_span_singleton_iff, inner_sub_right, inner_smul_right]
        field_simp [h]âŸ©
    left_inv := by
      rintro âŸ¨y, hyâŸ©
      ext
      exact foo h y hy
    right_inv := by
      rintro âŸ¨y, hyâŸ©
      ext
      dsimp
      rw [map_sub, pr[x]á—®.map_smul, orthogonalProjection_orthogonalComplement_singleton_eq_zero,
        smul_zero, sub_zero, orthogonalProjection_eq_self_iff.mpr hy]
    continuous_toFun := (pr[x]á—®.comp (subtypeL {.xâ‚€}á—®)).continuous
    continuous_invFun := by fun_prop }

theorem orthogonalProjection_comp_coe (K : Submodule â„ E) [CompleteSpace K] :
    orthogonalProjection K âˆ˜ ((â†‘) : K â†’ E) = id := by
  ext1 x
  exact orthogonalProjection_mem_subspace_eq_self x

variable (E)

-- Is this really missing??
theorem NormedSpace.continuousAt_iff {E F : Type*} [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup F] (f : E â†’ F) (x : E) :
    ContinuousAt f x â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y, â€–y - xâ€– < Î´ â†’ â€–f y - f xâ€– < Îµ := by
  simp_rw [Metric.continuousAt_iff, dist_eq_norm]

theorem NormedSpace.continuousAt_iff' {E F : Type*} [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup F] (f : E â†’ F) (x : E) :
    ContinuousAt f x â†” âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y, â€–y - xâ€– â‰¤ Î´ â†’ â€–f y - f xâ€– â‰¤ Îµ := by
  rw [NormedSpace.continuousAt_iff]
  constructor <;> intro h Îµ Îµ_pos
  Â· rcases h Îµ Îµ_pos with âŸ¨Î·, Î·_pos, hÎ·âŸ©
    refine âŸ¨Î· / 2, half_pos Î·_pos, fun h hy â†¦ le_of_lt (hÎ·  _ ?_)âŸ©
    linarith
  Â· rcases h (Îµ / 2) (half_pos Îµ_pos) with âŸ¨Î´, Î´_pos, hÎ´âŸ©
    refine âŸ¨Î´, Î´_pos, fun y hy â†¦ ?_âŸ©
    linarith [hÎ´ y (by linarith)]

-- Is this really missing??
theorem NormedSpace.continuous_iff {E F : Type*} [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup F] (f : E â†’ F) :
    Continuous f â†” âˆ€ x, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y, â€–y - xâ€– < Î´ â†’ â€–f y - f xâ€– < Îµ := by
  simp_rw [Metric.continuous_iff, dist_eq_norm]

theorem NormedSpace.continuous_iff' {E F : Type*} [SeminormedAddCommGroup E]
    [SeminormedAddCommGroup F] (f : E â†’ F) :
    Continuous f â†” âˆ€ x, âˆ€ Îµ > 0, âˆƒ Î´ > 0, âˆ€ y, â€–y - xâ€– â‰¤ Î´ â†’ â€–f y - f xâ€– â‰¤ Îµ := by
  simp_rw [continuous_iff_continuousAt, NormedSpace.continuousAt_iff']

variable {E}

attribute [fun_prop] continuous_norm' continuous_norm

@[continuity, fun_prop]
theorem continuousAt_orthogonalProjection_orthogonal {xâ‚€ : E} (hxâ‚€ : xâ‚€ â‰  0) :
    ContinuousAt (fun x : E â†¦ {.x}á—®.subtypeL.comp pr[x]á—®) xâ‚€ := by
  rw [NormedSpace.continuousAt_iff']
  intro Îµ Îµ_pos
  have hNxâ‚€ : 0 < â€–xâ‚€â€– := norm_pos_iff.mpr hxâ‚€
  have hNxâ‚€2 : 0 < â€–xâ‚€â€– ^ 2 := by apply pow_pos hNxâ‚€
  suffices
    âˆƒ Î´ > 0, âˆ€ y, â€–y - xâ‚€â€– â‰¤ Î´ â†’ âˆ€ x, â€–(âŸªxâ‚€, xâŸ« / âŸªxâ‚€, xâ‚€âŸ«) â€¢ xâ‚€ - (âŸªy, xâŸ« / âŸªy, yâŸ«) â€¢ yâ€– â‰¤ Îµ * â€–xâ€–
    by
    simpa only [ContinuousLinearMap.opNorm_le_iff (le_of_lt Îµ_pos),
      orthogonalProjection_orthogonal_singleton, ContinuousLinearMap.coe_sub',
      ContinuousLinearMap.coe_comp', coe_subtypeL', Submodule.coe_subtype, Pi.sub_apply, comp_apply,
      coe_mk, sub_sub_sub_cancel_left]
  let N : E â†’ E := fun x â†¦ âŸªx, xâŸ«â»Â¹ â€¢ x
  have hNxâ‚€ : 0 < â€–N xâ‚€â€– := by
    -- and now let's suffer
    rw [norm_smul, real_inner_self_eq_norm_sq, norm_inv]
    exact mul_pos (inv_pos_of_pos (norm_pos_iff.mpr hNxâ‚€2.ne')) hNxâ‚€
  have cont : ContinuousAt N xâ‚€ := by
    simp_rw [N, real_inner_self_eq_norm_sq]
    fun_prop (disch := exact hNxâ‚€2.ne')
  have lim : Tendsto (fun y â†¦ â€–N xâ‚€ - N yâ€– * â€–yâ€–) (ğ“ xâ‚€) (ğ“ 0) := by
    rw [â† MulZeroClass.zero_mul â€–xâ‚€â€–]
    apply Tendsto.mul
    rw [â† show â€–N xâ‚€ - N xâ‚€â€– = 0 by simp]
    exact (tendsto_const_nhds.sub cont).norm
    exact continuous_norm.continuousAt
  have key :
    âˆ€ x y,
      (âŸªxâ‚€, xâŸ« / âŸªxâ‚€, xâ‚€âŸ«) â€¢ xâ‚€ - (âŸªy, xâŸ« / âŸªy, yâŸ«) â€¢ y =
        âŸªN xâ‚€, xâŸ« â€¢ (xâ‚€ - y) + âŸªN xâ‚€ - N y, xâŸ« â€¢ y := by
    intro x y
    simp only [N, inner_smul_left, inner_sub_left, RCLike.conj_to_real, smul_sub, sub_smul]
    field_simp
  simp only [key]
  simp_rw [Metric.tendsto_nhds_nhds, Real.dist_0_eq_abs, dist_eq_norm] at lim
  rcases lim (Îµ / 2) (half_pos Îµ_pos) with âŸ¨Î·, Î·_pos, hÎ·âŸ©
  refine âŸ¨min (Îµ / 2 / â€–N xâ‚€â€–) (Î· / 2), by positivity, ?_âŸ©
  intro y hy x
  have hyâ‚ := hy.trans (min_le_left _ _); have hyâ‚‚ := hy.trans (min_le_right _ _); clear hy
  specialize hÎ· (by linarith : â€–y - xâ‚€â€– < Î·)
  rw [abs_of_nonneg (by positivity)] at hÎ·
  calc â€–âŸªN xâ‚€, xâŸ« â€¢ (xâ‚€ - y) + âŸªN xâ‚€ - N y, xâŸ« â€¢ yâ€–
      _ â‰¤ â€–âŸªN xâ‚€, xâŸ« â€¢ (xâ‚€ - y)â€– + â€–âŸªN xâ‚€ - N y, xâŸ« â€¢ yâ€– := norm_add_le _ _
      _ â‰¤ â€–N xâ‚€â€– * â€–xâ€– * â€–xâ‚€ - yâ€– + â€–N xâ‚€ - N yâ€– * â€–xâ€– * â€–yâ€– := (add_le_add ?_ ?_)
      _ â‰¤ Îµ / 2 * â€–xâ€– + Îµ / 2 * â€–xâ€– := (add_le_add ?_ ?_)
      _ = Îµ * â€–xâ€– := by linarith
  Â· rw [norm_smul]
    gcongr
    exact norm_inner_le_norm _ _
  Â· rw [norm_smul]
    gcongr
    exact norm_inner_le_norm _ _
  Â· rw [mul_comm, â† mul_assoc, norm_sub_rev]
    gcongr
    exact (le_div_iffâ‚€ hNxâ‚€).mp hyâ‚
  Â· rw [mul_comm, â† mul_assoc, mul_comm â€–yâ€–]
    gcongr
