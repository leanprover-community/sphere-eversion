import SphereEversion.ToMathlib.Partition
import Mathlib.Geometry.Manifold.Notation

noncomputable section

open scoped Topology Manifold ContDiff

open Set Function Filter

section

variable {Î¹ : Type*}

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E] {H : Type*}
  [TopologicalSpace H] {I : ModelWithCorners â„ E H} {M : Type*} [TopologicalSpace M]
  [ChartedSpace H M] [IsManifold I âˆ M] [SigmaCompactSpace M] [T2Space M]

section

variable {F : Type*} [AddCommGroup F] [Module â„ F]

theorem exists_of_convex {P : (Î£ x : M, Germ (ğ“ x) F) â†’ Prop}
    (hP : âˆ€ x : M, ReallyConvex (smoothGerm I x) {Ï† | P âŸ¨x, Ï†âŸ©})
    (hP' : âˆ€ x : M, âˆƒ f : M â†’ F, âˆ€á¶  x' in ğ“ x, P âŸ¨x', fâŸ©) : âˆƒ f : M â†’ F, âˆ€ x, P âŸ¨x, fâŸ© := by
  replace hP' : âˆ€ x : M, âˆƒ f : M â†’ F, âˆƒ U âˆˆ ğ“ x, âˆ€ x' âˆˆ U, P âŸ¨x', fâŸ© := by
    intro x
    rcases hP' x with âŸ¨f, hfâŸ©
    exact âŸ¨f, {x' | P âŸ¨x', â†‘fâŸ©}, hf, fun _ â†¦ idâŸ©
  choose Ï† U hU hÏ† using hP'
  rcases SmoothBumpCovering.exists_isSubordinate I isClosed_univ fun x _ â†¦ hU x with âŸ¨Î¹, b, hbâŸ©
  let Ï := b.toSmoothPartitionOfUnity
  refine âŸ¨fun x : M â†¦ âˆ‘á¶  i, Ï i x â€¢ Ï† (b.c i) x, fun xâ‚€ â†¦ ?_âŸ©
  let g : Î¹ â†’ Germ (ğ“ xâ‚€) F := fun i â†¦ Ï† (b.c i)
  have :
    (fun x : M â†¦ âˆ‘á¶  i, Ï i x â€¢ Ï† (b.c i) x : Germ (ğ“ xâ‚€) F) âˆˆ
      reallyConvexHull (smoothGerm I xâ‚€) (g '' Ï.fintsupport xâ‚€) :=
    Ï.germ_combine_mem fun i x â†¦ Ï† (b.c i) x
  simp_rw [reallyConvex_iff_hull] at hP
  apply hP xâ‚€; clear hP
  have H : g '' (Ï.fintsupport xâ‚€) âŠ† {Ï† : (ğ“ xâ‚€).Germ F | P âŸ¨xâ‚€, Ï†âŸ©} := by
    rintro _ âŸ¨i, hi, rflâŸ©
    exact hÏ† _ _ (hb.toSmoothPartitionOfUnity  i <| (Ï.mem_fintsupport_iff _ i).mp hi)
  exact reallyConvexHull_mono H this

end

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

variable {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G] {HG : Type*} [TopologicalSpace HG]
  (IG : ModelWithCorners â„ G HG) {N : Type*} [TopologicalSpace N] [ChartedSpace HG N]
  [IsManifold IG âˆ N]

variable (I)

omit [FiniteDimensional â„ E] [IsManifold I âˆ M] [SigmaCompactSpace M] [T2Space M] in
theorem reallyConvex_contMDiffAt (x : M) (n : â„•âˆ) :
    ReallyConvex (smoothGerm I x) {Ï† : Germ (ğ“ x) F | Ï†.ContMDiffAt I n} := by
  classical
  rw [Nontrivial.reallyConvex_iff]
  rintro w _w_pos w_supp w_sum
  have : (support w).Finite := support_finite_of_finsum_eq_one w_sum
  set fin_supp := this.toFinset with H
  have : (support fun i : (ğ“ x).Germ F â†¦ w i â€¢ i) âŠ† fin_supp := by
    rw [Set.Finite.coe_toFinset]
    exact support_smul_subset_left w id
  rw [finsum_eq_sum_of_support_subset _ this]
  apply Filter.Germ.ContMDiffAt.sum fun Ï† hÏ† â†¦ (smoothGerm.contMDiffAt _).smul (w_supp ?_)
  simpa [H] using hÏ†

theorem exists_contMDiff_of_convex {P : M â†’ F â†’ Prop} (hP : âˆ€ x, Convex â„ {y | P x y}) {n : â„•âˆ}
    (hP' : âˆ€ x : M, âˆƒ U âˆˆ ğ“ x, âˆƒ f : M â†’ F, CMDiff[U] n f âˆ§ âˆ€ x âˆˆ U, P x (f x)) :
    âˆƒ f : M â†’ F, CMDiff n f âˆ§ âˆ€ x, P x (f x) := by
  let PP : (Î£ x : M, Germ (ğ“ x) F) â†’ Prop := fun p â†¦ p.2.ContMDiffAt I n âˆ§ P p.1 p.2.value
  have hPP : âˆ€ x : M, ReallyConvex (smoothGerm I x) {Ï† | PP âŸ¨x, Ï†âŸ©} := fun x â†¦ by
    apply ReallyConvex.inter
    apply reallyConvex_contMDiffAt
    let v : Germ (ğ“ x) F â†’â‚›â‚—[smoothGerm.valueRingHom I x] F := Filter.Germ.valueâ‚›â‚— I x
    change ReallyConvex (smoothGerm I x) (v â»Â¹' {y | P x y})
    dsimp only [â† smoothGerm.valueOrderRingHom_toRingHom] at v
    apply ReallyConvex.preimageâ‚›â‚—
    rw [reallyConvex_iff_convex]
    apply hP
  have hPP' : âˆ€ x, âˆƒ f : M â†’ F, âˆ€á¶  x' in ğ“ x, PP âŸ¨x', fâŸ© := fun x â†¦ by
    rcases hP' x with âŸ¨U, U_in, f, hf, hf'âŸ©
    use f
    filter_upwards [eventually_mem_nhds_iff.mpr U_in] with y hy
    exact âŸ¨hf.contMDiffAt hy, hf' y (mem_of_mem_nhds hy)âŸ©
  rcases exists_of_convex hPP hPP' with âŸ¨f, hfâŸ©
  exact âŸ¨f, fun x â†¦ (hf x).1, fun x â†¦ (hf x).2âŸ©

theorem exists_contDiff_of_convex {P : E â†’ F â†’ Prop} (hP : âˆ€ x, Convex â„ {y | P x y}) {n : â„•âˆ}
    (hP' : âˆ€ x : E, âˆƒ U âˆˆ ğ“ x, âˆƒ f : E â†’ F, ContDiffOn â„ n f U âˆ§ âˆ€ x âˆˆ U, P x (f x)) :
    âˆƒ f : E â†’ F, ContDiff â„ n f âˆ§ âˆ€ x, P x (f x) := by
  simp_rw [â† contMDiff_iff_contDiff]
  simp_rw [â† contMDiffOn_iff_contDiffOn] at hP' âŠ¢
  exact exists_contMDiff_of_convex _ hP hP'

end

section

variable {Eâ‚ Eâ‚‚ F : Type*}
  [NormedAddCommGroup Eâ‚] [NormedSpace â„ Eâ‚] [FiniteDimensional â„ Eâ‚]
  [NormedAddCommGroup Eâ‚‚] [NormedSpace â„ Eâ‚‚] [FiniteDimensional â„ Eâ‚‚]
  [NormedAddCommGroup F] [NormedSpace â„ F]

variable {Hâ‚ Mâ‚ Hâ‚‚ Mâ‚‚ : Type*}
  [TopologicalSpace Hâ‚] (Iâ‚ : ModelWithCorners â„ Eâ‚ Hâ‚)
  [TopologicalSpace Mâ‚] [ChartedSpace Hâ‚ Mâ‚] [IsManifold Iâ‚ âˆ Mâ‚]
  [TopologicalSpace Hâ‚‚] (Iâ‚‚ : ModelWithCorners â„ Eâ‚‚ Hâ‚‚)
  [TopologicalSpace Mâ‚‚] [ChartedSpace Hâ‚‚ Mâ‚‚] [IsManifold Iâ‚‚ âˆ Mâ‚‚]

@[inherit_doc] local notation "ğ“’" => ContMDiff (Iâ‚.prod Iâ‚‚) ğ“˜(â„, F)

@[inherit_doc] local notation "ğ“’_on" => ContMDiffOn (Iâ‚.prod Iâ‚‚) ğ“˜(â„, F)

omit [FiniteDimensional â„ Eâ‚] [FiniteDimensional â„ Eâ‚‚]
  [IsManifold Iâ‚ âˆ Mâ‚] [IsManifold Iâ‚‚ âˆ Mâ‚‚] in
theorem reallyConvex_contMDiffAtProd {x : Mâ‚} (n : â„•âˆ) :
    ReallyConvex (smoothGerm Iâ‚ x) {Ï† : Germ (ğ“ x) (Mâ‚‚ â†’ F) | Ï†.ContMDiffAtProd Iâ‚ Iâ‚‚ n} := by
  classical
  rw [Nontrivial.reallyConvex_iff]
  rintro w _w_pos w_supp w_sum
  have : (support w).Finite := support_finite_of_finsum_eq_one w_sum
  set fin_supp := this.toFinset with H
  have : (support fun i : (ğ“ x).Germ (Mâ‚‚ â†’ F) â†¦ w i â€¢ i) âŠ† fin_supp := by
    rw [Set.Finite.coe_toFinset]
    exact support_smul_subset_left w id
  rw [finsum_eq_sum_of_support_subset _ this]
  apply Filter.Germ.ContMDiffAtProd.sum
  refine fun Ï† hÏ† â†¦ (smoothGerm.contMDiffAt _).smul_prod (w_supp ?_)
  simpa [H] using hÏ†

omit [FiniteDimensional â„ Eâ‚‚] [IsManifold Iâ‚‚ âˆ Mâ‚‚] in
theorem exists_contMDiff_of_convexâ‚‚ {P : Mâ‚ â†’ (Mâ‚‚ â†’ F) â†’ Prop} [SigmaCompactSpace Mâ‚] [T2Space Mâ‚]
    (hP : âˆ€ x, Convex â„ {f | P x f}) {n : â„•âˆ}
    (hP' : âˆ€ x : Mâ‚, âˆƒ U âˆˆ ğ“ x, âˆƒ f : Mâ‚ â†’ Mâ‚‚ â†’ F,
      ğ“’_on n (uncurry f) (U Ã—Ë¢ (univ : Set Mâ‚‚)) âˆ§ âˆ€ y âˆˆ U, P y (f y)) :
    âˆƒ f : Mâ‚ â†’ Mâ‚‚ â†’ F, ğ“’ n (uncurry f) âˆ§ âˆ€ x, P x (f x) := by
  let PP : (Î£ x : Mâ‚, Germ (ğ“ x) (Mâ‚‚ â†’ F)) â†’ Prop := fun p â†¦
    p.2.ContMDiffAtProd Iâ‚ Iâ‚‚ n âˆ§ P p.1 p.2.value
  have hPP : âˆ€ x : Mâ‚, ReallyConvex (smoothGerm Iâ‚ x) {Ï† | PP âŸ¨x, Ï†âŸ©} := fun x â†¦ by
    apply ReallyConvex.inter
    apply reallyConvex_contMDiffAtProd
    let v : Germ (ğ“ x) (Mâ‚‚ â†’ F) â†’â‚›â‚—[smoothGerm.valueRingHom Iâ‚ x] Mâ‚‚ â†’ F := Filter.Germ.valueâ‚›â‚— Iâ‚ x
    change ReallyConvex (smoothGerm Iâ‚ x) (v â»Â¹' {y | P x y})
    dsimp only [â† smoothGerm.valueOrderRingHom_toRingHom] at v
    apply ReallyConvex.preimageâ‚›â‚—
    rw [reallyConvex_iff_convex]
    apply hP
  have hPP' : âˆ€ x, âˆƒ f : Mâ‚ â†’ Mâ‚‚ â†’ F, âˆ€á¶  x' in ğ“ x, PP âŸ¨x', fâŸ© := fun x â†¦ by
    rcases hP' x with âŸ¨U, U_in, f, hf, hf'âŸ©
    use f
    filter_upwards [eventually_mem_nhds_iff.mpr U_in] with y hy
    exact âŸ¨fun z â†¦ hf.contMDiffAt (prod_mem_nhds hy univ_mem), hf' y (mem_of_mem_nhds hy)âŸ©
  rcases exists_of_convex hPP hPP' with âŸ¨f, hfâŸ©
  exact âŸ¨f, fun âŸ¨x, yâŸ© â†¦ (hf x).1 y, fun x â†¦ (hf x).2âŸ©

omit [FiniteDimensional â„ Eâ‚‚] in
theorem exists_contDiff_of_convexâ‚‚ {P : Eâ‚ â†’ (Eâ‚‚ â†’ F) â†’ Prop} (hP : âˆ€ x, Convex â„ {f | P x f})
    {n : â„•âˆ}
    (hP' : âˆ€ x : Eâ‚, âˆƒ U âˆˆ ğ“ x, âˆƒ f : Eâ‚ â†’ Eâ‚‚ â†’ F,
      ContDiffOn â„ n (uncurry f) (U Ã—Ë¢ (univ : Set Eâ‚‚)) âˆ§ âˆ€ y âˆˆ U, P y (f y)) :
    âˆƒ f : Eâ‚ â†’ Eâ‚‚ â†’ F, ContDiff â„ n (uncurry f) âˆ§ âˆ€ x, P x (f x) := by
  simp_rw [â† contMDiffOn_iff_contDiffOn, modelWithCornersSelf_prod] at hP'
  simp_rw [â† contMDiff_iff_contDiff, modelWithCornersSelf_prod]
  rw [â† chartedSpaceSelf_prod] at hP' âŠ¢
  -- Why does `simp_rw` not succeed here?
  exact exists_contMDiff_of_convexâ‚‚ ğ“˜(â„, Eâ‚) ğ“˜(â„, Eâ‚‚) hP hP'

end

section

variable {Î¹ : Type*}

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E] {H : Type*}
  [TopologicalSpace H] {I : ModelWithCorners â„ E H} {M : Type*} [TopologicalSpace M]
  [ChartedSpace H M] [IsManifold I âˆ M] [SigmaCompactSpace M] [T2Space M]

open TopologicalSpace

example {f : E â†’ â„} (h : âˆ€ x : E, âˆƒ U âˆˆ ğ“ x, âˆƒ Îµ : â„, âˆ€ x' âˆˆ U, 0 < Îµ âˆ§ Îµ â‰¤ f x') :
    âˆƒ f' : E â†’ â„, ContDiff â„ âˆ f' âˆ§ âˆ€ x, 0 < f' x âˆ§ f' x â‰¤ f x := by
  let P : E â†’ â„ â†’ Prop := fun x t â†¦ 0 < t âˆ§ t â‰¤ f x
  have hP : âˆ€ x, Convex â„ {y | P x y} := fun x â†¦ convex_Ioc _ _
  apply exists_contDiff_of_convex hP
  intro x
  rcases h x with âŸ¨U, U_in, Îµ, hUâŸ©
  exact âŸ¨U, U_in, fun _ â†¦ Îµ, contDiffOn_const, hUâŸ©

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

theorem convex_setOf_imp_eq (P : Prop) (y : F) : Convex â„ {x : F | P â†’ x = y} := by
  by_cases hP : P <;> simp [hP, convex_singleton, convex_univ]

-- lemma exists_smooth_and_eqOn_aux1 {f : E â†’ F} {Îµ : E â†’ â„} (hf : continuous f)
--   (hÎµ : continuous Îµ) (h2Îµ : âˆ€ x, 0 < Îµ x) (xâ‚€ : E) :
--   âˆƒ U âˆˆ ğ“ xâ‚€, âˆ€ x âˆˆ U, dist (f xâ‚€) (f x) < Îµ x :=
-- begin
--   have h0 : âˆ€ x, dist (f x) (f x) < Îµ x := Î» x, by simp_rw [dist_self, h2Îµ],
--   refine âŸ¨_, (isOpen_lt (continuous_const.dist hf) hÎµ).mem_nhds $ h0 xâ‚€, Î» x hx, hxâŸ©
-- end
-- lemma exists_smooth_and_eqOn_aux2 {n : â„•âˆ} {f : E â†’ F} {Îµ : E â†’ â„} (hf : continuous f)
--   (hÎµ : continuous Îµ) (h2Îµ : âˆ€ x, 0 < Îµ x)
--   {s : set E} (hs : is_closed s) (hfs : âˆƒ U âˆˆ ğ“Ë¢ s, cont_diff_on â„ n f U)
--   (xâ‚€ : E) :
--   âˆƒ U âˆˆ ğ“ xâ‚€, âˆ€ x âˆˆ U, dist (f xâ‚€) (f x) < Îµ x :=
-- begin
--   have h0 : âˆ€ x, dist (f x) (f x) < Îµ x := Î» x, by simp_rw [dist_self, h2Îµ],
--   refine âŸ¨_, (isOpen_lt (continuous_const.dist hf) hÎµ).mem_nhds $ h0 xâ‚€, Î» x hx, hxâŸ©
-- end
theorem exists_smooth_and_eqOn {n : â„•âˆ} {f : E â†’ F} {Îµ : E â†’ â„} (hf : Continuous f)
    (hÎµ : Continuous Îµ) (h2Îµ : âˆ€ x, 0 < Îµ x) {s : Set E} (hs : IsClosed s)
    (hfs : âˆƒ U âˆˆ ğ“Ë¢ s, ContDiffOn â„ n f U) :
    âˆƒ f' : E â†’ F, ContDiff â„ n f' âˆ§ (âˆ€ x, dist (f' x) (f x) < Îµ x) âˆ§ EqOn f' f s := by
  have h0 : âˆ€ x, dist (f x) (f x) < Îµ x := fun x â†¦ by simp_rw [dist_self, h2Îµ]
  let P : E â†’ F â†’ Prop := fun x t â†¦ dist t (f x) < Îµ x âˆ§ (x âˆˆ s â†’ t = f x)
  have hP : âˆ€ x, Convex â„ {y | P x y} := fun x â†¦
    (convex_ball (f x) (Îµ x)).inter (convex_setOf_imp_eq _ _)
  obtain âŸ¨f', hf', hPf'âŸ© : âˆƒ f' : E â†’ F, ContDiff â„ n f' âˆ§  âˆ€ x, P x (f' x) := by
    apply exists_contDiff_of_convex hP
    intro x
    obtain âŸ¨U, hU, hfUâŸ© := hfs
    by_cases hx : x âˆˆ s
    Â· refine âŸ¨U, mem_nhdsSet_iff_forall.mp hU x hx, ?_âŸ©
      exact âŸ¨f, hfU, fun y _ â†¦ âŸ¨h0 y, fun _ â†¦ rflâŸ©âŸ©
    Â· have : IsOpen {y : E | dist (f x) (f y) < Îµ y} := isOpen_lt (continuous_const.dist hf) hÎµ
      exact âŸ¨_, (this.sdiff hs).mem_nhds âŸ¨h0 x, hxâŸ©, fun _ â†¦ f x, contDiffOn_const, fun y hy â†¦
        âŸ¨hy.1, fun h2y â†¦ (hy.2 h2y).elimâŸ©âŸ©
  exact âŸ¨f', hf', fun x â†¦ (hPf' x).1, fun x â†¦ (hPf' x).2âŸ©

end
