import Mathlib.Analysis.Normed.Order.Lattice
import Mathlib.Algebra.Ring.Periodic
import SphereEversion.ToMathlib.Topology.Separation.Hausdorff

-- TODO: the file this references doesn't exist in mathlib any more; rename this one appropriately!

/-!

# Boundedness property of periodic function

The main purpose of that file it to prove
```
lemma Continuous.bounded_of_onePeriodic_of_isCompact {f : X â†’ â„ â†’ E} (cont : Continuous â†¿f)
  (hper : âˆ€ x, OnePeriodic (f x)) {K : Set X} (hK : IsCompact K) (hfK : âˆ€ x âˆ‰ K, f x = 0) :
  âˆƒ C, âˆ€ x t, â€–f x tâ€– â‰¤ C
```

This is done by introducing the quotient ğ•Šâ‚ = â„/â„¤ as a compact topological space.
Patrick is not sure this is the optimal version.

In the first part, generalize many lemmas to any period and add to `Algebra.Ring.Periodic.lean`?
-/


noncomputable section

open Set Function Filter TopologicalSpace Int

open scoped Topology

section OnePeriodic

variable {Î± : Type*}

/-- The integers as an additive subgroup of the reals. -/
def â„¤Subâ„ : AddSubgroup â„ := AddMonoidHom.range (Int.castAddHom â„)

/-- The equivalence relation on `â„` corresponding to its partition as cosets of `â„¤`. -/
def transOne : Setoid â„ := QuotientAddGroup.leftRel â„¤Subâ„

/-- The proposition that a function on `â„` is periodic with period `1`. -/
def OnePeriodic (f : â„ â†’ Î±) : Prop := Periodic f 1

theorem OnePeriodic.add_nat {f : â„ â†’ Î±} (h : OnePeriodic f) (k : â„•) (x : â„) : f (x + k) = f x := by
  simpa using h.nat_mul k x

theorem OnePeriodic.add_int {f : â„ â†’ Î±} (h : OnePeriodic f) (k : â„¤) (x : â„) : f (x + k) = f x := by
  simpa using h.int_mul k x

/-- The circle `ğ•Šâ‚ := â„/â„¤`.

TODO [Yury]: use `AddCircle`. -/
def ğ•Šâ‚ :=
  Quotient transOne
deriving TopologicalSpace, Inhabited

theorem transOne_rel_iff {a b : â„} : transOne.r a b â†” âˆƒ k : â„¤, b = a + k := by
  refine QuotientAddGroup.leftRel_apply.trans (exists_congr fun k â†¦ ?_)
  rw [coe_castAddHom, eq_neg_add_iff_add_eq, eq_comm]

section

attribute [local instance] transOne

/-- The quotient map from the reals to the circle `â„ â§¸ â„¤`. -/
def projğ•Šâ‚ : â„ â†’ ğ•Šâ‚ :=
  Quotient.mk'

@[simp]
theorem projğ•Šâ‚_add_int (t : â„) (k : â„¤) : projğ•Šâ‚ (t + k) = projğ•Šâ‚ t :=
  (Quotient.sound (transOne_rel_iff.mpr âŸ¨k, rflâŸ©)).symm

/-- The unique representative in the half-open interval `[0, 1)` for each coset of `â„¤` in `â„`,
regarded as a map from the circle `ğ•Šâ‚ â†’ â„`. -/
def ğ•Šâ‚.repr (x : ğ•Šâ‚) : â„ :=
  let t := Quotient.out x
  fract t

theorem ğ•Šâ‚.repr_mem (x : ğ•Šâ‚) : x.repr âˆˆ (Ico 0 1 : Set â„) :=
  âŸ¨fract_nonneg _, fract_lt_one _âŸ©

theorem ğ•Šâ‚.proj_repr (x : ğ•Šâ‚) : projğ•Šâ‚ x.repr = x := by
  symm
  conv_lhs => rw [â† Quotient.out_eq x]
  rw [â† fract_add_floor (Quotient.out x)]
  apply projğ•Šâ‚_add_int

theorem image_projğ•Šâ‚_Ico : projğ•Šâ‚ '' Ico 0 1 = univ := by
  rw [eq_univ_iff_forall]
  exact fun x â†¦ âŸ¨x.repr, x.repr_mem, x.proj_reprâŸ©

theorem image_projğ•Šâ‚_Icc : projğ•Šâ‚ '' Icc 0 1 = univ :=
  eq_univ_of_subset (image_subset projğ•Šâ‚ Ico_subset_Icc_self) image_projğ•Šâ‚_Ico

@[continuity, fun_prop]
theorem continuous_projğ•Šâ‚ : Continuous projğ•Šâ‚ := continuous_quotient_mk'

theorem isOpenMap_projğ•Šâ‚ : IsOpenMap projğ•Šâ‚ := QuotientAddGroup.isOpenMap_coe

theorem quotientMap_id_projğ•Šâ‚ {X : Type*} [TopologicalSpace X] :
    Topology.IsQuotientMap fun p : X Ã— â„ â†¦ (p.1, projğ•Šâ‚ p.2) :=
  (IsOpenMap.id.prodMap isOpenMap_projğ•Šâ‚).isQuotientMap (by fun_prop)
    (surjective_id.prodMap Quotient.exists_rep)

/-- A one-periodic function on `â„` descends to a function on the circle `â„ â§¸ â„¤`. -/
def OnePeriodic.lift {f : â„ â†’ Î±} (h : OnePeriodic f) : ğ•Šâ‚ â†’ Î± :=
  Quotient.lift f (by intro a b hab; rcases transOne_rel_iff.mp hab with âŸ¨k, rflâŸ©; rw [h.add_int])

end

@[inherit_doc] local notation "Ï€" => projğ•Šâ‚

instance : CompactSpace ğ•Šâ‚ :=
  âŸ¨by rw [â† image_projğ•Šâ‚_Icc]; exact isCompact_Icc.image continuous_projğ•Šâ‚âŸ©

theorem isClosed_int : IsClosed (range ((â†‘) : â„¤ â†’ â„)) :=
  Int.isClosedEmbedding_coe_real.isClosed_range

instance : T2Space ğ•Šâ‚ := by
  have Ï€cont : Continuous Ï€ := continuous_quotient_mk'
  rw [t2Space_iff_of_continuous_surjective_open Ï€cont Quotient.mk''_surjective isOpenMap_projğ•Šâ‚]
  have : {q : â„ Ã— â„ | Ï€ q.fst = Ï€ q.snd} = {q : â„ Ã— â„ | âˆƒ k : â„¤, q.2 = q.1 + k} := by
    ext âŸ¨a, bâŸ©
    exact Quotient.eq''.trans transOne_rel_iff
  have : {q : â„ Ã— â„ | Ï€ q.fst = Ï€ q.snd} =
      (fun q : â„ Ã— â„ â†¦ q.2 - q.1) â»Â¹' (range <| ((â†‘) : â„¤ â†’ â„)) := by
    rw [this]
    ext âŸ¨a, bâŸ©
    refine exists_congr fun k â†¦ ?_
    conv_rhs => rw [eq_comm, sub_eq_iff_eq_add']
  rw [this]
  exact IsClosed.preimage (continuous_snd.sub continuous_fst) isClosed_int

variable {X E : Type*} [TopologicalSpace X] [NormedAddCommGroup E]

theorem Continuous.bounded_on_compact_of_onePeriodic {f : X â†’ â„ â†’ E} (cont : Continuous â†¿f)
    (hper : âˆ€ x, OnePeriodic (f x)) {K : Set X} (hK : IsCompact K) :
    âˆƒ C, âˆ€ x âˆˆ K, âˆ€ t, â€–f x tâ€– â‰¤ C := by
  let F : X Ã— ğ•Šâ‚ â†’ E := fun p : X Ã— ğ•Šâ‚ â†¦ (hper p.1).lift p.2
  have Fcont : Continuous F := by
    have qm : Topology.IsQuotientMap fun p : X Ã— â„ â†¦ (p.1, Ï€ p.2) := quotientMap_id_projğ•Šâ‚
    -- avoid weird elaboration issue
    have : â†¿f = F âˆ˜ fun p : X Ã— â„ â†¦ (p.1, Ï€ p.2) := by ext p; rfl
    rwa [this, â† qm.continuous_iff] at cont
  obtain âŸ¨C, hCâŸ© :=
    (hK.prod isCompact_univ).bddAbove_image (continuous_norm.comp Fcont).continuousOn
  exact âŸ¨C, fun x x_in t â†¦ hC âŸ¨(x, Ï€ t), âŸ¨x_in, mem_univ _âŸ©, rflâŸ©âŸ©

theorem Continuous.bounded_of_onePeriodic_of_compact {f : X â†’ â„ â†’ E} (cont : Continuous â†¿f)
    (hper : âˆ€ x, OnePeriodic (f x)) {K : Set X} (hK : IsCompact K)
    (hfK : âˆ€ x âˆ‰ K, f x = 0) : âˆƒ C, âˆ€ x t, â€–f x tâ€– â‰¤ C := by
  obtain âŸ¨C, hCâŸ© := cont.bounded_on_compact_of_onePeriodic hper hK
  refine âŸ¨max C 0, fun x t â†¦ ?_âŸ©
  by_cases hx : x âˆˆ K
  Â· exact le_max_of_le_left (hC x hx t)
  Â· simp [hfK, hx]

end OnePeriodic
