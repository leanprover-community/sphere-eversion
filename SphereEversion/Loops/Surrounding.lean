import SphereEversion.InductiveConstructions
import SphereEversion.Loops.Basic
import SphereEversion.ToMathlib.Analysis.Normed.Module.FiniteDimension
import SphereEversion.ToMathlib.ExistsOfConvex
import SphereEversion.ToMathlib.SmoothBarycentric
import SphereEversion.ToMathlib.Topology.Path
import Mathlib.Analysis.Convex.Caratheodory

/-!
# Surrounding families of loops

In order to carry out the corrugation technique of convex integration, one needs to a family of
loops with various prescribed properties.

This file begins the work of constructing such a family.

The key definitions are:
 * `Surrounded`
 * `SurroundingPts`
 * `SurroundingFamily`

The key results are:
 * `surrounded_iff_mem_interior_convexHull_aff_basis`
 * `surrounded_of_convexHull`
 * `smooth_surrounding`
 * `eventually_surroundingPts_of_tendsto_of_tendsto`
 * `surrounding_loop_of_convexHull`
 * `local_loops`
 * `satisfied_or_refund`
 * `extend_loops`
 * `exists_surrounding_loops`
-/

-- to obtain that normed spaces are locally connected
open Set Function Module Int Prod Path Filter
open scoped Topology unitInterval ContDiff

namespace IsPathConnected

-- we redo `exists_path_through_family` to use `def`s
variable {X : Type*} [TopologicalSpace X] {F : Set X}

/-- An arbitrary path joining `x` and `y` in `F`. -/
noncomputable def somePath (hF : IsPathConnected F) {x y : X} (hx : x âˆˆ F) (hy : y âˆˆ F) :
    Path x y :=
  (hF.joinedIn x hx y hy).somePath

theorem somePath_mem (hF : IsPathConnected F) {x y : X} (hx : x âˆˆ F) (hy : y âˆˆ F) (t : I) :
    hF.somePath hx hy t âˆˆ F :=
  JoinedIn.somePath_mem _ t

theorem range_somePath_subset (hF : IsPathConnected F) {x y : X} (hx : x âˆˆ F) (hy : y âˆˆ F) :
    range (hF.somePath hx hy) âŠ† F := by rintro _ âŸ¨t, rflâŸ©; apply somePath_mem

open Fin.NatCast in -- TODO: fix this
/-- A path through `p 0`, ..., `p n`. Usually this is used with `n := m`. -/
noncomputable def pathThrough (hF : IsPathConnected F) {m : â„•} {p : Fin (m + 1) â†’ X}
    (hp : âˆ€ i, p i âˆˆ F) : âˆ€ n : â„•, Path (p 0) (p n)
  | 0 => Path.refl (p 0)
  | n + 1 => (pathThrough hF hp n).trans <| hF.somePath (hp _) (hp _)

attribute [simp] Path.trans_range

theorem range_pathThrough_subset (hF : IsPathConnected F) {m : â„•} {p : Fin (m + 1) â†’ X}
    (hp : âˆ€ i, p i âˆˆ F) : âˆ€ {n : â„•}, range (hF.pathThrough hp n) âŠ† F
  | 0 => by simp [pathThrough, hp]
  | n + 1 => by simpa [pathThrough, hp, range_somePath_subset]
    using range_pathThrough_subset hF hp (n := n)

open Fin.NatCast in -- TODO: fix this
theorem mem_range_pathThrough' (hF : IsPathConnected F) {m : â„•} {p : Fin (m + 1) â†’ X}
    (hp : âˆ€ i, p i âˆˆ F) {i n : â„•} (h : i â‰¤ n) : p i âˆˆ range (hF.pathThrough hp n) := by
  induction' h with n _ ih
  Â· exact âŸ¨1, by simpâŸ©
  Â· simp only [pathThrough, Path.trans_range, mem_union, ih, true_or]

theorem mem_range_pathThrough (hF : IsPathConnected F) {m : â„•} {p : Fin (m + 1) â†’ X}
    (hp : âˆ€ i, p i âˆˆ F) {i : Fin (m + 1)} : p i âˆˆ range (hF.pathThrough hp m) := by
  convert hF.mem_range_pathThrough' hp (Nat.le_of_lt_succ i.2); simp [Fin.cast_val_eq_self]

end IsPathConnected

noncomputable section

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

set_option hygiene false
@[inherit_doc] local notation "d" => finrank â„ F
set_option hygiene true

/-
The definition below gets a prime because it clashes with a manifold definition
in mathlib which is in the root namespace.
-/
/-- `f` is smooth at `x` if `f` is smooth on some neighborhood of `x`. -/
def SmoothAt' (f : E â†’ F) (x : E) : Prop :=
  âˆƒ s âˆˆ ğ“ x, smooth_on f s

section SurroundingPoints

set_option hygiene false
@[inherit_doc] local notation "Î¹" => Fin (d + 1)
set_option hygiene true

-- def:surrounds_points
/-- `p` is a collection of points surrounding `f` with weights `w` (that are positive and sum to 1)
if the weighted average of the points `p` is `f` and the points `p` form an affine basis of the
space. -/
structure SurroundingPts (f : F) (p : Î¹ â†’ F) (w : Î¹ â†’ â„) : Prop where
  indep : AffineIndependent â„ p
  w_pos : âˆ€ i, 0 < w i
  w_sum : âˆ‘ i, w i = 1
  avg : âˆ‘ i, w i â€¢ p i = f

theorem SurroundingPts.tot [FiniteDimensional â„ F] {f : F} {p : Î¹ â†’ F} {w : Î¹ â†’ â„}
    (h : SurroundingPts f p w) : affineSpan â„ (range p) = âŠ¤ :=
  h.indep.affineSpan_eq_top_iff_card_eq_finrank_add_one.mpr (Fintype.card_fin _)

theorem SurroundingPts.mem_affineBases [FiniteDimensional â„ F] {f : F} {p : Î¹ â†’ F} {w : Î¹ â†’ â„}
    (h : SurroundingPts f p w) : p âˆˆ affineBases Î¹ â„ F :=
  âŸ¨h.indep, h.totâŸ©

theorem SurroundingPts.coord_eq_w [FiniteDimensional â„ F] {f : F} {p : Î¹ â†’ F} {w : Î¹ â†’ â„}
    (h : SurroundingPts f p w) : (âŸ¨p, h.indep, h.totâŸ© : AffineBasis Î¹ â„ F).coords f = w := by
  let b : AffineBasis Î¹ â„ F := âŸ¨p, h.indep, h.totâŸ©
  change b.coords f = w
  ext i
  rw [â† h.avg, â† Finset.univ.affineCombination_eq_linear_combination _ w h.w_sum,
    AffineBasis.coords_apply]
  exact AffineBasis.coord_apply_combination_of_mem _ (Finset.mem_univ i) h.w_sum

/-- `f` is surrounded by a set `s` if there is an affine basis `p` in `s` with weighted average `f`.
-/
def Surrounded (f : F) (s : Set F) : Prop :=
  âˆƒ p w, SurroundingPts f p w âˆ§ âˆ€ i, p i âˆˆ s

theorem surrounded_iff_mem_interior_convexHull_aff_basis [FiniteDimensional â„ F] {f : F}
    {s : Set F} :
    Surrounded f s â†”
      âˆƒ b, b âŠ† s âˆ§ AffineIndependent â„ ((â†‘) : b â†’ F) âˆ§ affineSpan â„ b = âŠ¤ âˆ§
        f âˆˆ interior (convexHull â„ b) := by
  constructor
  Â· rintro âŸ¨p, w, âŸ¨âŸ¨indep, w_pos, w_sum, rflâŸ©, h_memâŸ©âŸ©
    have h_tot : affineSpan â„ (range p) = âŠ¤ :=
      indep.affineSpan_eq_top_iff_card_eq_finrank_add_one.mpr (Fintype.card_fin _)
    refine âŸ¨range p, range_subset_iff.mpr h_mem, indep.range, h_tot, ?_âŸ©
    let basis : AffineBasis Î¹ â„ F := âŸ¨p, indep, h_totâŸ©
    erw [basis.interior_convexHull]
    intro i
    erw [â† Finset.affineCombination_eq_linear_combination _ _ _ w_sum,
      basis.coord_apply_combination_of_mem (Finset.mem_univ i) w_sum]
    exact w_pos i
  Â· rintro âŸ¨b, hâ‚€, hâ‚, hâ‚‚, hâ‚ƒâŸ©
    haveI : Fintype b := (finite_set_of_fin_dim_affineIndependent â„ hâ‚).fintype
    have hb : Fintype.card b = d + 1 := by
      rw [â† hâ‚.affineSpan_eq_top_iff_card_eq_finrank_add_one, Subtype.range_coe_subtype,
        setOf_mem_eq, hâ‚‚]
    let p := ((â†‘) : _ â†’ F) âˆ˜ (Fintype.equivFinOfCardEq hb).symm
    have hp : b = range p := by
      ext x
      exact âŸ¨by intro h; use Fintype.equivFinOfCardEq hb âŸ¨x, hâŸ©; simp [p], by
        rintro âŸ¨y, rflâŸ©; apply Subtype.coe_propâŸ©
    rw [hp] at hâ‚€ hâ‚‚ hâ‚ƒ
    replace hâ‚ : AffineIndependent â„ p :=
      hâ‚.comp_embedding (Fintype.equivFinOfCardEq hb).symm.toEmbedding
    let basis : AffineBasis Î¹ â„ F := âŸ¨_, hâ‚, hâ‚‚âŸ©
    erw [basis.interior_convexHull, mem_setOf_eq] at hâ‚ƒ
    refine âŸ¨p, fun i â†¦ basis.coord i f, âŸ¨hâ‚, hâ‚ƒ, ?_, ?_âŸ©, fun i â†¦ hâ‚€ (mem_range_self i)âŸ©
    Â· exact basis.sum_coord_apply_eq_one f
    Â· erw [â† Finset.univ.affineCombination_eq_linear_combination p _
        (basis.sum_coord_apply_eq_one f), basis.affineCombination_coord_eq_self]

--- `prop:surrounded_by_open`
theorem surrounded_of_convexHull [FiniteDimensional â„ F] {f : F} {s : Set F} (hs : IsOpen s)
    (hsf : f âˆˆ convexHull â„ s) : Surrounded f s := by
  rw [surrounded_iff_mem_interior_convexHull_aff_basis]
  obtain âŸ¨t, hts, hai, hfâŸ© :
    âˆƒ t : Finset F,
      (t : Set F) âŠ† s âˆ§ AffineIndependent â„ ((â†‘) : t â†’ F) âˆ§ f âˆˆ convexHull â„ (t : Set F) := by
    simp_rw [â† exists_prop, â† mem_iUnion, â† convexHull_eq_union]
    exact hsf
  have htne : (t : Set F).Nonempty := convexHull_nonempty_iff.mp âŸ¨f, hfâŸ©
  obtain âŸ¨b, hbâ‚, hbâ‚‚, hbâ‚ƒ, hbâ‚„âŸ© := hs.exists_between_affineIndependent_span_eq_top hts htne hai
  have hbâ‚€ : b.Finite := finite_set_of_fin_dim_affineIndependent â„ hbâ‚ƒ
  obtain âŸ¨c, hcâŸ© := interior_convexHull_nonempty_iff_affineSpan_eq_top.mpr hbâ‚„
  rw [â† hs.interior_eq] at hbâ‚‚
  obtain âŸ¨Îµ, hÎµ, hcsâŸ© :=
    (eventually_homothety_image_subset_of_finite_subset_interior â„ c hbâ‚€ hbâ‚‚).exists_gt
  have hbÎµ := Convex.subset_interior_image_homothety_of_one_lt (convex_convexHull â„ _) hc Îµ hÎµ
  rw [AffineMap.image_convexHull] at hbÎµ
  let t : Units â„ := Units.mk0 Îµ (by linarith)
  refine âŸ¨AffineMap.homothety c (t : â„) '' b, hcs, ?_, ?_, hbÎµ (convexHull_mono hbâ‚ hf)âŸ©
  Â· rwa [(AffineEquiv.homothetyUnitsMulHom c t).affineIndependent_set_of_eq_iff]
  Â· exact (AffineEquiv.homothetyUnitsMulHom c t).span_eq_top_iff.mp hbâ‚„

/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
-- `lem:smooth_barycentric_coord`
theorem smooth_surrounding [FiniteDimensional â„ F] {x : F} {p : Î¹ â†’ F} {w : Î¹ â†’ â„}
    (h : SurroundingPts x p w) :
    âˆƒ W : F â†’ (Î¹ â†’ F) â†’ Î¹ â†’ â„, âˆ€á¶  yq : F Ã— (Î¹ â†’ F) in ğ“ (x, p),
      SmoothAt' (uncurry W) yq âˆ§ (âˆ€ i, 0 < W yq.1 yq.2 i) âˆ§ âˆ‘ i, W yq.1 yq.2 i = 1 âˆ§
        âˆ‘ i, W yq.1 yq.2 i â€¢ yq.2 i = yq.1 := by
  classical
  use evalBarycentricCoords Î¹ â„ F
  let V : Set (Î¹ â†’ â„) := Set.pi Set.univ fun _ â†¦ Ioi (0 : â„)
  let W' : F Ã— (Î¹ â†’ F) â†’ Î¹ â†’ â„ := uncurry (evalBarycentricCoords Î¹ â„ F)
  let A : Set (F Ã— (Î¹ â†’ F)) := univ Ã—Ë¢ affineBases Î¹ â„ F
  let U : Set (F Ã— (Î¹ â†’ F)) := A âˆ© W' â»Â¹' V
  have hÎ¹ : Fintype.card Î¹ = d + 1 := Fintype.card_fin _
  have hp : p âˆˆ affineBases Î¹ â„ F := h.mem_affineBases
  have hV : IsOpen V := isOpen_set_pi finite_univ fun _ _ â†¦ isOpen_Ioi
  have hW' : ContinuousOn W' A := (smooth_barycentric Î¹ â„ F hÎ¹ (n := 0)).continuousOn
  have hxp : W' (x, p) âˆˆ V := by simp [W', V, hp, h.coord_eq_w, h.w_pos]
  have hA : IsOpen A := by
    simp only [A, affineBases_findim Î¹ â„ F hÎ¹]
    exact isOpen_univ.prod (isOpen_affineIndependent â„ F)
  have hUâ‚‚ : IsOpen U := hW'.isOpen_inter_preimage hA hV
  have hUâ‚ƒ : U âˆˆ ğ“ (x, p) :=
    mem_nhds_iff.mpr âŸ¨U, le_refl U, hUâ‚‚, Set.mem_inter (by simp [hp, A]) (mem_preimage.mpr hxp)âŸ©
  apply eventually_of_mem hUâ‚ƒ
  rintro âŸ¨y, qâŸ© hyq
  have hq : q âˆˆ affineBases Î¹ â„ F := by simpa [A] using inter_subset_left hyq
  have hyq' : (y, q) âˆˆ W' â»Â¹' V := inter_subset_right hyq
  refine âŸ¨âŸ¨U, mem_nhds_iff.mpr âŸ¨U, le_refl U, hUâ‚‚, hyqâŸ©,
    ((smooth_barycentric Î¹ â„ F hÎ¹).mono inter_subset_left).of_le le_topâŸ©, ?_, ?_, ?_âŸ©
  Â· simpa [V] using hyq'
  Â· simp [hq]
  Â· simp [hq]; exact AffineBasis.linear_combination_coord_eq_self _ y

theorem smooth_surroundingPts [FiniteDimensional â„ F] {x : F} {p : Î¹ â†’ F} {w : Î¹ â†’ â„}
    (h : SurroundingPts x p w) :
    âˆƒ W : F â†’ (Î¹ â†’ F) â†’ Î¹ â†’ â„,
      âˆ€á¶  yq : F Ã— (Î¹ â†’ F) in ğ“ (x, p),
        SmoothAt' (uncurry W) yq âˆ§ SurroundingPts yq.1 yq.2 (W yq.1 yq.2) := by
  refine Exists.imp (fun W hW â†¦ ?_) (smooth_surrounding h)
  rw [nhds_prod_eq] at hW âŠ¢
  have := (IsOpen.eventually_mem (isOpen_affineIndependent â„ F) h.indep).prod_inr (ğ“ x)
  filter_upwards [hW, this]; rintro âŸ¨y, qâŸ© âŸ¨hW, h2W, h3W, hqâŸ© h2q
  exact âŸ¨hW, h2q, h2W, h3W, hqâŸ©

theorem surroundingPts_evalBarycentricCoords_iff (q : F) (v : Î¹ â†’ F)
    [DecidablePred (Â· âˆˆ affineBases Î¹ â„ F)] :
    SurroundingPts q v (evalBarycentricCoords Î¹ â„ F q v) â†”
      âˆ€ i, 0 < evalBarycentricCoords Î¹ â„ F q v i := by
  refine âŸ¨fun h â†¦ h.w_pos, fun h â†¦ ?_âŸ©
  have hv : v âˆˆ affineBases Î¹ â„ F := by
    by_contra contra
    simpa [evalBarycentricCoords_apply_of_not_mem_bases Î¹ â„ F q contra] using h 0
  have hv' : âˆ‘ i, evalBarycentricCoords Î¹ â„ F q v i = 1 := by
    simp [evalBarycentricCoords_apply_of_mem_bases Î¹ â„ F q hv]
  refine âŸ¨hv.1, h, hv', ?_âŸ©
  simp_rw [â† Finset.univ.affineCombination_eq_linear_combination v _ hv',
    evalBarycentricCoords_apply_of_mem_bases Î¹ â„ F q hv]
  convert AffineBasis.affineCombination_coord_eq_self _ q
  rfl

end SurroundingPoints

section SurroundingPointsLimits

variable {X Y : Type*} [FiniteDimensional â„ F]

local macro:arg "Î¹" : term => `(Fin (finrank â„ F + 1))

theorem eventually_surroundingPts_of_tendsto_of_tendsto {l : Filter X} {m : Filter Y} {v : Î¹ â†’ F}
    {q : F} {p : Î¹ â†’ X â†’ F} {f : Y â†’ F} (hq : âˆƒ w, SurroundingPts q v w)
    (hp : âˆ€ i, Tendsto (p i) l (ğ“ (v i))) (hf : Tendsto f m (ğ“ q)) :
    âˆ€á¶  z : X Ã— Y in l Ã—Ë¢ m, âˆƒ w, SurroundingPts (f z.2) (fun i â†¦ p i z.1) w := by
  classical
  obtain âŸ¨w, hwâŸ© := hq
  let V : Set (Î¹ â†’ â„) := Set.pi Set.univ fun _ â†¦ Ioi (0 : â„)
  let W' : F Ã— (Î¹ â†’ F) â†’ Î¹ â†’ â„ := uncurry (evalBarycentricCoords Î¹ â„ F)
  let A : Set (F Ã— (Î¹ â†’ F)) := (univ : Set F) Ã—Ë¢ affineBases Î¹ â„ F
  let S : Set (F Ã— (Î¹ â†’ F)) := W' â»Â¹' V
  have hÎ¹ : Fintype.card Î¹ = finrank â„ F + 1 := Fintype.card_fin _
  have hq' : v âˆˆ affineBases Î¹ â„ F := hw.mem_affineBases
  have hqv : (q, v) âˆˆ A := by simp [A, hq']
  have hxp : W' (q, v) âˆˆ V := by simp [W', V, hq', hw.coord_eq_w, hw.w_pos]
  have hV' : V âˆˆ ğ“ (W' (q, v)) := (isOpen_set_pi finite_univ fun _ _ â†¦ isOpen_Ioi).mem_nhds hxp
  have hA : IsOpen A := by
    simp only [A, affineBases_findim Î¹ â„ F hÎ¹]
    exact isOpen_univ.prod (isOpen_affineIndependent â„ F)
  have hW' : ContinuousAt W' (q, v) :=
    (smooth_barycentric Î¹ â„ F hÎ¹ (n := 0)).continuousOn.continuousAt
      (mem_nhds_iff.mpr âŸ¨A, Subset.rfl, hA, hqvâŸ©)
  have hS : S âˆˆ ğ“ (q, v) := hW'.preimage_mem_nhds hV'
  obtain âŸ¨nâ‚, hnâ‚, nâ‚‚, hnâ‚‚, hS'âŸ© := mem_nhds_prod_iff.mp hS
  have hnâ‚' := tendsto_def.mp hf _ hnâ‚
  have hnâ‚‚' := tendsto_def.mp (tendsto_pi_nhds.mpr hp) _ hnâ‚‚
  have come_on : (swap p â»Â¹' nâ‚‚) Ã—Ë¢ (f â»Â¹' nâ‚) âˆˆ l  Ã—Ë¢ m :=
    mem_prod_iff.mpr âŸ¨_, hnâ‚‚', _, hnâ‚', Subset.rflâŸ©
  filter_upwards [come_on]
  rintro âŸ¨yâ‚‚, yâ‚âŸ© âŸ¨hyâ‚‚ : swap p yâ‚‚ âˆˆ nâ‚‚, hyâ‚ : f yâ‚ âˆˆ nâ‚âŸ©
  refine âŸ¨W' (f yâ‚, swap p yâ‚‚),
    (surroundingPts_evalBarycentricCoords_iff (f yâ‚) (swap p yâ‚‚)).mpr fun i â†¦ ?_âŸ©
  change W' (f yâ‚, swap p yâ‚‚) i âˆˆ Ioi (0 : â„)
  suffices (f yâ‚, swap p yâ‚‚) âˆˆ S from this i (mem_univ _)
  apply hS'
  simp [hyâ‚, hyâ‚‚]

theorem eventually_surroundingPts_of_tendsto_of_tendsto' {v : Î¹ â†’ F} {q : F} {p : Î¹ â†’ X â†’ F}
    {l : Filter X} {f : X â†’ F} (hq : âˆƒ w, SurroundingPts q v w)
    (hp : âˆ€ i, Tendsto (p i) l (ğ“ (v i))) (hf : Tendsto f l (ğ“ q)) :
    âˆ€á¶  y in l, âˆƒ w, SurroundingPts (f y) (fun i â†¦ p i y) w := by
  have := eventually_surroundingPts_of_tendsto_of_tendsto hq hp hf
  simp_rw [eventually_iff_exists_mem, mem_prod_iff] at this
  obtain âŸ¨nnn, âŸ¨nâ‚, hnâ‚, nâ‚‚, hnâ‚‚, hhâŸ©, hâŸ© := this
  rw [eventually_iff_exists_mem]
  exact âŸ¨nâ‚ âˆ© nâ‚‚, inter_mem hnâ‚ hnâ‚‚, fun y hy â†¦ h (y, y) (by apply hh; simpa using hy)âŸ©

end SurroundingPointsLimits

namespace Loop

variable {Î³ Î³' : Loop F} {x y : F} {t : â„}

/-- A loop `Î³` surrounds a point `x` if `x` is surrounded by values of `Î³`. -/
def Surrounds (Î³ : Loop F) (x : F) : Prop :=
  âˆƒ t w : Fin (d + 1) â†’ â„, SurroundingPts x (Î³ âˆ˜ t) w

theorem surrounds_iff_range_subset_range :
    Î³.Surrounds x â†”
      âˆƒ (p : Fin (d + 1) â†’ F) (w : Fin (d + 1) â†’ â„), SurroundingPts x p w âˆ§ range p âŠ† range Î³ := by
  constructor
  Â· exact fun âŸ¨t, w, hâŸ© â†¦ âŸ¨Î³ âˆ˜ t, w, h, range_comp_subset_range _ _âŸ©
  Â· rintro âŸ¨p, w, hâ‚€, hâ‚âŸ©
    rw [range_subset_iff] at hâ‚
    choose t ht using hâ‚
    have hpt : Î³ âˆ˜ t = p := funext ht
    exact âŸ¨t, w, hpt.symm â–¸ hâ‚€âŸ©

theorem affineEquiv_surrounds_iff (e : F â‰ƒáµƒ[â„] F) :
    Î³.Surrounds x â†” (Î³.transform e).Surrounds (e x) := by
  suffices âˆ€ (Î³ : Loop F) (x) (e : F â‰ƒáµƒ[â„] F), Î³.Surrounds x â†’ (Î³.transform e).Surrounds (e x) by
    refine âŸ¨this Î³ x e, fun h â†¦ ?_âŸ©
    specialize this (Î³.transform e) (e x) e.symm h
    rw [AffineEquiv.symm_apply_apply] at this
    convert this
    ext
    simp
  rintro Î³ x e âŸ¨t, w, indep, w_pos, w_sum, rflâŸ©
  refine âŸ¨t, w, âŸ¨e.affineIndependent_iff.mpr indep, w_pos, w_sum, ?_âŸ©âŸ©
  simp only [â† Finset.affineCombination_eq_linear_combination _ _ _ w_sum]
  erw [Finset.map_affineCombination _ (Î³ âˆ˜ t) _ w_sum (e : F â†’áµƒ[â„] F)]
  congr

omit [NormedSpace â„ F] in
@[simp]
theorem zero_vadd : (0 : F) +áµ¥ Î³ = Î³ := by
  ext t
  simp

theorem vadd_surrounds : Î³.Surrounds x â†” (y +áµ¥ Î³).Surrounds (y + x) := by
  rw [add_comm]
  convert affineEquiv_surrounds_iff (AffineEquiv.vaddConst â„ y) using 2
  ext u
  simp [add_comm y]

theorem Surrounds.vadd (h : Î³.Surrounds x) : (y +áµ¥ Î³).Surrounds (y + x) :=
  vadd_surrounds.mp h

theorem Surrounds.vadd0 (h : Î³.Surrounds 0) : (y +áµ¥ Î³).Surrounds y := by
  convert h.vadd
  rw [add_zero]

theorem Surrounds.smul0 (h : Î³.Surrounds 0) (ht : t â‰  0) : (t â€¢ Î³).Surrounds 0 := by
  rw [affineEquiv_surrounds_iff (AffineEquiv.homothetyUnitsMulHom (0 : F) (Units.mk0 t ht)â»Â¹),
    AffineEquiv.coe_homothetyUnitsMulHom_apply, AffineMap.homothety_apply_same]
  convert h
  ext u
  simp [AffineMap.homothety_apply, smul_smul, inv_mul_cancelâ‚€ ht]

theorem Surrounds.mono (h : Î³.Surrounds x) (h2 : range Î³ âŠ† range Î³') : Î³'.Surrounds x := by
  revert h; simp_rw [Loop.surrounds_iff_range_subset_range]
  refine Exists.imp fun t â†¦ ?_
  refine Exists.imp fun w â†¦ ?_
  exact And.imp_right fun h3 â†¦ Subset.trans h3 h2

protected theorem Surrounds.reparam (h : Î³.Surrounds x) {Ï† : EquivariantMap} (hÏ† : Continuous Ï†) :
    (Î³.reparam Ï†).Surrounds x := by
  refine h.mono ?_
  convert subset_of_eq (range_comp Î³ Ï†).symm
  rw [(Ï†.surjective hÏ†).range_eq, image_univ]

/-- This is only a stepping stone potentially useful for `SurroundingFamily.surrounds_of_close`,
  but not needed by itself. -/
theorem Surrounds.eventually_surrounds [FiniteDimensional â„ F] (h : Î³.Surrounds x) :
    âˆƒ Îµ > 0,
      âˆ€ (Î³' : Loop F) (y : F), (âˆ€ z, dist (Î³' z) (Î³ z) < Îµ) â†’ dist y x < Îµ â†’ Î³'.Surrounds y := by
  obtain âŸ¨t, w, hâŸ© := h
  obtain âŸ¨W, hWâŸ© := smooth_surroundingPts h
  obtain âŸ¨Îµ, hÎµ, hâŸ© := Metric.eventually_nhds_iff.mp hW
  refine âŸ¨Îµ, hÎµ, fun Î³' y hÎ³' hy â†¦ âŸ¨t, W y (Î³' âˆ˜ t), ?_âŸ©âŸ©
  refine (@h âŸ¨y, Î³' âˆ˜ tâŸ© ?_).2
  simp_rw [Prod.dist_eq, max_lt_iff, dist_pi_lt_iff hÎµ]
  exact âŸ¨hy, fun b â†¦ hÎ³' (t b)âŸ©

end Loop

section surroundingLoop

variable {O : Set F} {b : F} {p : Fin (d + 1) â†’ F} (O_conn : IsPathConnected O)
  (hp : âˆ€ i, p i âˆˆ O) (hb : b âˆˆ O)

/-- witness of `surrounding_loop_of_convexHull` -/
def surroundingLoop : â„ â†’ Loop F :=
  Loop.roundTripFamily <| (O_conn.somePath hb (hp 0)).trans <| O_conn.pathThrough hp d

variable {O_conn hp hb}

/-- TODO: continuity note -/
@[fun_prop]
theorem continuous_surroundingLoop : Continuous â†¿(surroundingLoop O_conn hp hb) := by
  unfold surroundingLoop; fun_prop

@[simp]
theorem surroundingLoop_zero_right (t : â„) : surroundingLoop O_conn hp hb t 0 = b :=
  Loop.roundTripFamily_based_at t

@[simp]
theorem surroundingLoop_zero_left (s : â„) : surroundingLoop O_conn hp hb 0 s = b := by
  simp only [surroundingLoop, Loop.roundTripFamily_zero]; rfl

theorem surroundingLoop_mem (t s : â„) : surroundingLoop O_conn hp hb t s âˆˆ O := by
  revert s
  rw [â† range_subset_iff]
  simp only [surroundingLoop, Loop.roundTripFamily, Loop.roundTrip_range,
    cast_coe]
  refine Subset.trans (truncate_range _) ?_
  simp only [trans_range, union_subset_iff, O_conn.range_somePath_subset,
    O_conn.range_pathThrough_subset, true_and]

theorem surroundingLoop_surrounds {f : F} {w : Fin (d + 1) â†’ â„} (h : SurroundingPts f p w) :
    (surroundingLoop O_conn hp hb 1).Surrounds f := by
  rw [Loop.surrounds_iff_range_subset_range]
  refine âŸ¨p, w, h, ?_âŸ©
  simp only [surroundingLoop, Loop.roundTripFamily_one, Loop.roundTrip_range, trans_range,
    range_subset_iff, mem_union, O_conn.mem_range_pathThrough, or_true, forall_true_iff]

theorem surroundingLoop_projI (t : â„) :
    surroundingLoop O_conn hp hb (projI t) = surroundingLoop O_conn hp hb t :=
  Loop.roundTrip_eq fun s â†¦ by simp_rw [Path.cast_coe, truncate_projI_right]

-- unused
theorem surroundingLoop_of_le_zero (s : â„) {t : â„} (ht : t â‰¤ 0) :
    surroundingLoop O_conn hp hb t s = b := by
  rw [â† surroundingLoop_projI, projI_eq_zero.mpr ht, surroundingLoop_zero_left]

-- unused
theorem surroundingLoop_of_ge_one (s : â„) {t : â„} (ht : 1 â‰¤ t) :
    surroundingLoop O_conn hp hb t s = surroundingLoop O_conn hp hb 1 s := by
  rw [â† surroundingLoop_projI t, projI_eq_one.mpr ht]

theorem surrounding_loop_of_convexHull [FiniteDimensional â„ F] {f b : F} {O : Set F}
    (O_op : IsOpen O) (O_conn : IsConnected O) (hsf : f âˆˆ convexHull â„ O) (hb : b âˆˆ O) :
    âˆƒ Î³ : â„ â†’ Loop F,
      Continuous â†¿Î³ âˆ§
        (âˆ€ t, Î³ t 0 = b) âˆ§
          (âˆ€ s, Î³ 0 s = b) âˆ§
            (âˆ€ s t, Î³ (projI t) s = Î³ t s) âˆ§ (âˆ€ t s, Î³ t s âˆˆ O) âˆ§ (Î³ 1).Surrounds f := by
  rcases surrounded_of_convexHull O_op hsf with âŸ¨p, w, h, hpâŸ©
  rw [O_op.isConnected_iff_isPathConnected] at O_conn
  exact âŸ¨surroundingLoop O_conn hp hb, continuous_surroundingLoop, surroundingLoop_zero_right,
    surroundingLoop_zero_left, fun s t â†¦ by rw [surroundingLoop_projI], surroundingLoop_mem,
    surroundingLoop_surrounds hâŸ©

end surroundingLoop

/-- `Î³` forms a family of loops surrounding `g` with base `b`.
In contrast to the notes we assume that `base` and `tâ‚€` hold universally. -/
structure SurroundingFamily (g b : E â†’ F) (Î³ : E â†’ â„ â†’ Loop F) (U : Set E) : Prop where
  protected base : âˆ€ (x : E) (t : â„), Î³ x t 0 = b x
  protected tâ‚€ : âˆ€ (x : E) (s : â„), Î³ x 0 s = b x
  protected projI : âˆ€ (x : E) (t : â„) (s : â„), Î³ x (projI t) s = Î³ x t s
  protected surrounds : âˆ€ x âˆˆ U, (Î³ x 1).Surrounds <| g x
  protected cont : Continuous â†¿Î³

/-- `Î³` forms a family of loops surrounding `g` with base `b` in `Î©`. -/
structure SurroundingFamilyIn (g b : E â†’ F) (Î³ : E â†’ â„ â†’ Loop F) (U : Set E)
    (Î© : Set <| E Ã— F) : Prop extends SurroundingFamily g b Î³ U where
  val_in' : âˆ€ x âˆˆ U, âˆ€ t âˆˆ I, âˆ€ s âˆˆ I, (x, Î³ x t s) âˆˆ Î©

namespace SurroundingFamily

variable {g b : E â†’ F} {Î³ : E â†’ â„ â†’ Loop F} {U : Set E}

omit [NormedSpace â„ E]

protected theorem one (h : SurroundingFamily g b Î³ U) (x : E) (t : â„) : Î³ x t 1 = b x := by
  rw [Loop.one, h.base]

protected theorem t_le_zero (h : SurroundingFamily g b Î³ U) (x : E) (s : â„) {t : â„} (ht : t â‰¤ 0) :
    Î³ x t s = Î³ x 0 s := by rw [â† h.projI, projI_eq_zero.mpr ht]

protected theorem t_le_zero_eq_b (h : SurroundingFamily g b Î³ U) (x : E) (s : â„) {t : â„}
    (ht : t â‰¤ 0) : Î³ x t s = b x := by rw [h.t_le_zero x s ht, h.tâ‚€]

protected theorem t_ge_one (h : SurroundingFamily g b Î³ U) (x : E) (s : â„) {t : â„} (ht : 1 â‰¤ t) :
    Î³ x t s = Î³ x 1 s := by rw [â† h.projI, projI_eq_one.mpr ht]

protected theorem mono (h : SurroundingFamily g b Î³ U) {V : Set E} (hVU : V âŠ† U) :
    SurroundingFamily g b Î³ V :=
  âŸ¨h.base, h.tâ‚€, h.projI, fun x hx â†¦ h.surrounds x (hVU hx), h.contâŸ©

variable [NormedSpace â„ E] in
protected theorem surrounds_of_close_univ [FiniteDimensional â„ E] [FiniteDimensional â„ F]
    (hg : Continuous g) (h : SurroundingFamily g b Î³ univ) :
    âˆƒ Îµ : E â†’ â„,
      (âˆ€ x, 0 < Îµ x) âˆ§
        Continuous Îµ âˆ§
          âˆ€ (x) (Î³' : Loop F), (âˆ€ z, dist (Î³' z) (Î³ x 1 z) < Îµ x) â†’ Î³'.Surrounds (g x) := by
  let P : E â†’ â„ â†’ Prop := fun x t â†¦
    0 < t âˆ§ âˆ€ Î³' : Loop F, (âˆ€ z, dist (Î³' z) (Î³ x 1 z) < t) â†’ Î³'.Surrounds (g x)
  have hP : âˆ€ x, Convex â„ {t | P x t} := by
    intro x
    rw [convex_iff_ordConnected]
    constructor
    rintro Îµâ‚ hÎµâ‚ Îµâ‚‚ hÎµâ‚‚ Îµâ‚ƒ âŸ¨hÎµâ‚â‚ƒ, hÎµâ‚ƒâ‚‚âŸ©
    exact âŸ¨hÎµâ‚.1.trans_le hÎµâ‚â‚ƒ, fun Î³ hÎ³ â†¦ hÎµâ‚‚.2 Î³ fun z â†¦ (hÎ³ z).trans_le hÎµâ‚ƒâ‚‚âŸ©
  rsuffices âŸ¨Îµ, hÎµ, hPÎµâŸ© : âˆƒ Îµ : E â†’ â„, ğ’ 0 Îµ âˆ§ âˆ€ x, P x (Îµ x)
  -- obtain âŸ¨Îµ, hÎµ, hPÎµâŸ© := exists_contDiff_of_convex hP _
  Â· exact âŸ¨Îµ, fun x â†¦ (hPÎµ x).1, contDiff_zero.mp hÎµ, fun x â†¦ (hPÎµ x).2âŸ©
  refine exists_contDiff_of_convex hP fun x â†¦ ?_
  obtain âŸ¨Îµ, hÎµ, h2âŸ© := (h.surrounds x (mem_univ _)).eventually_surrounds
  have h3 : {y : E | dist (g y) (g x) < Îµ} âˆˆ ğ“ x :=
    (Metric.isOpen_ball.preimage hg).mem_nhds
      (by simp_rw [mem_preimage, Metric.mem_ball, dist_self, hÎµ.lt])
  have h4 : {y : E | âˆ€ z, dist (Î³ y 1 z) (Î³ x 1 z) < Îµ / 2} âˆˆ ğ“ x := by
    refine IsOpen.mem_nhds ?_ fun z â†¦ by simp_rw [dist_self, half_pos hÎµ]
    have hc : Continuous â†¿fun y s â†¦ dist (Î³ y 1 s) (Î³ x 1 s) :=
      (h.cont.compâ‚ƒ continuous_fst continuous_const continuous_snd).dist
        (h.cont.compâ‚ƒ continuous_const continuous_const continuous_snd)
    have : IsOpen {y : E | sSup ((fun z â†¦ dist (Î³ y 1 z) (Î³ x 1 z)) '' I) < Îµ / 2} :=
      isOpen_lt (isCompact_Icc.continuous_sSup hc) continuous_const
    have hc : âˆ€ y, Continuous fun s â†¦ dist (Î³ y 1 s) (Î³ x 1 s) := fun y â†¦
      hc.compâ‚‚ continuous_const continuous_id
    simp_rw [isCompact_Icc.sSup_lt_iff_of_continuous (nonempty_Icc.mpr zero_le_one)
        (hc _).continuousOn] at this
    convert this using 1
    ext y
    refine âŸ¨fun h z _ â†¦ h z, fun h z â†¦ ?_âŸ©
    rw [â† (Î³ y 1).fract_eq, â† (Î³ x 1).fract_eq]
    exact h _ (unitInterval.fract_mem _)
  refine âŸ¨_, inter_mem h4 h3, fun _ â†¦ Îµ / 2, contDiffOn_const, fun y hy â†¦
      âŸ¨half_pos hÎµ, fun Î³' hÎ³' â†¦ h2 _ _ (fun z â†¦ ?_) hy.2âŸ©âŸ©
  exact (dist_triangle _ _ _).trans_lt ((add_lt_add (hÎ³' z) (hy.1 z)).trans_le (add_halves Îµ).le)

-- proof using `surrounds_of_close`
-- begin
--   obtain âŸ¨Îµ, hÎµ, hcÎµ, hÎ³ÎµâŸ© := h.surrounds_of_close hg isOpen_univ,
--   exact âŸ¨Îµ, Î» x, hÎµ x (mem_univ _), continuous_iff_continuous_on_univ.mpr hcÎµ,
--     Î» x, hÎ³Îµ x (mem_univ _)âŸ©
-- end
/-- A surrounding family induces a family of paths from `b x` to `b x`.
We defined the concatenation we need on `path`, so we need to turn a surrounding
family into the family of paths. -/
@[simps]
protected def path (h : SurroundingFamily g b Î³ U) (x : E) (t : â„) : Path (b x) (b x) where
  toFun s := Î³ x t s
  continuous_toFun :=
    (h.cont.compâ‚ƒ continuous_const continuous_const continuous_id).comp continuous_subtype_val
  source' := h.base x t
  target' := h.one x t

@[fun_prop]
theorem continuous_path {X : Type*} [TopologicalSpace X] (h : SurroundingFamily g b Î³ U)
    {t : X â†’ â„} {f : X â†’ E} {s : X â†’ I} (hf : Continuous f) (ht : Continuous t)
    (hs : Continuous s) : Continuous fun x â†¦ h.path (f x) (t x) (s x) :=
  h.cont.compâ‚ƒ hf ht hs.subtype_val

@[simp]
theorem path_extend_fract (h : SurroundingFamily g b Î³ U) (t s : â„) (x : E) :
    (h.path x t).extend (fract s) = Î³ x t s := by
  rw [extend_extends _ (unitInterval.fract_mem s), â† Loop.fract_eq]; rfl

@[simp]
theorem range_path (h : SurroundingFamily g b Î³ U) (x : E) (t : â„) :
    range (h.path x t) = range (Î³ x t) := by
  simp only [Loop.range_eq_image, SurroundingFamily.path, Path.coe_mk_mk]
  erw [range_comp]
  simp only [Subtype.range_coe]

@[simp]
theorem path_tâ‚€ (h : SurroundingFamily g b Î³ U) (x : E) : h.path x 0 = .refl (b x) := by
  ext t
  exact h.tâ‚€ x t

end SurroundingFamily

variable {g b : E â†’ F} {U K C : Set E} {Î© : Set (E Ã— F)}

namespace SurroundingFamilyIn

variable {Î³ : E â†’ â„ â†’ Loop F}

omit [NormedSpace â„ E]

/-- Abbreviation for `toSurroundingFamily` -/
theorem to_sf (h : SurroundingFamilyIn g b Î³ U Î©) : SurroundingFamily g b Î³ U :=
  h.toSurroundingFamily

theorem val_in (h : SurroundingFamilyIn g b Î³ U Î©) {x : E} (hx : x âˆˆ U) {t : â„} {s : â„} :
    (x, Î³ x t s) âˆˆ Î© := by
  rw [â† Loop.fract_eq, â† h.projI]
  exact h.val_in' x hx (projI t) projI_mem_Icc (fract s) (unitInterval.fract_mem s)

protected theorem mono (h : SurroundingFamilyIn g b Î³ U Î©) {V : Set E} (hVU : V âŠ† U) :
    SurroundingFamilyIn g b Î³ V Î© :=
  âŸ¨h.to_sf.mono hVU, fun x hx â†¦ h.val_in' x (hVU hx)âŸ©

/-- Continuously reparameterize a `surroundingFamilyIn` so that it is constant near
  `s âˆˆ {0,1}` and `t âˆˆ {0,1}` -/
protected theorem reparam (h : SurroundingFamilyIn g b Î³ U Î©) :
    SurroundingFamilyIn g b (fun x t â†¦ (Î³ x (linearReparam t)).reparam linearReparam) U Î© := by
  refine âŸ¨âŸ¨?_, ?_, ?_, ?_, ?_âŸ©, ?_âŸ©
  Â· intro x t; rw [Loop.reparam_apply, linearReparam_zero, h.base]
  Â· intro x s; rw [Loop.reparam_apply, linearReparam_zero, h.tâ‚€]
  Â· intro x t s; simp_rw [Loop.reparam_apply, linearReparam_projI, h.projI]
  Â· intro x hx; rw [linearReparam_one]
    exact (h.surrounds x hx).reparam continuous_linearReparam
  Â· exact h.cont.compâ‚ƒ continuous_fst continuous_linearReparam.fst'.snd'
      continuous_linearReparam.snd'.snd'
  Â· exact fun x hx t _ s _ â†¦ h.val_in hx

end SurroundingFamilyIn

section local_loops

variable {xâ‚€ : E} (hÎ©_conn : IsPathConnected (Prod.mk xâ‚€ â»Â¹' Î©)) (hb_in : (xâ‚€, b xâ‚€) âˆˆ Î©)
  {p : Fin (d + 1) â†’ F} (hp : âˆ€ i, p i âˆˆ Prod.mk xâ‚€ â»Â¹' Î©)

omit [NormedSpace â„ E]

/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
-- /-- The witness of `local_loops`. -/
-- def local_loops_def (x : E) (t : â„) : loop F :=
-- b x - b xâ‚€ +áµ¥ surrounding_loop hÎ©_conn hp hb_in t
/-- Note: The conditions in this lemma are currently a bit weaker than the ones mentioned in the
blueprint.
TODO: use `local_loops_def`
-/
theorem local_loops [FiniteDimensional â„ F] {xâ‚€ : E} (hÎ©_op : âˆƒ U âˆˆ ğ“ xâ‚€, IsOpen (Î© âˆ© fst â»Â¹' U))
    (hg : ContinuousAt g xâ‚€) (hb : Continuous b)
    (hconv : g xâ‚€ âˆˆ convexHull â„ (connectedComponentIn (Prod.mk xâ‚€ â»Â¹' Î©) <| b xâ‚€)) :
    âˆƒ Î³ : E â†’ â„ â†’ Loop F, âˆƒ U âˆˆ ğ“ xâ‚€, SurroundingFamilyIn g b Î³ U Î© := by
  have hÎ©_op_xâ‚€ : IsOpen (connectedComponentIn (Prod.mk xâ‚€ â»Â¹' Î©) <| b xâ‚€) :=
    (isOpen_slice_of_isOpen_over hÎ©_op).connectedComponentIn
  have b_in : b xâ‚€ âˆˆ Prod.mk xâ‚€ â»Â¹' Î© :=
    connectedComponentIn_nonempty_iff.mp (convexHull_nonempty_iff.mp âŸ¨g xâ‚€, hconvâŸ©)
  have hÎ©_conn : IsConnected (connectedComponentIn (Prod.mk xâ‚€ â»Â¹' Î©) <| b xâ‚€) :=
    isConnected_connectedComponentIn_iff.mpr b_in
  have hb_in : b xâ‚€ âˆˆ (connectedComponentIn (Prod.mk xâ‚€ â»Â¹' Î©) <| b xâ‚€) :=
    mem_connectedComponentIn b_in
  rcases surrounding_loop_of_convexHull hÎ©_op_xâ‚€ hÎ©_conn hconv hb_in with
    âŸ¨Î³, h1Î³, h2Î³, h3Î³, h4Î³, h5Î³, h6Î³âŸ©
  have h5Î³ : âˆ€ t s : â„, Î³ t s âˆˆ mk xâ‚€ â»Â¹' Î© := fun t s â†¦ connectedComponentIn_subset _ _ (h5Î³ t s)
  let Î´ : E â†’ â„ â†’ Loop F := fun x t â†¦ (b x - b xâ‚€) +áµ¥ Î³ t
  have hÎ´ : Continuous â†¿Î´ := by
    dsimp only [Î´, HasUncurry.uncurry, Loop.vadd_apply]
    fun_prop
  have hÎ´xâ‚€ : âˆ€ t s, Î´ xâ‚€ t s = Î³ t s := by
    intro t s
    simp only [Î´, zero_add, Loop.vadd_apply, sub_self]
  have hÎ´s0 : âˆ€ x t, Î´ x t 0 = b x := by intro x t; simp [Î´, h2Î³]
  have hÎ´t0 : âˆ€ x s, Î´ x 0 s = b x := by intro x s; simp [Î´, h3Î³]
  have hÎ´t1 : âˆ€ x t s, Î´ x (projI t) s = Î´ x t s := by intro x t s; simp [Î´, h4Î³]
  have hÎ´Î© : âˆ€á¶  x in ğ“ xâ‚€, âˆ€ t âˆˆ I, âˆ€ s âˆˆ I, (x, Î´ x t s) âˆˆ Î© := by
    rcases hÎ©_op with âŸ¨U, hUxâ‚€, hUâŸ©
    -- todo: this is nicer with `IsCompact.eventually_forall_of_forall_eventually` twice, but then
    -- we need the continuity of `Î´` with the arguments reassociated differently.
    have : âˆ€á¶  x : E in ğ“ xâ‚€, âˆ€ ts : â„ Ã— â„, ts âˆˆ I Ã—Ë¢ I â†’ (x, Î´ x ts.1 ts.2) âˆˆ Î© := by
      apply (isCompact_Icc.prod isCompact_Icc).eventually_forall_mem
      Â· fun_prop
      Â· rintro âŸ¨t, sâŸ© _
        rw [hÎ´xâ‚€]
        change Î© âˆˆ ğ“ (xâ‚€, Î³ t s)
        exact mem_nhds_iff.mpr
          âŸ¨_, inter_subset_left, hU, âŸ¨h5Î³ t s, show xâ‚€ âˆˆ U from mem_of_mem_nhds hUxâ‚€âŸ©âŸ©
    refine this.mono ?_; intro x h t ht s hs; exact h (t, s) âŸ¨ht, hsâŸ©
  have hÎ´surr : âˆ€á¶  x in ğ“ xâ‚€, (Î´ x 1).Surrounds (g x) := by
    rcases h6Î³ with âŸ¨p, w, hâŸ©
    obtain âŸ¨W, hWâŸ© := smooth_surroundingPts h
    let c : E â†’ F Ã— (Fin (d + 1) â†’ F) := fun x â†¦ (g x, Î´ x 1 âˆ˜ p)
    have hc : ContinuousAt c xâ‚€ := by fun_prop
    have hcxâ‚€ : c xâ‚€ = (g xâ‚€, Î³ 1 âˆ˜ p) := by
      simp [c, Î´]
    rw [â† hcxâ‚€] at hW
    filter_upwards [hc.tendsto.eventually hW]
    rintro x âŸ¨_, hxâŸ©
    exact âŸ¨_, _, hxâŸ©
  exact âŸ¨Î´, _, hÎ´Î©.and hÎ´surr, âŸ¨âŸ¨hÎ´s0, hÎ´t0, hÎ´t1, fun x â†¦ And.right, hÎ´âŸ©, fun x â†¦ And.leftâŸ©âŸ©

/-- A tiny reformulation of `local_loops` where the existing `U` is open. -/
theorem local_loops_open [FiniteDimensional â„ F] {xâ‚€ : E}
    (hÎ©_op : âˆƒ U âˆˆ ğ“ xâ‚€, IsOpen (Î© âˆ© fst â»Â¹' U)) (hg : ContinuousAt g xâ‚€) (hb : Continuous b)
    (hconv : g xâ‚€ âˆˆ convexHull â„ (connectedComponentIn (Prod.mk xâ‚€ â»Â¹' Î©) <| b xâ‚€)) :
    âˆƒ (Î³ : E â†’ â„ â†’ Loop F) (U : Set E), IsOpen U âˆ§ xâ‚€ âˆˆ U âˆ§ SurroundingFamilyIn g b Î³ U Î© := by
  obtain âŸ¨Î³, U, hU, hÎ³âŸ© := local_loops hÎ©_op hg hb hconv
  obtain âŸ¨V, hVU, hV, hxâ‚€VâŸ© := mem_nhds_iff.mp hU
  exact âŸ¨Î³, V, hV, hxâ‚€V, hÎ³.mono hVUâŸ©

end local_loops

/-- Function used in `satisfied_or_refund`. Rename. -/
def Ï (t : â„) : â„ :=
  projI <| 2 * (1 - t)

attribute [fun_prop] continuous_projI

@[fun_prop]
theorem continuous_Ï : Continuous Ï := by unfold Ï; fun_prop

@[simp]
theorem Ï_eq_one {x : â„} : Ï x = 1 â†” x â‰¤ 1 / 2 := by
  rw [Ï, projI_eq_one]
  constructor <;> intros <;> linarith

@[simp]
theorem Ï_eq_one_of_le {x : â„} (h : x â‰¤ 1 / 2) : Ï x = 1 :=
  Ï_eq_one.mpr h

@[simp]
theorem Ï_eq_one_of_nonpos {x : â„} (h : x â‰¤ 0) : Ï x = 1 :=
  Ï_eq_one_of_le <| h.trans <| by norm_num

@[simp]
theorem Ï_eq_zero {x : â„} : Ï x = 0 â†” 1 â‰¤ x := by
  rw [Ï, projI_eq_zero]
  constructor <;> intros <;> linarith

@[simp]
theorem Ï_eq_zero_of_le {x : â„} (h : 1 â‰¤ x) : Ï x = 0 :=
  Ï_eq_zero.mpr h

theorem Ï_mem_I {x : â„} : Ï x âˆˆ I :=
  projI_mem_Icc

section satisfied_or_refund

variable {Î³â‚€ Î³â‚ : E â†’ â„ â†’ Loop F}

variable (hâ‚€ : SurroundingFamily g b Î³â‚€ U) (hâ‚ : SurroundingFamily g b Î³â‚ U)

/-- The homotopy of surrounding families of loops used in lemma `satisfied_or_refund`.
  Having this as a separate definition is useful, because the construction actually gives some
  more information about the homotopy than the theorem `satisfied_or_refund` gives. -/
def sfHomotopy (Ï„ : â„) (x : E) (t : â„) :=
  Loop.ofPath <|
    (hâ‚€.path x <| Ï Ï„ * projI t).strans (hâ‚.path x <| Ï (1 - Ï„) * projI t) <|
      projIcc 0 1 zero_le_one (1 - Ï„)

variable {hâ‚€ hâ‚}

omit [NormedSpace â„ E]

@[simp]
theorem sfHomotopy_zero : sfHomotopy hâ‚€ hâ‚ 0 = Î³â‚€ := by
  ext x t s
  simp only [sfHomotopy, one_mul, Ï_eq_one_of_nonpos le_rfl, SurroundingFamily.path_extend_fract,
             sub_zero, Loop.ofPath_apply, Icc.mk_one, projIcc_right, Path.strans_one, hâ‚€.projI]

@[simp]
theorem sfHomotopy_one : sfHomotopy hâ‚€ hâ‚ 1 = Î³â‚ := by
  ext x t s
  simp only [sfHomotopy, Path.strans_zero, Icc.mk_zero, one_mul, Ï_eq_one_of_nonpos le_rfl,
    SurroundingFamily.path_extend_fract, projIcc_left, Loop.ofPath_apply, sub_self, hâ‚.projI]

attribute [fun_prop] continuous_projIcc

@[fun_prop]
theorem Continuous.sfHomotopy {X : Type*} [UniformSpace X]
    [LocallyCompactSpace X] {Ï„ t s : X â†’ â„} {f : X â†’ E} (hÏ„ : Continuous Ï„) (hf : Continuous f)
    (ht : Continuous t) (hs : Continuous s) :
    Continuous fun x â†¦ sfHomotopy hâ‚€ hâ‚ (Ï„ x) (f x) (t x) (s x) := by
  refine Continuous.ofPath _ _ _ ?_ hs
  refine Continuous.path_strans ?_ ?_ ?_ ?_ ?_ continuous_snd
  Â· fun_prop
  Â· fun_prop
  Â· intro x s hs; simp only [projIcc_eq_zero, sub_nonpos] at hs
    simp only [hs, hâ‚€.tâ‚€, MulZeroClass.zero_mul, SurroundingFamily.path_apply, Ï_eq_zero_of_le]
  Â· intro x s hs; simp only [projIcc_eq_one] at hs
    simp only [hs, hâ‚.tâ‚€, MulZeroClass.zero_mul, SurroundingFamily.path_apply, Ï_eq_zero_of_le]
  Â· fun_prop

/-- In this lemmas and the lemmas below we add `FiniteDimensional â„ E` so that we can conclude
 `LocallyCompactSpace E`. -/
@[fun_prop]
theorem continuous_sfHomotopy [NormedSpace â„ E] [FiniteDimensional â„ E] :
    Continuous â†¿(sfHomotopy hâ‚€ hâ‚) := by fun_prop

theorem surroundingFamily_sfHomotopy [NormedSpace â„ E] [FiniteDimensional â„ E] (Ï„ : â„) :
    SurroundingFamily g b (sfHomotopy hâ‚€ hâ‚ Ï„) U := by
  constructor
  Â· intro x t;
    simp only [sfHomotopy, Icc.mk_zero, zero_le_one, extend_extends, Path.source, Loop.ofPath_apply,
      left_mem_Icc, fract_zero]
  Â· intro x s
    -- have h2t : Ï Ï„ * t â‰¤ 0 := mul_nonpos_of_nonneg_of_nonpos (Ï_nonneg Ï„) ht,
    -- have h3t : Ï (1 - Ï„) * t â‰¤ 0 := mul_nonpos_of_nonneg_of_nonpos (Ï_nonneg _) ht,
    -- have h4t : t â‰¤ 1 := ht.trans zero_le_one,
    simp [sfHomotopy]
  Â· intro x t s; simp only [sfHomotopy, projI_projI]
  -- { intros x t s ht, simp only [sfHomotopy, min_eq_left ht, min_self] },
  Â· intro x hx; obtain (h | h) := le_total Ï„ (1 / 2)
    Â· have : Ï„ < 1 := h.trans_lt (by norm_num)
      refine (hâ‚€.surrounds x hx).mono ?_
      simp only [mul_one, Loop.range_ofPath, sfHomotopy, projI_one]
      exact Subset.trans (by simp only [SurroundingFamily.range_path, Ï_eq_one_of_le h]; rfl)
        (subset_range_strans_left <| by simp [this])
    Â· have : 0 < Ï„ := lt_of_lt_of_le (by norm_num) h
      have h : 1 - Ï„ â‰¤ 1 / 2 := by linarith
      refine (hâ‚.surrounds x hx).mono ?_
      simp only [mul_one, Loop.range_ofPath, sfHomotopy, projI_one]
      exact Subset.trans (by simp only [SurroundingFamily.range_path, Ï_eq_one_of_le h]; rfl)
        (subset_range_strans_right <| by simp [this])
  Â· fun_prop

/-- A more precise version of `sfHomotopy_in`. -/
theorem sfHomotopy_in' {Î¹} (hâ‚€ : SurroundingFamily g b Î³â‚€ U) (hâ‚ : SurroundingFamily g b Î³â‚ U)
    (Ï„ : Î¹ â†’ â„) (x : Î¹ â†’ E) (i : Î¹) {V : Set E} (hx : x i âˆˆ V) {t : â„} (ht : t âˆˆ I) {s : â„}
    (h_inâ‚€ : âˆ€ i, x i âˆˆ V â†’ âˆ€ t âˆˆ I, âˆ€ (s : â„), Ï„ i â‰  1 â†’ (x i, Î³â‚€ (x i) t s) âˆˆ Î©)
    (h_inâ‚ : âˆ€ i, x i âˆˆ V â†’ âˆ€ t âˆˆ I, âˆ€ (s : â„), Ï„ i â‰  0 â†’ (x i, Î³â‚ (x i) t s) âˆˆ Î©) :
    (x i, sfHomotopy hâ‚€ hâ‚ (Ï„ i) (x i) t s) âˆˆ Î© := by
  by_cases hÏ„0 : Ï„ i = 0; Â· simp [hÏ„0]; exact h_inâ‚€ i hx t ht s (by norm_num [hÏ„0])
  by_cases hÏ„1 : Ï„ i = 1; Â· simp [hÏ„1]; exact h_inâ‚ i hx t ht s (by norm_num [hÏ„1])
  generalize hy : sfHomotopy hâ‚€ hâ‚ (Ï„ i) (x i) t s = y
  have h2y : y âˆˆ range (sfHomotopy hâ‚€ hâ‚ (Ï„ i) (x i) t) := by rw [â† hy]; exact mem_range_self _
  rw [sfHomotopy, Loop.range_ofPath, projI_eq_self.mpr ht] at h2y
  replace h2y := range_strans_subset h2y
  rcases h2y with (âŸ¨s', rflâŸ© | âŸ¨s', rflâŸ©)
  Â· exact h_inâ‚€ _ hx _ (unitInterval.mul_mem Ï_mem_I ht) _ hÏ„1
  Â· exact h_inâ‚ _ hx _ (unitInterval.mul_mem Ï_mem_I ht) _ hÏ„0

theorem sfHomotopy_in (hâ‚€ : SurroundingFamilyIn g b Î³â‚€ U Î©) (hâ‚ : SurroundingFamilyIn g b Î³â‚ U Î©)
    (Ï„ : â„) â¦ƒx : Eâ¦„ (hx : x âˆˆ U) {t : â„} (ht : t âˆˆ I) {s : â„} :
    (x, sfHomotopy hâ‚€.to_sf hâ‚.to_sf Ï„ x t s) âˆˆ Î© :=
  sfHomotopy_in' hâ‚€.to_sf hâ‚.to_sf (fun _ â†¦ Ï„) (fun _ â†¦ x) () hx ht
    (fun _i hx _t _ _s _ â†¦ hâ‚€.val_in hx) fun _i hx _t _ _s _ â†¦ hâ‚.val_in hx

theorem surroundingFamilyIn_sfHomotopy [NormedSpace â„ E] [FiniteDimensional â„ E]
    (hâ‚€ : SurroundingFamilyIn g b Î³â‚€ U Î©) (hâ‚ : SurroundingFamilyIn g b Î³â‚ U Î©) (Ï„ : â„) :
    SurroundingFamilyIn g b (sfHomotopy hâ‚€.to_sf hâ‚.to_sf Ï„) U Î© :=
  âŸ¨surroundingFamily_sfHomotopy _, fun _x hx _t ht _s _hs â†¦ sfHomotopy_in hâ‚€ hâ‚ _ hx htâŸ©

theorem satisfied_or_refund [NormedSpace â„ E] [FiniteDimensional â„ E] {Î³â‚€ Î³â‚ : E â†’ â„ â†’ Loop F}
    (hâ‚€ : SurroundingFamilyIn g b Î³â‚€ U Î©) (hâ‚ : SurroundingFamilyIn g b Î³â‚ U Î©) :
    âˆƒ Î³ : â„ â†’ E â†’ â„ â†’ Loop F,
      (âˆ€ Ï„, SurroundingFamilyIn g b (Î³ Ï„) U Î©) âˆ§ Î³ 0 = Î³â‚€ âˆ§ Î³ 1 = Î³â‚ âˆ§ Continuous â†¿Î³ :=
  âŸ¨sfHomotopy hâ‚€.to_sf hâ‚.to_sf, surroundingFamilyIn_sfHomotopy hâ‚€ hâ‚, sfHomotopy_zero,
    sfHomotopy_one, continuous_sfHomotopyâŸ©

end satisfied_or_refund

section extend_loops

variable [FiniteDimensional â„ E]

/-
Note: we also want add the condition that `Î³ = Î³â‚€` outside a neighborhood of `Uâ‚á¶œ`.
This makes it easier to find the limit of a sequence of these constructions.
-/
theorem extend_loops {Uâ‚€ Uâ‚ Kâ‚€ Kâ‚ : Set E} (hUâ‚€ : IsOpen Uâ‚€) (hUâ‚ : IsOpen Uâ‚) (hKâ‚€ : IsClosed Kâ‚€)
    (hKâ‚ : IsClosed Kâ‚) (hKUâ‚€ : Kâ‚€ âŠ† Uâ‚€) (hKUâ‚ : Kâ‚ âŠ† Uâ‚) {Î³â‚€ Î³â‚ : E â†’ â„ â†’ Loop F}
    (hâ‚€ : SurroundingFamilyIn g b Î³â‚€ Uâ‚€ Î©) (hâ‚ : SurroundingFamilyIn g b Î³â‚ Uâ‚ Î©) :
    âˆƒ U âˆˆ ğ“Ë¢ (Kâ‚€ âˆª Kâ‚),
      âˆƒ Î³ : E â†’ â„ â†’ Loop F,
        SurroundingFamilyIn g b Î³ U Î© âˆ§
          (âˆ€á¶  x in ğ“Ë¢ Kâ‚€, Î³ x = Î³â‚€ x) âˆ§ âˆ€á¶  x in ğ“Ë¢ (Uâ‚á¶œ), Î³ x = Î³â‚€ x := by
  obtain âŸ¨Vâ‚€, hVâ‚€, hKVâ‚€, hVUâ‚€âŸ© := normal_exists_closure_subset hKâ‚€ hUâ‚€ hKUâ‚€
  let Lâ‚ := Kâ‚ \ Uâ‚€
  have hLâ‚ : IsClosed Lâ‚ := hKâ‚.sdiff hUâ‚€
  have hVâ‚€Lâ‚ : Disjoint (closure Vâ‚€) Lâ‚ := disjoint_sdiff_self_right.mono hVUâ‚€ Subset.rfl
  obtain âŸ¨Vâ‚‚, hVâ‚‚, hLVâ‚‚, h2Vâ‚‚âŸ© :=
    normal_exists_closure_subset hLâ‚ (isClosed_closure.isOpen_compl.inter hUâ‚)
      (subset_inter (subset_compl_iff_disjoint_left.mpr hVâ‚€Lâ‚) <| diff_subset.trans hKUâ‚)
  obtain âŸ¨Vâ‚, hVâ‚, hLVâ‚, hVâ‚â‚‚âŸ© := normal_exists_closure_subset hLâ‚ hVâ‚‚ hLVâ‚‚
  rw [subset_inter_iff, subset_compl_iff_disjoint_left] at h2Vâ‚‚
  rcases h2Vâ‚‚ with âŸ¨hVâ‚€â‚‚, hVâ‚‚Uâ‚âŸ©
  have hVUâ‚ : Vâ‚ âŠ† Uâ‚ := subset_closure.trans (hVâ‚â‚‚.trans <| subset_closure.trans hVâ‚‚Uâ‚)
  have hdisj : Disjoint (closure Vâ‚€ âˆª Vâ‚‚á¶œ) (closure Vâ‚) := by
    refine Disjoint.union_left (hVâ‚€â‚‚.mono_right (hVâ‚â‚‚.trans subset_closure)) ?_
    rw [â† subset_compl_iff_disjoint_left, compl_compl]; exact hVâ‚â‚‚
  refine âŸ¨Vâ‚€ âˆª Uâ‚ âˆ© Uâ‚€ âˆª Vâ‚, ((hVâ‚€.union <| hUâ‚.inter hUâ‚€).union hVâ‚).mem_nhdsSet.mpr ?_, ?_âŸ©
  Â· refine union_subset (hKVâ‚€.trans <| subset_union_left.trans <| subset_union_left) ?_
    rw [â† inter_union_diff Kâ‚];
    exact union_subset_union ((inter_subset_inter_left _ hKUâ‚).trans <| subset_union_right) hLVâ‚
  obtain âŸ¨Ï, h0Ï, h1Ï, -âŸ© :=
    exists_continuous_zero_one_of_isClosed (isClosed_closure.union hVâ‚‚.isClosed_compl)
      isClosed_closure hdisj
  let hâ‚€' : SurroundingFamilyIn g b Î³â‚€ (Uâ‚ âˆ© Uâ‚€) Î© := hâ‚€.mono inter_subset_right
  let hâ‚' : SurroundingFamilyIn g b Î³â‚ (Uâ‚ âˆ© Uâ‚€) Î© := hâ‚.mono inter_subset_left
  let Î³ := sfHomotopy hâ‚€'.to_sf hâ‚'.to_sf
  have hÎ³ : âˆ€ Ï„, SurroundingFamilyIn g b (Î³ Ï„) (Uâ‚ âˆ© Uâ‚€) Î© := surroundingFamilyIn_sfHomotopy hâ‚€' hâ‚'
  have heq1 : âˆ€ x âˆˆ closure Vâ‚€ âˆª Vâ‚‚á¶œ, Î³ (Ï x) x = Î³â‚€ x := by
    intro x hx
    simp_rw [Î³, h0Ï hx, Pi.zero_apply, sfHomotopy_zero]
  have heq2 : âˆ€ x âˆˆ Vâ‚€, Î³ (Ï x) x = Î³â‚€ x := fun x hx â†¦
    heq1 x (subset_closure.trans subset_union_left hx)
  refine âŸ¨fun x t â†¦ Î³ (Ï x) x t, ?_, ?_, ?_âŸ©
  Â· refine âŸ¨âŸ¨fun x â†¦ (hÎ³ <| Ï x).base x, fun x â†¦ (hÎ³ <| Ï x).tâ‚€ x, fun x â†¦ (hÎ³ <| Ï x).projI x,
      ?_, ?_âŸ©, ?_âŸ©
    Â· rintro x ((hx | hx) | hx)
      Â· simp_rw [heq2 x hx, hâ‚€.surrounds x (hVUâ‚€ <| subset_closure hx)]
      Â· simp_rw [Î³]
        exact (hÎ³ <| Ï x).surrounds x hx
      Â· simp_rw [Î³, h1Ï (subset_closure hx), Pi.one_apply, sfHomotopy_one, hâ‚.surrounds x (hVUâ‚ hx)]
    Â· fun_prop
    Â· intro x hx t ht s _; refine sfHomotopy_in' _ _ _ id _ hx ht ?_ ?_
      Â· intro x hx t _ht s hÏx; refine hâ‚€.val_in ?_; rcases hx with ((hx | âŸ¨-, hxâŸ©) | hx)
        Â· exact (subset_closure.trans hVUâ‚€) hx
        Â· exact hx
        Â· exact (hÏx <| h1Ï <| subset_closure hx).elim
      Â· intro x hx t _ht s hÏx; refine hâ‚.val_in ?_; rcases hx with ((hx | âŸ¨hx, -âŸ©) | hx)
        Â· exact (hÏx <| h0Ï <| subset_closure.trans subset_union_left hx).elim
        Â· exact hx
        Â· exact hVUâ‚ hx
  Â· exact eventually_of_mem (hVâ‚€.mem_nhdsSet.mpr hKVâ‚€) heq2
  Â· exact eventually_of_mem
      (isClosed_closure.isOpen_compl.mem_nhdsSet.mpr <| compl_subset_compl.mpr hVâ‚‚Uâ‚)
      fun x hx â†¦ heq1 x <| mem_union_right _ <| compl_subset_compl.mpr subset_closure hx

end extend_loops

def ContinuousGerm {x : E} (Ï† : Germ (ğ“ x) (â„ â†’ Loop F)) : Prop :=
  Quotient.liftOn' Ï†
    (fun Î³ â†¦ âˆ€ t s : â„, ContinuousAt (fun p : E Ã— â„ Ã— â„ â†¦ Î³ p.1 p.2.1 p.2.2) (x, t, s))
    (by
      rintro Î³ Î³' (h : {x | Î³ x = Î³' x} âˆˆ ğ“ x)
      ext
      refine forall_congr' fun t â†¦ forall_congr' fun s â†¦ continuousAt_congr ?_
      rw [nhds_prod_eq]
      apply mem_of_superset (Filter.prod_mem_prod h univ_mem)
      rintro âŸ¨x', pâŸ© âŸ¨hx' : Î³ x' = Î³' x', -âŸ©
      simp only [mem_setOf_eq, hx'])

variable (g b Î©)
omit [NormedSpace â„ E]

structure LoopFamilyGerm (x : E) (Ï† : Germ (ğ“ x) (â„ â†’ Loop F)) : Prop where
  base : âˆ€ t, Ï†.value t 0 = b x
  tâ‚€ : âˆ€ s, Ï†.value 0 s = b x
  projI : âˆ€ (t : â„) (s : â„), Ï†.value (projI t) s = Ï†.value t s
  cont : ContinuousGerm Ï†

structure SurroundingFamilyGerm (x : E) (Ï† : Germ (ğ“ x) (â„ â†’ Loop F)) : Prop where
  Surrounds : (Ï†.value 1).Surrounds <| g x
  val_in' : âˆ€ t âˆˆ I, âˆ€ s âˆˆ I, (x, Ï†.value t s) âˆˆ Î©

variable {g b Î©}

/-
The following proof is slightly tedious because the definition of `surroundingFamilyIn`
splits weirdly into `SurroundingFamily` which includes one condition on `C`
and one extra condition on `C` instead of putting everything which does not depend on `C`
on one side and the two conditions depending on `C` on the other side as we do here.
-/
theorem surroundingFamilyIn_iff_germ {Î³ : E â†’ â„ â†’ Loop F} :
    SurroundingFamilyIn g b Î³ C Î© â†”
      (âˆ€ x, LoopFamilyGerm b x Î³) âˆ§ âˆ€ x âˆˆ C, SurroundingFamilyGerm g Î© x Î³ := by
  constructor
  Â· rintro âŸ¨âŸ¨base, tâ‚€, projI, family_surrounds, family_contâŸ©, HâŸ©
    exact âŸ¨fun x â†¦ âŸ¨base x, tâ‚€ x, projI x, fun t s â†¦ family_cont.continuousAtâŸ©, fun x x_in â†¦
      âŸ¨family_surrounds x x_in, H x x_inâŸ©âŸ©
  Â· rintro âŸ¨h, h'âŸ©
    refine âŸ¨âŸ¨fun x â†¦ (h x).base, fun x â†¦ (h x).tâ‚€, fun x â†¦ (h x).projI,
        fun x hx â†¦ (h' x hx).Surrounds, ?_âŸ©,
      fun x hx â†¦ (h' x hx).val_in'âŸ©
    apply continuous_iff_continuousAt.mpr
    rintro âŸ¨x, t, sâŸ©
    apply (h x).cont

variable [NormedSpace â„ E] [FiniteDimensional â„ E] [FiniteDimensional â„ F]
  [SecondCountableTopology E]

theorem exists_surrounding_loops (hK : IsClosed K) (hÎ©_op : IsOpen Î©) (hg : âˆ€ x, ContinuousAt g x)
    (hb : Continuous b)
    (hconv : âˆ€ x, g x âˆˆ convexHull â„ (connectedComponentIn (Prod.mk x â»Â¹' Î©) <| b x))
    {Î³â‚€ : E â†’ â„ â†’ Loop F} (hÎ³â‚€_surr : âˆƒ V âˆˆ ğ“Ë¢ K, SurroundingFamilyIn g b Î³â‚€ V Î©) :
    âˆƒ Î³ : E â†’ â„ â†’ Loop F, SurroundingFamilyIn g b Î³ univ Î© âˆ§ âˆ€á¶  x in ğ“Ë¢ K, Î³ x = Î³â‚€ x := by
  rcases hÎ³â‚€_surr with âŸ¨V, V_in, hVâŸ©
  obtain âŸ¨hV, h'VâŸ© := surroundingFamilyIn_iff_germ.mp hV
  simp only [surroundingFamilyIn_iff_germ, mem_univ, forall_true_left, â† forall_and]
  apply
    relative_inductive_construction_of_loc (LoopFamilyGerm b) (SurroundingFamilyGerm g Î©) hK hV
      (mem_of_superset V_in h'V)
  Â· intro x
    rcases local_loops âŸ¨univ, univ_mem, by simp only [preimage_univ, inter_univ, hÎ©_op]âŸ© (hg x) hb
        (hconv x) with
      âŸ¨Î³, U, U_in, HâŸ©
    obtain âŸ¨H, H'âŸ© := surroundingFamilyIn_iff_germ.mp H
    exact âŸ¨Î³, H, mem_of_superset U_in H'âŸ©
  Â· intro Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ Î³â‚ Î³â‚‚ hUâ‚ hUâ‚‚ hKâ‚ hKâ‚‚ hKUâ‚ hKUâ‚‚ hÎ³â‚ hÎ³â‚‚ h'Î³â‚ h'Î³â‚‚
    rcases extend_loops hUâ‚ hUâ‚‚ hKâ‚.isClosed hKâ‚‚.isClosed hKUâ‚ hKUâ‚‚
      (surroundingFamilyIn_iff_germ.mpr âŸ¨hÎ³â‚, h'Î³â‚âŸ©)
      (surroundingFamilyIn_iff_germ.mpr âŸ¨hÎ³â‚‚, h'Î³â‚‚âŸ©) with âŸ¨U, U_in, Î³, H, H''âŸ©
    rcases surroundingFamilyIn_iff_germ.mp H with âŸ¨H, H'âŸ©
    exact âŸ¨Î³, H, mem_of_superset U_in H', Eventually.union_nhdsSet.mpr H''âŸ©
