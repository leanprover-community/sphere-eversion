import Mathlib.Analysis.Calculus.BumpFunction.InnerProduct
import Mathlib.Analysis.Convolution
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Periodic
import Mathlib.MeasureTheory.Measure.Haar.Unique
import Mathlib.Analysis.Calculus.BumpFunction.Normed
import SphereEversion.ToMathlib.Algebra.Ring.Periodic
import SphereEversion.ToMathlib.Analysis.ContDiff
import SphereEversion.Loops.Basic

/-!
# Delta mollifiers

A key ingredient in the proof of the reparametrization lemma is the existence of a smooth
approximation to the Dirac delta function. Such an approximation is a sequence of functions
`Œ¥ : ‚Ñï √ó S¬π ‚Üí S¬π`, `(i, t) ‚Ü¶ Œ¥·µ¢ t` such that:
 * `Œ¥·µ¢` is smooth for all `i`,
 * `Œ¥·µ¢` is non-negative for all `i`,
 * `‚à´ x in 0..1, (Œ¥·µ¢ x) = 1` for all `i`,
* `‚à´ x in 0..1, (Œ¥·µ¢ x) ‚Ä¢ f x ‚Üí f 0`, as `i ‚Üí ‚àû` for any continuous function `f` on `S¬π`.

This file contains a construction `approxDirac` of such a family `Œ¥` together with code which
packages this into the precise form required for the proof of the reparametrization lemma:
`deltaMollifier`, `Loop.mollify`.

The key ingredients are the existence of smooth "bump functions" and a powerful theory of
convolutions.
-/


noncomputable section

open Set Function MeasureTheory.MeasureSpace ContinuousLinearMap
open scoped Topology Filter Convolution ContDiff

variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ‚Ñù F]
section

/-! ## Bump family

In this section we construct `bump (n : ‚Ñï)`, a bump function with support in
`Ioo (-1/(n+2)) (1/(n+2))`.
-/


/-- `bump n` is a bump function on `‚Ñù` which has support `Ioo (-(1/(n+2))) (1/(n+2))`
and equals one on `Icc (-(1/(n+3))) (1/(n+3))`.
-/
def bump (n : ‚Ñï) : ContDiffBump (0 : ‚Ñù) where
  rIn := 1 / (n + 3)
  rOut := 1 / (n + 2)
  rIn_pos := by positivity
  rIn_lt_rOut := by
    apply one_div_lt_one_div_of_lt
    ¬∑ exact_mod_cast Nat.succ_pos _
    ¬∑ exact_mod_cast lt_add_one (n + 2)

theorem support_bump (n : ‚Ñï) : support (bump n) = Ioo (-((1 : ‚Ñù) / (n + 2))) (1 / (n + 2)) := by
  rw [(bump n).support_eq, Real.ball_eq_Ioo, zero_sub, zero_add, bump]

theorem support_bump_subset (n : ‚Ñï) : support (bump n) ‚äÜ Ioc (-(1 / 2)) (1 / 2) := by
  have ineg : 1 / (n + 2 : ‚Ñù) ‚â§ 1 / 2 := by apply one_div_le_one_div_of_le <;> norm_num
  rw [support_bump n]
  exact Ioo_subset_Ioc_self.trans (Ioc_subset_Ioc (neg_le_neg ineg) ineg)

theorem support_shifted_normed_bump_subset (n : ‚Ñï) (t : ‚Ñù) :
    (support fun x ‚Ü¶ (bump n).normed volume (x - t)) ‚äÜ Ioc (t - 1 / 2) (t + 1 / 2) := by
  change support ((bump n).normed volume ‚àò (¬∑ - t)) ‚äÜ _
  rw [Function.support_comp_eq_preimage, (bump n).support_normed_eq, ‚Üê (bump n).support_eq]
  refine (preimage_mono (support_bump_subset n)).trans ?_
  simp [sub_eq_add_neg, add_comm]

end

section

/-! # Periodize

In this section we turn any function `f : ‚Ñù ‚Üí E` into a 1-periodic function
`fun t : ‚Ñù ‚Ü¶ ‚àë·∂† n : ‚Ñ§, f (t+n)`.
-/


variable {M : Type*} [AddCommMonoid M]

/-- Turn a function into a 1-periodic function. If its support lies in a (non-closed) interval
of length 1, then this will be that function made periodic with period 1. -/
def periodize (f : ‚Ñù ‚Üí M) (t : ‚Ñù) : M :=
  ‚àë·∂† n : ‚Ñ§, f (t + n)

theorem periodic_periodize (f : ‚Ñù ‚Üí M) : Periodic (periodize f) 1 := by
  intro t
  unfold periodize
  have : (fun n : ‚Ñ§ ‚Ü¶ f (t + 1 + ‚Üën)) = fun n ‚Ü¶ f (t + (n + 1 : ‚Ñ§)) := by
    ext n; simp_rw [Int.cast_add, Int.cast_one, add_assoc, add_comm]
  simp_rw [this]
  let e := Equiv.addRight (1 : ‚Ñ§)
  let F : ‚Ñ§ ‚Üí M := fun n ‚Ü¶ f (t + n)
  change ‚àë·∂† n : ‚Ñ§, F (e n) = ‚àë·∂† n : ‚Ñ§, f (t + ‚Üën)
  apply finsum_comp_equiv

theorem periodize_nonneg {f : ‚Ñù ‚Üí ‚Ñù} (h : ‚àÄ t, 0 ‚â§ f t) (t : ‚Ñù) : 0 ‚â§ periodize f t := by
  unfold periodize
  obtain (H | H) := (support fun i : ‚Ñ§ ‚Ü¶ f (t + i)).finite_or_infinite
  ¬∑ rw [finsum_eq_sum _ H]
    exact Finset.sum_nonneg fun i _ ‚Ü¶ h _
  ¬∑ rwa [finsum_of_infinite_support]

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ‚Ñù E]

@[fun_prop]
theorem ContDiff.periodize {f : ‚Ñù ‚Üí E} {n : ‚Ñï‚àû} (h : ContDiff ‚Ñù n f) (h' : HasCompactSupport f) :
    ContDiff ‚Ñù n (periodize f) := by
  refine contDiff_iff_contDiffAt.mpr fun x ‚Ü¶ contDiffAt_finsum ?_ ?_
  ¬∑ intro y
    set N := Ioo (y - 1) (y + 1)
    refine ‚ü®N, (nhds_basis_Ioo_pos y).mem_of_mem zero_lt_one, ?_‚ü©
    let e := fun i : ‚Ñ§ ‚Ü¶ Equiv.addRight (i : ‚Ñù)
    change {i : ‚Ñ§ | ((support fun x : ‚Ñù ‚Ü¶ f (e i x)) ‚à© N).Nonempty}.Finite
    have hsupp : ‚àÄ i : ‚Ñ§, (support fun x : ‚Ñù ‚Ü¶ f (e i x)) = e i ‚Åª¬π' support f := fun _ ‚Ü¶ rfl
    have hsupp' : ‚àÄ i, (e i ‚Åª¬π' support f ‚à© N).Nonempty ‚Üî (support f ‚à© e i '' N).Nonempty := by
      intro i
      conv_lhs => rw [‚Üê (e i).preimage_image N, ‚Üê preimage_inter]
      rw [(e i).surjective.nonempty_preimage]
    simp_rw [hsupp, hsupp', inter_comm (support f)]
    refine (ProperlyDiscontinuousVAdd.finite_disjoint_inter_image
      (isCompact_Icc : IsCompact <| Icc (y - 1) (y + 1)) h').subset ?_
    intro i hi
    rw [mem_setOf_eq, ‚Üê nonempty_iff_ne_empty]
    apply Nonempty.mono _ hi
    gcongr
    ¬∑ rw [show (e i : ‚Ñù ‚Üí ‚Ñù) = VAdd.vadd i by ext x; exact add_comm x i]
      exact image_mono Ioo_subset_Icc_self
    exact subset_tsupport f
  ¬∑ fun_prop

theorem periodize_comp_sub (f : ‚Ñù ‚Üí M) (x t : ‚Ñù) :
    periodize (fun x' ‚Ü¶ f (x' - t)) x = periodize f (x - t) := by
  simp_rw [periodize, sub_add_eq_add_sub]

theorem periodize_smul_periodic (f : ‚Ñù ‚Üí ‚Ñù) {g : ‚Ñù ‚Üí E} (hg : Periodic g 1) (t : ‚Ñù) :
    periodize f t ‚Ä¢ g t = periodize (fun x ‚Ü¶ f x ‚Ä¢ g x) t := by
  dsimp only [periodize]
  rw [finsum_smul]
  congr 1
  ext n
  rw [OnePeriodic.add_int hg]

open MeasureTheory

theorem integral_periodize (f : ‚Ñù ‚Üí E) {a : ‚Ñù} (hf : support f ‚äÜ Ioc a (a + 1)) :
    ‚à´ t in a..a + 1, periodize f t = ‚à´ t in a..a + 1, f t := by
  apply intervalIntegral.integral_congr_ae
  have : ‚àÄ·µê x : ‚Ñù, x ‚àà uIoc a (a + 1) ‚Üí x ‚àà Ioo a (a + 1) := by
    rw [uIoc_of_le (le_add_of_nonneg_right (zero_le_one : (0 : ‚Ñù) ‚â§ 1))]
    have : ‚àÄ·µê x : ‚Ñù, x ‚â† a + 1 := by simp [ae_iff]
    apply this.mono
    rintro x (x_ne : x ‚â† a + 1) ‚ü®hx, hx'‚ü©
    exact ‚ü®hx, x_ne.le_iff_lt.mp hx'‚ü©
  apply this.mono
  intro t ht ht'
  specialize ht ht'
  dsimp only [periodize]
  have : (support fun n : ‚Ñ§ ‚Ü¶ f (t + n)) ‚äÜ ({0} : Finset ‚Ñ§) := fun n hn ‚Ü¶ by
    suffices n = 0 by simpa
    replace hn : t + n ‚àà Ioc a (a + 1) := hf (mem_support.mpr hn)
    cases ht
    cases hn
    have : -(1 : ‚Ñù) < n := by linarith
    have : -1 < n := mod_cast this
    have : (n : ‚Ñù) < 1 := by linarith
    norm_cast at this
    linarith
  simp [finsum_eq_sum_of_support_subset _ this]

-- if convenient we could set `[c,d] = [0,1]`
theorem intervalIntegral_periodize_smul (f : ‚Ñù ‚Üí ‚Ñù) (Œ≥ : Loop E) {a b c d : ‚Ñù} (h : b ‚â§ a + 1)
    (h2 : d = c + 1) (hf : support f ‚äÜ Ioc a b) :
    ‚à´ t in c..d, periodize f t ‚Ä¢ Œ≥ t = ‚à´ t, f t ‚Ä¢ Œ≥ t := by
  rw [h2]
  have : (support fun t ‚Ü¶ f t ‚Ä¢ Œ≥ t) ‚äÜ Ioc a (a + 1) := by
    erw [support_smul]
    exact (inter_subset_left.trans hf).trans (Ioc_subset_Ioc_right h)
  rw [‚Üê intervalIntegral.integral_eq_integral_of_support_subset this]
  simp_rw [periodize_smul_periodic _ Œ≥.periodic,
    Function.Periodic.intervalIntegral_add_eq (periodic_periodize fun x : ‚Ñù ‚Ü¶ f x ‚Ä¢ Œ≥ x) c a]
  exact integral_periodize _ this

end

section DeltaApprox

/-! ## An approximate Dirac "on the circle". -/


/-- A periodized bump function, which we can view as a function from `ùïä¬π ‚Üí ‚Ñù`. As `n ‚Üí ‚àû` this
tends to the Dirac delta function located at `0`. -/
def approxDirac (n : ‚Ñï) : ‚Ñù ‚Üí ‚Ñù :=
  periodize <| (bump n).normed volume

theorem periodic_approxDirac (n : ‚Ñï) : Periodic (approxDirac n) 1 := periodic_periodize _

theorem approxDirac_nonneg (n : ‚Ñï) (t : ‚Ñù) : 0 ‚â§ approxDirac n t :=
  periodize_nonneg (bump n).nonneg_normed t

@[fun_prop]
theorem contDiff_approxDirac (n : ‚Ñï) : ContDiff ‚Ñù ‚àû (approxDirac n) :=
  (bump n).contDiff_normed.periodize (bump n).hasCompactSupport_normed

theorem approxDirac_integral_eq_one (n : ‚Ñï) {a b : ‚Ñù} (h : b = a + 1) :
    ‚à´ s in a..b, approxDirac n s = 1 := by
  have supp : support ((bump n).normed volume) ‚äÜ Ioc (-(1 / 2)) (-(1 / 2) + 1) := by
    rw [show -(1 / 2 : ‚Ñù) + 1 = 1 / 2 by norm_num,
      show support ((bump n).normed volume) = _ from (bump n).support_normed_eq, Real.ball_zero_eq,
      show (bump n).rOut = 1 / (n + 2 : ‚Ñù) from rfl]
    have key : 1 / (n + 2 : ‚Ñù) ‚â§ 1 / 2 := by
      apply one_div_le_one_div_of_le
      ¬∑ norm_num
      norm_cast
      norm_num
    exact (Ioo_subset_Ioo (neg_le_neg key) key).trans Ioo_subset_Ioc_self
  rw [approxDirac, h,
    Function.Periodic.intervalIntegral_add_eq (periodic_periodize _) a (-(1 / 2 : ‚Ñù)),
    integral_periodize _ supp, intervalIntegral.integral_eq_integral_of_support_subset supp]
  exact (bump n).integral_normed

end DeltaApprox

section VersionOfDeltaMollifierUsingN

/-- A sequence of functions that converges to the Dirac delta function located at `t`, with the
properties that this sequence is everywhere positive and -/
def deltaMollifier (n : ‚Ñï) (t : ‚Ñù) : ‚Ñù ‚Üí ‚Ñù := fun x ‚Ü¶
  n / (n + 1) * approxDirac n (x - t) + 1 / (n + 1)

variable {n : ‚Ñï} {t : ‚Ñù}

theorem deltaMollifier_periodic : Periodic (deltaMollifier n t) 1 := fun x ‚Ü¶ by
  simp_rw [deltaMollifier, ‚Üê sub_add_eq_add_sub, periodic_approxDirac n (x - t)]

theorem deltaMollifier_pos (s : ‚Ñù) : 0 < deltaMollifier n t s := by
  have := approxDirac_nonneg n (s - t)
  exact add_pos_of_nonneg_of_pos (by positivity) (by positivity)

@[fun_prop]
theorem contDiff_deltaMollifier : ContDiff ‚Ñù ‚àû (deltaMollifier n t) := by
  unfold deltaMollifier; fun_prop

open intervalIntegral

@[simp]
theorem deltaMollifier_integral_eq_one : ‚à´ s in (0)..1, deltaMollifier n t s = 1 := by
  simp_rw [deltaMollifier]
  rw [integral_comp_sub_right (fun x ‚Ü¶ (n : ‚Ñù) / (n + 1) * approxDirac n x + 1 / (n + 1)) t,
    integral_add, integral_const_mul, integral_const, zero_sub, sub_neg_eq_add, sub_add_cancel,
    one_smul, approxDirac_integral_eq_one, mul_one, ‚Üê add_div, div_self]
  ¬∑ exact n.cast_add_one_pos.ne'
  ¬∑ rw [sub_eq_add_neg, add_comm]
  ¬∑ exact ((contDiff_approxDirac n).continuous.intervalIntegrable _ _).const_mul _
  ¬∑ exact intervalIntegrable_const

/-- `Œ≥.mollify n t` is a weighted average of `Œ≥` using weights `deltaMollifier n t`.
This means that as `n ‚Üí ‚àû` this value tends to `Œ≥ t`, but because `deltaMollifier n t` is positive,
we know that we can reparametrize `Œ≥` to obtain a loop that has `Œ≥.mollify n t` as its actual
average. -/
def Loop.mollify (Œ≥ : Loop F) (n : ‚Ñï) (t : ‚Ñù) : F :=
  ‚à´ s in (0)..1, deltaMollifier n t s ‚Ä¢ Œ≥ s

theorem Loop.mollify_eq_convolution (Œ≥ : Loop F) (hŒ≥ : Continuous Œ≥) (t : ‚Ñù) :
    Œ≥.mollify n t =
      ((n : ‚Ñù) / (n + 1)) ‚Ä¢ ((bump n).normed volume ‚ãÜ[lsmul ‚Ñù ‚Ñù]Œ≥) t +
        ((1 : ‚Ñù) / (n + 1)) ‚Ä¢ ‚à´ t in (0)..1, Œ≥ t := by
  simp_rw [Loop.mollify, deltaMollifier, add_smul, mul_smul]
  rw [integral_add]
  on_goal 1 =>
    simp_rw [integral_smul, approxDirac, ‚Üê periodize_comp_sub]
    rw [intervalIntegral_periodize_smul _ Œ≥ _ _ (support_shifted_normed_bump_subset n t)]
  ¬∑ simp_rw [MeasureTheory.convolution_eq_swap, ‚Üê neg_sub t, (bump n).normed_neg, lsmul_apply]
  ¬∑ linarith
  ¬∑ rw [zero_add]
  ¬∑ exact (continuous_const.smul
      (((contDiff_approxDirac n).continuous.comp (by fun_prop)).smul hŒ≥)).intervalIntegrable _ _
  ¬∑ apply Continuous.intervalIntegrable (by fun_prop)

end VersionOfDeltaMollifierUsingN
