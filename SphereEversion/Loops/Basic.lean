import SphereEversion.Notations
import SphereEversion.ToMathlib.Equivariant
import SphereEversion.ToMathlib.MeasureTheory.ParametricIntervalIntegral

/-!
# Basic definitions and properties of loops
-/

open Set Function FiniteDimensional Int TopologicalSpace

open scoped Topology unitInterval

noncomputable section

variable {K X X' Y Z : Type*}

-- variable [TopologicalSpace X'] [TopologicalSpace Y] [TopologicalSpace Z]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F] {F' : Type*} [NormedAddCommGroup F'] [NormedSpace â„ F']

/-! ## Definition and periodicity lemmas -/


variable (X)

/-- A loop is a function with domain `â„` and is periodic with period 1. -/
structure Loop where
  toFun : â„ â†’ X
  per' : âˆ€ t, toFun (t + 1) = toFun t

attribute [coe] Loop.toFun

instance : CoeFun (Loop X) fun _ â†¦ â„ â†’ X :=
  âŸ¨fun Î³ â†¦ Î³.toFunâŸ©

initialize_simps_projections Loop (toFun â†’ apply)

/-- Any function `Ï† : Î± â†’ loop X` can be seen as a function `Î± Ã— â„ â†’ X`. -/
instance hasUncurryLoop {Î± : Type*} : HasUncurry (Î± â†’ Loop X) (Î± Ã— â„) X :=
  âŸ¨fun Ï† p â†¦ Ï† p.1 p.2âŸ©

variable {X}

namespace Loop

@[simp]
protected theorem coe_mk {Î³ : â„ â†’ X} (h : âˆ€ t, Î³ (t + 1) = Î³ t) : â‡‘(âŸ¨Î³, hâŸ© : Loop X) = Î³ :=
  rfl

@[ext]
protected theorem ext : âˆ€ {Î³â‚ Î³â‚‚ : Loop X}, (Î³â‚ : â„ â†’ X) = Î³â‚‚ â†’ Î³â‚ = Î³â‚‚
  | âŸ¨_x, _h1âŸ©, âŸ¨.(_x), _h2âŸ©, rfl => rfl

/-- The constant loop. -/
@[simps]
def const (f : X) : Loop X :=
  âŸ¨fun _ â†¦ f, fun _ â†¦ rflâŸ©

instance Zero [Zero X] : Zero (Loop X) :=
  âŸ¨const 0âŸ©

@[simp]
theorem zero_fun [_root_.Zero X] : ((0 : Loop X) : â„ â†’ X) = (0 : â„ â†’ X) :=
  rfl

-- unused
@[simp]
theorem const_zero [_root_.Zero X] : const (0 : X) = (0 : Loop X) :=
  rfl

instance [Inhabited X] : Inhabited (Loop X) :=
  âŸ¨Loop.const defaultâŸ©

/-- Periodicity of loops restated in terms of the function coercion. -/
theorem per (Î³ : Loop X) : âˆ€ t, Î³ (t + 1) = Î³ t :=
  Loop.per' Î³

theorem periodic (Î³ : Loop X) : Function.Periodic Î³ 1 :=
  Loop.per' Î³

protected theorem one (Î³ : Loop X) : Î³ 1 = Î³ 0 := by convert Î³.per 0; rw [zero_add]

-- unused
theorem add_nat_eq (Î³ : Loop X) (t : â„) : âˆ€ n : â„•, Î³ (t + n) = Î³ t
  | 0 => by rw [Nat.cast_zero, add_zero]
  | Nat.succ n => by rw [â† Î³.add_nat_eq t n, Nat.cast_succ, â† add_assoc, Î³.per]

theorem add_int_eq (Î³ : Loop X) (t : â„) (n : â„¤) : Î³ (t + n) = Î³ t := by
  induction n using Int.induction_on with
  | zero => norm_cast; rw [add_zero]
  | succ n hn => rw [â† hn, Int.cast_add, â† add_assoc, Int.cast_one, Î³.per]
  | pred n hn => rw [â† hn, Int.cast_sub, add_sub, Int.cast_one, â† Î³.per, sub_add_cancel]

theorem fract_eq (Î³ : Loop X) : âˆ€ t, Î³ (fract t) = Î³ t := by
  intro t
  unfold fract
  rw [sub_eq_add_neg, â† Int.cast_neg]
  exact Î³.add_int_eq ..

theorem range_eq_image (Î³ : Loop X) : range Î³ = Î³ '' I := by
  apply eq_of_subset_of_subset
  Â· rw [range_subset_iff]
    exact fun y â†¦ âŸ¨fract y, unitInterval.fract_mem y, Î³.fract_eq _âŸ©
  Â· rintro y âŸ¨x, -, hxyâŸ©
    exact âŸ¨x, hxyâŸ©

/-- Transforming a loop by applying function `f`. -/
@[simps]
def transform (Î³ : Loop X) (f : X â†’ X') : Loop X' :=
  âŸ¨fun t â†¦ f (Î³ t), fun t â†¦ by dsimp only; rw [Î³.per]âŸ©

/-- Adding two loops pointwise. -/
@[simps]
instance Add [Add X] : Add (Loop X) :=
  âŸ¨fun Î³â‚ Î³â‚‚ â†¦ âŸ¨fun t â†¦ Î³â‚ t + Î³â‚‚ t, fun t â†¦ by simp_rw [Loop.per]âŸ©âŸ©

@[simps]
instance Neg [Neg X] : Neg (Loop X) :=
  âŸ¨fun Î³ â†¦ âŸ¨fun t â†¦ -Î³ t, fun t â†¦ by simp_rw [Loop.per]âŸ©âŸ©

instance [AddCommGroup X] : AddCommGroup (Loop X) :=
  { Loop.Add, Loop.Zero, Loop.Neg with
    add_assoc := fun Î³â‚ Î³â‚‚ Î³â‚ƒ â†¦ by ext t; apply add_assoc
    add_comm := fun Î³â‚ Î³â‚‚ â†¦ by ext t; apply add_comm
    zero_add := fun Î³ â†¦ by ext t; apply zero_add
    add_zero := fun Î³ â†¦ by ext t; apply add_zero
    neg_add_cancel := fun Î³ â†¦ by ext t; apply neg_add_cancel
    nsmul := nsmulRec
    zsmul := zsmulRec }

/-- Shifting a loop, or equivalently, adding a constant value to a loop. -/
instance [_root_.Add X] : VAdd X (Loop X) :=
  âŸ¨fun x Î³ â†¦ Î³.transform fun y â†¦ x + yâŸ©

@[simp]
theorem vadd_apply [_root_.Add X] {x : X} {Î³ : Loop X} {t : â„} : (x +áµ¥ Î³) t = x + Î³ t :=
  rfl

/-- Multiplying a loop by a scalar value. -/
instance [SMul K X] : SMul K (Loop X) :=
  âŸ¨fun k Î³ â†¦ Î³.transform fun y â†¦ k â€¢ yâŸ©

instance [Semiring K] [AddCommGroup X] [Module K X] : Module K (Loop X) where
  one_smul Î³ := by ext t; apply one_smul
  mul_smul kâ‚ kâ‚‚ Î³ := by ext t; apply mul_smul
  smul_zero k := by ext; apply smul_zero
  smul_add k Î³â‚ Î³â‚‚ := by ext t; apply smul_add
  add_smul kâ‚ kâ‚‚ Î³ := by ext t; apply add_smul
  zero_smul Î³ := by ext t; apply zero_smul

@[simp]
theorem smul_apply [SMul K X] {k : K} {Î³ : Loop X} {t : â„} : (k â€¢ Î³) t = k â€¢ Î³ t :=
  rfl

/-- Reparametrizing loop `Î³` using an equivariant map `Ï†`. -/
@[simps +simpRhs]
def reparam {F : Type*} (Î³ : Loop F) (Ï† : EquivariantMap) : Loop F where
  toFun := Î³ âˆ˜ Ï†
  per' t := by rw [comp_apply, Ï†.eqv, Î³.per]; rfl

/-! ## Support of a loop family -/


/-- A loop is constant if it takes the same value at every time.
See also `Loop.isConst_iff_forall_avg` and `Loop.isConst_iff_const_avg` for characterizations in
terms of average values. -/
def IsConst (Î³ : Loop X) :=
  âˆ€ t s, Î³ t = Î³ s

theorem isConst_of_eq {Î³ : Loop X} {f : X} (H : âˆ€ t, Î³ t = f) : Î³.IsConst := fun t t' â†¦ by
  rw [H, H]

variable [TopologicalSpace X] [TopologicalSpace X'] [TopologicalSpace Y] [TopologicalSpace Z]

/-- The support of a loop family is the closure of the set of parameters where
the loop is not constant. -/
def support (Î³ : X â†’ Loop X') : Set X :=
  closure {x | Â¬(Î³ x).IsConst}

omit [TopologicalSpace X'] in
theorem not_mem_support {Î³ : X â†’ Loop X'} {x : X} (h : âˆ€á¶  y in ğ“ x, (Î³ y).IsConst) :
    x âˆ‰ Loop.support Î³ := by
  intro hx
  rw [support, mem_closure_iff_nhds] at hx
  rcases hx _ h with âŸ¨z, hz, hz'âŸ©
  exact hz' hz

/-! ## From paths to loops -/


/-- Turn a path into a loop. -/
@[simps]
noncomputable def ofPath {x : X} (Î³ : Path x x) : Loop X where
  toFun t := Î³.extend (fract t)
  per' t := by rw [fract_add_one]

@[simp]
theorem range_ofPath {x : X} (Î³ : Path x x) : range (ofPath Î³) = range Î³ := by
  rw [Loop.range_eq_image, ofPath, image_eq_range]
  apply congrArg
  ext t
  by_cases ht1 : t.val = 1
  Â· have : t = âŸ¨1, right_mem_Icc.mpr zero_le_oneâŸ© := Subtype.ext ht1
    rw [this]
    norm_cast
    simp only [fract, floor_one, Path.extend_zero, Int.cast_one, sub_self]
    exact Î³.target.symm
  Â· change (t : â„) â‰  1 at ht1
    have : fract â†‘t = t.val := by
      rw [fract_eq_iff]
      refine âŸ¨t.2.1, t.2.2.lt_of_ne ht1, âŸ¨0, ?_âŸ©âŸ©
      rw [Int.cast_zero, sub_self]
    simp only [this, Î³.extend_extends t.2]

/-- `Loop.ofPath` is continuous, general version. -/
@[fun_prop]
theorem _root_.Continuous.ofPath (x : X â†’ Y) (t : X â†’ â„) (Î³ : âˆ€ i, Path (x i) (x i))
    (hÎ³ : Continuous â†¿Î³) (ht : Continuous t) : Continuous fun i â†¦ ofPath (Î³ i) (t i) := by
  change Continuous fun i â†¦ (fun s â†¦ (Î³ s).extend) i (fract (t i))
  refine ContinuousOn.comp_fract ?_ ht ?_
  Â· have : Continuous (fun x : X Ã— â„ â†¦ (x.1, projIcc 0 1 zero_le_one x.2)) :=
      continuous_id.prodMap continuous_projIcc
    exact (hÎ³.comp this).continuousOn
  Â· simp only [Icc.mk_zero, zero_le_one, Path.target, Path.extend_extends, imp_true_iff,
      Path.source, right_mem_Icc, left_mem_Icc, Icc.mk_one]

/-- `Loop.ofPath` is continuous, where the endpoints of `Î³` are fixed. TODO: remove -/
@[fun_prop]
theorem ofPath_continuous_family {x : Y} (Î³ : X â†’ Path x x) (h : Continuous â†¿Î³) :
    Continuous â†¿fun s â†¦ ofPath <| Î³ s :=
  Continuous.ofPath _ _ (fun i : X Ã— â„ â†¦ Î³ i.1) (h.comp <| continuous_fst.prodMap continuous_id)
    continuous_snd

/-! ## Round trips -/


/-- The round-trip defined by `Î³` is `Î³` followed by `Î³â»Â¹`. -/
def roundTrip {x y : X} (Î³ : Path x y) : Loop X :=
  ofPath (Î³.trans Î³.symm)

theorem roundTrip_range {x y : X} {Î³ : Path x y} : range (roundTrip Î³) = range Î³ := by
  simp [roundTrip, range_ofPath, Path.trans_range, Path.symm_range]

theorem roundTrip_based_at {x y : X} {Î³ : Path x y} : roundTrip Î³ 0 = x := by
  simp [roundTrip, ofPath, fract_zero]

theorem roundTrip_eq {x y x' y' : X} {Î³ : Path x y} {Î³' : Path x' y'} (h : âˆ€ s, Î³ s = Î³' s) :
    roundTrip Î³ = roundTrip Î³' := by
  obtain rfl : x = x' := Î³.source.symm.trans ((h 0).trans Î³'.source)
  obtain rfl : y = y' := Î³.target.symm.trans ((h 1).trans Î³'.target)
  obtain rfl : Î³ = Î³' := by ext; apply h
  rfl

/-- The round trip loop family associated to a path `Î³`. For each parameter `t`,
the loop `roundTripFamily Î³ t` backtracks at `Î³ t`. -/
noncomputable def roundTripFamily {x y : X} (Î³ : Path x y) : â„ â†’ Loop X :=
  have key : âˆ€ {t}, x = Î³.extend (min 0 t) := (Î³.extend_of_le_zero <| min_le_left _ _).symm
  fun t â†¦ roundTrip ((Î³.truncate 0 t).cast key rfl)

attribute [fun_prop] Path.trans_continuous_family

@[fun_prop]
theorem roundTripFamily_continuous {x y : X} {Î³ : Path x y} : Continuous â†¿(roundTripFamily Î³) :=
  ofPath_continuous_family _
    (Path.trans_continuous_family _ (Î³.truncate_const_continuous_family 0) _ <|
      Path.symm_continuous_family _ <|
        -- Was `fun_prop` before v4.21.0
        (by continuity))

theorem roundTripFamily_based_at {x y : X} {Î³ : Path x y} : âˆ€ t, (roundTripFamily Î³) t 0 = x :=
  fun _ â†¦ roundTrip_based_at

theorem roundTripFamily_zero {x y : X} {Î³ : Path x y} :
    (roundTripFamily Î³) 0 = ofPath (Path.refl x) := by
  simp only [roundTripFamily, roundTrip, Path.truncate_zero_zero, ofPath]
  congr with t
  simp

theorem roundTripFamily_one {x y : X} {Î³ : Path x y} : (roundTripFamily Î³) 1 = roundTrip Î³ := by
  simp only [roundTripFamily, roundTrip, Path.truncate_zero_one, ofPath]
  rfl

/-! ## Average value of a loop -/
section Average

/-- The average value of a loop. -/
noncomputable def average (Î³ : Loop F) : F :=
  âˆ« x in (0 : â„)..1, Î³ x

-- unused
@[simp]
theorem zero_average : average (0 : Loop F) = 0 :=
  intervalIntegral.integral_zero

theorem isConst_iff_forall_avg [CompleteSpace F] {Î³ : Loop F} :
    Î³.IsConst â†” âˆ€ t, Î³ t = Î³.average := by
  constructor <;> intro h
  Â· intro t
    have : Î³ = Loop.const (Î³ t) := by
      ext s
      exact h s t
    rw [this]
    simp [average, const_apply, intervalIntegral.integral_const, one_smul, sub_zero]
  Â· exact isConst_of_eq h

@[simp]
theorem average_const [CompleteSpace F] {f : F} : (const f).average = f := by simp [Loop.average]

open MeasureTheory

@[simp]
theorem average_add {Î³â‚ Î³â‚‚ : Loop F} (hÎ³â‚ : IntervalIntegrable Î³â‚ volume 0 1)
    (hÎ³â‚‚ : IntervalIntegrable Î³â‚‚ volume 0 1) : (Î³â‚ + Î³â‚‚).average = Î³â‚.average + Î³â‚‚.average := by
  simp [Loop.average, intervalIntegral.integral_add hÎ³â‚ hÎ³â‚‚]

@[simp]
theorem average_smul {Î³ : Loop F} {c : â„} : (c â€¢ Î³).average = c â€¢ Î³.average := by
  simp [Loop.average, intervalIntegral.integral_smul]

theorem isConst_iff_const_avg [CompleteSpace F] {Î³ : Loop F} : Î³.IsConst â†” Î³ = const Î³.average := by
  rw [Loop.isConst_iff_forall_avg, Loop.ext_iff, funext_iff]; rfl

omit [NormedAddCommGroup F] [NormedSpace â„ F] in
theorem isConst_of_not_mem_support {Î³ : X â†’ Loop F} {x : X} (hx : x âˆ‰ support Î³) :
    (Î³ x).IsConst := by
  by_contra H
  exact hx (subset_closure H)

@[fun_prop]
theorem continuous_average {E : Type*} [TopologicalSpace E] [FirstCountableTopology E]
    [LocallyCompactSpace E] {Î³ : E â†’ Loop F} (hÎ³_cont : Continuous â†¿Î³) :
    Continuous fun x â†¦ (Î³ x).average :=
  intervalIntegral.continuous_parametric_intervalIntegral_of_continuous' (by apply hÎ³_cont) _ _

/-- The normalization of a loop `Î³` is the loop `Î³ - Î³.average`. -/
def normalize (Î³ : Loop F) : Loop F where
  toFun t := Î³ t - Î³.average
  per' t := by simp [Î³.per]

@[simp]
theorem normalize_apply (Î³ : Loop F) (t : â„) : Loop.normalize Î³ t = Î³ t - Î³.average :=
  rfl

@[simp]
theorem normalize_of_isConst [CompleteSpace F] {Î³ : Loop F} (h : Î³.IsConst) : Î³.normalize = 0 := by
  ext t
  simp [isConst_iff_forall_avg.mp h]

end Average

end Loop

section C1

/-! ## Differentiation of loop families -/


@[inherit_doc] local notation "âˆ‚â‚" => partialFDerivFst â„

variable (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ Loop F) (hÎ³ : IsCompact (Loop.support Î³))

/-- Differential of a loop family with respect to the parameter. -/
def Loop.diff (Î³ : E â†’ Loop F) (e : E) : Loop (E â†’L[â„] F) where
  toFun t := âˆ‚â‚ (fun e t â†¦ Î³ e t) e t
  per' t := by simp only [partialFDerivFst, Loop.per]

@[simp]
theorem Loop.diff_apply (Î³ : E â†’ Loop F) (e : E) (t : â„) :
    Loop.diff Î³ e t = âˆ‚â‚ (fun e t â†¦ Î³ e t) e t :=
  rfl

@[fun_prop]
theorem Loop.continuous_diff {Î³ : E â†’ Loop F} (h : ğ’ 1 â†¿Î³) : Continuous â†¿(Loop.diff Î³) :=
  ContDiff.continuous_partial_fst (h : _)

theorem ContDiff.partial_loop {Î³ : E â†’ Loop F} {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) :
    âˆ€ t, ğ’ n fun e â†¦ Î³ e t := by
  fun_prop

variable [FiniteDimensional â„ F]

theorem Loop.support_diff {Î³ : E â†’ Loop F} : Loop.support (Loop.diff Î³) âŠ† Loop.support Î³ := by
  unfold Loop.support
  erw [closure_compl, closure_compl]
  rw [compl_subset_compl]
  intro x hx
  rw [mem_interior_iff_mem_nhds] at *
  rcases mem_nhds_iff.mp hx with âŸ¨U, hU, U_op, hxUâŸ©
  apply Filter.mem_of_superset (U_op.mem_nhds hxU) fun y hy â†¦ ?_
  have Hy : âˆ€ t, (fun z â†¦ Î³ z t) =á¶ [ğ“ y] fun z â†¦ (Î³ z).average :=
    fun t â†¦ Filter.mem_of_superset (U_op.mem_nhds hy)
      (fun z hz â†¦ Loop.isConst_iff_forall_avg.mp (hU hz) t)
  have : âˆ€ t : â„, Loop.diff Î³ y t = D (fun z : E â†¦ (Î³ z).average) y := fun t â†¦ (Hy t).fderiv_eq
  intro t s
  simp only [this]


variable [FiniteDimensional â„ E]

theorem Loop.average_diff {Î³ : E â†’ Loop F} (hÎ³_diff : ğ’ 1 â†¿Î³) (e : E) :
    (Loop.diff Î³ e).average = D (fun e â†¦ (Î³ e).average) e := by
  change ğ’ 1 â†¿fun (e : E) (t : â„) â†¦ Î³ e t at hÎ³_diff
  simp only [Loop.average, hÎ³_diff.fderiv_parametric_integral]
  rfl

attribute [fun_prop] contDiff_parametric_integral_of_contDiff
theorem ContDiff.loop_average {Î³ : E â†’ Loop F} {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) :
    ğ’ n fun e â†¦ (Î³ e).average := by
  unfold Loop.average; fun_prop

theorem Loop.diff_normalize {Î³ : E â†’ Loop F} (hÎ³_diff : ğ’ 1 â†¿Î³) (e : E) :
    (Loop.diff Î³ e).normalize = Loop.diff (fun e â†¦ (Î³ e).normalize) e := by
  ext t x
  simp only [Loop.diff_apply, Loop.normalize_apply, partialFDerivFst]
  rw [fderiv_fun_sub ((hÎ³_diff.partial_loop t).differentiable le_rfl).differentiableAt,
    Loop.average_diff hÎ³_diff]
  exact (hÎ³_diff.loop_average.differentiable le_rfl).differentiableAt

variable {Î³}

theorem contDiff_average {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) : ğ’ n fun x â†¦ (Î³ x).average := by
  unfold Loop.average; fun_prop

theorem contDiff_sub_average {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) :
    ğ’ n â†¿fun (x : E) (t : â„) â†¦ (Î³ x) t - (Î³ x).average := by
  unfold Loop.average; fun_prop

end C1
