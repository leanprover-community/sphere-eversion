import Mathlib.Analysis.Calculus.BumpFunction.Convolution
import Mathlib.Analysis.Calculus.BumpFunction.InnerProduct
import Mathlib.MeasureTheory.Integral.IntervalIntegral.IntegrationByParts
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Periodic
import SphereEversion.Loops.Surrounding
import SphereEversion.Loops.DeltaMollifier
import SphereEversion.ToMathlib.ExistsOfConvex
import SphereEversion.ToMathlib.Analysis.ContDiff

/-!
#Â The reparametrization lemma

This file contains a proof of Gromov's parametric reparametrization lemma. It concerns the behaviour
of the average value of a loop `Î³ : SÂ¹ â†’ F` when the loop is reparametrized by precomposing with a
diffeomorphism `SÂ¹ â†’ SÂ¹`.

Given a loop `Î³ : SÂ¹ â†’ F` for some real vector space `F`, one may integrate to obtain its average
`âˆ« x in 0..1, (Î³ x)` in `F`. Although this average depends on the loop's parametrization, it
satisfies a contraint that depends only on the image of the loop: the average is contained in the
convex hull of the image of `Î³`. The non-parametric version of the reparametrization lemma says that
conversely, given any point `g` in the interior of the convex hull of the image of `Î³`, one may find
a reparametrization of `Î³` whose average is `g`.

The reparametrization lemma thus allows one to reduce the problem of constructing a loop whose
average is a given point, to the problem of constructing a loop subject to a condition that depends
only on its image.

In fact the reparametrization lemma holds parametrically. Given a smooth family of loops:
`Î³ : E Ã— SÂ¹ â†’ F`, `(x, t) â†¦ Î³â‚“ t`, together with a smooth function `g : E â†’ F`, such that `g x` is
contained in the interior of the convex hull of the image of `Î³â‚“` for all `x`, there exists a smooth
family of diffeomorphism `Ï† : E Ã— SÂ¹ â†’ SÂ¹`, `(x, t) â†¦ Ï†â‚“ t` such that the average of `Î³â‚“ âˆ˜ Ï†â‚“` is
`g x` for all `x`.

The idea of the proof is simple: since `g x` is contained in the interior of the convex hull of
the image of `Î³â‚“` one may find `tâ‚€, tâ‚, ..., tâ‚™` and barycentric coordinates `wâ‚€, wâ‚, ..., wâ‚™` such
that `g x = âˆ‘áµ¢ wáµ¢ â€¢ Î³â‚“(táµ¢)`. If there were no smoothness requirement on `Ï†â‚“` one could define
it to be a step function which spends time `wáµ¢` at each `táµ¢`. However because there is a smoothness
condition, one rounds off the corners of the would-be step function by using a "delta mollifier"
(an approximation to a Dirac delta function).

The above construction works locally in the neighbourhood of any `x` in `E` and one uses a partition
of unity to globalise all the local solutions into the required family: `Ï† : E Ã— SÂ¹ â†’ SÂ¹`.

The key ingredients are theories of calculus, convex hulls, barycentric coordinates,
existence of delta mollifiers, partitions of unity, and the inverse function theorem.
-/


noncomputable section

open Set Function MeasureTheory Module intervalIntegral Filter
open scoped Topology Manifold ContDiff

variable {E F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]

set_option hygiene false
notation "Î¹" => Fin (finrank â„ F + 1)
set_option hygiene true

section MetricSpace

variable [MetricSpace E] [FiniteDimensional â„ F]

theorem Loop.tendsto_mollify_apply (Î³ : E â†’ Loop F) (h : Continuous â†¿Î³) (x : E) (t : â„) :
    Tendsto (fun z : E Ã— â„• â†¦ (Î³ z.1).mollify z.2 t) (ğ“ x Ã—Ë¢ atTop) (ğ“ (Î³ x t)) := by
  have hÎ³ : âˆ€ x, Continuous (Î³ x) := fun x â†¦ h.comp <| Continuous.prodMk_right _
  simp_rw [Loop.mollify_eq_convolution _ (hÎ³ _)]
  rw [â† add_zero (Î³ x t)]
  refine Tendsto.add ?_ ?_
  Â· rw [â† one_smul â„ (Î³ x t)]
    refine ((tendsto_natCast_div_add_atTop 1).comp tendsto_snd).smul ?_
    refine ContDiffBump.convolution_tendsto_right ?_ ?_ ?_ tendsto_const_nhds
    Â· simp_rw [bump]; norm_cast
      exact
        ((tendsto_add_atTop_iff_nat 2).2 (tendsto_const_div_atTop_nhds_zero_nat 1)).comp tendsto_snd
    Â· filter_upwards with x using (hÎ³ _).aestronglyMeasurable
    Â· have := h.tendsto (x, t)
      rw [nhds_prod_eq] at this
      exact this.comp ((tendsto_fst.comp tendsto_fst).prodMk tendsto_snd)
  Â· have : Continuous fun z â†¦ intervalIntegral (Î³ z) 0 1 volume :=
      intervalIntegral.continuous_parametric_intervalIntegral_of_continuous (by apply h)
        continuous_const
    rw [â† zero_smul â„ (_ : F)]
    exact (tendsto_one_div_add_atTop_nhds_zero_nat.comp tendsto_snd).smul
      ((this.tendsto x).comp tendsto_fst)

end MetricSpace

variable [NormedAddCommGroup E] [NormedSpace â„ E]

/-- Given a smooth function `g : E â†’ F` between normed vector spaces, a smooth surrounding family
is a smooth family of loops `E â†’ loop F`, `x â†¦ Î³â‚“` such that `Î³â‚“` surrounds `g x` for all `x`. -/
structure SmoothSurroundingFamily (g : E â†’ F) where
  smooth_surrounded : ğ’ âˆ g
  toFun : E â†’ Loop F
  smooth : ğ’ âˆ â†¿toFun
  Surrounds : âˆ€ x, (toFun x).Surrounds <| g x

namespace SmoothSurroundingFamily

variable {g : E â†’ F} (Î³ : SmoothSurroundingFamily g) (x y : E)

instance : CoeFun (SmoothSurroundingFamily g) fun _ â†¦ E â†’ Loop F :=
  âŸ¨toFunâŸ©

protected theorem continuous : Continuous (Î³ x) := by
  apply Continuous.uncurry_left x Î³.smooth.continuous

/-- Given `Î³ : SmoothSurroundingFamily g` and `x : E`, `Î³.surroundingParametersAt x` are the
`táµ¢ : â„`, for `i = 0, 1, ..., dim F` such that `Î³ x táµ¢` surround `g x`. -/
def surroundingParametersAt : Î¹ â†’ â„ :=
  Classical.choose (Î³.Surrounds x)

/-- Given `Î³ : SmoothSurroundingFamily g` and `x : E`, `Î³.surroundingPointsAt x` are the
points `Î³ x táµ¢` surrounding `g x` for parameters `táµ¢ : â„`, `i = 0, 1, ..., dim F` (defined
by `Î³.surroundingParametersAt x`). -/
def surroundingPointsAt : Î¹ â†’ F :=
  Î³ x âˆ˜ Î³.surroundingParametersAt x

/-- Given `Î³ : SmoothSurroundingFamily g` and `x : E`, `Î³.surrounding_weights_at x` are the
barycentric coordinates of `g x` wrt to the points `Î³ x táµ¢`, for parameters `táµ¢ : â„`,
`i = 0, 1, ..., dim F` (defined by `Î³.surroundingParametersAt x`). -/
def surroundingWeightsAt : Î¹ â†’ â„ :=
  Classical.choose (Classical.choose_spec (Î³.Surrounds x))

theorem surroundPtsPointsWeightsAt :
    SurroundingPts (g x) (Î³.surroundingPointsAt x) (Î³.surroundingWeightsAt x) :=
  Classical.choose_spec _

/-- Note that we are mollifying the loop `Î³ y` at the surrounding parameters for `Î³ x`. -/
def approxSurroundingPointsAt (n : â„•) (i : Î¹) : F :=
  (Î³ y).mollify n (Î³.surroundingParametersAt x i)

variable [FiniteDimensional â„ E] [CompleteSpace F] in
theorem approxSurroundingPointsAt_smooth (n : â„•) :
    ğ’ âˆ fun y â†¦ Î³.approxSurroundingPointsAt x y n := by
  refine contDiff_pi.mpr fun i â†¦ ?_
  suffices ğ’ âˆ fun y â†¦ âˆ« s in (0 : â„)..1, deltaMollifier n (Î³.surroundingParametersAt x i) s â€¢ Î³ y s
    by simpa [approxSurroundingPointsAt, Loop.mollify]
  apply contDiff_parametric_integral_of_contDiff
  exact ContDiff.smul deltaMollifier_smooth.snd' Î³.smooth

variable [FiniteDimensional â„ F]

/-- The key property from which it should be easy to construct `localCenteringDensity`,
`localCenteringDensityNhd` etc below. -/
theorem eventually_exists_surroundingPts_approxSurroundingPointsAt :
    âˆ€á¶  z : E Ã— â„• in ğ“ x Ã—Ë¢ atTop,
      âˆƒ w, SurroundingPts (g z.1) (Î³.approxSurroundingPointsAt x z.1 z.2) w := by
  let a : Î¹ â†’ E Ã— â„• â†’ F := fun i z â†¦ Î³.approxSurroundingPointsAt x z.1 z.2 i
  suffices âˆ€ i, Tendsto (a i) (ğ“ x Ã—Ë¢ atTop) (ğ“ (Î³.surroundingPointsAt x i)) by
    have hg : Tendsto (fun z : E Ã— â„• â†¦ g z.fst) (ğ“ x Ã—Ë¢ atTop) (ğ“ (g x)) :=
      Tendsto.comp Î³.smooth_surrounded.continuous.continuousAt tendsto_fst
    exact eventually_surroundingPts_of_tendsto_of_tendsto' âŸ¨_, Î³.surroundPtsPointsWeightsAt xâŸ© this hg
  intro i
  let t := Î³.surroundingParametersAt x i
  change Tendsto (fun z : E Ã— â„• â†¦ (Î³ z.1).mollify z.2 t) (ğ“ x Ã—Ë¢ atTop) (ğ“ (Î³ x t))
  exact Loop.tendsto_mollify_apply Î³ Î³.smooth.continuous x t

/-- This is an auxiliary definition to help construct `centeringDensity` below.

Given `x : E`, it represents a smooth probability distribution on the circle with the property that:
`âˆ« s in 0..1, Î³.localCenteringDensity x y s â€¢ Î³ y s = g y`
for all `y` in a neighbourhood of `x` (see `localCenteringDensity_average` below). -/
def localCenteringDensity [DecidablePred (Â· âˆˆ affineBases Î¹ â„ F)] : E â†’ â„ â†’ â„ := fun y â†¦ by
  choose n hnâ‚ _ using
    Filter.eventually_iff_exists_mem.mp
      (Î³.eventually_exists_surroundingPts_approxSurroundingPointsAt x)
  choose u _u v hv _huv using mem_prod_iff.mp hnâ‚
  choose m _hmv using mem_atTop_sets.mp hv
  exact âˆ‘ i, evalBarycentricCoords Î¹ â„ F (g y) (Î³.approxSurroundingPointsAt x y m) i â€¢
      deltaMollifier m (Î³.surroundingParametersAt x i)

/-- This is an auxiliary definition to help construct `centeringDensity` below. -/
def localCenteringDensityMp : â„• := by
  choose n hnâ‚ _hnâ‚‚ using
    Filter.eventually_iff_exists_mem.mp
      (Î³.eventually_exists_surroundingPts_approxSurroundingPointsAt x)
  choose u _hu v hv _huv using mem_prod_iff.mp hnâ‚
  choose m _hmv using mem_atTop_sets.mp hv
  exact m

theorem localCenteringDensity_spec [DecidablePred (Â· âˆˆ affineBases Î¹ â„ F)] :
    Î³.localCenteringDensity x y =
      âˆ‘ i,
        evalBarycentricCoords Î¹ â„ F (g y)
            (Î³.approxSurroundingPointsAt x y (Î³.localCenteringDensityMp x)) i â€¢
          deltaMollifier (Î³.localCenteringDensityMp x) (Î³.surroundingParametersAt x i) :=
  rfl

/-- This is an auxiliary definition to help construct `centeringDensity` below. -/
def localCenteringDensityNhd : Set E := by
  choose n hnâ‚ _hnâ‚‚ using
    Filter.eventually_iff_exists_mem.mp
      (Î³.eventually_exists_surroundingPts_approxSurroundingPointsAt x)
  choose u _hu v _hv _huv using mem_prod_iff.mp hnâ‚
  exact interior u

theorem localCenteringDensityNhd_isOpen : IsOpen <| Î³.localCenteringDensityNhd x :=
  isOpen_interior

theorem localCenteringDensityNhd_self_mem : x âˆˆ Î³.localCenteringDensityNhd x := by
  let h :=
    Filter.eventually_iff_exists_mem.mp
      (Î³.eventually_exists_surroundingPts_approxSurroundingPointsAt x)
  exact mem_interior_iff_mem_nhds.mpr
    (Classical.choose_spec <| mem_prod_iff.mp (Classical.choose_spec h).1).1

-- unused
theorem localCenteringDensityNhd_covers : univ âŠ† â‹ƒ x, Î³.localCenteringDensityNhd x := fun x _ â†¦
  mem_iUnion.mpr âŸ¨x, Î³.localCenteringDensityNhd_self_mem xâŸ©

theorem approxSurroundingPointsAt_of_localCenteringDensityNhd
    (hy : y âˆˆ Î³.localCenteringDensityNhd x) :
    âˆƒ w, SurroundingPts (g y) (Î³.approxSurroundingPointsAt x y (Î³.localCenteringDensityMp x)) w := by
  let h :=
    Filter.eventually_iff_exists_mem.mp
      (Î³.eventually_exists_surroundingPts_approxSurroundingPointsAt x)
  let nn := Classical.choose h
  let hnn := mem_prod_iff.mp (Classical.choose_spec h).1
  let n := Classical.choose hnn
  let hn := Classical.choose_spec hnn
  change y âˆˆ interior n at hy
  let v := Classical.choose hn.2
  let hv : v âˆˆ atTop := (Classical.choose_spec hn.2).1
  let m := Classical.choose (mem_atTop_sets.mp hv)
  let hm := Classical.choose_spec (mem_atTop_sets.mp hv)
  change âˆƒ w, SurroundingPts (g y) (Î³.approxSurroundingPointsAt x y m) w
  suffices (y, m) âˆˆ nn from (Classical.choose_spec h).2 _ this
  apply (Classical.choose_spec hn.2).2
  change y âˆˆ n âˆ§ m âˆˆ v
  exact âŸ¨interior_subset hy, hm _ (le_refl _)âŸ©

theorem approxSurroundingPointsAt_mem_affineBases (hy : y âˆˆ Î³.localCenteringDensityNhd x) :
    Î³.approxSurroundingPointsAt x y (Î³.localCenteringDensityMp x) âˆˆ affineBases Î¹ â„ F :=
  (Classical.choose_spec
      (Î³.approxSurroundingPointsAt_of_localCenteringDensityNhd x y hy)).mem_affineBases

variable [DecidablePred (Â· âˆˆ affineBases Î¹ â„ F)]

@[simp]
theorem localCenteringDensity_pos (hy : y âˆˆ Î³.localCenteringDensityNhd x) (t : â„) :
    0 < Î³.localCenteringDensity x y t := by
  simp only [Î³.localCenteringDensity_spec x, Fintype.sum_apply, Pi.smul_apply,
    Algebra.id.smul_eq_mul]
  refine Finset.sum_pos (fun i _ â†¦ mul_pos ?_ (deltaMollifier_pos _)) Finset.univ_nonempty
  obtain âŸ¨w, hwâŸ© := Î³.approxSurroundingPointsAt_of_localCenteringDensityNhd x y hy
  convert hw.w_pos i
  rw [â† hw.coord_eq_w]
  simp [evalBarycentricCoords, Î³.approxSurroundingPointsAt_mem_affineBases x y hy]

theorem localCenteringDensity_periodic : Periodic (Î³.localCenteringDensity x y) 1 :=
  Finset.univ.periodic_sum fun _ _ â†¦ Periodic.smul deltaMollifier_periodic _

variable [FiniteDimensional â„ E] in
theorem localCenteringDensity_smooth_on :
    smooth_on â†¿(Î³.localCenteringDensity x) <| Î³.localCenteringDensityNhd x Ã—Ë¢ (univ : Set â„) := by
  let hâ‚€ (yt : E Ã— â„) (_ : yt âˆˆ Î³.localCenteringDensityNhd x Ã—Ë¢ (univ : Set â„)) :=
    congr_fun (Î³.localCenteringDensity_spec x yt.fst) yt.snd
  refine ContDiffOn.congr ?_ hâ‚€
  simp only [Fintype.sum_apply, Pi.smul_apply, Algebra.id.smul_eq_mul]
  refine ContDiffOn.sum fun i _ â†¦ ContDiffOn.mul ?_ (ContDiff.contDiffOn ?_)
  Â· let w : F Ã— (Î¹ â†’ F) â†’ â„ := fun z â†¦ evalBarycentricCoords Î¹ â„ F z.1 z.2 i
    let z : E â†’ F Ã— (Î¹ â†’ F) :=
      (Prod.map g fun y â†¦ Î³.approxSurroundingPointsAt x y (Î³.localCenteringDensityMp x)) âˆ˜
        fun x â†¦ (x, x)
    change ContDiffOn â„ âˆ ((w âˆ˜ z) âˆ˜ Prod.fst) (Î³.localCenteringDensityNhd x Ã—Ë¢ (univ : Set â„))
    rw [prod_univ]
    refine ContDiffOn.comp ?_ contDiff_fst.contDiffOn Subset.rfl
    have hâ‚ := smooth_barycentric Î¹ â„ F (Fintype.card_fin _) (n := âˆ)
    have hâ‚‚ : ğ’ âˆ (eval i : (Î¹ â†’ â„) â†’ â„) := contDiff_apply _ _ i
    refine (hâ‚‚.comp_contDiffOn hâ‚).comp ?_ ?_
    Â· have hâ‚ƒ := (diag_preimage_prod_self (Î³.localCenteringDensityNhd x)).symm.subset
      refine ContDiffOn.comp ?_ (contDiff_id.prodMk contDiff_id).contDiffOn hâ‚ƒ
      refine Î³.smooth_surrounded.contDiffOn.prodMap (ContDiff.contDiffOn ?_)
      exact Î³.approxSurroundingPointsAt_smooth x _
    Â· intro y hy
      simp [z, Î³.approxSurroundingPointsAt_mem_affineBases x y hy]
  Â· exact deltaMollifier_smooth.comp contDiff_snd

variable [FiniteDimensional â„ E] in
theorem localCenteringDensity_continuous (hy : y âˆˆ Î³.localCenteringDensityNhd x) :
    Continuous fun t â†¦ Î³.localCenteringDensity x y t := by
  refine continuous_iff_continuousAt.mpr fun t â†¦ ?_
  have hyt : Î³.localCenteringDensityNhd x Ã—Ë¢ univ âˆˆ ğ“ (y, t) :=
    mem_nhds_prod_iff'.mpr
      âŸ¨Î³.localCenteringDensityNhd x, univ, Î³.localCenteringDensityNhd_isOpen x, hy,
        isOpen_univ, mem_univ t, rfl.subsetâŸ©
  exact ((Î³.localCenteringDensity_smooth_on x).continuousOn.continuousAt hyt).comp
    (Continuous.prodMk_right y).continuousAt

@[simp]
theorem localCenteringDensity_integral_eq_one (hy : y âˆˆ Î³.localCenteringDensityNhd x) :
    âˆ« s in (0 : â„)..1, Î³.localCenteringDensity x y s = 1 := by
  let n := Î³.localCenteringDensityMp x
  simp only [Î³.localCenteringDensity_spec x, Prod.forall, exists_prop, gt_iff_lt,
    Fintype.sum_apply, Pi.smul_apply, Algebra.id.smul_eq_mul, Finset.sum_smul]
  rw [intervalIntegral.integral_finset_sum]
  Â· have h : Î³.approxSurroundingPointsAt x y n âˆˆ affineBases Î¹ â„ F :=
      Î³.approxSurroundingPointsAt_mem_affineBases x y hy
    simp_rw [â† smul_eq_mul, intervalIntegral.integral_smul, deltaMollifier_integral_eq_one,
      Algebra.id.smul_eq_mul, mul_one]
    rw [evalBarycentricCoords_apply_of_mem_bases Î¹ â„ F (g y) h]
    simp_rw [AffineBasis.coords_apply, AffineBasis.sum_coord_apply_eq_one]
  Â· simp_rw [â† smul_eq_mul]
    refine fun i hi â†¦ (Continuous.const_smul ?_ _).intervalIntegrable 0 1
    exact deltaMollifier_smooth.continuous

@[simp]
theorem localCenteringDensity_average (hy : y âˆˆ Î³.localCenteringDensityNhd x) :
    âˆ« s in (0 : â„)..1, Î³.localCenteringDensity x y s â€¢ Î³ y s = g y := by
  let n := Î³.localCenteringDensityMp x
  simp only [Î³.localCenteringDensity_spec x, Prod.forall, exists_prop, gt_iff_lt,
    Fintype.sum_apply, Pi.smul_apply, Algebra.id.smul_eq_mul, Finset.sum_smul]
  rw [intervalIntegral.integral_finset_sum]
  Â· simp_rw [mul_smul, intervalIntegral.integral_smul]
    change âˆ‘ i, _ â€¢ Î³.approxSurroundingPointsAt x y n i = _
    have h : Î³.approxSurroundingPointsAt x y n âˆˆ affineBases Î¹ â„ F :=
      Î³.approxSurroundingPointsAt_mem_affineBases x y hy
    erw [evalBarycentricCoords_apply_of_mem_bases Î¹ â„ F (g y) h]
    simp only [AffineBasis.coords_apply]
    exact AffineBasis.linear_combination_coord_eq_self ..
  Â· simp_rw [mul_smul]
    refine fun i hi â†¦ ((Continuous.smul ?_ (Î³.continuous y)).const_smul _).intervalIntegrable 0 1
    exact deltaMollifier_smooth.continuous

/-- Given `Î³ : SmoothSurroundingFamily g`, together with a point `x : E` and a map `f : â„ â†’ â„`,
`Î³.is_centeringDensity x f` is the proposition that `f` is periodic, strictly positive, and
has integral one and that the average of `Î³â‚“` with respect to the measure that `f` defines on
the circle is `g x`.

The continuity assumption is just a legacy convenience and should be dropped. -/
structure IsCenteringDensity (x : E) (f : â„ â†’ â„) : Prop where
  Pos : âˆ€ t, 0 < f t
  Periodic : Periodic f 1
  integral_one : âˆ« s in (0 : â„)..1, f s = 1
  average : âˆ« s in (0 : â„)..1, f s â€¢ Î³ x s = g x
  Continuous : Continuous f

omit [FiniteDimensional â„ F] [DecidablePred fun x â†¦ x âˆˆ affineBases Î¹ â„ F] in
-- Can drop if/when have `intervalIntegrable.smul_continuous_on`
theorem isCenteringDensity_convex (x : E) : Convex â„ { f | Î³.IsCenteringDensity x f } := by
  classical
  rintro f âŸ¨hfâ‚, hfâ‚‚, hfâ‚ƒ, hfâ‚„, hfâ‚…âŸ© k âŸ¨hkâ‚, hkâ‚‚, hkâ‚ƒ, hkâ‚„, hkâ‚…âŸ© a b ha hb hab
  have hfâ‚† : IntervalIntegrable f volume 0 1 := by
    apply intervalIntegrable_of_integral_ne_zero
    rw [hfâ‚ƒ]; exact one_ne_zero
  have hfâ‚‡ : IntervalIntegrable (f â€¢ (Î³ x : â„ â†’ F)) volume 0 1 :=
    (hfâ‚….smul (Î³.continuous x)).intervalIntegrable 0 1
  have hkâ‚† : IntervalIntegrable k volume 0 1 := by
    apply intervalIntegrable_of_integral_ne_zero
    rw [hkâ‚ƒ]; exact one_ne_zero
  have hkâ‚‡ : IntervalIntegrable (k â€¢ (Î³ x : â„ â†’ F)) volume 0 1 :=
    (hkâ‚….smul (Î³.continuous x)).intervalIntegrable 0 1
  exact {
    Pos := fun t â†¦ convex_Ioi (0 : â„) (hfâ‚ t) (hkâ‚ t) ha hb hab
    Periodic := (hfâ‚‚.smul a).add (hkâ‚‚.smul b)
    integral_one := by
      simp_rw [Pi.add_apply]
      rw [intervalIntegral.integral_add (hfâ‚†.smul a) (hkâ‚†.smul b)]
      simp [intervalIntegral.integral_smul, hfâ‚ƒ, hkâ‚ƒ, hab]
    average := by
      simp_rw [Pi.add_apply, Pi.smul_apply, add_smul, smul_assoc]
      erw [intervalIntegral.integral_add (hfâ‚‡.smul a) (hkâ‚‡.smul b)]
      simp [intervalIntegral.integral_smul, â† add_smul, hfâ‚„, hkâ‚„, hab]
    Continuous := Continuous.add (hfâ‚….const_smul a) (hkâ‚….const_smul b) }

variable [FiniteDimensional â„ E]

theorem exists_smooth_isCenteringDensity (x : E) :
    âˆƒ U âˆˆ ğ“ x,
      âˆƒ f : E â†’ â„ â†’ â„,
        smooth_on (uncurry f) (U Ã—Ë¢ (univ : Set â„)) âˆ§ âˆ€ y âˆˆ U, Î³.IsCenteringDensity y (f y) :=
  âŸ¨Î³.localCenteringDensityNhd x,
    mem_nhds_iff.mpr
      âŸ¨_, Subset.rfl, Î³.localCenteringDensityNhd_isOpen x, Î³.localCenteringDensityNhd_self_mem xâŸ©,
    Î³.localCenteringDensity x, Î³.localCenteringDensity_smooth_on x, fun y hy â†¦
    âŸ¨Î³.localCenteringDensity_pos x y hy, Î³.localCenteringDensity_periodic x y,
      Î³.localCenteringDensity_integral_eq_one x y hy, Î³.localCenteringDensity_average x y hy,
      Î³.localCenteringDensity_continuous x y hyâŸ©âŸ©

/-- This the key construction. It represents a smooth probability distribution on the circle with
the property that:
`âˆ« s in 0..1, Î³.centeringDensity x s â€¢ Î³ x s = g x`
for all `x : E` (see `centeringDensity_average` below). -/
def centeringDensity : E â†’ â„ â†’ â„ :=
  Classical.choose
    (exists_contDiff_of_convexâ‚‚ Î³.isCenteringDensity_convex Î³.exists_smooth_isCenteringDensity)

theorem centeringDensity_smooth : ğ’ âˆ <| uncurry fun x t â†¦ Î³.centeringDensity x t :=
  (Classical.choose_spec <|
      exists_contDiff_of_convexâ‚‚ Î³.isCenteringDensity_convex Î³.exists_smooth_isCenteringDensity).1

theorem isCenteringDensityCenteringDensity (x : E) :
    Î³.IsCenteringDensity x (Î³.centeringDensity x) :=
  (Classical.choose_spec <|
        exists_contDiff_of_convexâ‚‚ Î³.isCenteringDensity_convex Î³.exists_smooth_isCenteringDensity).2
    x

@[simp]
theorem centeringDensity_pos (t : â„) : 0 < Î³.centeringDensity x t :=
  (Î³.isCenteringDensityCenteringDensity x).Pos t

theorem centeringDensity_periodic : Periodic (Î³.centeringDensity x) 1 :=
  (Î³.isCenteringDensityCenteringDensity x).Periodic

@[simp]
theorem centeringDensity_integral_eq_one : âˆ« s in (0)..1, Î³.centeringDensity x s = 1 :=
  (Î³.isCenteringDensityCenteringDensity x).integral_one

@[simp]
theorem centeringDensity_average : âˆ« s in (0)..1, Î³.centeringDensity x s â€¢ Î³ x s = g x :=
  (Î³.isCenteringDensityCenteringDensity x).average

theorem centeringDensity_continuous : Continuous (Î³.centeringDensity x) :=
  (Î³.centeringDensity_smooth.continuous).uncurry_left x

theorem centeringDensity_intervalIntegrable (tâ‚ tâ‚‚ : â„) :
    IntervalIntegrable (Î³.centeringDensity x) volume tâ‚ tâ‚‚ :=
  (Î³.centeringDensity_continuous x).intervalIntegrable tâ‚ tâ‚‚

@[simp]
theorem integral_add_one_centeringDensity (t : â„) :
    âˆ« s in (0)..t + 1, Î³.centeringDensity x s = (âˆ« s in (0)..t, Î³.centeringDensity x s) + 1 := by
  have hâ‚ := Î³.centeringDensity_intervalIntegrable x 0 t
  have hâ‚‚ := Î³.centeringDensity_intervalIntegrable x t (t + 1)
  simp [â† integral_add_adjacent_intervals hâ‚ hâ‚‚,
    (Î³.centeringDensity_periodic x).intervalIntegral_add_eq t 0]

theorem deriv_integral_centeringDensity_pos (t : â„) :
    0 < deriv (fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s) t := by
  rw [intervalIntegral.deriv_integral_right (Î³.centeringDensity_intervalIntegrable _ _ _)
      ((Î³.centeringDensity_continuous x).stronglyMeasurableAtFilter volume (ğ“ t))
      (centeringDensity_continuous Î³ x).continuousAt]
  exact centeringDensity_pos Î³ x t

theorem strictMono_integral_centeringDensity :
    StrictMono fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s :=
  strictMono_of_deriv_pos (Î³.deriv_integral_centeringDensity_pos x)

theorem surjective_integral_centeringDensity :
    Surjective fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s :=
  haveI : Continuous fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s :=
    continuous_primitive (Î³.centeringDensity_intervalIntegrable x) 0
  EquivariantMap.surjective
    âŸ¨fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s, Î³.integral_add_one_centeringDensity xâŸ© this

/-- Given `Î³ : SmoothSurroundingFamily g`, `x â†¦ Î³.reparametrize x` is a smooth family of
diffeomorphisms of the circle such that reparametrizing `Î³â‚“` by `Î³.reparametrize x` gives a loop
with average `g x`.

This is the key construction and the main "output" of the reparametrization lemma. -/
def reparametrize : E â†’ EquivariantEquiv := fun x â†¦
  ({ toFun := fun t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s
     invFun :=
       (StrictMono.orderIsoOfSurjective _ (Î³.strictMono_integral_centeringDensity x)
           (Î³.surjective_integral_centeringDensity x)).symm
     left_inv := StrictMono.orderIsoOfSurjective_symm_apply_self _ _ _
     right_inv := fun t â†¦ StrictMono.orderIsoOfSurjective_self_symm_apply _
       (Î³.strictMono_integral_centeringDensity x) _ t
     map_zero' := integral_same
     eqv' := Î³.integral_add_one_centeringDensity x } : EquivariantEquiv).symm

theorem hasDerivAt_reparametrize_symm (s : â„) :
    HasDerivAt (Î³.reparametrize x).symm (Î³.centeringDensity x s) s :=
  integral_hasDerivAt_right (Î³.centeringDensity_intervalIntegrable x 0 s)
    ((Î³.centeringDensity_continuous x).stronglyMeasurableAtFilter _ _)
    (Î³.centeringDensity_continuous x).continuousAt

-- ğ’ âˆ â†¿Î³.reparametrize
theorem reparametrize_smooth : ğ’ âˆ <| uncurry fun (x : E) (t : â„) â†¦ Î³.reparametrize x t := by
  let f : E â†’ â„ â†’ â„ := fun x t â†¦ âˆ« s in (0)..t, Î³.centeringDensity x s
  change ğ’ âˆ fun p : E Ã— â„ â†¦ (StrictMono.orderIsoOfSurjective (f p.1) _ _).symm p.2
  apply contDiff_parametric_symm_of_deriv_pos
  Â· exact contDiff_parametric_primitive_of_contDiff'' Î³.centeringDensity_smooth 0
  Â· exact fun x â†¦ deriv_integral_centeringDensity_pos Î³ x
  Â· exact fun x â†¦ surjective_integral_centeringDensity Î³ x

@[simp]
theorem reparametrize_average :
    ((Î³ x).reparam <| (Î³.reparametrize x).equivariantMap).average = g x := by
  change âˆ« s : â„ in (0)..1, Î³ x (Î³.reparametrize x s) = g x
  have hâ‚ :
    âˆ€ s, s âˆˆ uIcc 0 (1 : â„) â†’ HasDerivAt (Î³.reparametrize x).symm (Î³.centeringDensity x s) s :=
    fun s _ â†¦ Î³.hasDerivAt_reparametrize_symm x s
  have hâ‚‚ : ContinuousOn (fun s â†¦ Î³.centeringDensity x s) (uIcc 0 1) :=
    (Î³.centeringDensity_continuous x).continuousOn
  have hâ‚ƒ : Continuous fun s â†¦ Î³ x (Î³.reparametrize x s) :=
    (Î³.continuous x).comp (Î³.reparametrize_smooth.continuous.uncurry_left x)
  rw [â† (Î³.reparametrize x).symm.map_zero, â† (Î³.reparametrize x).symm.map_one, â†
    integral_comp_smul_deriv hâ‚ hâ‚‚ hâ‚ƒ]
  simp only [comp_apply, EquivariantEquiv.apply_symm_apply, centeringDensity_average]

end SmoothSurroundingFamily
