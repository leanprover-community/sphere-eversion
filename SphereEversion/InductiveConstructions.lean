import Mathlib.Topology.Germ
import Mathlib.Analysis.RCLike.Basic
import Mathlib.Data.Complex.Norm
import SphereEversion.ToMathlib.Topology.Misc
import SphereEversion.Indexing
import SphereEversion.Notations

-- set_option trace.filter_inst_type true

attribute [gcongr] nhdsSet_mono

open Set Filter Function
open scoped Topology unitInterval

/-!
Notes by Patrick:

The goal of this file is to explore how to prove `exists_surrounding_loops` and the local to global
inductive homotopy construction in a way that uncouples the general
topological argument from the things specific to loops or homotopies of jet sections.

First there is a lemma `inductive_construction` which abstracts the locally ultimately constant
arguments, assuming we work with a fixed covering. It builds on
`LocallyFinite.exists_forall_eventually_of_indexType`.

From `inductive_construction` alone we deduce `inductive_htpy_construction` which builds a homotopy
in a similar context. This is meant to be used to go from Chapter 2 to Chapter 3.

Combining `inductive_construction` with an argument using local existence and exhaustions, we
get `inductive_construction_of_loc` building a function from local existence and patching
assumptions. It also has a version `relative_inductive_construction_of_loc` which does this
relative to a closed set. This is used for `exists_surrounding_loops`.

This file also contains supporting lemmas about `IndexType`. A short term goal will be to
get rid of the `indexing` abstraction and do everything in terms of `IndexType`, unless
`indexing` makes those supporting lemmas really cleaner to prove.
-/


section inductive_construction

theorem LocallyFinite.exists_forall_eventually_of_indexType {Î± X : Type*} [TopologicalSpace X]
    {N : â„•} {f : IndexType N â†’ X â†’ Î±} {V : IndexType N â†’ Set X} (hV : LocallyFinite V)
    (h : âˆ€ n : IndexType N, Â¬IsMax n â†’ âˆ€ x âˆ‰ (V n.succ), f n.succ x = f n x) :
    âˆƒ F : X â†’ Î±, âˆ€ x : X, âˆ€á¶  n in Filter.atTop, f n =á¶ [ğ“ x] F := by
  choose U hUx hU using hV
  choose iâ‚€ hiâ‚€ using fun x â†¦ (hU x).bddAbove
  have key : âˆ€ {x} {n}, n â‰¥ iâ‚€ x â†’ âˆ€ {y}, y âˆˆ U x â†’ f n y = f (iâ‚€ x) y := fun {x} â†¦ by
    refine @IndexType.induction_from N (fun i â†¦ âˆ€ {y}, y âˆˆ U x â†’ f i y = f (iâ‚€ x) y) _ ?_ ?_
    Â· exact fun _ â†¦ rfl
    Â· intro i hi h'i ih y hy
      rw [h i h'i, ih hy]
      intro h'y
      replace hiâ‚€ := mem_upperBounds.mp (hiâ‚€ x) i.succ âŸ¨y, h'y, hyâŸ©
      exact lt_irrefl _ (((i.lt_succ h'i).trans_le hiâ‚€).trans_le hi)
  refine âŸ¨fun x â†¦ f (iâ‚€ x) x, fun x â†¦ ?_âŸ©
  refine (eventually_ge_atTop (iâ‚€ x)).mono fun n hn â†¦ ?_
  refine mem_of_superset (hUx x) fun y hy â†¦ ?_
  calc
    f n y = f (iâ‚€ x) y := key hn hy
    _ = f (max (iâ‚€ x) (iâ‚€ y)) y := (key (le_max_left ..) hy).symm
    _ = f (iâ‚€ y) y := key (le_max_right ..) (mem_of_mem_nhds <| hUx y)

@[inherit_doc] local notation "ğ“˜" => IndexType

theorem inductive_construction {X Y : Type*} [TopologicalSpace X] {N : â„•} {U : IndexType N â†’ Set X}
    (Pâ‚€ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop) (Pâ‚ : âˆ€ _i : IndexType N, âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    (Pâ‚‚ : IndexType N â†’ (X â†’ Y) â†’ Prop) (U_fin : LocallyFinite U)
    (init : âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ Pâ‚‚ 0 f)
    (ind : âˆ€ (i : IndexType N) (f : X â†’ Y), (âˆ€ x, Pâ‚€ x f) â†’ Pâ‚‚ i f â†’ (âˆ€ j < i, âˆ€ x, Pâ‚ j x f) â†’
      âˆƒ f' : X â†’ Y, (âˆ€ x, Pâ‚€ x f') âˆ§ (Â¬IsMax i â†’ Pâ‚‚ i.succ f') âˆ§ (âˆ€ j â‰¤ i, âˆ€ x, Pâ‚ j x f') âˆ§
        âˆ€ x âˆ‰ U i, f' x = f x) :
    âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ âˆ€ j, âˆ€ x, Pâ‚ j x f := by
  let P : ğ“˜ N â†’ (X â†’ Y) â†’ Prop := fun n f â†¦
    (âˆ€ x, Pâ‚€ x f) âˆ§ (Â¬IsMax n â†’ Pâ‚‚ n.succ f) âˆ§ âˆ€ j â‰¤ n, âˆ€ x, Pâ‚ j x f
  let Q : ğ“˜ N â†’ (X â†’ Y) â†’ (X â†’ Y) â†’ Prop := fun n f f' â†¦ âˆ€ x âˆ‰ (U n.succ), f' x = f x
  obtain âŸ¨f, hfâŸ© : âˆƒ f : ğ“˜ N â†’ X â†’ Y, âˆ€ n, P n (f n) âˆ§ (Â¬IsMax n â†’ Q n (f n) (f n.succ)) := by
    apply IndexType.exists_by_induction
    Â· rcases init with âŸ¨fâ‚€, hâ‚€fâ‚€, hâ‚fâ‚€âŸ©
      rcases ind 0 fâ‚€ hâ‚€fâ‚€ hâ‚fâ‚€ (by simp [IndexType.not_lt_zero]) with âŸ¨f', hâ‚€f', hâ‚‚f', hâ‚f', -âŸ©
      exact âŸ¨f', hâ‚€f', hâ‚‚f', hâ‚f'âŸ©
    Â· rintro n f âŸ¨hâ‚€f, hâ‚‚f, hâ‚fâŸ© hn
      rcases ind _ f hâ‚€f (hâ‚‚f hn) fun j hj â†¦ hâ‚f _ <| j.le_of_lt_succ hj with
        âŸ¨f', hâ‚€f', hâ‚‚f', hâ‚f', hf'âŸ©
      exact âŸ¨f', âŸ¨hâ‚€f', hâ‚‚f', hâ‚f'âŸ©, hf'âŸ©
  dsimp only [P] at hf
  simp only [forall_and] at hf
  rcases hf with âŸ¨âŸ¨hâ‚€f, -, hâ‚fâŸ©, hfUâŸ©
  rcases U_fin.exists_forall_eventually_of_indexType hfU with âŸ¨F, hFâŸ©
  refine âŸ¨F, fun x â†¦ ?_, fun j â†¦ ?_âŸ©
  Â· rcases(hF x).exists with âŸ¨nâ‚€, hnâ‚€âŸ©
    simp only [Germ.coe_eq.mpr hnâ‚€.symm, hâ‚€f nâ‚€ x]
  Â· intro x
    rcases((hF x).and <| eventually_ge_atTop j).exists with âŸ¨nâ‚€, hnâ‚€, hnâ‚€'âŸ©
    convert (hâ‚f _ _ hnâ‚€' x) using 1
    exact Germ.coe_eq.mpr hnâ‚€.symm

theorem inductive_construction_of_loc' {X Y : Type*} [EMetricSpace X] [LocallyCompactSpace X]
    [SecondCountableTopology X] (Pâ‚€ Pâ‚€' Pâ‚ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop) {fâ‚€ : X â†’ Y}
    (hPâ‚€fâ‚€ : âˆ€ x, Pâ‚€ x fâ‚€ âˆ§ Pâ‚€' x fâ‚€)
    (loc : âˆ€ x, âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ âˆ€á¶  x' in ğ“ x, Pâ‚ x' f)
    (ind : âˆ€ {Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ : Set X} {fâ‚ fâ‚‚ : X â†’ Y}, IsOpen Uâ‚ â†’ IsOpen Uâ‚‚ â†’
      IsCompact Kâ‚ â†’ IsCompact Kâ‚‚ â†’ Kâ‚ âŠ† Uâ‚ â†’ Kâ‚‚ âŠ† Uâ‚‚ â†’
      (âˆ€ x, Pâ‚€ x fâ‚ âˆ§ Pâ‚€' x fâ‚) â†’ (âˆ€ x, Pâ‚€ x fâ‚‚) â†’ (âˆ€ x âˆˆ Uâ‚, Pâ‚ x fâ‚) â†’ (âˆ€ x âˆˆ Uâ‚‚, Pâ‚ x fâ‚‚) â†’
      âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f) âˆ§
        (âˆ€á¶  x near Kâ‚ âˆª Kâ‚‚, Pâ‚ x f) âˆ§ âˆ€á¶  x near Kâ‚ âˆª Uâ‚‚á¶œ, f x = fâ‚ x) :
    âˆƒ f : X â†’ Y, âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f âˆ§ Pâ‚ x f := by
  let P : Set X â†’ Prop := fun U â†¦ âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ âˆ€ x âˆˆ U, Pâ‚ x f
  have hPâ‚ : Antitone P := by
    rintro U V hUV âŸ¨f, h, h'âŸ©
    exact âŸ¨f, h, fun x hx â†¦ h' x (hUV hx)âŸ©
  have hPâ‚‚ : P âˆ… := âŸ¨fâ‚€, fun x â†¦ (hPâ‚€fâ‚€ x).1, fun x h â†¦ h.elimâŸ©
  have hPâ‚ƒ : âˆ€ x : X, x âˆˆ univ â†’ âˆƒ V âˆˆ ğ“ x, P V := fun x _ â†¦ by
    rcases loc x with âŸ¨f, hâ‚€f, hâ‚fâŸ©
    exact âŸ¨_, hâ‚f, f, hâ‚€f, fun x â†¦ idâŸ©
  rcases exists_locallyFinite_subcover_of_locally isClosed_univ hPâ‚ hPâ‚‚ hPâ‚ƒ with
    âŸ¨K, U : IndexType 0 â†’ Set X, K_cpct, U_op, hU, hKU, U_loc, hKâŸ©
  have ind' : âˆ€ (i : ğ“˜ 0) (f : X â†’ Y), (âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f) â†’
      (âˆ€ j < i, âˆ€ x, RestrictGermPredicate Pâ‚ (K j) x â†‘f) â†’
      âˆƒ f' : X â†’ Y, (âˆ€ x : X, Pâ‚€ x â†‘f' âˆ§ Pâ‚€' x â†‘f') âˆ§
        (âˆ€ j â‰¤ i, âˆ€ x, RestrictGermPredicate Pâ‚ (K j) x f') âˆ§ âˆ€ x, x âˆ‰ U i â†’ f' x = f x := by
    simp_rw [forall_restrictGermPredicate_iff, â† eventually_nhdsSet_iUnionâ‚‚]
    rintro (i : â„•) f hâ‚€f hâ‚f
    have cpct : IsCompact (â‹ƒ j < i, K j) :=
      (finite_lt_nat i).isCompact_biUnion fun j _ â†¦ K_cpct j
    rcases hU i with âŸ¨f', hâ‚€f', hâ‚f'âŸ©
    rcases mem_nhdsSet_iff_exists.mp hâ‚f with âŸ¨V, V_op, hKV, hâ‚VâŸ©
    rcases ind V_op (U_op i) cpct (K_cpct i) hKV (hKU i) hâ‚€f hâ‚€f' hâ‚V hâ‚f' with
      âŸ¨F, hâ‚€F, hâ‚F, hFâŸ©
    rw [â† biUnion_le] at hâ‚F
    exact âŸ¨F, hâ‚€F, hâ‚F, fun x hx â†¦ hF.self_of_nhdsSet x (Or.inr hx)âŸ©
  have :=
    inductive_construction (fun x Ï† â†¦ Pâ‚€ x Ï† âˆ§ Pâ‚€' x Ï†)
      (fun j : ğ“˜ 0 â†¦ RestrictGermPredicate Pâ‚ (K j)) (fun _ _ â†¦ True) U_loc âŸ¨fâ‚€, hPâ‚€fâ‚€, trivialâŸ©
  simp only [IndexType.not_isMax, not_false_iff, forall_true_left, true_and] at this
  rcases this ind' with âŸ¨f, h, h'âŸ©
  refine âŸ¨f, fun x â†¦ âŸ¨(h x).1, (h x).2, ?_âŸ©âŸ©
  rcases mem_iUnion.mp (hK trivial : x âˆˆ â‹ƒ j, K j) with âŸ¨j, hjâŸ©
  exact (h' j x hj).self_of_nhds


/-- We are given a suitably nice extended metric space `X` and three local constraints `Pâ‚€`,`Pâ‚€'`
and `Pâ‚` on maps from `X` to some type `Y`. All maps entering the discussion are required to
statisfy `Pâ‚€` everywhere. The goal is to turn a map `fâ‚€` satisfying `Pâ‚` near a compact set `K` into
one satisfying everywhere without changing `fâ‚€` near `K`. The assumptions are:
* For every `x` in `X` there is a map which satisfies `Pâ‚` near `x`
* One can patch two maps `fâ‚ fâ‚‚` satisfying `Pâ‚` on open sets `Uâ‚` and `Uâ‚‚` respectively
  and such that `fâ‚` satisfies `Pâ‚€'` everywhere into a map satisfying `Pâ‚` on `Kâ‚ âˆª Kâ‚‚` for any
  compact sets `Káµ¢ âŠ† Uáµ¢` and `Pâ‚€'` everywhere. -/
theorem inductive_construction_of_loc {X Y : Type*} [EMetricSpace X] [LocallyCompactSpace X]
    [SecondCountableTopology X] (Pâ‚€ Pâ‚€' Pâ‚ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop) {fâ‚€ : X â†’ Y}
    (hPâ‚€fâ‚€ : âˆ€ x, Pâ‚€ x fâ‚€ âˆ§ Pâ‚€' x fâ‚€)
    (loc : âˆ€ x, âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ âˆ€á¶  x' in ğ“ x, Pâ‚ x' f)
    (ind : âˆ€ {Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ : Set X} {fâ‚ fâ‚‚ : X â†’ Y}, IsOpen Uâ‚ â†’ IsOpen Uâ‚‚ â†’
      IsCompact Kâ‚ â†’ IsCompact Kâ‚‚ â†’ Kâ‚ âŠ† Uâ‚ â†’ Kâ‚‚ âŠ† Uâ‚‚ â†’
      (âˆ€ x, Pâ‚€ x fâ‚ âˆ§ Pâ‚€' x fâ‚) â†’ (âˆ€ x, Pâ‚€ x fâ‚‚) â†’ (âˆ€ x âˆˆ Uâ‚, Pâ‚ x fâ‚) â†’ (âˆ€ x âˆˆ Uâ‚‚, Pâ‚ x fâ‚‚) â†’
      âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f) âˆ§
        (âˆ€á¶  x near Kâ‚ âˆª Kâ‚‚, Pâ‚ x f) âˆ§ âˆ€á¶  x near Kâ‚ âˆª Uâ‚‚á¶œ, f x = fâ‚ x) :
    âˆƒ f : X â†’ Y, âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f âˆ§ Pâ‚ x f := by
  apply inductive_construction_of_loc' Pâ‚€ Pâ‚€' Pâ‚ hPâ‚€fâ‚€ loc
  intro Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ fâ‚ fâ‚‚ hUâ‚ hUâ‚‚ hKâ‚ hKâ‚‚
  solve_by_elim

theorem set_juggling {X : Type*} [TopologicalSpace X] [NormalSpace X] [T2Space X]
    {K : Set X} (hK : IsClosed K) {Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ : Set X} (Uâ‚_op : IsOpen Uâ‚)
    (Uâ‚‚_op : IsOpen Uâ‚‚) (Kâ‚_cpct : IsCompact Kâ‚) (Kâ‚‚_cpct : IsCompact Kâ‚‚) (hKâ‚Uâ‚ : Kâ‚ âŠ† Uâ‚)
    (hKâ‚‚Uâ‚‚ : Kâ‚‚ âŠ† Uâ‚‚) (U : Set X) (U_op : IsOpen U) (hKU : K âŠ† U) :
    âˆƒ Kâ‚' Kâ‚‚' Uâ‚' Uâ‚‚',
      IsOpen Uâ‚' âˆ§ IsOpen Uâ‚‚' âˆ§ IsCompact Kâ‚' âˆ§ IsCompact Kâ‚‚' âˆ§ Kâ‚ âŠ† Kâ‚' âˆ§ Kâ‚' âŠ† Uâ‚' âˆ§ Kâ‚‚' âŠ† Uâ‚‚' âˆ§
      Kâ‚' âˆª Kâ‚‚' = Kâ‚ âˆª Kâ‚‚ âˆ§ K âŠ† Uâ‚‚'á¶œ âˆ§ Uâ‚' âŠ† U âˆª Uâ‚ âˆ§ Uâ‚‚' âŠ† Uâ‚‚ := by
  obtain âŸ¨U', U'_op, hKU', hU'UâŸ© : âˆƒ U' : Set X, IsOpen U' âˆ§ K âŠ† U' âˆ§ closure U' âŠ† U :=
    normal_exists_closure_subset hK U_op hKU
  refine âŸ¨Kâ‚ âˆª closure (Kâ‚‚ âˆ© U'), Kâ‚‚ \ U', Uâ‚ âˆª U, Uâ‚‚ \ K,
    Uâ‚_op.union U_op, Uâ‚‚_op.sdiff hK, ?_, Kâ‚‚_cpct.diff U'_op, ?_, ?_, ?_, ?_, ?_, ?_, ?_âŸ©
  Â· exact Kâ‚_cpct.union (Kâ‚‚_cpct.closure_of_subset inter_subset_left)
  Â· exact subset_union_left
  Â· apply union_subset_union hKâ‚Uâ‚ (subset_trans _ hU'U)
    gcongr
    exact inter_subset_right
  Â· exact diff_subset_diff hKâ‚‚Uâ‚‚ hKU'
  Â· rw [union_assoc]
    congr
    apply subset_antisymm
    Â· exact union_subset (Kâ‚‚_cpct.isClosed.closure_subset_iff.mpr inter_subset_left) diff_subset
    Â· calc Kâ‚‚ = Kâ‚‚ âˆ© U' âˆª Kâ‚‚ \ U' := (inter_union_diff Kâ‚‚ U').symm
        _     âŠ† closure (Kâ‚‚ âˆ© U') âˆª Kâ‚‚ \ U' := union_subset_union_left (Kâ‚‚ \ U') subset_closure
  Â· exact fun x hx hx' â†¦ hx'.2 hx
  Â· rw [union_comm]
  Â· exact diff_subset

/-- We are given a suitably nice extended metric space `X` and three local constraints `Pâ‚€`,`Pâ‚€'`
and `Pâ‚` on maps from `X` to some type `Y`. All maps entering the discussion are required to
statisfy `Pâ‚€` everywhere. The goal is to turn a map `fâ‚€` satisfying `Pâ‚` near a compact set `K` into
one satisfying everywhere without changing `fâ‚€` near `K`. The assumptions are:
* For every `x` in `X` there is a map which satisfies `Pâ‚` near `x`
* One can patch two maps `fâ‚ fâ‚‚` satisfying `Pâ‚` on open sets `Uâ‚` and `Uâ‚‚` respectively
  into a map satisfying `Pâ‚` on `Kâ‚ âˆª Kâ‚‚` for any compact sets `Káµ¢ âŠ† Uáµ¢`.
This is deduced this version from the version where `K` is empty but adding some `P'â‚€`, see
`inductive_construction_of_loc`. -/
theorem relative_inductive_construction_of_loc {X Y : Type*} [EMetricSpace X]
    [LocallyCompactSpace X] [SecondCountableTopology X] (Pâ‚€ Pâ‚ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    {K : Set X} (hK : IsClosed K) {fâ‚€ : X â†’ Y} (hPâ‚€fâ‚€ : âˆ€ x, Pâ‚€ x fâ‚€) (hPâ‚fâ‚€ : âˆ€á¶  x near K, Pâ‚ x fâ‚€)
    (loc : âˆ€ x, âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ âˆ€á¶  x' in ğ“ x, Pâ‚ x' f)
    (ind : âˆ€ {Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ : Set X} {fâ‚ fâ‚‚ : X â†’ Y},
      IsOpen Uâ‚ â†’ IsOpen Uâ‚‚ â†’ IsCompact Kâ‚ â†’ IsCompact Kâ‚‚ â†’ Kâ‚ âŠ† Uâ‚ â†’ Kâ‚‚ âŠ† Uâ‚‚ â†’
      (âˆ€ x, Pâ‚€ x fâ‚) â†’ (âˆ€ x, Pâ‚€ x fâ‚‚) â†’ (âˆ€ x âˆˆ Uâ‚, Pâ‚ x fâ‚) â†’ (âˆ€ x âˆˆ Uâ‚‚, Pâ‚ x fâ‚‚) â†’
      âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f) âˆ§ (âˆ€á¶  x near Kâ‚ âˆª Kâ‚‚, Pâ‚ x f) âˆ§ âˆ€á¶  x near Kâ‚ âˆª Uâ‚‚á¶œ, f x = fâ‚ x) :
    âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚ x f) âˆ§ âˆ€á¶  x near K, f x = fâ‚€ x := by
  let Pâ‚€' : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop := RestrictGermPredicate (fun x Ï† â†¦ Ï†.value = fâ‚€ x) K
  have hfâ‚€ : âˆ€ x, Pâ‚€ x fâ‚€ âˆ§ Pâ‚€' x fâ‚€ := fun x â†¦
    âŸ¨hPâ‚€fâ‚€ x, fun _ â†¦ Eventually.of_forall fun x' â†¦ rflâŸ©
  have ind' : âˆ€ {Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ : Set X} {fâ‚ fâ‚‚ : X â†’ Y},
      IsOpen Uâ‚ â†’ IsOpen Uâ‚‚ â†’ IsCompact Kâ‚ â†’ IsCompact Kâ‚‚ â†’ Kâ‚ âŠ† Uâ‚ â†’ Kâ‚‚ âŠ† Uâ‚‚ â†’
      (âˆ€ x, Pâ‚€ x fâ‚ âˆ§ Pâ‚€' x fâ‚) â†’ (âˆ€ x, Pâ‚€ x fâ‚‚) â†’ (âˆ€ x âˆˆ Uâ‚, Pâ‚ x fâ‚) â†’ (âˆ€ x âˆˆ Uâ‚‚, Pâ‚ x fâ‚‚) â†’
      âˆƒ f : X â†’ Y, (âˆ€ x, Pâ‚€ x f âˆ§ Pâ‚€' x f) âˆ§
        (âˆ€á¶  x near Kâ‚ âˆª Kâ‚‚, Pâ‚ x f) âˆ§ âˆ€á¶  x near Kâ‚ âˆª Uâ‚‚á¶œ, f x = fâ‚ x := by
    intro Uâ‚ Uâ‚‚ Kâ‚ Kâ‚‚ fâ‚ fâ‚‚ Uâ‚_op Uâ‚‚_op Kâ‚_cpct Kâ‚‚_cpct hKâ‚Uâ‚ hKâ‚‚Uâ‚‚ hfâ‚ hfâ‚‚ hfâ‚Uâ‚ hfâ‚‚Uâ‚‚
    obtain âŸ¨hâ‚€fâ‚, hâ‚€'fâ‚âŸ© := forall_and.mp hfâ‚
    rw [forall_restrictGermPredicate_iff] at hâ‚€'fâ‚
    rcases(hasBasis_nhdsSet K).mem_iff.mp (hPâ‚fâ‚€.germ_congr_set hâ‚€'fâ‚) with âŸ¨U,
      âŸ¨U_op, hKUâŸ©, hU : âˆ€ {x}, x âˆˆ U â†’ Pâ‚ x fâ‚âŸ©
    obtain âŸ¨Kâ‚', Kâ‚‚', Uâ‚', Uâ‚‚', Uâ‚'_op, Uâ‚‚'_op, Kâ‚'_cpct, Kâ‚‚'_cpct, hKâ‚Kâ‚', hKâ‚'Uâ‚', hKâ‚‚'Uâ‚‚',
        hKâ‚'Kâ‚‚', hKUâ‚‚', hUâ‚'U, hUâ‚‚'Uâ‚‚âŸ© : âˆƒ (Kâ‚' Kâ‚‚' Uâ‚' Uâ‚‚' : Set X),
        IsOpen Uâ‚' âˆ§ IsOpen Uâ‚‚' âˆ§ IsCompact Kâ‚' âˆ§ IsCompact Kâ‚‚' âˆ§
        Kâ‚ âŠ† Kâ‚' âˆ§ Kâ‚' âŠ† Uâ‚' âˆ§ Kâ‚‚' âŠ† Uâ‚‚' âˆ§ Kâ‚' âˆª Kâ‚‚' = Kâ‚ âˆª Kâ‚‚ âˆ§ K âŠ† Uâ‚‚'á¶œ âˆ§
        Uâ‚' âŠ† U âˆª Uâ‚ âˆ§ Uâ‚‚' âŠ† Uâ‚‚ := by
      apply set_juggling <;> assumption
    have hUâ‚'Pâ‚ : âˆ€ x âˆˆ Uâ‚', Pâ‚ x â†‘fâ‚ :=
      fun x hx â†¦ (hUâ‚'U hx).casesOn (fun h _ â†¦ hU h) (fun h _ â†¦ hfâ‚Uâ‚ x h) (hUâ‚'U hx)
    rcases ind Uâ‚'_op Uâ‚‚'_op Kâ‚'_cpct Kâ‚‚'_cpct hKâ‚'Uâ‚' hKâ‚‚'Uâ‚‚' hâ‚€fâ‚ hfâ‚‚ hUâ‚'Pâ‚
      (fun x hx â†¦ hfâ‚‚Uâ‚‚ x (hUâ‚‚'Uâ‚‚ hx)) with âŸ¨f, hâ‚€f, hf, h'fâŸ©
    refine âŸ¨f, fun x â†¦ âŸ¨hâ‚€f x, restrictGermPredicate_congr (hfâ‚ x).2 ?_âŸ©, ?_, ?_âŸ©
    Â· exact h'f.filter_mono (nhdsSet_mono <| subset_union_of_subset_right hKUâ‚‚' Kâ‚')
    Â· rwa [hKâ‚'Kâ‚‚'] at hf
    Â· apply h'f.filter_mono; gcongr
  rcases inductive_construction_of_loc Pâ‚€ Pâ‚€' Pâ‚ hfâ‚€ loc ind' with âŸ¨f, hfâŸ©
  simp only [forall_and, forall_restrictGermPredicate_iff, Pâ‚€'] at hf âŠ¢
  exact âŸ¨f, âŸ¨hf.1, hf.2.2âŸ©, hf.2.1âŸ©

end inductive_construction

section Htpy

private noncomputable def T : â„• â†’ â„ := fun n â†¦ Nat.rec 0 (fun k x â†¦ x + 1 / (2 : â„) ^ (k + 1)) n

-- Note this is more painful than Patrick hoped for. Maybe this should be the definition of T.
private theorem T_eq (n : â„•) : T n = 1 - (1 / 2 : â„) ^ n := by
  unfold T
  induction n with
  | zero => simp [Nat.zero_eq]
  | succ n ihn =>
    simp_rw [ihn, pow_succ']
    field_simp
    ring

private theorem T_lt (n : â„•) : T n < 1 := by
  rw [T_eq]
  have : (0 : â„) < (1 / 2 : â„) ^ n := by positivity
  linarith

private theorem T_lt_succ (n : â„•) : T n < T (n + 1) :=
  lt_add_of_le_of_pos le_rfl (one_div_pos.mpr (pow_pos zero_lt_two _))

private theorem T_le_succ (n : â„•) : T n â‰¤ T (n + 1) :=
  (T_lt_succ n).le

private theorem T_succ_sub (n : â„•) : T (n + 1) - T n = 1 / (2 : â„) ^ (n + 1) :=
  add_sub_cancel_left ..

private theorem mul_T_succ_sub (n : â„•) : (2 : â„) ^ (n + 1) * (T (n + 1) - T n) = 1 := by
  rw [T_succ_sub]
  field_simp

private theorem T_one : T 1 = 1 / 2 := by simp [T]

private theorem T_pos {n : â„•} (hn : n â‰  0) : 0 < T n := by
  rw [T_eq, sub_pos]
  apply pow_lt_oneâ‚€ <;> first | assumption | norm_num

private theorem T_nonneg (n : â„•) : 0 â‰¤ T n := by
  rw [T_eq, sub_nonneg]
  apply pow_le_oneâ‚€ <;> norm_num

private theorem not_T_succ_le (n : â„•) : Â¬T (n + 1) â‰¤ 0 :=
  (T_pos n.succ_ne_zero).not_le

theorem inductive_htpy_construction' {X Y : Type*} [TopologicalSpace X] {N : â„•}
    {U K : IndexType N â†’ Set X} (Pâ‚€ Pâ‚ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    (Pâ‚‚ : âˆ€ p : â„ Ã— X, Germ (ğ“ p) Y â†’ Prop)
    (hPâ‚‚ : âˆ€ (a b) (p : â„ Ã— X) (f : â„ Ã— X â†’ Y), Pâ‚‚ (a * p.1 + b, p.2) f â†’
      Pâ‚‚ p fun p : â„ Ã— X â†¦ f (a * p.1 + b, p.2))
    (U_fin : LocallyFinite U) (K_cover : (â‹ƒ i, K i) = univ) {fâ‚€ : X â†’ Y} (init : âˆ€ x, Pâ‚€ x fâ‚€)
    (init' : âˆ€ p, Pâ‚‚ p fun p : â„ Ã— X â†¦ fâ‚€ p.2)
    -- Not in the original version
    (ind : âˆ€ (i : IndexType N) (f : X â†’ Y), (âˆ€ x, Pâ‚€ x f) â†’ (âˆ€á¶  x near â‹ƒ j < i, K j, Pâ‚ x f) â†’
      âˆƒ F : â„ â†’ X â†’ Y, (âˆ€ t, âˆ€ x, Pâ‚€ x <| F t) âˆ§ (âˆ€á¶  x near â‹ƒ j â‰¤ i, K j, Pâ‚ x <| F 1) âˆ§
        (âˆ€ p, Pâ‚‚ p â†¿F) âˆ§ (âˆ€ t, âˆ€ x âˆ‰ (U i), F t x = f x) âˆ§
          (âˆ€á¶  t near Iic 0, F t = f) âˆ§ âˆ€á¶  t near Ici 1, F t = F 1) :
    âˆƒ F : â„ â†’ X â†’ Y, F 0 = fâ‚€ âˆ§ (âˆ€ t x, Pâ‚€ x (F t)) âˆ§ (âˆ€ x, Pâ‚ x (F 1)) âˆ§ âˆ€ p, Pâ‚‚ p â†¿F := by
  let PPâ‚€ : âˆ€ p : â„ Ã— X, Germ (ğ“ p) Y â†’ Prop := fun p Ï† â†¦
    Pâ‚€ p.2 Ï†.sliceRight âˆ§ (p.1 = 0 â†’ Ï†.value = fâ‚€ p.2) âˆ§ Pâ‚‚ p Ï†
  let PPâ‚ : IndexType N â†’ âˆ€ p : â„ Ã— X, Germ (ğ“ p) Y â†’ Prop := fun i p Ï† â†¦
    p.1 = 1 â†’ RestrictGermPredicate Pâ‚ (K i) p.2 Ï†.sliceRight
  let PPâ‚‚ : IndexType N â†’ (â„ Ã— X â†’ Y) â†’ Prop := fun i f â†¦
    âˆ€ x, âˆ€ t â‰¥ T i.toNat, f (t, x) = f (T i.toNat, x)
  have hPPâ‚€ : âˆ€ p : â„ Ã— X, PPâ‚€ p fun p : â„ Ã— X â†¦ fâ‚€ p.2 := fun (t, x) â†¦
    âŸ¨init x, fun _ â†¦ rfl, init' _âŸ©
  have ind' : âˆ€ (i) (f : â„ Ã— X â†’ Y), (âˆ€ p, PPâ‚€ p f) â†’ PPâ‚‚ i f â†’ (âˆ€ j < i, âˆ€ p, PPâ‚ j p f) â†’
      âˆƒ f' : â„ Ã— X â†’ Y, (âˆ€ p, PPâ‚€ p f') âˆ§ (Â¬IsMax i â†’ PPâ‚‚ i.succ f') âˆ§
        (âˆ€ j â‰¤ i, âˆ€ p, PPâ‚ j p f') âˆ§ âˆ€ p âˆ‰ Ici (T i.toNat) Ã—Ë¢ U i, f' p = f p := by
    rintro i F hâ‚€F hâ‚‚F hâ‚F
    replace hâ‚F : âˆ€á¶  x : X near â‹ƒ j < i, K j, Pâ‚ x fun x â†¦ F (T i.toNat, x) := by
      rw [eventually_nhdsSet_iUnionâ‚‚]
      intro j hj
      have : âˆ€ x : X, RestrictGermPredicate Pâ‚ (K j) x fun x' â†¦ F (1, x') := fun x â†¦
        hâ‚F j hj (1, x) rfl
      apply (forall_restrictGermPredicate_iff.mp this).germ_congr_set
      filter_upwards with x
      rw [hâ‚‚F _ _ (T_lt _).le]
    rcases ind i (fun x â†¦ F (T i.toNat, x)) (fun x â†¦ (hâ‚€F (_, x)).1) hâ‚F with
        âŸ¨F', hâ‚€F', hâ‚F', hâ‚‚F', hUF', hpast_F', hfutur_F'âŸ©
    clear ind
    let F'' : â„ Ã— X â†’ Y := fun p : â„ Ã— X â†¦
      if p.1 â‰¤ T i.toNat then F p else F' ((2 : â„) ^ (i.toNat + 1) * (p.1 - T i.toNat)) p.2
    have locâ‚ : âˆ€ p : â„ Ã— X, p.1 â‰¤ T i.toNat â†’ (F'' : Germ (ğ“ p) Y) = F := by
      dsimp only [PPâ‚‚] at hâ‚‚F
      rintro âŸ¨t, xâŸ© (ht : t â‰¤ _)
      rcases eq_or_lt_of_le ht with (rfl | ht)
      Â· apply Quotient.sound
        replace hpast_F' : â†¿F' =á¶ [ğ“ (0, x)] fun q : â„ Ã— X â†¦ F (T i.toNat, q.2) := by
          have : ğ“ (0 : â„) â‰¤ ğ“Ë¢ (Iic 0) := nhds_le_nhdsSet right_mem_Iic
          apply mem_of_superset (prod_mem_nhds (hpast_F'.filter_mono this) univ_mem)
          rintro âŸ¨t', x'âŸ© âŸ¨ht', -âŸ©
          exact (congr_fun ht' x' : _)
        have lim : Tendsto (fun x : â„ Ã— X â†¦ ((2 : â„) ^ (i.toNat + 1) * (x.1 - T i.toNat), x.2))
            (ğ“ (T i.toNat, x)) (ğ“ (0, x)) := by
          rw [nhds_prod_eq, nhds_prod_eq]
          have limt : Tendsto (fun t â†¦ (2 : â„) ^ (i.toNat + 1) * (t - T i.toNat))
              (ğ“ (T i.toNat)) (ğ“ 0) :=
            Continuous.tendsto' (by fun_prop) _ _ (by simp)
          exact limt.prodMap tendsto_id
        filter_upwards [hpast_F'.comp_tendsto lim]
        dsimp [F'']
        rintro âŸ¨t, xâŸ© h'
        split_ifs with h
        Â· rfl
        Â· push_neg at h
          change (â†¿F') ((2 : â„) ^ (i.toNat + 1) * (t - T i.toNat), x) = _
          rw [h', hâ‚‚F x t h.le]
      Â· have hp : âˆ€á¶  p : â„ Ã— X in ğ“ (t, x), p.1 â‰¤ T i.toNat :=
          continuousAt_fst (p := (t, x)) (Iic_mem_nhds ht)
        apply Quotient.sound
        exact hp.mono fun p hp â†¦ if_pos hp
    have locâ‚‚ : âˆ€ p : â„ Ã— X, p.1 > T i.toNat â†’
        (F'' : Germ (ğ“ p) Y) = fun p : â„ Ã— X â†¦
          F' ((2 : â„) ^ (i.toNat + 1) * (p.1 - T i.toNat)) p.2 := fun (t, x) ht â†¦ by
      apply Quotient.sound
      have hp : âˆ€á¶  p : â„ Ã— X in ğ“ (t, x), Â¬p.1 â‰¤ T i.toNat := by
        apply mem_of_superset (prod_mem_nhds (Ioi_mem_nhds ht) univ_mem)
        rintro âŸ¨t', x'âŸ© âŸ¨ht', -âŸ©
        simpa using ht'
      exact hp.mono fun q hq â†¦ if_neg hq
    refine âŸ¨F'', ?_, ?_, ?_, ?_âŸ©
    Â· rintro p
      by_cases ht : p.1 â‰¤ T i.toNat
      Â· rw [locâ‚ _ ht]
        apply hâ‚€F
      Â· push_neg at ht
        obtain âŸ¨t, xâŸ© :=p
        rw [locâ‚‚ _ ht]
        refine âŸ¨hâ‚€F' ((2 : â„) ^ (i.toNat + 1) * (t - T i.toNat)) x, ?_, ?_âŸ©
        Â· rintro (rfl : t = 0)
          exact (lt_irrefl _ ((T_nonneg i.toNat).trans_lt ht)).elim
        Â· simpa only [mul_sub, neg_mul]
            using hPâ‚‚ ((2 : â„) ^ (i.toNat + 1)) (-(2 : â„) ^ (i.toNat + 1) * T i.toNat)
              (t, x) (â†¿F') (hâ‚‚F' _)
    Â· intro hi x t ht
      rw [i.toNat_succ hi] at ht âŠ¢
      have hâ‚‚t : Â¬t â‰¤ T i.toNat := by
        exact ((T_lt_succ i.toNat).trans_le ht).not_le
      dsimp only [F'']
      rw [if_neg hâ‚‚t, if_neg]
      Â· rw [hfutur_F'.self_of_nhdsSet, mul_T_succ_sub]
        conv =>
          rw [mem_Ici]
          congr
          rw [â† mul_T_succ_sub i.toNat]
        exact mul_le_mul_of_nonneg_left (sub_le_sub_right ht _) (pow_nonneg zero_le_two _)
      Â· push_neg
        apply T_lt_succ
    Â· rintro j hj âŸ¨t, xâŸ© (rfl : t = 1)
      replace hâ‚F' := eventually_nhdsSet_iUnionâ‚‚.mp hâ‚F' j hj
      rw [locâ‚‚ (1, x) (T_lt i.toNat)]
      revert x
      change âˆ€ x : X, RestrictGermPredicate Pâ‚ (K j) x fun x' : X â†¦
          F' ((2 : â„) ^ (i.toNat + 1) * (1 - T i.toNat)) x'
      rw [forall_restrictGermPredicate_iff]
      apply hâ‚F'.germ_congr_set
      filter_upwards
      apply congr_fun (hfutur_F'.self_of_nhdsSet ..)
      rw [mem_Ici]
      conv => congr; skip; rw [â† mul_T_succ_sub i.toNat]
      exact mul_le_mul_of_nonneg_left (sub_le_sub_right (T_lt _).le _) (pow_nonneg zero_le_two _)
    Â· rintro âŸ¨t, xâŸ© htx
      simp only [prodMk_mem_set_prod_eq, mem_Ici, not_and_or, not_le] at htx
      obtain (ht | hx) := htx
      Â· change (â†‘F'' : Germ (ğ“ (t, x)) Y).value = (â†‘F : Germ (ğ“ (t, x)) Y).value
        rw [locâ‚ (t, x) ht.le]
      Â· dsimp only [F'']
        split_ifs with ht
        Â· rfl
        Â· rw [hUF' _ x hx]
          push_neg at ht
          rw [hâ‚‚F x _ ht.le]
  rcases inductive_construction PPâ‚€ PPâ‚ PPâ‚‚ (U_fin.prod_left fun i â†¦ Ici (T i.toNat))
      âŸ¨fun p â†¦ fâ‚€ p.2, hPPâ‚€, fun x t _ â†¦ rflâŸ© ind' with
    âŸ¨F, hF, h'FâŸ©
  clear ind ind' hPPâ‚€
  refine âŸ¨curry F, ?_, ?_, ?_, ?_âŸ©
  Â· exact funext fun x â†¦ (hF (0, x)).2.1 rfl
  Â· exact fun t x â†¦ (hF (t, x)).1
  Â· intro x
    obtain âŸ¨j, hjâŸ© : âˆƒ j, x âˆˆ K j := by simpa using (by simp [K_cover] : x âˆˆ â‹ƒ j, K j)
    exact (h'F j (1, x) rfl hj).self_of_nhds
  Â· exact fun p â†¦ (hF p).2.2

theorem inductive_htpy_construction {X Y : Type*}
    [EMetricSpace X] [LocallyCompactSpace X] [SecondCountableTopology X]
    (Pâ‚€ Pâ‚ : âˆ€ x : X, Germ (ğ“ x) Y â†’ Prop)
    (Pâ‚‚ : âˆ€ p : â„ Ã— X, Germ (ğ“ p) Y â†’ Prop)
    (hPâ‚‚ : âˆ€ (a b) (p : â„ Ã— X) (f : â„ Ã— X â†’ Y), Pâ‚‚ (a * p.1 + b, p.2) f â†’
      Pâ‚‚ p fun p : â„ Ã— X â†¦ f (a * p.1 + b, p.2))
    (hPâ‚‚' : âˆ€ t x (f : X â†’ Y), Pâ‚€ x f â†’ Pâ‚‚ (t, x) fun p : â„ Ã— X â†¦ f p.2)
    {fâ‚€ : X â†’ Y} (init : âˆ€ x, Pâ‚€ x fâ‚€)
    (ind : âˆ€ x, âˆƒ V âˆˆ ğ“ x, âˆ€ Kâ‚ âŠ† V, âˆ€ Kâ‚€ âŠ† interior Kâ‚, IsCompact Kâ‚€ â†’ IsCompact Kâ‚ â†’
      âˆ€ (C : Set X) (f : X â†’ Y), IsClosed C â†’ (âˆ€ x, Pâ‚€ x f) â†’
      (âˆ€á¶  x near C, Pâ‚ x f) â†’ âˆƒ F : â„ â†’ X â†’ Y, (âˆ€ t, âˆ€ x, Pâ‚€ x <| F t)
        âˆ§ (âˆ€á¶  x near C âˆª Kâ‚€, Pâ‚ x <| F 1) âˆ§
      (âˆ€ p, Pâ‚‚ p â†¿F) âˆ§ (âˆ€ t, âˆ€ x âˆ‰ Kâ‚, F t x = f x) âˆ§
      (âˆ€á¶  t near Iic 0, F t = f) âˆ§ âˆ€á¶  t near Ici 1, F t = F 1) :
    âˆƒ F : â„ â†’ X â†’ Y, F 0 = fâ‚€ âˆ§ (âˆ€ t x, Pâ‚€ x (F t)) âˆ§ (âˆ€ x, Pâ‚ x (F 1)) âˆ§ âˆ€ p, Pâ‚‚ p â†¿F := by
  let P (V : Set X) : Prop :=  âˆ€ Kâ‚ âŠ† V, âˆ€ Kâ‚€ âŠ† interior Kâ‚, IsCompact Kâ‚€ â†’ IsCompact Kâ‚ â†’
      âˆ€ (C : Set X) (f : X â†’ Y), IsClosed C â†’ (âˆ€ x, Pâ‚€ x f) â†’
      (âˆ€á¶  x near C, Pâ‚ x f) â†’ âˆƒ F : â„ â†’ X â†’ Y, (âˆ€ t, âˆ€ x, Pâ‚€ x <| F t)
        âˆ§ (âˆ€á¶  x near C âˆª Kâ‚€, Pâ‚ x <| F 1) âˆ§
      (âˆ€ p, Pâ‚‚ p â†¿F) âˆ§ (âˆ€ t, âˆ€ x âˆ‰ Kâ‚, F t x = f x) âˆ§
      (âˆ€á¶  t near Iic 0, F t = f) âˆ§ âˆ€á¶  t near Ici 1, F t = F 1
  have P_anti : Antitone P := fun U V UV hV Kâ‚ Kâ‚U â†¦ hV Kâ‚ (Kâ‚U.trans UV)
  have P_empty : P âˆ… := by
    intro Kâ‚ Kâ‚V Kâ‚€ Kâ‚€Kâ‚ _ _ C f _ hf hf'
    have Kâ‚€_eq : Kâ‚€ = âˆ… := subset_empty_iff.mp <| Kâ‚€Kâ‚.trans interior_subset |>.trans Kâ‚V
    use fun _ x â†¦ f x
    simp [hf, hf', Kâ‚€_eq]
    tauto
  rcases exists_locallyFinite_subcover_of_locally isClosed_univ P_anti P_empty
    (by simpa only [mem_univ, forall_true_left] using ind) with
    âŸ¨K : IndexType 0 â†’ Set X, W : IndexType 0 â†’ Set X, K_cpct, W_op, hW, K_subW, W_fin, K_coverâŸ©
  apply inductive_htpy_construction' Pâ‚€ Pâ‚ Pâ‚‚ hPâ‚‚ W_fin (univ_subset_iff.mp K_cover) init
    (fun âŸ¨t, xâŸ© â†¦  hPâ‚‚' t x fâ‚€ (init x))
  intro i f hfâ‚€ hfâ‚
  obtain âŸ¨Kâ‚, Kâ‚_cpct, KiKâ‚, Kâ‚WâŸ© : âˆƒ Kâ‚, IsCompact Kâ‚ âˆ§ K i âŠ† interior Kâ‚ âˆ§ Kâ‚ âŠ† W i :=
    exists_compact_between (K_cpct i) (W_op i) (K_subW i)
  rcases hW i Kâ‚ Kâ‚W (K i) KiKâ‚ (K_cpct i) Kâ‚_cpct (â‹ƒ j < i, K j) f
    ((finite_lt_nat i).isClosed_biUnion fun j _ â†¦ (K_cpct j).isClosed) hfâ‚€ hfâ‚
    with âŸ¨F, hFâ‚€, hFâ‚, hFâ‚‚, hFKâ‚, htâŸ©
  refine âŸ¨F, hFâ‚€, ?_, hFâ‚‚, ?_, htâŸ©
  Â· apply hFâ‚.filter_mono
    gcongr
    rw [biUnion_le]
  Â· exact fun t x hx â†¦ hFKâ‚ t x (notMem_subset Kâ‚W hx)
end Htpy
