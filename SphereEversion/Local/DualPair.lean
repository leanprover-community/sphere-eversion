import Mathlib.Analysis.Calculus.ContDiff.FiniteDimension
import Mathlib.Analysis.Complex.Norm
import Mathlib.Analysis.Normed.Module.Completion
import Mathlib.LinearAlgebra.Dual.Lemmas
import SphereEversion.Notations
import SphereEversion.ToMathlib.Analysis.NormedSpace.OperatorNorm.Prod
import SphereEversion.ToMathlib.LinearAlgebra.Basic

/-! # Dual pairs

Convex integration improves maps by successively modify their derivative in a direction of a
vector field (almost) without changing their derivative along some complementary hyperplace.

In this file we introduce the linear algebra part of the story of such modifications.
The main gadget here are dual pairs on a real topological vector space `E`.
Each `p : DualPair E` is made of a (continuous) linear form `p.Ï€ : E â†’L[â„] â„` and a vector
`p.v : E` such that `p.Ï€ p.v = 1`.

Those pairs are used to build the updating operation turning `Ï† : E â†’L[â„] F` into
`p.update Ï† w` which equals `Ï†` on `ker p.Ï€` and sends `v` to the given `w : F`.

After formalizing the above definitions, we first record a number of trivial algebraic lemmas.
Then we prove a lemma which is geometrically obvious but not so easy to formalize:
`injective_update_iff` states, starting with an injective `Ï†`, that the updated
map `p.update Ï† w` is injective if and only if `w` isn't in the image of `ker p.Ï€` under `Ï†`.
This is crucial in order to apply convex integration to immersions.

Then we prove continuity and smoothness lemmas for this operation.
-/


noncomputable section

open Function ContinuousLinearMap

open LinearMap (ker)

open scoped ContDiff

/-! ## General theory -/

section NoNorm

variable (E : Type*) {E' F G : Type*}

variable [AddCommGroup E] [Module â„ E] [TopologicalSpace E]

variable [AddCommGroup E'] [Module â„ E'] [TopologicalSpace E']

variable [NormedAddCommGroup F] [NormedSpace â„ F] [NormedAddCommGroup G] [NormedSpace â„ G]

/-- A continuous linear form `Ï€` and a vector `v` that pair to one. In particular `ker Ï€` is a
hyperplane and `v` spans a complement of this hyperplane. -/
structure DualPair where
  Ï€ : E â†’L[â„] â„
  v : E
  pairing : Ï€ v = 1

namespace DualPair

variable {E}

attribute [local simp] ContinuousLinearMap.toSpanSingleton_apply

theorem pi_ne_zero (p : DualPair E) : p.Ï€ â‰  0 := fun H â†¦ by
  simpa [H] using p.pairing

theorem ker_pi_ne_top (p : DualPair E) : ker p.Ï€ â‰  âŠ¤ := fun H â†¦ by
  have : p.Ï€.toLinearMap p.v = 1 := p.pairing
  simpa [LinearMap.ker_eq_top.mp H]

theorem v_ne_zero (p : DualPair E) : p.v â‰  0 := fun H â†¦ by simpa [H] using p.pairing

/-- Given a dual pair `p`, `p.span_v` is the line spanned by `p.v`. -/
def spanV (p : DualPair E) : Submodule â„ E :=
  Submodule.span â„ {p.v}

theorem mem_spanV (p : DualPair E) {u : E} : u âˆˆ p.spanV â†” âˆƒ t : â„, u = t â€¢ p.v := by
  simp [DualPair.spanV, Submodule.mem_span_singleton, eq_comm]

/-- Update a continuous linear map `Ï† : E â†’L[â„] F` using a dual pair `p` on `E` and a
vector `w : F`. The new map coincides with `Ï†` on `ker p.Ï€` and sends `p.v` to `w`. -/
def update (p : DualPair E) (Ï† : E â†’L[â„] F) (w : F) : E â†’L[â„] F :=
  Ï† + (w - Ï† p.v) â¬ p.Ï€

@[simp]
theorem update_ker_pi (p : DualPair E) (Ï† : E â†’L[â„] F) (w : F) {u} (hu : u âˆˆ ker p.Ï€) :
    p.update Ï† w u = Ï† u := by
  rw [LinearMap.mem_ker] at hu
  simp [update, hu]

@[simp]
theorem update_v (p : DualPair E) (Ï† : E â†’L[â„] F) (w : F) : p.update Ï† w p.v = w := by
  simp [update, p.pairing]

@[simp]
theorem update_self (p : DualPair E) (Ï† : E â†’L[â„] F) : p.update Ï† (Ï† p.v) = Ï† := by
  simp only [update, add_zero, ContinuousLinearMap.toSpanSingleton_zero,
    ContinuousLinearMap.zero_comp, sub_self]

@[simp]
theorem update_update (p : DualPair E) (Ï† : E â†’L[â„] F) (w w' : F) :
    p.update (p.update Ï† w') w = p.update Ï† w := by
  simp_rw [update, add_apply, coe_comp', (Â· âˆ˜ Â·), toSpanSingleton_apply, p.pairing, one_smul,
    add_sub_cancel, add_assoc, â† ContinuousLinearMap.add_comp, â† toSpanSingleton_add,
    sub_add_eq_add_sub, add_sub_cancel]

theorem inf_eq_bot (p : DualPair E) : ker p.Ï€ âŠ“ p.spanV = âŠ¥ := bot_unique <| fun x hx â†¦ by
  have : p.Ï€ x = 0 âˆ§ âˆƒ a : â„, a â€¢ p.v = x := by
    simpa [DualPair.spanV, Submodule.mem_span_singleton] using hx
  rcases this with âŸ¨H, t, rflâŸ©
  rw [p.Ï€.map_smul, p.pairing, smul_eq_mul, mul_one] at H
  simp [H]

theorem sup_eq_top (p : DualPair E) : ker p.Ï€ âŠ” p.spanV = âŠ¤ := by
  rw [Submodule.sup_eq_top_iff]
  intro x
  refine âŸ¨x - p.Ï€ x â€¢ p.v, ?_, p.Ï€ x â€¢ p.v, ?_, ?_âŸ© <;>
    simp [DualPair.spanV, Submodule.mem_span_singleton, p.pairing]

theorem decomp (p : DualPair E) (e : E) : âˆƒ u âˆˆ ker p.Ï€, âˆƒ t : â„, e = u + t â€¢ p.v := by
  have : e âˆˆ ker p.Ï€ âŠ” p.spanV := by
    rw [p.sup_eq_top]
    exact Submodule.mem_top
  simp_rw [Submodule.mem_sup, DualPair.mem_spanV] at this
  rcases this with âŸ¨u, hu, -, âŸ¨t, rflâŸ©, rflâŸ©
  exact âŸ¨u, hu, t, rflâŸ©

-- useful with `DualPair.decomp`
theorem update_apply (p : DualPair E) (Ï† : E â†’L[â„] F) {w : F} {t : â„} {u} (hu : u âˆˆ ker p.Ï€) :
    p.update Ï† w (u + t â€¢ p.v) = Ï† u + t â€¢ w := by
  rw [map_add, map_smul, p.update_v, p.update_ker_pi _ _ hu]

/-- Map a dual pair under a linear equivalence. -/
@[simps]
def map (p : DualPair E) (L : E â‰ƒL[â„] E') : DualPair E' :=
  âŸ¨p.Ï€ âˆ˜L â†‘L.symm, L p.v, (congr_arg p.Ï€ <| L.symm_apply_apply p.v).trans p.pairingâŸ©

theorem update_comp_left (p : DualPair E) (Ïˆ' : F â†’L[â„] G) (Ï† : E â†’L[â„] F) (w : F) :
    p.update (Ïˆ' âˆ˜L Ï†) (Ïˆ' w) = Ïˆ' âˆ˜L p.update Ï† w := by
  ext1 u
  simp only [update, add_apply, ContinuousLinearMap.comp_apply, toSpanSingleton_apply, map_add,
    map_smul, map_sub]

theorem map_update_comp_right (p : DualPair E) (Ïˆ' : E â†’L[â„] F) (Ï† : E â‰ƒL[â„] E') (w : F) :
    (p.map Ï†).update (Ïˆ' âˆ˜L â†‘Ï†.symm) w = p.update Ïˆ' w âˆ˜L â†‘Ï†.symm := by
  ext1 u
  simp [update]

/-! ## Injectivity criterion -/


@[simp]
theorem injective_update_iff (p : DualPair E) {Ï† : E â†’L[â„] F} (hÏ† : Injective Ï†) {w : F} :
    Injective (p.update Ï† w) â†” w âˆ‰ (ker p.Ï€).map Ï† := by
  constructor
  Â· rintro h âŸ¨u, hu, rflâŸ©
    have : p.update Ï† (Ï† u) p.v = Ï† u := p.update_v Ï† (Ï† u)
    conv_rhs at this => rw [â† p.update_ker_pi Ï† (Ï† u) hu]
    rw [â† h this] at hu
    simp only [SetLike.mem_coe, LinearMap.mem_ker] at hu
    rw [p.pairing] at hu
    linarith
  Â· intro hw
    refine (injective_iff_map_eq_zero (p.update Ï† w)).mpr fun x hx â†¦ ?_
    rcases p.decomp x with âŸ¨u, hu, t, rflâŸ©
    rw [map_add, map_smul, update_v, p.update_ker_pi _ _ hu] at hx
    obtain rfl : t = 0 := by
      by_contra ht
      apply hw
      refine âŸ¨-tâ»Â¹ â€¢ u, (ker p.Ï€).smul_mem _ hu, ?_âŸ©
      rw [map_smul]
      have : -tâ»Â¹ â€¢ (Ï† u + t â€¢ w) + w = -tâ»Â¹ â€¢ (0 : F) + w := congr_arg (-tâ»Â¹ â€¢ Â· + w) hx
      rwa [smul_add, neg_smul, neg_smul, inv_smul_smulâ‚€ ht, smul_zero, zero_add,
        neg_add_cancel_right, â† neg_smul] at this
    rw [zero_smul, add_zero] at hx âŠ¢
    exact (injective_iff_map_eq_zero Ï†).mp hÏ† u hx

end DualPair

end NoNorm

/-! ## Smoothness and continuity -/


namespace DualPair

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

/- In the next two lemmas, finite dimensionality of `E` is clearly uneeded, but allows
to use `contDiff_clm_apply_iff` and `continuous_clm_apply`. -/
theorem smooth_update [FiniteDimensional â„ E] (p : DualPair E) {G : Type*} [NormedAddCommGroup G]
    [NormedSpace â„ G] {Ï† : G â†’ E â†’L[â„] F} (hÏ† : ğ’ âˆ Ï†) {w : G â†’ F} (hw : ğ’ âˆ w) :
    ğ’ âˆ fun g â†¦ p.update (Ï† g) (w g) := by
  apply hÏ†.add
  rw [contDiff_clm_apply_iff]
  intro y
  exact (hw.sub (contDiff_clm_apply_iff.mp hÏ† p.v)).const_smul _

theorem continuous_update [FiniteDimensional â„ E] (p : DualPair E) {X : Type*} [TopologicalSpace X]
    {Ï† : X â†’ E â†’L[â„] F} (hÏ† : Continuous Ï†) {w : X â†’ F} (hw : Continuous w) :
    Continuous fun g â†¦ p.update (Ï† g) (w g) := by
  apply hÏ†.add
  rw [continuous_clm_apply]
  intro y
  exact (hw.sub (continuous_clm_apply.mp hÏ† p.v)).const_smul _

end DualPair

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type*} [NormedAddCommGroup F]
  [NormedSpace â„ F]

/-- Given a finite basis `e : basis Î¹ â„ E`, and `i : Î¹`,
`e.DualPair i` is given by the `i`th basis element and its dual. -/
def Module.Basis.dualPair [FiniteDimensional â„ E] {Î¹ : Type*} [Fintype Î¹] [DecidableEq Î¹]
    (e : Basis Î¹ â„ E) (i : Î¹) : DualPair E where
  Ï€ := LinearMap.toContinuousLinearMap (e.dualBasis i)
  v := e i
  pairing := by simp
