import Mathlib.Analysis.Asymptotics.Lemmas
import Mathlib.LinearAlgebra.Dual.Lemmas
import Mathlib.Analysis.Calculus.ParametricIntegral
import Mathlib.MeasureTheory.Integral.IntervalIntegral.Periodic
import SphereEversion.ToMathlib.Algebra.Ring.Periodic
import SphereEversion.ToMathlib.MeasureTheory.BorelSpace
import SphereEversion.Loops.Basic
import SphereEversion.Local.DualPair

/-! # TheilliÃ¨re's corrugation operation

This files introduces the fundamental calculus tool of convex integration. The version of convex
integration that we formalize is TheilliÃ¨re's corrugation-based convex integration.
It improves a map `f : E â†’ F` by adding to it some corrugation map
`fun x â†¦ (1/N) â€¢ âˆ« t in 0..(N*Ï€ x), (Î³ x t - (Î³ x).average)` where `Î³` is a family of loops in
`F` parametrized by `E` and `N` is some (very large) real number.

Under the assumption that `âˆ€ x, (Î³ x).average = D f x p.v` for some dual pair `p`, this improved
map will have a derivative which is almost a value of `Î³ x` in direction `p.v` and almost the
derivative of `f` in direction `ker p.Ï€`. More precisely, its derivative will be almost
`p.update (D f x) (Î³ x (N*p.Ï€ x))`. In addition the improved map will be very close to `f`
in Câ° topology. All those "almost" and "very close" refer to the asymptotic behavior when `N`
is very large.

The main definition is `corrugation`. The main results are:
* `fderiv_corrugated_map` computing the difference between `D (f + corrugation p.Ï€ N Î³) x` and
    `p.update (D f x) (Î³ x (N*p.Ï€ x)) + corrugation.remainder p.Ï€ N Î³ x`
* `remainder_c0_small_on` saying this difference is very small
* `corrugation.c0_small_on` saying that corrugations are Câ°-small

-/

noncomputable section

open Set Function Filter MeasureTheory ContinuousLinearMap
open scoped Topology unitInterval

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F]
  {G : Type*} [NormedAddCommGroup G] [NormedSpace â„ G]
  {H : Type*} [NormedAddCommGroup H] [NormedSpace â„ H] [FiniteDimensional â„ H]
  {Ï€ : E â†’L[â„] â„} (N : â„) (Î³ : E â†’ Loop F)

open scoped Borelize

/-- TheilliÃ¨re's corrugations. -/
def corrugation (Ï€ : E â†’L[â„] â„) (N : â„) (Î³ : E â†’ Loop F) : E â†’ F := fun x â†¦
  (1 / N) â€¢ âˆ« t in (0)..N * Ï€ x, Î³ x t - (Î³ x).average

@[inherit_doc] local notation "ğ’¯" => corrugation Ï€

/-- The integral appearing in corrugations is periodic. -/
theorem per_corrugation (Î³ : Loop F) (hÎ³ : âˆ€ s t, IntervalIntegrable Î³ volume s t) :
    OnePeriodic fun s â†¦ âˆ« t in (0)..s, Î³ t - Î³.average := by
  have int_avg : âˆ€ s t, IntervalIntegrable (fun _ : â„ â†¦ Î³.average) volume s t := fun s t â†¦
    intervalIntegrable_const
  intro s
  have intâ‚ : IntervalIntegrable (fun t â†¦ Î³ t - Î³.average) volume 0 s := (hÎ³ _ _).sub (int_avg _ _)
  have intâ‚‚ : IntervalIntegrable (fun t â†¦ Î³ t - Î³.average) volume s (s + 1) :=
    (hÎ³ _ _).sub (int_avg _ _)
  have intâ‚ƒ : IntervalIntegrable Î³ volume s (s + 1) := hÎ³ _ _
  have intâ‚„ : IntervalIntegrable (fun _ â†¦ Î³.average) volume s (s + 1) := int_avg _ _
  dsimp only
  /- Rmk: Lean doesn't want to rewrite using `intervalIntegral.integral_sub` without being
      given the integrability assumptions :-( -/
  rw [â† intervalIntegral.integral_add_adjacent_intervals intâ‚ intâ‚‚,
    intervalIntegral.integral_sub intâ‚ƒ intâ‚„, Î³.periodic.intervalIntegral_add_eq s 0, zero_add,
    Loop.average]
  simp only [add_zero, add_tsub_cancel_left, intervalIntegral.integral_const, one_smul, sub_self]

@[simp]
theorem corrugation_const {x : E} (h : (Î³ x).IsConst) : ğ’¯ N Î³ x = 0 := by
  unfold corrugation
  rw [Loop.isConst_iff_const_avg] at h
  rw [h]
  simp only [add_zero, intervalIntegral.integral_const, Loop.const_apply, Loop.average_const,
    smul_zero, sub_self]

variable (Ï€)

theorem corrugation.support : support (ğ’¯ N Î³) âŠ† Loop.support Î³ := fun x x_in â†¦ by
  apply subset_closure
  intro h
  apply x_in
  simp [h]

theorem corrugation_eq_zero (x) (H : x âˆ‰ Loop.support Î³) : corrugation Ï€ N Î³ x = 0 :=
  notMem_support.mp fun hx â†¦ H (corrugation.support Ï€ N Î³ hx)

open intervalIntegral in
theorem corrugation.c0_small_on [FirstCountableTopology E] [LocallyCompactSpace E]
    {Î³ : â„ â†’ E â†’ Loop F} {K : Set E} (hK : IsCompact K) (h_le : âˆ€ x, âˆ€ t â‰¤ 0, Î³ t x = Î³ 0 x)
    (h_ge : âˆ€ x, âˆ€ t â‰¥ 1, Î³ t x = Î³ 1 x) (hÎ³_cont : Continuous â†¿Î³) {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆ€á¶  N in atTop, âˆ€ x âˆˆ K, âˆ€ (t), â€–ğ’¯ N (Î³ t) xâ€– < Îµ := by
  set Ï† := fun (q : â„ Ã— E) t â†¦ âˆ« t in (0)..t, (Î³ q.1 q.2) t - (Î³ q.1 q.2).average
  have cont' : Continuous â†¿Ï† := by
    refine continuous_parametric_intervalIntegral_of_continuous ?_ continuous_snd
    refine (hÎ³_cont.compâ‚ƒ continuous_fst.fst.fst continuous_fst.fst.snd continuous_snd).sub ?_
    refine Loop.continuous_average ?_
    exact hÎ³_cont.compâ‚ƒ continuous_fst.fst.fst.fst continuous_fst.fst.fst.snd continuous_snd
  have hper : âˆ€ q, OnePeriodic (Ï† q) := fun _ â†¦ per_corrugation _ fun _ _ â†¦
    (hÎ³_cont.compâ‚ƒ continuous_const continuous_const continuous_id).intervalIntegrable _ _
  rcases cont'.bounded_on_compact_of_onePeriodic hper ((isCompact_Icc : IsCompact I).prod hK)
    with âŸ¨C, hCâŸ©
  Â· apply (const_mul_one_div_lt Îµ_pos C).mono
    intro N hN x hx t
    rw [corrugation, norm_smul, mul_comm]
    apply (mul_le_mul_of_nonneg_right _ (norm_nonneg <| 1 / N)).trans_lt hN
    obtain (ht | ht) := le_or_gt t 0
    Â· rw [h_le x t ht]
      apply hC (0, x)
      simp [hx]
    Â· obtain (ht' | ht') := le_or_gt 1 t
      Â· rw [h_ge x t ht']
        apply hC (1, x)
        simp [hx]
      Â· exact hC (t, x) (mk_mem_prod âŸ¨ht.le, ht'.leâŸ© hx) _

variable {Î³}

theorem corrugation.contDiff' {n : â„•âˆ} {Î³ : G â†’ E â†’ Loop F} (hÎ³_diff : ğ’ n â†¿Î³) {x : H â†’ E}
    (hx : ğ’ n x) {g : H â†’ G} (hg : ğ’ n g) : ğ’ n fun h â†¦ ğ’¯ N (Î³ <| g h) <| x h := by
  apply ContDiff.const_smul
  apply contDiff_parametric_primitive_of_contDiff
  Â· apply ContDiff.sub
    Â· exact hÎ³_diff.compâ‚ƒ hg.fst' hx.fst' contDiff_snd
    Â· apply contDiff_average
      exact hÎ³_diff.compâ‚ƒ hg.fst'.fst' hx.fst'.fst' contDiff_snd
  Â· exact contDiff_const.mul (Ï€.contDiff.comp hx)

theorem corrugation.contDiff [FiniteDimensional â„ E] {n : â„•âˆ} (hÎ³_diff : ğ’ n â†¿Î³) : ğ’ n (ğ’¯ N Î³) :=
  (contDiff_parametric_primitive_of_contDiff (contDiff_sub_average hÎ³_diff)
    (Ï€.contDiff.const_smul N) 0).const_smul _

notation "âˆ‚â‚" => partialFDerivFst â„

/-- The remainder appearing when differentiating a corrugation.
-/
def corrugation.remainder (Ï€ : E â†’ â„) (N : â„) (Î³ : E â†’ Loop F) : E â†’ E â†’L[â„] F := fun x â†¦
  (1 / N) â€¢ âˆ« t in (0)..N * Ï€ x, âˆ‚â‚ (fun x t â†¦ (Î³ x).normalize t) x t

@[inherit_doc] local notation "R" => corrugation.remainder Ï€

variable [FiniteDimensional â„ E]

theorem remainder_eq (N : â„) {Î³ : E â†’ Loop F} (h : ğ’ 1 â†¿Î³) :
    R N Î³ = fun x â†¦ (1 / N) â€¢ âˆ« t in (0)..N * Ï€ x, (Loop.diff Î³ x).normalize t := by
  simp_rw [Loop.diff_normalize h]; rfl

-- The next lemma is a restatement of the above to emphasize that remainder is a corrugation
-- unused
theorem remainder_eq_corrugation (N : â„) {Î³ : E â†’ Loop F} (h : ğ’ 1 â†¿Î³) :
    R N Î³ = ğ’¯ N fun x â†¦ Loop.diff Î³ x :=
  remainder_eq _ _ h

@[simp]
theorem remainder_eq_zero (N : â„) {Î³ : E â†’ Loop F} (h : ğ’ 1 â†¿Î³) {x : E} (hx : x âˆ‰ Loop.support Î³) :
    R N Î³ x = 0 := by
  have hx' : x âˆ‰ Loop.support (Loop.diff Î³) := fun h â†¦ hx <| Loop.support_diff h
  simp [remainder_eq Ï€ N h, Loop.normalize_of_isConst (Loop.isConst_of_not_mem_support hx')]

theorem corrugation.fderiv_eq {N : â„} (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) :
    D (ğ’¯ N Î³) = fun x : E â†¦ ((Î³ x (N * Ï€ x) - (Î³ x).average) â¬ Ï€) + R N Î³ x := by
  ext1 xâ‚€
  have hÏ€_diff : ğ’ 1 Ï€ := Ï€.contDiff
  have diff := contDiff_sub_average hÎ³_diff
  have key := (hasFDerivAt_parametric_primitive_of_contDiff' diff (hÏ€_diff.const_smul N) xâ‚€ 0).2
  erw [fderiv_const_smul key.differentiableAt, key.fderiv, smul_add, add_comm]
  congr 1
  rw [fderiv_fun_const_smul (hÏ€_diff.differentiable le_rfl).differentiableAt N, Ï€.fderiv]
  simp only [smul_smul, inv_mul_cancelâ‚€ hN, one_div, Algebra.id.smul_eq_mul, one_smul,
    ContinuousLinearMap.comp_smul]

theorem corrugation.fderiv_apply (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) (x v : E) :
    D (ğ’¯ N Î³) x v = Ï€ v â€¢ (Î³ x (N * Ï€ x) - (Î³ x).average) + R N Î³ x v := by
  simp only [corrugation.fderiv_eq _ hN hÎ³_diff, toSpanSingleton_apply, add_apply,
    ContinuousLinearMap.comp_apply]

theorem fderiv_corrugated_map (hN : N â‰  0) (hÎ³_diff : ğ’ 1 â†¿Î³) {f : E â†’ F} (hf : ğ’ 1 f)
    (p : DualPair E) {x} (hfÎ³ : (Î³ x).average = D f x p.v) :
    D (f + corrugation p.Ï€ N Î³) x =
      p.update (D f x) (Î³ x (N * p.Ï€ x)) + corrugation.remainder p.Ï€ N Î³ x := by
  ext v
  erw [fderiv_add (hf.differentiable le_rfl).differentiableAt
      ((corrugation.contDiff _ N hÎ³_diff).differentiable le_rfl).differentiableAt]
  simp_rw [ContinuousLinearMap.add_apply, corrugation.fderiv_apply _ N hN hÎ³_diff, hfÎ³,
    DualPair.update, ContinuousLinearMap.add_apply, p.Ï€.comp_toSpanSingleton_apply, add_assoc]

open scoped ContDiff

theorem Remainder.smooth {Î³ : G â†’ E â†’ Loop F} (hÎ³_diff : ğ’ âˆ â†¿Î³) {x : H â†’ E} (hx : ğ’ âˆ x)
    {g : H â†’ G} (hg : ğ’ âˆ g) : ğ’ âˆ fun h â†¦ R N (Î³ <| g h) <| x h := by
  apply ContDiff.const_smul
  apply contDiff_parametric_primitive_of_contDiff
  Â· let Ïˆ : E â†’ H Ã— â„ â†’ F := fun x q â†¦ (Î³ (g q.1) x).normalize q.2
    change  ğ’ âˆ fun q : H Ã— â„ â†¦ âˆ‚â‚ Ïˆ (x q.1) (q.1, q.2)
    refine (ContDiff.contDiff_top_partial_fst ?_).compâ‚‚ hx.fst' (contDiff_fst.prodMk contDiff_snd)
    dsimp [Loop.normalize]
    apply ContDiff.sub
    Â· apply hÎ³_diff.compâ‚ƒ hg.fst'.snd' contDiff_fst contDiff_snd.snd
    Â· apply contDiff_average
      exact hÎ³_diff.compâ‚ƒ hg.fst'.snd'.fst' contDiff_fst.fst' contDiff_snd
  Â· exact contDiff_const.mul (Ï€.contDiff.comp hx)

set_option linter.style.multiGoal false in
theorem remainder_c0_small_on {K : Set E} (hK : IsCompact K) (hÎ³_diff : ğ’ 1 â†¿Î³) {Îµ : â„}
    (Îµ_pos : 0 < Îµ) : âˆ€á¶  N in atTop, âˆ€ x âˆˆ K, â€–R N Î³ xâ€– < Îµ := by
  simp_rw [fun N â†¦ remainder_eq Ï€ N hÎ³_diff]
  let g : â„ â†’ E â†’ Loop (E â†’L[â„] F) := fun _ â†¦ Loop.diff Î³
  have g_le : âˆ€ (x) (t : â„), t â‰¤ 0 â†’ g t x = g 0 x := fun _ _ _ â†¦ rfl
  have g_ge : âˆ€ (x) (t : â„), t â‰¥ 1 â†’ g t x = g 1 x := fun _ _ _ â†¦ rfl
  have g_cont : Continuous â†¿g := (Loop.continuous_diff hÎ³_diff).snd'
  apply (corrugation.c0_small_on _ hK g_le g_ge g_cont Îµ_pos).mono
  intro N H x x_in
  exact H x x_in 0
