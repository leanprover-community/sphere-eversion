import SphereEversion.ToMathlib.Analysis.NormedGroup
import SphereEversion.ToMathlib.LinearAlgebra.Basis
import SphereEversion.Loops.Exists
import SphereEversion.Local.Corrugation
import SphereEversion.Local.AmpleRelation

/-!
# Local h-principle for open and ample relations

This file proves lem:h_principle_open_ample_loc from the blueprint. This is the local
version of the h-principle for open and ample relations. The proof brings together the
main result `exist_loops` from the loop folder (Chapter 1 in the blueprint) and
the corrugation technique.

One formalization issue is that the whole construction carries around a lot of data.
On paper it is easy to state one lemma listing once all this data and proving many properties.
Here it is more convenient to give each property its own lemma so carrying around data,
assumptions and constructions requires some planning. Our way to mitigate this issue
is to use two ad-hoc structures `landscape` and `step_landscape` which partly bundle
all this.

The `landscape` structure record three sets in a vector space, a closed
set `C` and two nested compact sets `Kâ‚€` and `Kâ‚`. This is the ambiant data for
the local h-principle result. We call this partly bundled because it doesn't include
the data of the formal solution we want to improve. Instead we have a Prop-valued
structure `landscape.accepts` that takes a landscape and a formal solution and assert
some compatibility conditions. There are four conditions, which is already enough
motivation to introduce a structure instead of one definition using the logical
conjunction operator that would lead to awkward and error prone access to the
individual conditions.

The proof of this proposition involves an induction on a flag of subspaces (nested
subspaces of increasing dimensions). For the purpose of this induction we use
a second structure `step_landscape` that extends `landscape` with two more pieces
of data, a subspace and a dual pair, and a compatibility condition, namely the subspace
has to be in the hyperplane defined by the dual pair.

In this setup, given `(L : step_landscape E) {ğ“• : formal_sol R} (h : L.accepts R ğ“•)`,
the loop family constructed by Chapter 2 is `L.loop h`. Together with corrugation,
it is used to build `L.improveStep h` which is the homotopy of 1-jet sections improving
the formal solution `ğ“•` in that step of the main inductive proof. A rather long series of
lemmas prove all the required properties of that homotopy, corresponding to
lemma lem:integration_step from the blueprint.

The inductive proof itself is the proof of `rel_loc.FormalSol.improve`.
Here all conclusions are stated at once this the induction requires to know about each
of them to proceed to the next step. We could have introduced one more ad-hoc structure
to record those conclusion but this isn't needed (at least in that Chapter) since we
need to access its components only once.

-/


noncomputable section

open scoped unitInterval Classical Filter Topology

open Filter Set RelLoc

open LinearMap (ker)

local notation "âˆ" => (âŠ¤ : â„•âˆ)

variable (E : Type _) [NormedAddCommGroup E] [NormedSpace â„ E] {F : Type _} [NormedAddCommGroup F]
  [NormedSpace â„ F] {G : Type _} [NormedAddCommGroup G] [NormedSpace â„ G]

/-- The setup for local h-principle is two compact subsets `Kâ‚€ âŠ† Kâ‚` in `E` with
`Kâ‚€ âŠ† interior Kâ‚` and a closed subset `C`.
-/
structure Landscape where
  (C Kâ‚€ Kâ‚ : Set E)
  hC : IsClosed C
  hKâ‚€ : IsCompact Kâ‚€
  hKâ‚ : IsCompact Kâ‚
  hâ‚€â‚ : Kâ‚€ âŠ† interior Kâ‚

section ImproveStep

/-!
## Improvement step

This section proves lem:integration_step.
-/


/-- The setup for a one-step improvement towards a local h-principle is two compact subsets
`Kâ‚€ âŠ† Kâ‚` in `E` with `Kâ‚€ âŠ† interior Kâ‚` and a closed subset `C`
together with a dual pair `p` and a subspace `E'` of the corresponding hyperplane `ker p.Ï€`.
-/
structure StepLandscape extends Landscape E where
  E' : Submodule â„ E
  p : DualPair E
  hEp : E' â‰¤ ker p.Ï€

variable {E}

open scoped Classical

variable (R : RelLoc E F)

namespace StepLandscape

/-- A one-step improvement landscape accepts a formal solution if it can improve it. -/
structure Accepts (L : StepLandscape E) (ğ“• : JetSec E F) : Prop where
  h_op : IsOpen R
  hKâ‚€ : âˆ€á¶  x near L.Kâ‚€, ğ“•.IsPartHolonomicAt L.E' x
  hShort : âˆ€ x, ğ“•.IsShortAt R L.p x
  hC : âˆ€á¶  x near L.C, ğ“•.IsHolonomicAt x

/-- The union of all slices of `R` corresponding to `ğ“•`. -/
def Î© (L : StepLandscape E) (ğ“• : JetSec E F) : Set (E Ã— F) :=
  {p | p.2 âˆˆ ğ“•.sliceAt R L.p p.1}

--â‹ƒ x, ({x} : set E) Ã—Ë¢ (connected_component_in (ğ“•.slice_at R L.p x) $ ğ“•.Ï† x L.p.v)
/-- The linear form in a `step_landscape`, coming from the underlying dual pair. -/
def Ï€ (L : StepLandscape E) : E â†’L[â„] â„ :=
  L.p.Ï€

/-- The vector in a `step_landscape`, coming from the underlying dual pair. -/
def v (L : StepLandscape E) : E :=
  L.p.v

/-- One more compact set in the landscape: Kâ‚ âˆ© C, needed as an input to the
loop construction. -/
def K (L : StepLandscape E) : Set E :=
  L.Kâ‚ âˆ© L.C

/-- The base function for the loop family associated in any jet section in a
step landscape. -/
def b (L : StepLandscape E) (ğ“• : JetSec E F) : E â†’ F := fun x => ğ“•.Ï† x L.v

/-- The desired average for the loop family associated in any jet section in a
step landscape. -/
def g (L : StepLandscape E) (ğ“• : JetSec E F) : E â†’ F := fun x => D ğ“•.f x L.v

theorem isCompact_K (L : StepLandscape E) : IsCompact L.K :=
  L.hKâ‚.inter_right L.hC

variable {R}

theorem Accepts.open [FiniteDimensional â„ E] {L : StepLandscape E} {ğ“• : JetSec E F}
    (h : L.Accepts R ğ“•) : IsOpen (L.Î© R ğ“•) :=
  by
  set Ïˆ : E Ã— F â†’ OneJet E F := fun p => (p.1, ğ“•.f p.1, L.p.update (ğ“•.Ï† p.1) p.2)
  change IsOpen {p : E Ã— F | Ïˆ p âˆˆ R}
  apply IsOpen.preimage _ h.h_op
  apply continuous_fst.prod_mk (ğ“•.f_diff.continuous.fst'.prod_mk _)
  exact L.p.continuous_update ğ“•.Ï†_diff.continuous.fst' continuous_snd

theorem smooth_b (L : StepLandscape E) (ğ“• : JetSec E F) : ğ’ âˆ (L.b ğ“•) :=
  (ContinuousLinearMap.apply â„ F L.v).contDiff.comp ğ“•.Ï†_diff

theorem smooth_g (L : StepLandscape E) (ğ“• : JetSec E F) : ğ’ âˆ (L.g ğ“•) :=
  (ContinuousLinearMap.apply â„ F L.v).contDiff.comp (contDiff_top_iff_fderiv.mp ğ“•.f_diff).2

theorem Accepts.rel {L : StepLandscape E} {ğ“• : JetSec E F} (h : L.Accepts R ğ“•) :
    âˆ€á¶  x : E near L.K, (L.g ğ“•) x = (L.b ğ“•) x :=
  by
  apply (h.hC.filter_mono <| monotone_nhdsSet (inter_subset_right L.Kâ‚ L.C)).mono
  intro x hx
  dsimp [JetSec.IsHolonomicAt] at hx
  dsimp [StepLandscape.g, StepLandscape.b]
  rw [hx]

variable [FiniteDimensional â„ E] [FiniteDimensional â„ F]

open scoped Borelize
variable (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•)
-- Porting note: the elaboration smiley below was not necessary in Lean 3.
/-- The loop family to use in some landscape to improve a formal solution. -/
def loop (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) : â„ â†’ E â†’ Loop F :=
  Classical.choose ((exist_loops L.isCompact_K h.open (L.smooth_g ğ“•) (L.smooth_b ğ“•):) h.rel h.hShort)

theorem nice (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) :
    NiceLoop (L.g â†‘ğ“•) (L.b â†‘ğ“•) (Î© R L ğ“•) L.K (L.loop h) :=
  Classical.choose_spec <|
    (exist_loops L.isCompact_K h.open (L.smooth_g ğ“•) (L.smooth_b ğ“•):) h.rel h.hShort

theorem update_zero (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) (x : E) (s : â„) :
    L.p.update (ğ“•.Ï† x) ((L.loop h 0 x) s) = ğ“•.Ï† x :=
  by
  rw [(L.nice h).t_zero x s]
  exact L.p.update_self _

theorem loop_smooth (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) : ğ’ âˆ â†¿(L.loop h) :=
  (L.nice h).smooth

theorem loop_smooth' (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) {t : G â†’ â„}
    (ht : ğ’ âˆ t) {s : G â†’ â„} (hs : ğ’ âˆ s) {x : G â†’ E} (hx : ğ’ âˆ x) :
    ğ’ âˆ fun g => L.loop h (t g) (x g) (s g) :=
  (L.loop_smooth h).comp (ht.prod <| hx.prod hs)

theorem loop_C1 (L : StepLandscape E) {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) :
    âˆ€ t, ğ’ 1 â†¿(L.loop h t) := fun _ =>
  (L.loop_smooth' h contDiff_const contDiff_snd contDiff_fst).of_le le_top

variable (L : StepLandscape E)

/-- The cut-off function associated to a step landscape, equal to one near Kâ‚€ and
zero outside Kâ‚. -/
def Ï (L : StepLandscape E) : E â†’ â„ :=
  (exists_contDiff_one_nhds_of_interior L.hKâ‚€.isClosed L.hâ‚€â‚).choose

theorem Ï_smooth (L : StepLandscape E) : ğ’ âˆ L.Ï :=
  (exists_contDiff_one_nhds_of_interior L.hKâ‚€.isClosed L.hâ‚€â‚).choose_spec.1

theorem Ï_mem (L : StepLandscape E) (x : E) : L.Ï x âˆˆ I :=
  (exists_contDiff_one_nhds_of_interior L.hKâ‚€.isClosed L.hâ‚€â‚).choose_spec.2.2.2 x

theorem Ï_le (L : StepLandscape E) (x : E) : |L.Ï x| â‰¤ 1 :=
  by
  cases' L.Ï_mem x with h h'
  rw [abs_le]
  refine' âŸ¨_, h'âŸ©
  linarith

theorem hÏâ‚€ (L : StepLandscape E) : âˆ€á¶  x near L.Kâ‚€, L.Ï x = 1 :=
  (exists_contDiff_one_nhds_of_interior L.hKâ‚€.isClosed L.hâ‚€â‚).choose_spec.2.1

theorem hÏ_compl_Kâ‚ (L : StepLandscape E) {x : E} : x âˆ‰ L.Kâ‚ â†’ L.Ï x = 0 :=
  (exists_contDiff_one_nhds_of_interior L.hKâ‚€.isClosed L.hâ‚€â‚).choose_spec.2.2.1 x

/-- Homotopy of formal solutions obtained by corrugation in the direction of `p : dualPair E`
in some landscape to improve a formal solution `ğ“•` from being `L.E'`-holonomic to
`L.E' âŠ” span {p.v}`-holonomic near `L.Kâ‚€`.
-/
def improveStep {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) (N : â„) : HtpyJetSec E F
    where
  f t x := ğ“•.f x + (smoothStep t * L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x
  f_diff :=
    ğ“•.f_diff.snd'.add <|
      (smoothStep.smooth.fst'.mul L.Ï_smooth.snd').smul <|
        corrugation.contDiff' _ N (L.loop_smooth h) contDiff_snd contDiff_fst
  Ï† t x :=
    L.p.update (ğ“•.Ï† x) (L.loop h (smoothStep t * L.Ï x) x <| N * L.Ï€ x) +
      (smoothStep t * L.Ï x) â€¢ corrugation.remainder L.p.Ï€ N (L.loop h 1) x
  Ï†_diff := by
    apply ContDiff.add
    apply L.p.smooth_update
    apply ğ“•.Ï†_diff.snd'
    apply L.loop_smooth'
    exact smoothStep.smooth.fst'.mul L.Ï_smooth.snd'
    apply contDiff_const.mul L.Ï€.contDiff.snd'
    exact contDiff_snd
    apply ContDiff.smul
    exact smoothStep.smooth.fst'.mul L.Ï_smooth.snd'
    exact Remainder.smooth _ _ (L.loop_smooth h) contDiff_snd contDiff_const

variable {L}
variable {ğ“• : FormalSol R} (h : L.Accepts R ğ“•) (N : â„)

@[simp]
theorem improveStep_apply (t : â„) (x : E) :
    L.improveStep h N t x =
      (ğ“•.f x + (smoothStep t * L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x,
        L.p.update (ğ“•.Ï† x) (L.loop h (smoothStep t * L.Ï x) x <| N * L.Ï€ x) +
          (smoothStep t * L.Ï x) â€¢ corrugation.remainder L.p.Ï€ N (L.loop h 1) x) :=
  by simp [h]; rfl

@[simp]
theorem improveStep_apply_f (t : â„) (x : E) :
    (L.improveStep h N t).f x = ğ“•.f x + (smoothStep t * L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) x :=
  rfl

@[simp]
theorem improveStep_apply_Ï† (t : â„) (x : E) :
    (L.improveStep h N t).Ï† x =
      L.p.update (ğ“•.Ï† x) (L.loop h (smoothStep t * L.Ï x) x <| N * L.Ï€ x) +
        (smoothStep t * L.Ï x) â€¢ corrugation.remainder L.p.Ï€ N (L.loop h 1) x :=
  rfl

theorem improveStep_of_support (t : â„) {x : E} (H : âˆ€ t, x âˆ‰ Loop.support (L.loop h t)) :
    L.improveStep h N t x = ğ“• x :=
  by
  have : âˆ€ t s, L.loop h t x s = ğ“•.Ï† x L.v := by
    intro t s
    rw [Loop.isConst_of_not_mem_support (H t) s 0]
    apply (L.nice h).s_zero x t
  rw [improveStep_apply L h, corrugation_eq_zero _ _ _ _ (H t),
    remainder_eq_zero _ _ (L.loop_C1 h 1) (H 1)]
  simp only [FormalSol.toJetSec_eq_coe, smul_zero, add_zero, this]
  erw [L.p.update_self]

theorem improveStep_rel_t_eq_0 : L.improveStep h N 0 = ğ“• := by
  ext x : 2
  Â· rw [improveStep_apply_f _ h]
    simp [(L.nice h).t_zero x]
  Â· rw [improveStep_apply_Ï† _ h]
    simp only [FormalSol.toJetSec_eq_coe, MulZeroClass.zero_mul, smoothStep.zero, zero_smul,
      add_zero]
    erw [L.update_zero h]

theorem improveStep_rel_compl_Kâ‚ {x} (hx : x âˆ‰ L.Kâ‚) (t) : L.improveStep h N t x = ğ“• x :=
  by
  rw [improveStep_apply _ h, L.hÏ_compl_Kâ‚ hx]
  simp only [FormalSol.toJetSec_eq_coe, MulZeroClass.mul_zero, zero_smul, add_zero]
  erw [L.update_zero h]

theorem improveStep_rel_K : âˆ€á¶  x near L.K, âˆ€ t, L.improveStep h N t x = ğ“• x :=
  by
  have : âˆ€á¶  x near L.K, âˆ€ t, x âˆ‰ Loop.support (L.loop h t) :=
    by
    apply (L.nice h).rel_K.eventually_nhdsSet.mono
    intro x hx t
    apply Loop.not_mem_support
    apply hx.mono
    intro y hy
    exact Loop.isConst_of_eq (hy t)
  apply this.mono
  intro x hx t
  exact improveStep_of_support _ _ _ _ hx

theorem improveStep_rel_C : âˆ€á¶  x near L.C, âˆ€ t, L.improveStep h N t x = ğ“• x :=
  by
  apply Eventually.filter_mono (L.hKâ‚.isClosed.nhdsSet_le_sup' L.C)
  rw [eventually_sup]
  constructor
  Â· apply improveStep_rel_K
  Â· rw [eventually_principal]
    exact fun x => improveStep_rel_compl_Kâ‚ _ h N

-- In the next lemma we reintroduce F to appaise the unused argument linter since
-- `finite_dimensional â„ F` isn't needed here.
theorem bu_lt {F : Type _} [NormedAddCommGroup F] [NormedSpace â„ F] (t : â„) (x : E) {v : F} {Îµ : â„}
    (hv : â€–vâ€– < Îµ) : â€–(smoothStep t * L.Ï x) â€¢ vâ€– < Îµ :=
  calc
    â€–(smoothStep t * L.Ï x) â€¢ vâ€– = |smoothStep t| * |L.Ï x| * â€–vâ€– := by
      rw [norm_smul, Real.norm_eq_abs, abs_mul]
    _ â‰¤ â€–vâ€– :=
      (mul_le_of_le_one_left (norm_nonneg _)
        (mul_le_one (smoothStep.abs_le t) (abs_nonneg _) (L.Ï_le x)))
    _ < Îµ := hv

theorem improveStep_c0_close {Îµ : â„} (Îµ_pos : 0 < Îµ) :
    âˆ€á¶  N in atTop, âˆ€ x t, â€–(L.improveStep h N t).f x - ğ“•.f xâ€– â‰¤ Îµ :=
  by
  set Î³ := L.loop h
  have Î³_cont : Continuous â†¿fun t x => Î³ t x := (L.nice h).smooth.continuous
  have Î³_C1 : ğ’ 1 â†¿(Î³ 1) := ((L.nice h).smooth.comp (contDiff_prod_mk_right 1)).of_le le_top
  apply
    ((corrugation.c0_small_on _ L.hKâ‚ (L.nice h).t_le_zero (L.nice h).t_ge_one Î³_cont Îµ_pos).and <|
        remainder_c0_small_on L.Ï€ L.hKâ‚ Î³_C1 Îµ_pos).mono
  rintro N âŸ¨H, _âŸ© x t
  by_cases hx : x âˆˆ L.Kâ‚
  Â· rw [improveStep_apply_f _ h]
    suffices â€–(smoothStep t * L.Ï x) â€¢ corrugation L.Ï€ N (L.loop h t) xâ€– â‰¤ Îµ by simpa
    exact (bu_lt _ _ _ <| H _ hx t).le
  Â· rw [show (L.improveStep h N t).f x = ğ“•.f x from
        congr_arg Prod.fst (improveStep_rel_compl_Kâ‚ _ h N hx t)]
    simp [Îµ_pos.le]

theorem improveStep_part_hol {N : â„} (hN : N â‰  0) :
    âˆ€á¶  x near L.Kâ‚€, (L.improveStep h N 1).IsPartHolonomicAt (L.E' âŠ” L.p.spanV) x :=
  by
  have Î³_C1 : ğ’ 1 â†¿(L.loop h 1) := ((L.nice h).smooth.comp (contDiff_prod_mk_right 1)).of_le le_top
  let ğ“•' : JetSec E F :=
    { f := fun x => ğ“•.f x + corrugation L.Ï€ N (L.loop h 1) x
      f_diff := ğ“•.f_diff.add (corrugation.contDiff' _ _ (L.loop_smooth h) contDiff_id contDiff_const)
      Ï† := fun x =>
        L.p.update (ğ“•.Ï† x) (L.loop h 1 x <| N * L.Ï€ x) +
          corrugation.remainder L.p.Ï€ N (L.loop h 1) x
      Ï†_diff := by
        apply ContDiff.add
        apply L.p.smooth_update
        apply ğ“•.Ï†_diff
        apply L.loop_smooth'
        apply contDiff_const
        apply contDiff_const.mul L.Ï€.contDiff
        exact contDiff_id
        exact Remainder.smooth _ _ (L.loop_smooth h) contDiff_id contDiff_const }
  have H : âˆ€á¶  x near L.Kâ‚€, L.improveStep h N 1 x = ğ“•' x :=
    by
    apply L.hÏâ‚€.mono
    intro x hx
    simp [improveStep_apply _ h, hx]
  have fderiv_ğ“•' := fun x =>
    fderiv_corrugated_map N hN Î³_C1 (ğ“•.f_diff.of_le le_top) L.p ((L.nice h).avg x)
  rw [eventually_congr (H.isPartHolonomicAt_congr (L.E' âŠ” L.p.spanV))]
  apply h.hKâ‚€.mono
  intro x hx
  apply JetSec.IsPartHolonomicAt.sup
  Â· intro u hu
    have hu_ker := L.hEp hu
    unfold_let ğ“•'
    erw [fderiv_ğ“•', ContinuousLinearMap.add_apply, L.p.update_ker_pi _ _ hu_ker,
      ContinuousLinearMap.add_apply, L.p.update_ker_pi _ _ hu_ker, hx u hu]
  Â· intro u hu
    rcases Submodule.mem_span_singleton.mp hu with âŸ¨l, rflâŸ©
    rw [(D ğ“•'.f x).map_smul, (ğ“•'.Ï† x).map_smul]
    apply congr_arg
    unfold_let ğ“•'
    erw [fderiv_ğ“•', ContinuousLinearMap.add_apply, L.p.update_v, ContinuousLinearMap.add_apply,
         L.p.update_v]
    rfl

theorem improveStep_formalSol : âˆ€á¶  N in atTop, âˆ€ t, (L.improveStep h N t).IsFormalSol R := by
  set Î³ := L.loop h
  have Î³_cont : Continuous â†¿fun t x => Î³ t x := (L.nice h).smooth.continuous
  have Î³_C1 : ğ’ 1 â†¿(Î³ 1) := ((L.nice h).smooth.comp (contDiff_prod_mk_right 1)).of_le le_top
  set K :=
    (fun p : E Ã— â„ Ã— â„ => (p.1, ğ“•.f p.1, L.p.update (ğ“•.Ï† p.1) (L.loop h p.2.1 p.1 p.2.2))) ''
      L.Kâ‚ Ã—Ë¢ I Ã—Ë¢ I
  have K_cpt : IsCompact K :=
    by
    refine' (L.hKâ‚.prod (isCompact_Icc.prod isCompact_Icc)).image _
    refine' continuous_fst.prod_mk (ğ“•.f_diff.continuous.fst'.prod_mk _)
    apply L.p.continuous_update ğ“•.Ï†_diff.continuous.fst'
    change Continuous (â†¿(L.loop h) âˆ˜ fun g : E Ã— â„ Ã— â„ => (g.snd.fst, g.fst, g.snd.snd))
    exact (L.loop_smooth h).continuous.compâ‚ƒ continuous_snd.fst continuous_fst continuous_snd.snd
  have K_sub : K âŠ† R := by
    rintro _ âŸ¨âŸ¨x, t, sâŸ©, _, rflâŸ©
    exact (L.nice h).mem_Î© x t s
  obtain âŸ¨Îµ, Îµ_pos, hÎµâŸ© : âˆƒ Îµ, 0 < Îµ âˆ§ Metric.thickening Îµ K âŠ† R
  exact K_cpt.exists_thickening_subset_open h.h_op K_sub
  apply
    ((corrugation.c0_small_on _ L.hKâ‚ (L.nice h).t_le_zero (L.nice h).t_ge_one Î³_cont Îµ_pos).and <|
        remainder_c0_small_on L.Ï€ L.hKâ‚ Î³_C1 Îµ_pos).mono
  rintro N âŸ¨H, H'âŸ© t x
  by_cases hxKâ‚ : x âˆˆ L.Kâ‚
  Â· apply hÎµ
    rw [Metric.mem_thickening_iff]
    refine' âŸ¨(x, ğ“•.f x, L.p.update (ğ“•.Ï† x) <| L.loop h (smoothStep t * L.Ï x) x <| N * L.Ï€ x), _, _âŸ©
    Â· unfold_let K
      simp only [hxKâ‚, FormalSol.toJetSec_eq_coe, exists_prop, mem_setOf_eq, eq_self_iff_true,
        true_and_iff]
      exact
        âŸ¨âŸ¨x, smoothStep t * L.Ï x, Int.fract (N * L.Ï€ x)âŸ©,
          âŸ¨hxKâ‚, unitInterval.mul_mem (smoothStep.mem t) (L.Ï_mem x), unitInterval.fract_mem _âŸ©, by
          simp only [Loop.fract_eq]âŸ©
    Â· simp only [h, improveStep_apply_f, FormalSol.toJetSec_eq_coe, improveStep_apply_Ï†]
      rw [Prod.dist_eq, max_lt_iff, Prod.dist_eq, max_lt_iff]
      refine' âŸ¨by simpa using Îµ_pos, _, _âŸ© <;> dsimp only <;> rw [dist_self_add_left]
      Â· exact bu_lt _ _ _ <| H _ hxKâ‚ _
      Â· exact bu_lt _ _ _ <| H' _ hxKâ‚
  Â· rw [show ((L.improveStep h N) t).f x = ğ“•.f x from
        congr_arg Prod.fst <| improveStep_rel_compl_Kâ‚ _ h N hxKâ‚ t,
      show ((L.improveStep h N) t).Ï† x = ğ“•.Ï† x from
        congr_arg Prod.snd <| improveStep_rel_compl_Kâ‚ _ h N hxKâ‚ t]
    exact ğ“•.is_sol _

end StepLandscape

end ImproveStep

section Improve

/-!
## Full improvement

This section proves lem:h_principle_open_ample_loc.
-/


open FiniteDimensional Submodule StepLandscape

variable {E}
variable [FiniteDimensional â„ E] [FiniteDimensional â„ F] {R : RelLoc E F} (h_op : IsOpen R)
  (h_ample : R.IsAmple)

variable (L : Landscape E)

variable {Îµ : â„} (Îµ_pos : 0 < Îµ)

/--
Homotopy of formal solutions obtained by successive corrugations in some landscape `L` to improve a
formal solution `ğ“•` until it becomes holonomic near `L.Kâ‚€`.
-/
theorem RelLoc.FormalSol.improve (ğ“• : FormalSol R) (h_hol : âˆ€á¶  x near L.C, ğ“•.IsHolonomicAt x) :
    âˆƒ H : HtpyJetSec E F,
      (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
        (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
          (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x) âˆ§
            (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
              (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– â‰¤ Îµ) âˆ§
                (âˆ€ t, (H t).IsFormalSol R) âˆ§ âˆ€á¶  x near L.Kâ‚€, (H 1).IsHolonomicAt x := by
  let n := finrank â„ E
  let e := finBasis â„ E
  let E' := e.flag
  suffices
    âˆ€ k : Fin (n + 1),
      âˆ€ Î´ > (0 : â„),
        âˆƒ H : HtpyJetSec E F,
          (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
            (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
              (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x) âˆ§
                (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
                  (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– â‰¤ Î´) âˆ§
                    (âˆ€ t, (H t).IsFormalSol R) âˆ§ âˆ€á¶  x near L.Kâ‚€, (H 1).IsPartHolonomicAt (E' k) x
    by
    simpa only [show E' (Fin.last n) = âŠ¤ from e.flag_last, JetSec.isPartHolonomicAt_top] using
      this (Fin.last n) Îµ Îµ_pos
  intro k
  induction k using Fin.induction with
  | zero =>
    intro Î´ Î´_pos
    use ğ“•.toJetSec.constHtpy
    simp [show E' 0 = âŠ¥ from e.flag_zero, le_of_lt Î´_pos]
  | succ k HH =>
    rintro Î´ Î´_pos
    rcases HH (Î´ / 2) (half_pos Î´_pos) with âŸ¨H, hHâ‚€, _, hHC, hHKâ‚, hHc0, hH_sol, hH_holâŸ©; clear HH
    let S : StepLandscape E :=
      { L with
        E' := E' k
        p := e.dualPair k
        hEp := by unfold_let E'; simpa only [Basis.dualPair] using e.flag_le_ker_dual k }
    set Hâ‚ : FormalSol R := (hH_sol 1).formalSol
    have h_span : S.E' âŠ” S.p.spanV = E' k.succ := e.flag_span_succ k
    have acc : S.Accepts R Hâ‚ :=
      { h_op
        hKâ‚€ := by
          apply hH_hol.mono
          intro x hx
          unfold_let S
          convert hx
          rw [â† Fin.coe_eq_castSucc]
        hShort := fun x => h_ample.isShortAt Hâ‚ S.p x
        hC := by
          apply h_hol.congr (FormalSol.isHolonomicAt_congr _ _ _)
          apply hHC.mono
          intro x h
          exact (h 1).symm }
    have hHâ‚_rel_C : âˆ€á¶  x : E near S.C, Hâ‚ x = ğ“• x :=
      by
      apply hHC.mono
      intro x hx
      apply hx
    have hHâ‚_Kâ‚ : âˆ€ (x) (_ : x âˆ‰ L.Kâ‚), Hâ‚ x = ğ“• x :=
      by
      intro x hx
      apply hHKâ‚ x hx
    obtain âŸ¨N, âŸ¨hN_close, hN_solâŸ©, hNneqâŸ© :=
      (((improveStep_c0_close _ acc <| half_pos Î´_pos).and (improveStep_formalSol _ acc)).and <|
          eventually_ne_atTop (0 : â„)).exists
    have glue : H 1 = S.improveStep acc N 0 :=
      by
      rw [improveStep_rel_t_eq_0]
      rfl
    refine' âŸ¨H.comp (S.improveStep acc N) glue, _, _, _, _, _, _, _âŸ©
    Â· apply (H.comp_le_0 _ _).mono
      intro t ht
      rw [ht]
      exact hHâ‚€.on_set 0 right_mem_Iic
    -- t = 0
    Â· apply (H.comp_ge_1 _ _).mono
      intro t ht
      rw [ht, H.comp_1]
    Â· -- rel C
      apply (hHC.and <| hHâ‚_rel_C.and <| improveStep_rel_C _ acc N).mono
      rintro x âŸ¨hx, hx', hx''âŸ© t
      by_cases ht : t â‰¤ 1 / 2
      Â· simp only [ht, hx, HtpyJetSec.comp_of_le]
      Â· simp only [ht, hx', hx'', HtpyJetSec.comp_of_not_le, not_false_iff]
    Â· -- rel Kâ‚
      intro x hx t
      by_cases ht : t â‰¤ 1 / 2
      Â· simp only [ht, hx, hHKâ‚, HtpyJetSec.comp_of_le, not_false_iff]
      Â·
        simp only [ht, hx, hHâ‚_Kâ‚, improveStep_rel_compl_Kâ‚, HtpyJetSec.comp_of_not_le,
          not_false_iff]
    Â· -- Câ°-close
      intro x t
      by_cases ht : t â‰¤ 1 / 2
      Â· apply le_trans _ (half_le_self <| le_of_lt Î´_pos)
        simp only [ht, hHc0, HtpyJetSec.comp_of_le]
      Â· simp only [ht, HtpyJetSec.comp_of_not_le, not_false_iff]
        rw [â† add_halves Î´]
        exact norm_sub_le_add_of_le (hN_close _ _) (hHc0 _ _)
    Â· -- formal solution
      intro t
      by_cases ht : t â‰¤ 1 / 2
      Â· simp only [ht, hH_sol, HtpyJetSec.comp_of_le]
      Â· simp only [ht, hN_sol, HtpyJetSec.comp_of_not_le, not_false_iff]
    Â· -- part-hol E' (k + 1)
      rw [â† h_span, HtpyJetSec.comp_1]
      apply improveStep_part_hol _ acc hNneq

/-- A repackaging of `rel_loc.FormalSol.improve` for convenience. -/
theorem RelLoc.FormalSol.improve_htpy' (ğ“• : FormalSol R)
    (h_hol : âˆ€á¶  x near L.C, ğ“•.IsHolonomicAt x) :
    âˆƒ H : HtpyFormalSol R,
      (âˆ€á¶  t near Iic 0, H t = ğ“•) âˆ§
        (âˆ€á¶  t near Ici 1, H t = H 1) âˆ§
          (âˆ€á¶  x near L.C, âˆ€ t, H t x = ğ“• x) âˆ§
            (âˆ€ x, x âˆ‰ L.Kâ‚ â†’ âˆ€ t, H t x = ğ“• x) âˆ§
              (âˆ€ x t, â€–(H t).f x - ğ“•.f xâ€– < Îµ) âˆ§ âˆ€á¶  x near L.Kâ‚€, (H 1).IsHolonomicAt x :=
  by
  rcases ğ“•.improve h_op h_ample L (half_pos Îµ_pos) h_hol with âŸ¨H, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, hâ‚…, hâ‚†, hâ‚‡âŸ©
  exact
    âŸ¨{ H with is_sol := hâ‚† }, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„, fun x t => (hâ‚… x t).trans_lt (half_lt_self Îµ_pos), hâ‚‡âŸ©

end Improve
