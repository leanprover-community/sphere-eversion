import SphereEversion.Local.AmpleSet
import SphereEversion.Local.DualPair
import SphereEversion.Local.Relation

/-! # Slices of first order relations

Recal that a first order partial differential relation for maps between real normed vector spaces
`E` and `F` is a set `R` in `OneJet E F := E Ã— F Ã— (E â†’L[â„] F)`. In this file we study slices
of such relations. The word slice is meant to convey the idea of intersecting with an affine
subspace. Here we fix `(x, y, Ï†) : OneJet E F` and some hyperplane `H` in `E`. The points
`x` and `y` are fixed and we will take a slice in `E â†’L[â„] F` by intersecting `R` with the affine
subspace of linear maps that coincide with `Ï†` on `H`.

It will be convenient for convex integration purposes to identify this slice with `F`. There is
no natural identification but we can build one by fixing more data that a hyperplane in `E`.
Namely we fix `p : DualPair E` (where `ker p.Ï€` is the relevant hyperplane) and reformulate
"linear map that coincides with `Ï†` on `H`" as `p.update Ï† w` for some `w : F`.

This `slice` definition allows to define `RelLoc.isAmple`, the ampleness condition for first
order relations: a relation is ample if all its slices are ample sets.

At the end of the file we consider 1-jet sections and slices corresponding to points in their image.
-/


variable {E : Type _} [NormedAddCommGroup E] [NormedSpace â„ E]

variable {F : Type _} [NormedAddCommGroup F] [NormedSpace â„ F]

variable {X : Type _} [NormedAddCommGroup X] [InnerProductSpace â„ X]

variable {R : RelLoc E F}

open Set

/-! ## Slices and ampleness -/


namespace RelLoc

/-- The slice of a local relation `R : RelLoc E F` for a dual pair `p` at a jet `Î¸` is
the set of `w` in `F` such that updating `Î¸` using `p` and `w` leads to a jet in `R`. -/
def slice (R : RelLoc E F) (p : DualPair E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)) : Set F :=
  {w | (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R}

theorem mem_slice (R : RelLoc E F) {p : DualPair E} {Î¸ : E Ã— F Ã— (E â†’L[â„] F)} {w : F} :
    w âˆˆ R.slice p Î¸ â†” (Î¸.1, Î¸.2.1, p.update Î¸.2.2 w) âˆˆ R :=
  Iff.rfl

/-- A relation is ample if all its slices are ample. -/
def IsAmple (R : RelLoc E F) : Prop :=
  âˆ€ (p : DualPair E) (Î¸ : E Ã— F Ã— (E â†’L[â„] F)), AmpleSet (R.slice p Î¸)

theorem IsAmple.mem_hull (h : IsAmple R) {Î¸ : E Ã— F Ã— (E â†’L[â„] F)} (hÎ¸ : Î¸ âˆˆ R) (v : F) (p) :
    v âˆˆ hull (connectedComponentIn (R.slice p Î¸) (Î¸.2.2 p.v)) := by
  rw [h p Î¸ (Î¸.2.2 p.v)]
  exact mem_univ _
  rw [mem_slice, p.update_self]
  exact hÎ¸

theorem slice_update {Î¸ : E Ã— F Ã— (E â†’L[â„] F)} {p : DualPair E} (x : F) :
    R.slice p (Î¸.1, Î¸.2.1, p.update Î¸.2.2 x) = R.slice p Î¸ := by
  ext1 w
  dsimp [slice]
  rw [p.update_update]

/-- In order to check ampleness, it suffices to consider slices through elements of the relation. -/
theorem isAmple_iff :
    R.IsAmple â†” âˆ€ â¦ƒÎ¸ : OneJet E Fâ¦„ (p : DualPair E), Î¸ âˆˆ R â†’ AmpleSet (R.slice p Î¸) := by
  refine' âŸ¨fun h Î¸ p _ => h p Î¸, fun h p Î¸ w hw => _âŸ©
  dsimp [slice] at hw
  have := h p hw
  rw [slice_update] at this
  exact this w hw

open scoped Pointwise

theorem slice_of_ker_eq_ker {Î¸ : OneJet E F} {p p' : DualPair E} (hpp' : p.Ï€ = p'.Ï€) :
    R.slice p Î¸ = Î¸.2.2 (p.v - p'.v) +áµ¥ R.slice p' Î¸ := by
  rcases Î¸ with âŸ¨x, y, Ï†âŸ©
  have key : âˆ€ w, p'.update Ï† w = p.update Ï† (w + Ï† (p.v - p'.v)) := fun w â†¦ by
    simp only [DualPair.update, hpp', map_sub, add_right_inj]
    congr 2
    abel
  ext w
  simp only [slice, mem_setOf_eq, map_sub, vadd_eq_add, mem_vadd_set_iff_neg_vadd_mem, key]
  have : -(Ï† p.v - Ï† p'.v) + w + (Ï† p.v - Ï† p'.v) = w := by abel
  rw [this]

theorem ample_slice_of_ample_slice {Î¸ : OneJet E F} {p p' : DualPair E} (hpp' : p.Ï€ = p'.Ï€)
    (h : AmpleSet (R.slice p Î¸)) : AmpleSet (R.slice p' Î¸) := by
  rw [slice_of_ker_eq_ker hpp'.symm]
  exact AmpleSet.vadd h

theorem ample_slice_of_forall (R : RelLoc E F) {x y Ï†} (p : DualPair E)
    (h : âˆ€ w, (x, y, p.update Ï† w) âˆˆ R) : AmpleSet (R.slice p (x, y, Ï†)) := by
  rw [show R.slice p (x, y, Ï†) = univ from eq_univ_of_forall h]
  exact ampleSet_univ

end RelLoc

open RelLoc

/-! ## Slices for 1-jet sections and formal solutions. -/

namespace JetSec

/-- The slice associated to a jet section and a dual pair at some point. -/
def sliceAt (ğ“• : JetSec E F) (R : RelLoc E F) (p : DualPair E) (x : E) : Set F :=
  R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- A 1-jet section `ğ“•` is short for a dual pair `p` at a point `x` if the derivative of
the function `ğ“•.f` at `x` is in the convex hull of the relevant connected component of the
corresponding slice. -/
def IsShortAt (ğ“• : JetSec E F) (R : RelLoc E F) (p : DualPair E) (x : E) : Prop :=
  D ğ“•.f x p.v âˆˆ hull (connectedComponentIn (ğ“•.sliceAt R p x) <| ğ“•.Ï† x p.v)

end JetSec

namespace RelLoc.FormalSol

/-- The slice associated to a formal solution and a dual pair at some point. -/
def sliceAt (ğ“• : FormalSol R) (p : DualPair E) (x : E) : Set F :=
  R.slice p (x, ğ“•.f x, ğ“•.Ï† x)

/-- A formal solution `ğ“•` is short for a dual pair `p` at a point `x` if the derivative of
the function `ğ“•.f` at `x` is in the convex hull of the relevant connected component of the
corresponding slice. -/
def IsShortAt (ğ“• : FormalSol R) (p : DualPair E) (x : E) : Prop :=
  D ğ“•.f x p.v âˆˆ hull (connectedComponentIn (ğ“•.sliceAt p x) <| ğ“•.Ï† x p.v)

end RelLoc.FormalSol

theorem RelLoc.IsAmple.isShortAt (hR : IsAmple R) (ğ“• : FormalSol R) (p : DualPair E) (x : E) :
    ğ“•.IsShortAt p x :=
  hR.mem_hull (ğ“•.is_sol x) _ p
