import SphereEversion.Local.HPrinciple
import SphereEversion.ToMathlib.Topology.Algebra.Module

/-!
In this file we prove the parametric version of the local h-principle.

We will not use this to prove the global version of the h-principle, but we do use this to conclude
the existence of sphere eversion from the local h-principle, which is proven in `Local.HPrinciple`.

The parametric h-principle states the following: Suppose that `R` is a local relation,
`ğ“•â‚€ : P â†’ JÂ¹(E, F)` is a family of formal solutions of `R` that is holonomic near some set
`C âŠ† P Ã— E`, `K âŠ† P Ã— E` is compact and `Îµ : â„`,
then there exists a homotopy `ğ“• : â„ Ã— P â†’ JÂ¹(E, F)` between `ğ“•` and a solution that is holonomic
near `K`, that agrees with `ğ“•â‚€` near `C` and is everywhere `Îµ`-close to `ğ“•â‚€`
-/


noncomputable section

open Metric FiniteDimensional Set Function RelLoc

open LinearMap (ker)

open scoped Topology Pointwise ContDiff

section ParameterSpace

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F]
  -- `G` will be `â„` in the proof of the parametric h-principle.
  -- It indicates the homotopy variable `t`.
  {G : Type*}
  [NormedAddCommGroup G] [NormedSpace â„ G] {P : Type*} [NormedAddCommGroup P] [NormedSpace â„ P]

variable {R : RelLoc E F}

/-- The projection `JÂ¹(P Ã— E, F) â†’ JÂ¹(E, F)`. -/
def oneJetSnd : OneJet (P Ã— E) F â†’ OneJet E F := fun p â†¦
  (p.1.2, p.2.1, p.2.2 âˆ˜L fderiv â„ (fun y â†¦ (p.1.1, y)) p.1.2)

theorem continuous_oneJetSnd : Continuous (oneJetSnd : OneJet (P Ã— E) F â†’ OneJet E F) :=
  continuous_fst.snd.prodMk <|
    continuous_snd.fst.prodMk <|
      continuous_snd.snd.clm_comp <|
        Continuous.fderiv (contDiff_fst.fst.prodMap contDiff_id) continuous_fst.snd le_top

theorem oneJetSnd_eq (p : OneJet (P Ã— E) F) :
    oneJetSnd p = (p.1.2, p.2.1, p.2.2 âˆ˜L ContinuousLinearMap.inr â„ P E) := by
  simp_rw [oneJetSnd, fderiv_prod_right]

variable (P)

/-- The relation `R.relativize P` (`ğ“¡ ^ P` in the blueprint) is the relation on `JÂ¹(P Ã— E, F)`
induced by `R`. -/
def RelLoc.relativize (R : RelLoc E F) : RelLoc (P Ã— E) F :=
  oneJetSnd â»Â¹' R

variable {P} (R)

theorem RelLoc.mem_relativize (w : OneJet (P Ã— E) F) :
    w âˆˆ R.relativize P â†” (w.1.2, w.2.1, w.2.2 âˆ˜L ContinuousLinearMap.inr â„ P E) âˆˆ R := by
  simp_rw [RelLoc.relativize, mem_preimage, oneJetSnd_eq]

theorem RelLoc.isOpen_relativize (R : RelLoc E F) (h2 : IsOpen R) : IsOpen (R.relativize P) :=
  h2.preimage continuous_oneJetSnd

variable {R}

theorem relativize_slice_loc {Ïƒ : OneJet (P Ã— E) F} {p : DualPair (P Ã— E)} (q : DualPair E)
    (hpq : p.Ï€.comp (ContinuousLinearMap.inr â„ P E) = q.Ï€) :
    (R.relativize P).slice p Ïƒ = Ïƒ.2.2 (p.v - (0, q.v)) +áµ¥ R.slice q (oneJetSnd Ïƒ) := by
  have h2pq : âˆ€ x : E, p.Ï€ ((0 : P), x) = q.Ï€ x := fun x â†¦ congr_arg (fun f : E â†’L[â„] â„ â†¦ f x) hpq
  ext1 w
  have h1 :
    (p.update Ïƒ.2.2 w).comp (ContinuousLinearMap.inr â„ P E) =
      q.update (oneJetSnd Ïƒ).2.2 (-Ïƒ.2.2 (p.v - (0, q.v)) +áµ¥ w) := by
    ext1 x
    simp_rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.inr_apply]
    rw [â† ContinuousLinearMap.map_neg, neg_sub]
    obtain âŸ¨u, hu, t, rflâŸ© := q.decomp x
    have hv : (0, q.v) - p.v âˆˆ ker p.Ï€ := by
      rw [LinearMap.mem_ker, map_sub, p.pairing, h2pq, q.pairing, sub_self]
    have hup : ((0 : P), u) âˆˆ ker p.Ï€ := (h2pq u).trans hu
    rw [q.update_apply _ hu, â† Prod.zero_mk_add_zero_mk, map_add, p.update_ker_pi _ _ hup, â†
      Prod.smul_zero_mk, map_smul, vadd_eq_add]
    conv_lhs => { rw [â† sub_add_cancel (0, q.v) p.v] }
    rw [map_add, p.update_ker_pi _ _ hv, p.update_v, oneJetSnd_eq]
    rfl
  have := preimage_vadd_neg (show F from Ïƒ.2.2 (p.v - (0, q.v))) (R.slice q (oneJetSnd Ïƒ))
  dsimp only at this
  simp_rw [â† this, mem_preimage, mem_slice, R.mem_relativize, h1]
  rfl

theorem relativize_slice_eq_univ_loc {Ïƒ : OneJet (P Ã— E) F} {p : DualPair (P Ã— E)}
    (hp : p.Ï€.comp (ContinuousLinearMap.inr â„ P E) = 0) :
    ((R.relativize P).slice p Ïƒ).Nonempty â†” (R.relativize P).slice p Ïƒ = univ := by
  have h2p : âˆ€ x : E, p.Ï€ ((0 : P), x) = 0 := fun x â†¦ congr_arg (fun f : E â†’L[â„] â„ â†¦ f x) hp
  have : âˆ€ y : F,
      (p.update Ïƒ.2.2 y).comp (ContinuousLinearMap.inr â„ P E) =
        Ïƒ.2.2.comp (ContinuousLinearMap.inr â„ P E) := by
    intro y
    ext1 x
    simp_rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.inr_apply,
      p.update_ker_pi _ _ (h2p x)]
  simp_rw [Set.Nonempty, eq_univ_iff_forall, mem_slice, R.mem_relativize, this, exists_const,
           forall_const]

variable (P)

theorem RelLoc.IsAmple.relativize (hR : R.IsAmple) : (R.relativize P).IsAmple := by
  intro p Ïƒ
  let p2 := p.Ï€.comp (ContinuousLinearMap.inr â„ P E)
  rcases eq_or_ne p2 0 with (h | h)
  Â· intro w hw
    rw [(relativize_slice_eq_univ_loc h).mp âŸ¨w, hwâŸ©, connectedComponentIn_univ,
      PreconnectedSpace.connectedComponent_eq_univ, convexHull_univ]
  obtain âŸ¨u', hu'âŸ© := ContinuousLinearMap.exists_ne_zero h
  let u := (p2 u')â»Â¹ â€¢ u'
  let q : DualPair E := âŸ¨p2, u, by rw [p2.map_smul, smul_eq_mul, inv_mul_cancelâ‚€ hu']âŸ©
  rw [relativize_slice_loc q rfl]
  exact (hR q _).vadd

variable {P}

/-- Turn a family of sections of `JÂ¹(E, E')` parametrized by `P` into a section of `JÂ¹(P Ã— E, E')`.
-/
@[simps]
def FamilyJetSec.uncurry (S : FamilyJetSec E F P) : JetSec (P Ã— E) F where
  f p := S.f p.1 p.2
  Ï† p := fderiv â„ (fun z : P Ã— E â†¦ S.f z.1 p.2) p + S.Ï† p.1 p.2 âˆ˜L fderiv â„ Prod.snd p
  f_diff := S.f_diff
  Ï†_diff := by
    refine (ContDiff.fderiv ?_ contDiff_id (m := âˆ) le_rfl).add (S.Ï†_diff.clm_comp ?_)
    Â· exact S.f_diff.comp (contDiff_snd.fst.prodMk contDiff_fst.snd)
    Â· exact ContDiff.fderiv contDiff_snd.snd contDiff_id le_top

theorem FamilyJetSec.uncurry_Ï†' (S : FamilyJetSec E F P) (p : P Ã— E) :
    S.uncurry.Ï† p =
      fderiv â„ (fun z â†¦ S.f z p.2) p.1 âˆ˜L ContinuousLinearMap.fst â„ P E +
        S.Ï† p.1 p.2 âˆ˜L ContinuousLinearMap.snd â„ P E := by
  simp_rw [S.uncurry_Ï†, fderiv_snd, add_left_inj]
  refine (fderiv_comp p ((S.f_diff.comp (contDiff_id.prodMk contDiff_const)).differentiable
    (mod_cast le_top) p.1) differentiableAt_fst).trans ?_
  rw [fderiv_fst]
  rfl

open ContinuousLinearMap
theorem FamilyJetSec.uncurry_mem_relativize (S : FamilyJetSec E F P) {s : P} {x : E} :
    ((s, x), S.uncurry (s, x)) âˆˆ R.relativize P â†” (x, S s x) âˆˆ R := by
  rw [RelLoc.relativize, mem_preimage, oneJetSnd_eq, JetSec.coe_apply, JetSec.coe_apply,
    S.uncurry_f, S.uncurry_Ï†']
  suffices ((D (fun z â†¦ f S z x) s).comp (fst â„ P E) + (Ï† S s x).comp (snd â„ P E)).comp
      (ContinuousLinearMap.inr â„ P E) = JetSec.Ï† (S s) x by
    rw [this]; rfl
  ext v
  simp_rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.add_apply,
    ContinuousLinearMap.comp_apply, ContinuousLinearMap.inr_apply, ContinuousLinearMap.coe_fst',
    ContinuousLinearMap.coe_snd', ContinuousLinearMap.map_zero, zero_add]
  rfl

theorem FamilyJetSec.isHolonomicAt_uncurry (S : FamilyJetSec E F P) {p : P Ã— E} :
    S.uncurry.IsHolonomicAt p â†” (S p.1).IsHolonomicAt p.2 := by
  simp_rw [JetSec.IsHolonomicAt, S.uncurry_Ï†]
  rw [show S.uncurry.f = fun x â†¦ S.uncurry.f x from rfl, funext S.uncurry_f,
    show (fun x : P Ã— E â†¦ S.f x.1 x.2) = â†¿S.f from rfl]
  rw [fderiv_prod_eq_add (S.f_diff.differentiable (mod_cast le_top) _), fderiv_snd]
  refine (add_right_inj _).trans ?_
  have := fderiv_comp p ((S p.1).f_diff.contDiffAt.differentiableAt (mod_cast le_top))
    differentiableAt_snd
  rw [show D (fun z : P Ã— E â†¦ (â†¿S.f) (p.fst, z.snd)) p = _ from this, fderiv_snd,
    (show Surjective (ContinuousLinearMap.snd â„ P E) from
          Prod.snd_surjective).clm_comp_injective.eq_iff]
  rfl

/-- Turn a family of formal solutions of `R âŠ† JÂ¹(E, E')` parametrized by `P` into a formal solution
of `R.relativize P`. -/
def RelLoc.FamilyFormalSol.uncurry (S : R.FamilyFormalSol P) : FormalSol (R.relativize P) := by
  refine âŸ¨S.toFamilyJetSec.uncurry, ?_âŸ©
  rintro âŸ¨s, xâŸ©
  exact S.toFamilyJetSec.uncurry_mem_relativize.mpr (S.is_sol s x)

theorem RelLoc.FamilyFormalSol.uncurry_Ï†' (S : R.FamilyFormalSol P) (p : P Ã— E) :
    (S.uncurry p).2 =
      fderiv â„ (fun z â†¦ S.f z p.2) p.1 âˆ˜L ContinuousLinearMap.fst â„ P E +
        S.Ï† p.1 p.2 âˆ˜L ContinuousLinearMap.snd â„ P E :=
  S.toFamilyJetSec.uncurry_Ï†' p

/-- Turn a family of sections of `JÂ¹(P Ã— E, F)` parametrized by `G` into a family of sections of
`JÂ¹(E, F)` parametrized by `G Ã— P`. -/
def FamilyJetSec.curry (S : FamilyJetSec (P Ã— E) F G) : FamilyJetSec E F (G Ã— P) where
  f p x := (S p.1).f (p.2, x)
  Ï† p x := (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (fun x â†¦ (p.2, x)) x
  f_diff := S.f_diff.comp ((contDiff_prodAssoc : ContDiff â„ Ï‰ (Equiv.prodAssoc G P E)).of_le le_top)
  Ï†_diff := by
    refine (S.Ï†_diff.comp
      ((contDiff_prodAssoc : ContDiff â„ Ï‰ (Equiv.prodAssoc G P E)).of_le le_top)).clm_comp ?_
    refine ContDiff.fderiv ?_ contDiff_snd le_top
    exact contDiff_fst.fst.snd.prodMk contDiff_snd

theorem FamilyJetSec.curry_f (S : FamilyJetSec (P Ã— E) F G) (p : G Ã— P) (x : E) :
    (S.curry p).f x = (S p.1).f (p.2, x) :=
  rfl

theorem FamilyJetSec.curry_Ï† (S : FamilyJetSec (P Ã— E) F G) (p : G Ã— P) (x : E) :
    (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (fun x â†¦ (p.2, x)) x :=
  rfl

theorem FamilyJetSec.curry_Ï†' (S : FamilyJetSec (P Ã— E) F G) (p : G Ã— P) (x : E) :
    (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L ContinuousLinearMap.inr â„ P E := by
  rw [S.curry_Ï†]
  congr 1
  refine ((differentiableAt_const _).fderiv_prodMk differentiableAt_id).trans ?_
  rw [fderiv_id, fderiv_fun_const]
  rfl

theorem FamilyJetSec.isHolonomicAt_curry (S : FamilyJetSec (P Ã— E) F G) {t : G} {s : P} {x : E}
    (hS : (S t).IsHolonomicAt (s, x)) : (S.curry (t, s)).IsHolonomicAt x := by
  simp_rw [JetSec.IsHolonomicAt, S.curry_Ï†] at hS âŠ¢
  rw [show (S.curry (t, s)).f = fun x â†¦ (S.curry (t, s)).f x from rfl, funext (S.curry_f _)]
  refine (fderiv_comp x ((S t).f_diff.contDiffAt.differentiableAt (mod_cast le_top))
    ((differentiableAt_const _).prodMk differentiableAt_id)).trans ?_
  rw [_root_.id, hS]
  rfl

theorem FamilyJetSec.curry_mem (S : FamilyJetSec (P Ã— E) F G) {p : G Ã— P} {x : E}
    (hR : ((p.2, x), S p.1 (p.2, x)) âˆˆ R.relativize P) : (x, S.curry p x) âˆˆ R := by
  unfold RelLoc.relativize at hR
  rwa [mem_preimage, JetSec.coe_apply, oneJetSnd_eq, â† S.curry_Ï†'] at hR

/-- Turn a family of formal solutions of `R.relativize P` parametrized by `G` into a family of
formal solutions of `R` parametrized by `G Ã— P`. -/
def RelLoc.FamilyFormalSol.curry (S : FamilyFormalSol G (R.relativize P)) :
    FamilyFormalSol (G Ã— P) R :=
  âŸ¨S.toFamilyJetSec.curry, fun _ _ â†¦ S.toFamilyJetSec.curry_mem (S.is_sol _ _)âŸ©

theorem RelLoc.FamilyFormalSol.curry_Ï† (S : FamilyFormalSol G (R.relativize P)) (p : G Ã— P)
    (x : E) : (S.curry p).Ï† x = (S p.1).Ï† (p.2, x) âˆ˜L fderiv â„ (fun x â†¦ (p.2, x)) x :=
  rfl

theorem RelLoc.FamilyFormalSol.curry_Ï†' (S : FamilyFormalSol G (R.relativize P)) (p : G Ã— P)
    (x : E) : (S.curry p x).2 = (S p.1 (p.2, x)).2 âˆ˜L ContinuousLinearMap.inr â„ P E :=
  S.toFamilyJetSec.curry_Ï†' p x

theorem curry_eq_iff_eq_uncurry_loc {ğ“• : FamilyFormalSol G (R.relativize P)}
    {ğ“•â‚€ : R.FamilyFormalSol P} {t : G} {x : E} {s : P} (h : ğ“• t (s, x) = ğ“•â‚€.uncurry (s, x)) :
    (ğ“•.curry (t, s)) x = ğ“•â‚€ s x := by
  simp_rw [Prod.ext_iff] at h âŠ¢
  refine âŸ¨h.1, ?_âŸ©
  simp_rw [ğ“•.curry_Ï†', h.2, ğ“•â‚€.uncurry_Ï†']
  change ((D (fun (z : P) â†¦ ğ“•â‚€.toFamilyJetSec.f z x) s).comp (fst â„ P E)
    + (ğ“•â‚€.toFamilyJetSec.Ï† s x).comp (snd â„ P E)).comp (inr â„ P E) = ((ğ“•â‚€ s) x).snd
  ext v
  simp_rw [ContinuousLinearMap.comp_apply, ContinuousLinearMap.add_apply,
    ContinuousLinearMap.comp_apply, ContinuousLinearMap.inr_apply, ContinuousLinearMap.coe_fst',
    ContinuousLinearMap.coe_snd', ContinuousLinearMap.map_zero, zero_add]
  rfl

end ParameterSpace

section ParametricHPrinciple

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E] {F : Type*}
  [NormedAddCommGroup F] [NormedSpace â„ F] [FiniteDimensional â„ F] {G : Type*}
  [NormedAddCommGroup G] [NormedSpace â„ G] {P : Type*} [NormedAddCommGroup P] [NormedSpace â„ P]
  [FiniteDimensional â„ P]

variable {R : RelLoc E F} (h_op : IsOpen R) (h_ample : R.IsAmple) (L : Landscape E)

include h_op h_ample in
-- The local parametric h-principle.
theorem RelLoc.FamilyFormalSol.improve_htpy {Îµ : â„} (Îµ_pos : 0 < Îµ) (C : Set (P Ã— E))
    (hC : IsClosed C) (K : Set (P Ã— E)) (hK : IsCompact K) (ğ“•â‚€ : FamilyFormalSol P R)
    (h_hol : âˆ€á¶  p : P Ã— E near C, (ğ“•â‚€ p.1).IsHolonomicAt p.2) :
    âˆƒ ğ“• : FamilyFormalSol (â„ Ã— P) R,
      (âˆ€ s x, ğ“• (0, s) x = ğ“•â‚€ s x) âˆ§
        (âˆ€á¶  p : P Ã— E near C, âˆ€ t, ğ“• (t, p.1) p.2 = ğ“•â‚€ p.1 p.2) âˆ§
          (âˆ€ s x t, â€–(ğ“• (t, s)).f x - ğ“•â‚€.f s xâ€– â‰¤ Îµ) âˆ§
            âˆ€á¶  p : P Ã— E near K, (ğ“• (1, p.1)).IsHolonomicAt p.2 := by
  let parametric_landscape : Landscape (P Ã— E) :=
    { C
      Kâ‚€ := K
      Kâ‚ := (exists_compact_superset hK).choose
      hC
      hKâ‚€ := hK
      hKâ‚ := (exists_compact_superset hK).choose_spec.1
      hâ‚€â‚ := (exists_compact_superset hK).choose_spec.2 }
  obtain âŸ¨ğ“•, hâ‚, -, hâ‚‚, -, hâ‚„, hâ‚…âŸ© :=
    ğ“•â‚€.uncurry.improve_htpy' (R.isOpen_relativize h_op) (h_ample.relativize P) parametric_landscape
      Îµ_pos (h_hol.mono fun p hp â†¦ ğ“•â‚€.isHolonomicAt_uncurry.mpr hp)
  have hâ‚ : âˆ€ p, ğ“• 0 p = ğ“•â‚€.uncurry p := by intro p; rw [hâ‚.self_of_nhdsSet 0 right_mem_Iic]; rfl
  refine âŸ¨ğ“•.curry, ?_, ?_, ?_, ?_âŸ©
  Â· intro s x; exact curry_eq_iff_eq_uncurry_loc (hâ‚ (s, x))
  Â· refine hâ‚‚.mono ?_; rintro âŸ¨s, xâŸ© hp t; exact curry_eq_iff_eq_uncurry_loc (hp t)
  Â· intro s x t; exact (hâ‚„ (s, x) t).le
  Â· refine hâ‚….mono ?_; rintro âŸ¨s, xâŸ© hp; exact ğ“•.toFamilyJetSec.isHolonomicAt_curry hp

open Filter

open scoped unitInterval

include h_op h_ample in
/-- A corollary of the local parametric h-principle, forgetting the homotopy and `Îµ`-closeness,
and just stating the existence of a solution that is holonomic near `K`.
Furthermore, we assume that `P = â„` and `K` is of the form `compact set Ã— I`.
This is sufficient to prove sphere eversion. -/
theorem RelLoc.HtpyFormalSol.exists_sol (ğ“•â‚€ : R.HtpyFormalSol) (C : Set (â„ Ã— E)) (hC : IsClosed C)
    (K : Set E) (hK : IsCompact K) (h_hol : âˆ€á¶  p : â„ Ã— E near C, (ğ“•â‚€ p.1).IsHolonomicAt p.2) :
    âˆƒ f : â„ â†’ E â†’ F,
      (ğ’ âˆ <| uncurry f) âˆ§
        (âˆ€ p âˆˆ C, f (p : â„ Ã— E).1 p.2 = (ğ“•â‚€ p.1).f p.2) âˆ§
          âˆ€ x âˆˆ K, âˆ€ t âˆˆ I, (x, f t x, D (f t) x) âˆˆ R := by
  obtain âŸ¨ğ“•, _, hâ‚‚, -, hâ‚„âŸ© :=
    ğ“•â‚€.improve_htpy h_op h_ample zero_lt_one C hC (I Ã—Ë¢ K) (isCompact_Icc.prod hK) h_hol
  refine âŸ¨fun s â†¦ (ğ“• (1, s)).f, ?_, ?_, ?_âŸ©
  Â· exact ğ“•.f_diff.comp ((contDiff_const.prodMk contDiff_id).prodMap contDiff_id)
  Â· intro p hp
    exact (Prod.ext_iff.mp ((hâ‚‚.forall_mem principal_le_nhdsSet) p hp 1)).1
  Â· intro x hx t ht
    rw [show D (ğ“• (1, t)).f x = (ğ“• (1, t)).Ï† x from
        (hâ‚„.forall_mem principal_le_nhdsSet) (t, x) (mk_mem_prod ht hx)]
    exact ğ“•.is_sol (1, t) x

end ParametricHPrinciple
