import SphereEversion.ToMathlib.Analysis.InnerProductSpace.Rotation
import SphereEversion.ToMathlib.Analysis.InnerProductSpace.Dual
import SphereEversion.Local.ParametricHPrinciple

/-!
This is file proves the existence of a sphere eversion from the local verson of the h-principle.

We define the relation of immersions `R = immersion_sphere_rel âŠ† JÂ¹(E, F)` which consist of all
`(x, y, Ï•)` such that if `x` is outside a ball around the origin with chosen radius `R < 1` then
`Ï•` must be injective on `(â„ âˆ™ x)á—®` (the orthogonal complement of the span of `x`).
We show that `R` is open and ample.

Furthermore, we define a formal solution of sphere eversion that is holonomic near `0` and `1`.
We have to be careful since we're not actually working on the sphere,
but in the ambient space `E â‰ƒ â„Â³`.
See `loc_formal_eversion` for the choice and constaints of the solution.

Finally, we obtain the existence of sphere eversion from the parametric local h-principle,
proven in `local.parametric_h_principle`.
-/


noncomputable section

open Metric FiniteDimensional Set Function RelLoc

open Filter hiding mem_map

open InnerProductSpace Submodule

open LinearMap (ker)

open scoped Topology RealInnerProductSpace

section SphereEversion

variable {E : Type _} [NormedAddCommGroup E] [InnerProductSpace â„ E] {F : Type _}
  [NormedAddCommGroup F] [InnerProductSpace â„ F]

local notation "ğ•ŠÂ²" => sphere (0 : E) 1

local notation "dim" => finrank â„

local notation "pr[" x "]á—®" => orthogonalProjection (â„ âˆ™ x)á—®

local notation:1000 R " âˆ™ " x => Submodule.span R {x}

local notation:1000 R " âˆ™ " x => Submodule.span R (@singleton _ _ Set.hasSingleton x)

local notation "B" => ball (0 : E) 0.9

/-- A map between vector spaces is a immersion viewed as a map on the sphere, when its
derivative at `x âˆˆ ğ•ŠÂ²` is injective on the orthogonal complement of `x`
(the tangent space to the sphere). Note that this implies `f` is differentiable at every point
`x âˆˆ ğ•ŠÂ²` since otherwise `D f x = 0`.
-/
def SphereImmersion (f : E â†’ F) : Prop :=
  âˆ€ x âˆˆ ğ•ŠÂ², InjOn (D f x) (â„ âˆ™ x)á—®

variable (E F)

/-- The relation of immersionsof a two-sphere into its ambient Euclidean space. -/
def immersionSphereRel : RelLoc E F :=
  {w : OneJet E F | w.1 âˆ‰ B â†’ InjOn w.2.2 (â„ âˆ™ w.1)á—®}

local notation "R" => immersionSphereRel E F

variable {E F}

@[simp]
theorem mem_loc_immersion_rel {x y Ï†} :
    (âŸ¨x, y, Ï†âŸ© : OneJet E F) âˆˆ immersionSphereRel E F â†” x âˆ‰ B â†’ InjOn Ï† (â„ âˆ™ x)á—® :=
  Iff.rfl

theorem sphereImmersion_of_sol (f : E â†’ F) :
    (âˆ€ x âˆˆ ğ•ŠÂ², (x, f x, fderiv â„ f x) âˆˆ immersionSphereRel E F) â†’ SphereImmersion f :=
  by
  intro h x x_in
  have : x âˆ‰ B := by
    rw [mem_sphere_zero_iff_norm] at x_in 
    norm_num [x_in]
  exact h x x_in this

theorem mem_slice_iff_of_not_mem {x : E} {w : F} {Ï† : E â†’L[â„] F} {p : DualPair E} (hx : x âˆ‰ B)
    (y : F) : w âˆˆ slice R p (x, y, Ï†) â†” InjOn (p.update Ï† w) (â„ âˆ™ x)á—® :=
  by
  change x âˆ‰ B â†’ inj_on (p.update Ï† w) (â„ âˆ™ x)á—® â†” inj_on (p.update Ï† w) (â„ âˆ™ x)á—®
  simp_rw [eq_true hx, true_imp_iff]

section AssumeFiniteDimensional

variable [FiniteDimensional â„ E]

-- The following is extracted from `loc_immersion_rel_open` because it is slow to typecheck
theorem loc_immersion_rel_open_aux {xâ‚€ : E} {yâ‚€ : F} {Ï†â‚€ : E â†’L[â„] F} (hxâ‚€ : xâ‚€ âˆ‰ B)
    (H : InjOn Ï†â‚€ (â„ âˆ™ xâ‚€)á—®) :
    âˆ€á¶  p : OneJet E F in ğ“ (xâ‚€, yâ‚€, Ï†â‚€),
      âŸªxâ‚€, p.1âŸ« â‰  0 âˆ§
        Injective ((p.2.2.comp <| (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp (â„ âˆ™ xâ‚€)á—®.subtypeL) :=
  by
  -- This is true at (xâ‚€, yâ‚€, Ï†â‚€) and is an open condition because `p â†¦ âŸªxâ‚€, p.1âŸ«` and
  -- `p â†¦ (p.2.2.comp $ (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€` are continuous
  set jâ‚€ := subtypeL (â„ âˆ™ xâ‚€)á—®
  let f : OneJet E F â†’ â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) := fun p =>
    (âŸªxâ‚€, p.1âŸ«, (p.2.2.comp <| (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€)
  let P : â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) â†’ Prop := fun q => q.1 â‰  0 âˆ§ injective q.2
  have xâ‚€_ne : xâ‚€ â‰  0 := by
    refine' fun hxâ‚€' => hxâ‚€ _
    rw [hxâ‚€']
    apply mem_ball_self
    norm_num
  -- The following suffices looks stupid but is much faster than using the change tactic.
  suffices âˆ€á¶  p : OneJet E F in ğ“ (xâ‚€, yâ‚€, Ï†â‚€), P (f p) by exact this
  apply ContinuousAt.eventually
  Â· refine' (continuous_at_const.inner continuousAt_fst).prod _
    apply ContinuousAt.compL
    Â· apply ContinuousAt.compL
      exact continuous_at_snd.comp continuousAt_snd
      -- Faster than change.
      suffices ContinuousAt ((fun x => (â„ âˆ™ x)á—®.subtypeL.comp pr[x]á—®) âˆ˜ Prod.fst) (xâ‚€, yâ‚€, Ï†â‚€) by
        exact this
      apply ContinuousAt.comp _ continuousAt_fst
      exact continuousAt_orthogonalProjection_orthogonal xâ‚€_ne
    exact continuousAt_const
  Â·
    exact
      (continuous_fst.is_open_preimage _ isOpen_compl_singleton).inter
        (continuous_snd.is_open_preimage _ ContinuousLinearMap.isOpen_injective)
  Â· constructor
    Â· change âŸªxâ‚€, xâ‚€âŸ« â‰  0
      apply inner_self_eq_zero.not.mpr xâ‚€_ne
    Â· change injective (Ï†â‚€ âˆ˜ coe âˆ˜ pr[xâ‚€]á—® âˆ˜ coe)
      rw [orthogonalProjection_comp_coe, comp.right_id]
      exact inj_on_iff_injective.mp H

theorem loc_immersion_rel_open : IsOpen (immersionSphereRel E F) :=
  by
  dsimp only [immersionSphereRel]
  rw [isOpen_iff_mem_nhds]
  rintro âŸ¨xâ‚€, yâ‚€, Ï†â‚€âŸ© (H : xâ‚€ âˆ‰ B â†’ inj_on Ï†â‚€ (â„ âˆ™ xâ‚€)á—®)
  change âˆ€á¶  p : OneJet E F in ğ“ (xâ‚€, yâ‚€, Ï†â‚€), _
  by_cases hxâ‚€ : xâ‚€ âˆˆ B
  Â· have : âˆ€á¶  p : OneJet E F in ğ“ (xâ‚€, yâ‚€, Ï†â‚€), p.1 âˆˆ B :=
      by
      rw [nhds_prod_eq]
      apply (is_open_ball.eventually_mem hxâ‚€).prod_inl
    apply this.mono
    rintro âŸ¨x, y, Ï†âŸ© (hx : x âˆˆ B) (Hx : x âˆ‰ B)
    exact (Hx hx).elim
  Â· replace H := H hxâ‚€
    set jâ‚€ := subtypeL (â„ âˆ™ xâ‚€)á—®
    let f : OneJet E F â†’ â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) := fun p =>
      (âŸªxâ‚€, p.1âŸ«, (p.2.2.comp <| (subtypeL (â„ âˆ™ p.1)á—®).comp pr[p.1]á—®).comp jâ‚€)
    let P : â„ Ã— ((â„ âˆ™ xâ‚€)á—® â†’L[â„] F) â†’ Prop := fun q => q.1 â‰  0 âˆ§ injective q.2
    have : âˆ€á¶  p : OneJet E F in ğ“ (xâ‚€, yâ‚€, Ï†â‚€), P (f p) := loc_immersion_rel_open_aux hxâ‚€ H
    apply this.mono; clear this
    rintro âŸ¨x, y, Ï†âŸ© âŸ¨hxxâ‚€ : âŸªxâ‚€, xâŸ« â‰  0, HÏ†âŸ© (hx : x âˆ‰ B)
    dsimp only [P, f] at HÏ† 
    change inj_on Ï† (â„ âˆ™ x)á—®
    have : range (subtypeL (â„ âˆ™ x)á—® âˆ˜ pr[x]á—® âˆ˜ jâ‚€) = (â„ âˆ™ x)á—® :=
      by
      rw [Function.Surjective.range_comp]
      exact Subtype.range_coe
      exact (orthogonalProjectionOrthogonalLineIso hxxâ‚€).Surjective
    rw [â† this]; clear this
    exact Function.Injective.injOn_range HÏ†

variable [FiniteDimensional â„ F]

-- In the next lemma the assumption `dim E = n + 1` is for convenience
-- using `finrank_orthogonal_span_singleton`. We could remove it to treat empty spheres...
theorem loc_immersion_rel_ample (n : â„•) [Fact (dim E = n + 1)] (h : finrank â„ E â‰¤ finrank â„ F) :
    (immersionSphereRel E F).IsAmple := by
  classical
  -- gives a minor speedup
  rw [is_ample_iff]
  rintro âŸ¨x, y, Ï†âŸ© p h_mem
  by_cases hx : x âˆˆ B
  Â· apply ample_slice_of_forall
    intro w
    simp only [hx, mem_loc_immersion_rel, not_true, IsEmpty.forall_iff]
  have x_ne : x â‰  0 := by
    rintro rfl
    apply hx
    apply mem_ball_self
    norm_num1
  have hÏ† : inj_on Ï† (â„ âˆ™ x)á—® := h_mem hx
  clear h_mem
  let u : E := (InnerProductSpace.toDual â„ E).symm p.Ï€
  have u_ne : u â‰  0 := (InnerProductSpace.toDual â„ E).symm.apply_ne_zero p.pi_ne_zero
  by_cases H : ker p.Ï€ = (â„ âˆ™ x)á—®
  Â· have key : âˆ€ w, eq_on (p.update Ï† w) Ï† (â„ âˆ™ x)á—® :=
      by
      intro w x
      rw [â† H]
      exact p.update_ker_pi Ï† w
    exact ample_slice_of_forall _ p fun w _ => hÏ†.congr (key w).symm
  obtain âŸ¨v', v'_in, hv', hÏ€v'âŸ© :
    âˆƒ v' : E, v' âˆˆ (â„ âˆ™ x)á—® âˆ§ ((â„ âˆ™ x)á—® = ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® âŠ” â„ âˆ™ v') âˆ§ p.Ï€ v' = 1 :=
    by
    have ne_z : p.Ï€ (pr[x]á—® u) â‰  0 := by
      rw [â† to_dual_symm_apply]
      change Â¬âŸªu, pr[x]á—® uâŸ« = 0
      rw [inner_projection_self_eq_zero_iff.not]
      contrapose! H
      rw [orthogonal_orthogonal] at H 
      rw [â† orthogonal_span_toDual_symm, span_singleton_eq_span_singleton_of_ne u_ne H]
      infer_instance
    have ne_z' : (p.Ï€ <| pr[x]á—® u)â»Â¹ â‰  0 := inv_ne_zero ne_z
    refine' âŸ¨(p.Ï€ <| pr[x]á—® u)â»Â¹ â€¢ pr[x]á—® u, (â„ âˆ™ x)á—®.smul_mem _ (pr[x]á—® u).2, _, _âŸ©
    Â· have := orthogonal_line_inf_sup_line u x
      rw [â† orthogonal_span_toDual_symm p.Ï€, span_singleton_smul_eq ne_z'.is_unit]
      exact (orthogonal_line_inf_sup_line u x).symm
    rw [p.Ï€.map_smul, smul_eq_mul, inv_mul_cancel ne_z]
  let p' : DualPair E :=
    { Ï€ := p.Ï€
      V := v'
      pairing := hÏ€v' }
  apply ample_slice_of_ample_slice (show p'.Ï€ = p.Ï€ from rfl)
  suffices slice R p' (x, y, Ï†) = map Ï† (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®)á¶œ
    by
    rw [this]
    apply ample_of_two_le_codim
    let Î¦ := Ï†.to_linear_map
    suffices 2 â‰¤ dim (F â§¸ map Î¦ (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®))
      by
      rw [â† finrank_eq_rank]
      exact_mod_cast this
    apply le_of_add_le_add_right
    rw [Submodule.finrank_quotient_add_finrank (map Î¦ <| ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®)]
    have : dim (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® : Submodule â„ E) + 1 = n :=
      by
      have eq := Submodule.finrank_sup_add_finrank_inf_eq (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®) (span â„ {v'})
      have eqâ‚ : dim (â„ âˆ™ x)á—® = n := finrank_orthogonal_span_singleton x_ne
      have eqâ‚‚ : ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® âŠ“ span â„ {v'} = (âŠ¥ : Submodule â„ E) := by
        erw [inf_left_right_swap, inf_comm, â† inf_assoc, p'.inf_eq_bot, bot_inf_eq]
      have eqâ‚ƒ : dim (span â„ {v'}) = 1; apply finrank_span_singleton p'.v_ne_zero
      rw [â† hv', eqâ‚, eqâ‚ƒ, eqâ‚‚] at eq 
      simpa only [finrank_bot] using Eq.symm
    have : dim E = n + 1 := Fact.out _
    linarith [finrank_map_le Î¦ (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®)]
  ext w
  rw [mem_slice_iff_of_not_mem hx y]
  rw [inj_on_iff_injective]
  let j := (â„ âˆ™ x)á—®.subtypeL
  let p'' : DualPair (â„ âˆ™ x)á—® := âŸ¨p.Ï€.comp j, âŸ¨v', v'_inâŸ©, hÏ€v'âŸ©
  have eq : ((â„ âˆ™ x)á—® : Set E).restrict (p'.update Ï† w) = p''.update (Ï†.comp j) w :=
    by
    ext z
    simp only [DualPair.update, restrict_apply, ContinuousLinearMap.add_apply,
      ContinuousLinearMap.coe_comp', coe_subtypeL', Submodule.coeSubtype, comp_app, coe_mk]
  have eq' : map (Ï†.comp j) (ker p''.Ï€) = map Ï† (ker p.Ï€ âŠ“ (â„ âˆ™ x)á—®) :=
    by
    have : map (â†‘j) (ker p''.Ï€) = ker p.Ï€ âŠ“ (â„ âˆ™ x)á—® :=
      by
      ext z
      simp only [mem_map, LinearMap.mem_ker, ContinuousLinearMap.coe_comp', coe_subtypeL',
        Submodule.coeSubtype, comp_app, mem_inf]
      constructor
      Â· rintro âŸ¨t, ht, rflâŸ©
        rw [ContinuousLinearMap.coe_coe, subtypeL_apply]
        exact âŸ¨ht, t.2âŸ©
      Â· rintro âŸ¨hz, z_inâŸ©
        exact âŸ¨âŸ¨z, z_inâŸ©, hz, rflâŸ©
    erw [â† this, map_comp]
    rfl
  rw [Eq, p''.injective_update_iff, mem_compl_iff, eq']
  exact Iff.rfl
  rw [â† show ((â„ âˆ™ x)á—® : Set E).restrict Ï† = Ï†.comp j by ext; rfl]
  exact hÏ†.injective

end AssumeFiniteDimensional

/-- The main ingredient of the linear map in the formal eversion of the sphere. -/
def locFormalEversionAuxÏ† [Fact (dim E = 3)] (Ï‰ : Orientation â„ E (Fin 3)) (t : â„) (x : E) :
    E â†’L[â„] E :=
  Ï‰.rot (t, x) - (2 * t) â€¢ Submodule.subtypeL (â„ âˆ™ x) âˆ˜L orthogonalProjection (â„ âˆ™ x)

section AssumeFiniteDimensional

variable [Fact (dim E = 3)] [FiniteDimensional â„ E] (Ï‰ : Orientation â„ E (Fin 3))

theorem smooth_at_locFormalEversionAuxÏ† {p : â„ Ã— E} (hx : p.2 â‰  0) :
    ContDiffAt â„ âˆ (uncurry (locFormalEversionAuxÏ† Ï‰)) p :=
  by
  refine' (Ï‰.cont_diff_rot hx).sub _
  refine' ContDiffAt.smul (cont_diff_at_const.mul contDiffAt_fst) _
  exact (contDiffAt_orthogonalProjection_singleton hx).comp p contDiffAt_snd

/-- A formal eversion of `ğ•ŠÂ²`, viewed as a homotopy. -/
def locFormalEversionAux : HtpyJetSec E E
    where
  f (t : â„) (x : E) := (1 - 2 * smoothStep t) â€¢ x
  Ï† t x := smoothStep (â€–xâ€– ^ 2) â€¢ locFormalEversionAuxÏ† Ï‰ (smoothStep t) x
  f_diff :=
    ContDiff.smul (contDiff_const.sub <| contDiff_const.mul <| smoothStep.smooth.comp contDiff_fst)
      contDiff_snd
  Ï†_diff := by
    refine' cont_diff_iff_cont_diff_at.mpr fun x => _
    cases' eq_or_ne x.2 0 with hx hx
    Â· refine' cont_diff_at_const.congr_of_eventually_eq _; exact 0
      have : (fun x => â€–xâ€– ^ 2) â»Â¹' Iio (1 / 4) âˆˆ ğ“ (0 : E) :=
        by
        refine' IsOpen.mem_nhds _ _
        exact is_open_Iio.preimage (contDiff_norm_sq â„ : ğ’ âˆ _).continuous
        simp_rw [mem_preimage, norm_zero, zero_pow two_pos, mem_Iio]
        norm_num
      have : (fun x => smoothStep (â€–xâ€– ^ 2)) â»Â¹' {0} âˆˆ ğ“ (0 : E) :=
        by
        refine' mem_of_superset this _
        rw [@preimage_comp _ _ _ _ smoothStep]
        refine' preimage_mono _
        intro x hx
        rw [mem_preimage, mem_singleton_iff, smoothStep.of_lt hx]
      have : (fun p : â„ Ã— E => smoothStep (â€–p.2â€– ^ 2)) â»Â¹' {0} âˆˆ ğ“ x := by rw [â† hx] at this ;
        exact continuous_at_snd.preimage_mem_nhds this
      refine' eventually_of_mem this _
      rintro âŸ¨t, xâŸ© hx
      simp_rw [mem_preimage, mem_singleton_iff] at hx 
      show smoothStep (â€–xâ€– ^ 2) â€¢ locFormalEversionAuxÏ† Ï‰ (smoothStep t) x = 0
      simp_rw [hx, zero_smul]
    refine' ContDiffAt.smul _ _
    refine' (smooth_step.smooth.comp <| (contDiff_norm_sq â„).comp contDiff_snd).contDiffAt
    exact
      (smooth_at_locFormalEversionAuxÏ† Ï‰ (show (Prod.map smoothStep id x).2 â‰  0 from hx)).comp x
        (smooth_step.smooth.prod_map contDiff_id).contDiffAt

/-- A formal eversion of `ğ•ŠÂ²` into its ambient Euclidean space.
The corresponding map `E â†’ E` is roughly a linear homotopy from `id` at `t = 0` to `- id` at
`t = 1`. The continuous linear maps are roughly rotations with angle `t * Ï€`. However, we have to
keep track of a few complications:
* We need the formal solution to be holonomic near `0` and `1`.
  Therefore, we compose the above maps with a smooth step function that is constant `0` near `t = 0`
  and constant `1` near `t = 1`.
* We need to modify the derivative of `Ï‰.rot` to also have the right behavior on `(â„ âˆ™ x)`
  at `t = 1` (it is the identity, but it should be `-id`). Therefore, we subtract
  `(2 * t) â€¢ (submodule.subtypeL (â„ âˆ™ x) âˆ˜L orthogonal_projection (â„ âˆ™ x))`,
  which is `2t` times the identity on `(â„ âˆ™ x)`.
* We have to make sure the family of continuous linear map is smooth at `x = 0`. Therefore, we
  multiply the family with a factor of `smooth_step (â€–xâ€– ^ 2)`.
-/
def locFormalEversion : HtpyFormalSol (immersionSphereRel E E) :=
  { locFormalEversionAux Ï‰ with
    is_sol := by
      intro t x
      change
        x âˆ‰ B â†’ inj_on (smoothStep (â€–xâ€– ^ 2) â€¢ locFormalEversionAuxÏ† Ï‰ (smoothStep t) x) (â„ âˆ™ x)á—®
      intro hx
      have h2x : smoothStep (â€–xâ€– ^ 2) = 1 :=
        by
        refine' smoothStep.of_gt _
        rw [mem_ball, not_lt, dist_zero_right] at hx 
        refine' show (3 : â„) / 4 < 0.9 ^ 2 by norm_num.trans_le _
        rwa [sq_le_sq, show |(0.9 : â„)| = 0.9 by norm_num, abs_norm]
      rw [h2x, one_smul]
      have h3x : x â‰  0 := by rintro rfl; apply hx; exact mem_ball_self (by norm_num)
      refine' (eq_on.inj_on_iff _).mpr (Ï‰.inj_on_rot_of_ne (smoothStep t) h3x)
      intro v hv
      simp_rw [locFormalEversionAuxÏ†, ContinuousLinearMap.sub_apply, ContinuousLinearMap.smul_apply,
        ContinuousLinearMap.comp_apply,
        orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv, _root_.map_zero,
        smul_zero, sub_zero] }

@[simp]
theorem locFormalEversion_f (t : â„) :
    (locFormalEversion Ï‰ t).f = fun x : E => ((1 : â„) - 2 * smoothStep t) â€¢ x :=
  rfl

theorem locFormalEversion_Ï† (t : â„) (x : E) (v : E) :
    (locFormalEversion Ï‰ t).Ï† x v =
      smoothStep (â€–xâ€– ^ 2) â€¢
        (Ï‰.rot (smoothStep t, x) v - (2 * smoothStep t) â€¢ orthogonalProjection (â„ âˆ™ x) v) :=
  rfl

theorem locFormalEversion_zero (x : E) : (locFormalEversion Ï‰ 0).f x = x := by simp

theorem locFormalEversion_one (x : E) : (locFormalEversion Ï‰ 1).f x = -x := by
  simp [show (1 : â„) - 2 = -1 by norm_num]

theorem locFormalEversionHolAtZero {t : â„} (ht : t < 1 / 4) {x : E}
    (hx : smoothStep (â€–xâ€– ^ 2) = 1) : (locFormalEversion Ï‰ t).IsHolonomicAt x := by
  simp_rw [JetSec.IsHolonomicAt, locFormalEversion_f, ContinuousLinearMap.ext_iff,
    locFormalEversion_Ï†, smoothStep.of_lt ht, hx, Ï‰.rot_zero, MulZeroClass.mul_zero, zero_smul,
    sub_zero, show (SMul.smul (1 : â„) : E â†’ E) = id from funext (one_smul â„), fderiv_id,
    Function.id_def, eq_self_iff_true, imp_true_iff]

theorem locFormalEversionHolAtOne {t : â„} (ht : 3 / 4 < t) {x : E} (hx : smoothStep (â€–xâ€– ^ 2) = 1) :
    (locFormalEversion Ï‰ t).IsHolonomicAt x :=
  by
  simp_rw [JetSec.IsHolonomicAt, locFormalEversion_f, ContinuousLinearMap.ext_iff,
    locFormalEversion_Ï†, smoothStep.of_gt ht, hx]
  intro v
  simp_rw [mul_one, show (1 : â„) - 2 = -1 by norm_num,
    show (SMul.smul (-1 : â„) : E â†’ E) = fun x => -x from funext fun v => by rw [neg_smul, one_smul],
    fderiv_neg, fderiv_id', ContinuousLinearMap.neg_apply, ContinuousLinearMap.id_apply]
  obtain âŸ¨v', hv', v, hv, rflâŸ© := Submodule.exists_add_mem_mem_orthogonal (â„ âˆ™ x) v
  simp_rw [ContinuousLinearMap.map_add, Ï‰.rot_one _ hv, Ï‰.rot_eq_of_mem_span (1, x) hv']
  simp_rw [neg_add, Submodule.coe_add, orthogonal_projection_eq_self_iff.mpr hv',
    orthogonalProjection_mem_subspace_orthogonalComplement_eq_zero hv, Submodule.coe_zero, add_zero,
    two_smul, one_smul]
  abel

/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
theorem locFormalEversion_hol :
    âˆ€á¶  p : â„ Ã— E near {0, 1} Ã—Ë¢ ğ•ŠÂ², (locFormalEversion Ï‰ p.1).IsHolonomicAt p.2 :=
  by
  have :
    (Iio (1 / 4 : â„) âˆª Ioi (3 / 4)) Ã—Ë¢ ((fun x => â€–xâ€– ^ 2) â»Â¹' Ioi (3 / 4)) âˆˆ
      ğ“Ë¢ (({0, 1} : Set â„) Ã—Ë¢ ğ•ŠÂ²) :=
    by
    refine' (IsOpen.mem_nhdsSet _).mpr _
    exact
      (is_open_Iio.union isOpen_Ioi).prod
        (is_open_Ioi.preimage (contDiff_norm_sq â„ : ğ’ âˆ _).continuous)
    rintro âŸ¨s, xâŸ© âŸ¨hs, hxâŸ©
    refine' âŸ¨_, _âŸ©
    simp_rw [mem_insert_iff, mem_singleton_iff] at hs 
    rcases hs with (rfl | rfl)
    exact Or.inl (show (0 : â„) < 1 / 4 by norm_num)
    exact Or.inr (show (3 / 4 : â„) < 1 by norm_num)
    simp_rw [mem_sphere_zero_iff_norm] at hx 
    simp_rw [mem_preimage, hx, one_pow, mem_Ioi]
    norm_num
  have :
    (Iio (1 / 4 : â„) âˆª Ioi (3 / 4)) Ã—Ë¢ ((fun x => smoothStep (â€–xâ€– ^ 2)) â»Â¹' {1}) âˆˆ
      ğ“Ë¢ (({0, 1} : Set â„) Ã—Ë¢ ğ•ŠÂ²) :=
    by
    refine' mem_of_superset this (prod_mono subset.rfl _)
    rw [@preimage_comp _ _ _ _ smoothStep]
    refine' preimage_mono _
    intro x hx
    rw [mem_preimage, mem_singleton_iff, smoothStep.of_gt hx]
  refine' eventually_of_mem this _
  rintro âŸ¨t, xâŸ© âŸ¨ht | ht, hxâŸ©
  Â· exact locFormalEversionHolAtZero Ï‰ ht hx
  Â· exact locFormalEversionHolAtOne Ï‰ ht hx

end AssumeFiniteDimensional

open scoped unitInterval

/- ./././Mathport/Syntax/Translate/Expr.lean:177:8: unsupported: ambiguous notation -/
theorem sphere_eversion_of_loc [Fact (dim E = 3)] :
    âˆƒ f : â„ â†’ E â†’ E,
      ğ’ âˆ â†¿f âˆ§ (âˆ€ x âˆˆ ğ•ŠÂ², f 0 x = x) âˆ§ (âˆ€ x âˆˆ ğ•ŠÂ², f 1 x = -x) âˆ§ âˆ€ t âˆˆ I, SphereImmersion (f t) :=
  by
  classical
  borelize E
  have rankE := Fact.out (dim E = 3)
  haveI : FiniteDimensional â„ E := finite_dimensional_of_finrank_eq_succ rankE
  let Ï‰ : Orientation â„ E (Fin 3) :=
    ((stdOrthonormalBasis _ _).reindex <| finCongr (Fact.out _ : dim E = 3)).toBasis.Orientation
  have is_closed_pair : IsClosed ({0, 1} : Set â„) := (by simp : ({0, 1} : Set â„).Finite).isClosed
  obtain âŸ¨f, hâ‚, hâ‚‚, hâ‚ƒâŸ© :=
    (locFormalEversion Ï‰).exists_sol loc_immersion_rel_open (loc_immersion_rel_ample 2 le_rfl)
      ({0, 1} Ã—Ë¢ ğ•ŠÂ²) (is_closed_pair.prod is_closed_sphere) ğ•ŠÂ² (isCompact_sphere 0 1)
      (locFormalEversion_hol Ï‰)
  refine' âŸ¨f, hâ‚, _, _, _âŸ©
  Â· intro x hx; rw [hâ‚‚ (0, x) (mk_mem_prod (by simp) hx), locFormalEversion_zero]
  Â· intro x hx; rw [hâ‚‚ (1, x) (mk_mem_prod (by simp) hx), locFormalEversion_one]
  Â· exact fun t ht => sphereImmersion_of_sol _ fun x hx => hâ‚ƒ x hx t ht

-- Stating the full statement with all type-class arguments and no uncommon notation.
example (E : Type _) [NormedAddCommGroup E] [InnerProductSpace â„ E] [Fact (finrank â„ E = 3)] :
    âˆƒ f : â„ â†’ E â†’ E,
      ContDiff â„ âŠ¤ (uncurry f) âˆ§
        (âˆ€ x âˆˆ sphere (0 : E) 1, f 0 x = x) âˆ§
          (âˆ€ x âˆˆ sphere (0 : E) 1, f 1 x = -x) âˆ§ âˆ€ t âˆˆ unitInterval, SphereImmersion (f t) :=
  sphere_eversion_of_loc

end SphereEversion

