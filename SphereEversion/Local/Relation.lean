import Mathlib.Analysis.Calculus.BumpFunction.InnerProduct
import Mathlib.Topology.MetricSpace.HausdorffDistance
import SphereEversion.Local.OneJet

/-!
# Local partial differential relations and their formal solutions

This file defines `rel_loc E F`, the type of first order partial differential relations
for maps between two real normed spaces `E` and `F`.

To any `R : rel_loc E F` we associate the type `sol R` of maps `f : E â†’ F` of
solutions of `R`, and its formal counterpart `formal_sol R`.

The h-principle question is whether we can deform any formal solution into a solution.
The type of deformations is `htpy_jet_sec E F` (homotopies of 1-jet sections).
-/


noncomputable section

open Set Function Real Filter

open scoped unitInterval Topology

variable (E : Type _) [NormedAddCommGroup E] [NormedSpace â„ E]

variable (F : Type _) [NormedAddCommGroup F] [NormedSpace â„ F]

variable (P : Type _) [NormedAddCommGroup P] [NormedSpace â„ P]

/-- A first order relation for maps between real vector spaces. -/
def RelLoc :=
  Set (OneJet E F)

instance : Membership (E Ã— F Ã— (E â†’L[â„] F)) (RelLoc E F) := by delta RelLoc; infer_instance

variable {E F}

/-- A predicate stating that a 1-jet section is a formal solution to a first order relation for
maps between vector spaces. -/
def JetSec.IsFormalSol (ð“• : JetSec E F) (R : RelLoc E F) : Prop :=
  âˆ€ x, (x, ð“•.f x, ð“•.Ï† x) âˆˆ R

namespace RelLoc

/-- A formal solution to a local relation `R`. -/
@[ext]
structure FormalSol (R : RelLoc E F) extends JetSec E F where
  is_sol : âˆ€ x, (x, f x, Ï† x) âˆˆ R

instance (R : RelLoc E F) : CoeOut (FormalSol R) (JetSec E F) :=
  âŸ¨FormalSol.toJetSecâŸ©

@[simp]
theorem FormalSol.toJetSec_eq_coe {R : RelLoc E F} (ð“• : FormalSol R) :
    ð“•.toJetSec = (ð“• : JetSec E F) :=
  rfl

@[simp]
theorem FormalSol.coeIsFormalSol {R : RelLoc E F} (ð“• : FormalSol R) :
    (ð“• : JetSec E F).IsFormalSol R :=
  ð“•.is_sol

/-- Bundling a formal solution from a 1-jet section that is a formal solution. -/
def _root_.JetSec.IsFormalSol.formalSol {ð“• : JetSec E F} {R : RelLoc E F} (h : ð“•.IsFormalSol R) :
    FormalSol R :=
  { ð“• with is_sol := h }

instance (R : RelLoc E F) : CoeFun (FormalSol R) fun _ => E â†’ F Ã— (E â†’L[â„] F) :=
  âŸ¨fun ð“• => fun x => (ð“•.f x, ð“•.Ï† x)âŸ©

@[simp]
theorem FormalSol.coe_apply {R : RelLoc E F} (ð“• : FormalSol R) (x : E) : (ð“• : JetSec E F) x = ð“• x :=
  rfl

variable {R : RelLoc E F}

theorem FormalSol.eq_iff {ð“• ð“•' : FormalSol R} {x : E} :
    ð“• x = ð“•' x â†” ð“•.f x = ð“•'.f x âˆ§ ð“•.Ï† x = ð“•'.Ï† x :=
  JetSec.eq_iff

/-- A formal solution (f, Ï†) is holonomic at `x` if the differential of `f` at `x` is `Ï† x`. -/
def FormalSol.IsHolonomicAt (ð“• : FormalSol R) (x : E) : Prop :=
  D ð“•.f x = ð“•.Ï† x

-- TODO: this should come from a lemma about `jet_sec`
theorem FormalSol.isHolonomicAt_congr (ð“• ð“•' : FormalSol R) {s : Set E}
    (h : âˆ€á¶  x near s, ð“• x = ð“•' x) : âˆ€á¶  x near s, ð“•.IsHolonomicAt x â†” ð“•'.IsHolonomicAt x :=
  by
  apply h.eventually_nhdsSet.mono
  intro x hx
  have hf : ð“•.f =á¶ [ð“ x] ð“•'.f := by
    apply hx.mono
    simp_rw [RelLoc.FormalSol.eq_iff]
    tauto
  unfold RelLoc.FormalSol.IsHolonomicAt
  rw [hf.fderiv_eq, (RelLoc.FormalSol.eq_iff.mp hx.self_of_nhds).2]

/-- A family of formal solutions is a 1-parameter family of formal solutions. -/
@[ext]
structure FamilyFormalSol (R : RelLoc E F) extends FamilyJetSec E F P where
  is_sol : âˆ€ t x, (x, f t x, Ï† t x) âˆˆ R

/-- A homotopy of formal solutions is a 1-parameter family of formal solutions. -/
@[reducible]
def HtpyFormalSol (R : RelLoc E F) :=
  R.FamilyFormalSol â„

def HtpyFormalSol.toHtpyJetSec {R : RelLoc E F} (ð“• : R.HtpyFormalSol) : HtpyJetSec E F :=
  ð“•.toFamilyJetSec

open RelLoc

instance (R : RelLoc E F) : CoeFun (FamilyFormalSol P R) fun _ => P â†’ JetSec E F :=
  âŸ¨fun S t =>
    { f := S.f t
      f_diff := S.f_diff.comp (contDiff_const.prod contDiff_id)
      Ï† := S.Ï† t
      Ï†_diff := S.Ï†_diff.comp (contDiff_const.prod contDiff_id) }âŸ©

end RelLoc
