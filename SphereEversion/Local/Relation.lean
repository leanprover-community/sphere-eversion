import Mathlib.Topology.MetricSpace.HausdorffDistance
import SphereEversion.Local.OneJet

/-!
# Local partial differential relations and their formal solutions

This file defines `RelLoc E F`, the type of first order partial differential relations
for maps between two real normed spaces `E` and `F`.

To any `R : RelLoc E F` we associate the type `sol R` of maps `f : E â†’ F` of
solutions of `R`, and its formal counterpart `FormalSol R`.
(FIXME(grunweg): `sol` is never mentioned; is this docstring outdated?)

The h-principle question is whether we can deform any formal solution into a solution.
The type of deformations is `HtpyJetSet E F` (homotopies of 1-jet sections).
-/


noncomputable section

open Set Function Real Filter

open scoped unitInterval Topology

variable (E : Type*) [NormedAddCommGroup E] [NormedSpace â„ E]
  (F : Type*) [NormedAddCommGroup F] [NormedSpace â„ F]
  (P : Type*) [NormedAddCommGroup P] [NormedSpace â„ P]

/-- A first order relation for maps between real vector spaces. -/
abbrev RelLoc :=
  Set (OneJet E F)

instance : Membership (E Ã— F Ã— (E â†’L[â„] F)) (RelLoc E F) := by delta RelLoc; infer_instance

variable {E F}

/-- A predicate stating that a 1-jet section is a formal solution to a first order relation for
maps between vector spaces. -/
def JetSec.IsFormalSol (ğ“• : JetSec E F) (R : RelLoc E F) : Prop :=
  âˆ€ x, (x, ğ“•.f x, ğ“•.Ï† x) âˆˆ R

namespace RelLoc

/-- A formal solution to a local relation `R`. -/
@[ext]
structure FormalSol (R : RelLoc E F) extends JetSec E F where
  is_sol : âˆ€ x, (x, f x, Ï† x) âˆˆ R

attribute [coe] FormalSol.toJetSec

instance (R : RelLoc E F) : CoeOut (FormalSol R) (JetSec E F) :=
  âŸ¨FormalSol.toJetSecâŸ©

-- Note: syntactic tautology
@[simp]
theorem FormalSol.toJetSec_eq_coe {R : RelLoc E F} (ğ“• : FormalSol R) :
    ğ“•.toJetSec = (ğ“• : JetSec E F) :=
  rfl

@[simp]
theorem FormalSol.coeIsFormalSol {R : RelLoc E F} (ğ“• : FormalSol R) :
    (ğ“• : JetSec E F).IsFormalSol R :=
  ğ“•.is_sol

/-- Bundling a formal solution from a 1-jet section that is a formal solution. -/
def _root_.JetSec.IsFormalSol.formalSol {ğ“• : JetSec E F} {R : RelLoc E F} (h : ğ“•.IsFormalSol R) :
    FormalSol R :=
  { ğ“• with is_sol := h }

instance (R : RelLoc E F) : FunLike (FormalSol R) E (F Ã— (E â†’L[â„] F)) :=
  âŸ¨fun ğ“• x â†¦ (ğ“•.f x, ğ“•.Ï† x),
  by
     intros ğ“• ğ“•' h
     ext x : 2 <;> replace h := Prod.mk_inj.mp <| congrFun h x
     exacts [h.1, h.2]âŸ©

@[simp]
theorem FormalSol.coe_apply {R : RelLoc E F} (ğ“• : FormalSol R) (x : E) : (ğ“• : JetSec E F) x = ğ“• x :=
  rfl

variable {R : RelLoc E F}

theorem FormalSol.eq_iff {ğ“• ğ“•' : FormalSol R} {x : E} :
    ğ“• x = ğ“•' x â†” ğ“•.f x = ğ“•'.f x âˆ§ ğ“•.Ï† x = ğ“•'.Ï† x :=
  JetSec.eq_iff

/-- A formal solution (f, Ï†) is holonomic at `x` if the differential of `f` at `x` is `Ï† x`. -/
def FormalSol.IsHolonomicAt (ğ“• : FormalSol R) (x : E) : Prop :=
  D ğ“•.f x = ğ“•.Ï† x

-- TODO: this should come from a lemma about `JetSec`
theorem FormalSol.isHolonomicAt_congr (ğ“• ğ“•' : FormalSol R) {s : Set E}
    (h : âˆ€á¶  x near s, ğ“• x = ğ“•' x) : âˆ€á¶  x near s, ğ“•.IsHolonomicAt x â†” ğ“•'.IsHolonomicAt x := by
  apply h.eventually_nhdsSet.mono
  intro x hx
  have hf : ğ“•.f =á¶ [ğ“ x] ğ“•'.f := by
    apply hx.mono
    simp_rw [RelLoc.FormalSol.eq_iff]
    tauto
  unfold RelLoc.FormalSol.IsHolonomicAt
  rw [hf.fderiv_eq, (RelLoc.FormalSol.eq_iff.mp hx.self_of_nhds).2]

/-- A family of formal solutions is a 1-parameter family of formal solutions. -/
@[ext]
structure FamilyFormalSol (R : RelLoc E F) extends FamilyJetSec E F P where
  is_sol : âˆ€ t x, (x, f t x, Ï† t x) âˆˆ R

/-- A homotopy of formal solutions is a 1-parameter family of formal solutions. -/
@[reducible]
def HtpyFormalSol (R : RelLoc E F) :=
  R.FamilyFormalSol â„

def HtpyFormalSol.toHtpyJetSec {R : RelLoc E F} (ğ“• : R.HtpyFormalSol) : HtpyJetSec E F :=
  ğ“•.toFamilyJetSec

open RelLoc

instance (R : RelLoc E F) : FunLike (FamilyFormalSol P R) P (JetSec E F) :=
  âŸ¨fun S â†¦ S.toFamilyJetSec, by
      intros S S' h
      ext p x : 3 <;> replace h := congrFun h p
      exacts [congrFun (JetSec.ext_iff.1 h).1 x, congrFun (JetSec.ext_iff.1 h).2 x]âŸ©

end RelLoc
