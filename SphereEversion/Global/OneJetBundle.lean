/-
Copyright (c) 2022 Patrick Massot. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Patrick Massot, Floris van Doorn

! This file was ported from Lean 3 source module global.one_jet_bundle
-/
import Mathlib.Tactic.Common

import Mathlib.Analysis.Normed.Module.Completion
import Mathlib.Geometry.Manifold.Algebra.Monoid
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import SphereEversion.ToMathlib.Geometry.Manifold.VectorBundle.Misc
import Mathlib.Geometry.Manifold.VectorBundle.Hom
import Mathlib.Geometry.Manifold.VectorBundle.Pullback
import Mathlib.Tactic.Monotonicity.Lemmas

/-!
# 1-jet bundles

This file contains the definition of the 1-jet bundle `JÂ¹(M, M')`, also known as
`OneJetBundle I M I' M'`.

We also define
* `OneJetExt I I' f : M â†’ JÂ¹(M, M')`: the 1-jet extension `jÂ¹f` of a map `f : M â†’ M'`

We prove
* If `f` is smooth, `jÂ¹f` is smooth.
* If `x â†¦ (fâ‚ x, fâ‚‚ x, Ï•â‚ x) : N â†’ JÂ¹(Mâ‚, Mâ‚‚)` and `x â†¦ (fâ‚‚ x, fâ‚ƒ x, Ï•â‚‚ x) : N â†’ JÂ¹(Mâ‚‚, Mâ‚ƒ)`
  are smooth, then so is `x â†¦ (fâ‚ x, fâ‚ƒ x, Ï•â‚‚ x âˆ˜ Ï•â‚ x) : N â†’ JÂ¹(Mâ‚, Mâ‚ƒ)`.
-/


noncomputable section

open Filter Set Equiv Bundle ContinuousLinearMap

open scoped Manifold Topology Bundle ContDiff

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I âˆ M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H')
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [IsManifold I' âˆ M']
  {E'' : Type*} [NormedAddCommGroup E''] [NormedSpace ğ•œ E'']
  {H'' : Type*} [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''}
  {M'' : Type*} [TopologicalSpace M''] [ChartedSpace H'' M''] [IsManifold I'' âˆ M'']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  {G : Type*} [TopologicalSpace G] (J : ModelWithCorners ğ•œ F G)
  {N : Type*} [TopologicalSpace N] [ChartedSpace G N]
  {F' : Type*} [NormedAddCommGroup F'] [NormedSpace ğ•œ F']
  {G' : Type*} [TopologicalSpace G'] (J' : ModelWithCorners ğ•œ F' G')
  {N' : Type*} [TopologicalSpace N'] [ChartedSpace G' N'] [IsManifold J' âˆ N']
  {Eâ‚‚ : Type*} [NormedAddCommGroup Eâ‚‚] [NormedSpace ğ•œ Eâ‚‚]
  {Hâ‚‚ : Type*} [TopologicalSpace Hâ‚‚] {Iâ‚‚ : ModelWithCorners ğ•œ Eâ‚‚ Hâ‚‚}
  {Mâ‚‚ : Type*} [TopologicalSpace Mâ‚‚] [ChartedSpace Hâ‚‚ Mâ‚‚] [IsManifold Iâ‚‚ âˆ Mâ‚‚]
  {Eâ‚ƒ : Type*} [NormedAddCommGroup Eâ‚ƒ] [NormedSpace ğ•œ Eâ‚ƒ]
  {Hâ‚ƒ : Type*} [TopologicalSpace Hâ‚ƒ] {Iâ‚ƒ : ModelWithCorners ğ•œ Eâ‚ƒ Hâ‚ƒ}
  {Mâ‚ƒ : Type*} [TopologicalSpace Mâ‚ƒ] [ChartedSpace Hâ‚ƒ Mâ‚ƒ] [IsManifold Iâ‚ƒ âˆ Mâ‚ƒ]

@[inherit_doc] local notation "Ïƒ" => RingHom.id ğ•œ

instance deleteme1 :
    âˆ€ x : M Ã— M',
      Module ğ•œ (((ContMDiffMap.fst : C^âˆâŸ®I.prod I', M Ã— M'; I, MâŸ¯) *áµ– (TangentSpace I)) x) := by
        infer_instance

instance deleteme2 :
    âˆ€ x : M Ã— M',
      Module ğ•œ (((ContMDiffMap.snd : C^âˆâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) *áµ– (TangentSpace I')) x) := by
        infer_instance

instance deleteme3 :
    VectorBundle ğ•œ E ((ContMDiffMap.fst : C^âˆâŸ®I.prod I', M Ã— M'; I, MâŸ¯) *áµ– (TangentSpace I)) := by
  infer_instance

instance deleteme4 :
    VectorBundle ğ•œ E' ((ContMDiffMap.snd : C^âˆâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) *áµ– (TangentSpace I')) := by
  infer_instance

instance deleteme5 :
    ContMDiffVectorBundle âˆ E ((ContMDiffMap.fst : C^âˆâŸ®I.prod I', M Ã— M'; I, MâŸ¯) *áµ– (TangentSpace I))
      (I.prod I') := by infer_instance

instance deleteme6 :
    ContMDiffVectorBundle âˆ E' ((ContMDiffMap.snd : C^âˆâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) *áµ– (TangentSpace I'))
      (I.prod I') := by infer_instance

set_option linter.unusedVariables false in
/-- The fibers of the one jet-bundle. -/
def OneJetSpace (p : M Ã— M') : Type _ :=
  Bundle.ContinuousLinearMap Ïƒ
    ((ContMDiffMap.fst : C^âˆâŸ®I.prod I', M Ã— M'; I, MâŸ¯) *áµ– (TangentSpace I))
    ((ContMDiffMap.snd : C^âˆâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) *áµ– (TangentSpace I')) p

instance (p : M Ã— M') : TopologicalSpace (OneJetSpace I I' p) := by
  delta OneJetSpace
  infer_instance

instance (p : M Ã— M') : AddCommGroup (OneJetSpace I I' p) := by
  delta OneJetSpace
  infer_instance

variable {I I'}

-- what is better notation for this?
/-- Local notation for the `OneJetSpace` on `M Ã— M'`, w.r.t. `I` and `I'` -/
local notation "FJÂ¹MM'" => (OneJetSpace I I' : M Ã— M' â†’ Type _)

variable (I I')

instance (p : M Ã— M') : FunLike (OneJetSpace I I' p) (TangentSpace I p.1) (TangentSpace I' p.2) where
  coe := fun Ï† â†¦ Ï†.toFun
  coe_injective' := fun _ _ h â†¦ ContinuousLinearMap.ext (congrFun h)

variable (M M')

-- is empty if the base manifold is empty
/-- The space of one jets of maps between two smooth manifolds.
Defined in terms of `Bundle.TotalSpace` to be able to put a suitable topology on it. -/
@[reducible]
def OneJetBundle :=
  TotalSpace (E â†’L[ğ•œ] E') (OneJetSpace I I' : M Ã— M' â†’ Type _)

variable {I I' M M'}

@[inherit_doc] local notation "JÂ¹MM'" => OneJetBundle I M I' M'

@[inherit_doc] local notation "HJ" => ModelProd (ModelProd H H') (E â†’L[ğ•œ] E')

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
@[ext]
theorem OneJetBundle.ext {x y : JÂ¹MM'} (h : x.1.1 = y.1.1) (h' : x.1.2 = y.1.2) (h'' : x.2 = y.2) :
    x = y := by
  rcases x with âŸ¨âŸ¨a, bâŸ©, câŸ©
  rcases y with âŸ¨âŸ¨d, eâŸ©, fâŸ©
  dsimp only at h h' h''
  rw [h, h', h'']

variable (I I' M M')

section OneJetBundleInstances

section

variable {M} (p : M Ã— M')

instance (x : M Ã— M') : Module ğ•œ (FJÂ¹MM' x) := by
  delta OneJetSpace
  infer_instance

end

instance : TopologicalSpace JÂ¹MM' := by
  delta OneJetSpace OneJetBundle
  infer_instance

instance : FiberBundle (E â†’L[ğ•œ] E') FJÂ¹MM' := by
  delta OneJetSpace
  infer_instance


instance : VectorBundle ğ•œ (E â†’L[ğ•œ] E') FJÂ¹MM' := by
  delta OneJetSpace
  infer_instance

instance : ContMDiffVectorBundle âˆ (E â†’L[ğ•œ] E') (OneJetSpace I I' : M Ã— M' â†’ Type _) (I.prod I') := by
  delta OneJetSpace
  infer_instance

instance : ChartedSpace HJ JÂ¹MM' := by
  delta OneJetSpace OneJetBundle
  infer_instance

instance : IsManifold ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ JÂ¹MM' := by
  apply Bundle.TotalSpace.isManifold

end OneJetBundleInstances

variable {I M I' M' J J'}

/-- The tangent bundle projection on the basis is a continuous map. -/
theorem oneJetBundle_proj_continuous : Continuous (Ï€ (E â†’L[ğ•œ] E') FJÂ¹MM') :=
  FiberBundle.continuous_proj (E â†’L[ğ•œ] E') FJÂ¹MM'

-- Porting note: removed next line
-- attribute [simps] ContMDiffMap.fst ContMDiffMap.snd

theorem oneJetBundle_trivializationAt (xâ‚€ x : JÂ¹MM') :
    (trivializationAt (E â†’L[ğ•œ] E') (OneJetSpace I I') xâ‚€.proj x).2 =
      inCoordinates E (TangentSpace I) E' (TangentSpace I') xâ‚€.proj.1 x.proj.1 xâ‚€.proj.2 x.proj.2
        x.2 := by
  delta OneJetSpace
  rw [continuousLinearMap_trivializationAt, Trivialization.continuousLinearMap_apply]
  simp only [inTangentCoordinates, inCoordinates]
  congr 2
  exact Trivialization.pullback_symmL ContMDiffMap.fst
    (trivializationAt E (TangentSpace I) xâ‚€.1.1) x.proj

theorem trivializationAt_oneJetBundle_source (xâ‚€ : M Ã— M') :
    (trivializationAt (E â†’L[ğ•œ] E') FJÂ¹MM' xâ‚€).source =
      Ï€ (E â†’L[ğ•œ] E') FJÂ¹MM' â»Â¹'
        (Prod.fst â»Â¹' (chartAt H xâ‚€.1).source âˆ© Prod.snd â»Â¹' (chartAt H' xâ‚€.2).source) :=
  rfl

theorem trivializationAt_oneJetBundle_target (xâ‚€ : M Ã— M') :
    (trivializationAt (E â†’L[ğ•œ] E') FJÂ¹MM' xâ‚€).target =
      (Prod.fst â»Â¹' (trivializationAt E (TangentSpace I) xâ‚€.1).baseSet âˆ©
          Prod.snd â»Â¹' (trivializationAt E' (TangentSpace I') xâ‚€.2).baseSet) Ã—Ë¢
        Set.univ :=
  rfl

/-- Computing the value of a chart around `v` at point `v'` in `JÂ¹(M, M')`.
  The last component equals the continuous linear map `v'.2`, composed on both sides by an
  appropriate coordinate change function. -/
theorem oneJetBundle_chartAt_apply (v v' : OneJetBundle I M I' M') :
    chartAt HJ v v' =
      ((chartAt H v.1.1 v'.1.1, chartAt H' v.1.2 v'.1.2),
        inCoordinates E (TangentSpace I) E' (TangentSpace I') v.1.1 v'.1.1 v.1.2 v'.1.2 v'.2) := by
  ext1
  Â· rfl
  rw [FiberBundle.chartedSpace_chartAt_snd]
  exact oneJetBundle_trivializationAt v v'

/-- In `JÂ¹(M, M')`, the source of a chart has a nice formula -/
theorem oneJetBundle_chart_source (xâ‚€ : JÂ¹MM') :
    (chartAt HJ xâ‚€).source =
      Ï€ (E â†’L[ğ•œ] E') FJÂ¹MM' â»Â¹' (chartAt (ModelProd H H') xâ‚€.proj).source := by
  -- Porting note: was
  -- simp only [FiberBundle.chartedSpace_chartAt, trivializationAt_oneJetBundle_source, mfld_simps]
  rw [FiberBundle.chartedSpace_chartAt]
  simp_rw [
    PartialHomeomorph.trans_toPartialEquiv,
    PartialEquiv.trans_source,
    PartialHomeomorph.prod_toPartialEquiv,
    PartialEquiv.prod_source,
    PartialHomeomorph.coe_coe,
    Trivialization.coe_coe,
    PartialHomeomorph.refl_partialEquiv,
    PartialEquiv.refl_source,
    prodChartedSpace_chartAt,
    PartialHomeomorph.prod_toPartialEquiv,
    trivializationAt_oneJetBundle_source,
    PartialEquiv.prod_source,
    Set.preimage_inter]
  simp_rw [prod_univ, â† preimage_inter, â† Set.prod_eq, preimage_preimage, inter_eq_left,
    subset_def, mem_preimage]
  intro x hx
  rwa [Trivialization.coe_fst]
  rwa [trivializationAt_oneJetBundle_source, mem_preimage, â† Set.prod_eq]

section

section

universe u v wâ‚ wâ‚‚ U

variable {B : Type u} {F : Type v} {E : B â†’ Type wâ‚} {B' : Type wâ‚‚}
  [TopologicalSpace B'] [TopologicalSpace (TotalSpace F E)]
  [TopologicalSpace F] [TopologicalSpace B] [(_b : B) â†’ Zero (E _b)]
  {K : Type U} [FunLike K B' B] [ContinuousMapClass K B' B]
  [(x : B) â†’ TopologicalSpace (E x)] [FiberBundle F E]

lemma trivializationAt_pullBack_baseSet (f : K) (x : B') :
    (trivializationAt F ((f : B' â†’ B) *áµ– E) x).baseSet =
    f â»Â¹' (trivializationAt F E (f x)).baseSet :=
  rfl
end

section
variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ] {E : Type*}
[NormedAddCommGroup E] [NormedSpace ğ•œ E] {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
{H : Type*} [TopologicalSpace H] {H' : Type*} [TopologicalSpace H']
{I : ModelWithCorners ğ•œ E H} {I' : ModelWithCorners ğ•œ E' H'} {M : Type*} [TopologicalSpace M]
[ChartedSpace H M] {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] {n : â„•âˆ}

@[simp]
lemma ContMDiffMap.coe_fst :
    ((ContMDiffMap.fst : C^nâŸ®I.prod I', M Ã— M'; I, MâŸ¯) : M Ã— M' â†’ M) = Prod.fst :=
  rfl

@[simp]
lemma ContMDiffMap.coe_snd :
    ((ContMDiffMap.snd : C^nâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) : M Ã— M' â†’ M') = Prod.snd :=
  rfl

@[simp]
lemma ContMDiffMap.fst_apply (x : M) (x' : M') :
    (ContMDiffMap.fst : C^nâŸ®I.prod I', M Ã— M'; I, MâŸ¯) (x, x') = x := rfl

@[simp]
lemma ContMDiffMap.snd_apply (x : M) (x' : M') :
    (ContMDiffMap.snd : C^nâŸ®I.prod I', M Ã— M'; I', M'âŸ¯) (x, x') = x' := rfl

end

/-- In `JÂ¹(M, M')`, the target of a chart has a nice formula -/
theorem oneJetBundle_chart_target (xâ‚€ : JÂ¹MM') :
    (chartAt HJ xâ‚€).target = Prod.fst â»Â¹' (chartAt (ModelProd H H') xâ‚€.proj).target := by
  rw [FiberBundle.chartedSpace_chartAt]
  simp only [prodChartedSpace_chartAt,
    PartialHomeomorph.trans_toPartialEquiv, PartialHomeomorph.prod_toPartialEquiv,
    PartialHomeomorph.refl_partialEquiv, PartialEquiv.trans_target, PartialEquiv.prod_target,
    PartialEquiv.refl_target]
  erw [hom_trivializationAt_target]
  simp only [trivializationAt_pullBack_baseSet, TangentBundle.trivializationAt_baseSet]
  rcases xâ‚€ with âŸ¨âŸ¨m, m'âŸ©, Ï†âŸ©
  simp only [ContMDiffMap.coe_fst, ContMDiffMap.fst_apply, ContMDiffMap.coe_snd,
    ContMDiffMap.snd_apply]
  erw [prod_univ, inter_eq_left, prod_univ, PartialEquiv.prod_symm, PartialEquiv.prod_symm]
  rw [preimage_preimage, â† Set.prod_eq, PartialEquiv.refl_symm, PartialEquiv.prod_coe,
      PartialEquiv.refl_coe]
  have : (fun x : ModelProd (ModelProd H H') (E â†’SL[Ïƒ] E') â†¦ ((chartAt H m).toPartialEquiv.symm.prod (chartAt H' m').toPartialEquiv.symm) x.1) =
      (Prod.map (chartAt H m).symm (chartAt H' m').symm) âˆ˜ Prod.fst := by
    ext x <;> rfl
  rw [this, preimage_comp, preimage_prod_map_prod]
  mono
  Â· exact (chartAt H m).target_subset_preimage_source
  Â· exact (chartAt H' m').target_subset_preimage_source

section Maps

theorem contMDiff_oneJetBundle_proj :
    ContMDiff ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) (I.prod I') âˆ (Ï€ (E â†’L[ğ•œ] E') FJÂ¹MM') := by
  apply contMDiff_proj _

theorem ContMDiff.oneJetBundle_proj {f : N â†’ JÂ¹MM'}
    (hf : ContMDiff J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ f) : ContMDiff J (I.prod I') âˆ fun x â†¦ (f x).1 :=
  contMDiff_oneJetBundle_proj.comp hf

theorem ContMDiffAt.oneJetBundle_proj {f : N â†’ JÂ¹MM'} {xâ‚€ : N}
    (hf : ContMDiffAt J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ f xâ‚€) :
    ContMDiffAt J (I.prod I') âˆ (fun x â†¦ (f x).1) xâ‚€ :=
  (contMDiff_oneJetBundle_proj _).comp xâ‚€ hf

/-- The constructor of `OneJetBundle`, in case `Sigma.mk` will not give the right type. -/
@[simp]
def OneJetBundle.mk (x : M) (y : M') (f : OneJetSpace I I' (x, y)) : JÂ¹MM' :=
  âŸ¨(x, y), fâŸ©

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
@[simp, mfld_simps]
theorem oneJetBundle_mk_fst {x : M} {y : M'} {f : OneJetSpace I I' (x, y)} :
    (OneJetBundle.mk x y f).1 = (x, y) :=
  rfl

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
@[simp, mfld_simps]
theorem oneJetBundle_mk_snd {x : M} {y : M'} {f : OneJetSpace I I' (x, y)} :
    (OneJetBundle.mk x y f).2 = f :=
  rfl

theorem contMDiffAt_oneJetBundle {f : N â†’ JÂ¹MM'} {xâ‚€ : N} :
    ContMDiffAt J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ f xâ‚€ â†”
      ContMDiffAt J I âˆ (fun x â†¦ (f x).1.1) xâ‚€ âˆ§
        ContMDiffAt J I' âˆ (fun x â†¦ (f x).1.2) xâ‚€ âˆ§
          ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') âˆ
            (inTangentCoordinates I I' (fun x â†¦ (f x).1.1) (fun x â†¦ (f x).1.2) (fun x â†¦ (f x).2)
              xâ‚€) xâ‚€ := by
  simp_rw [Bundle.contMDiffAt_totalSpace, contMDiffAt_prod_iff, and_assoc,
    oneJetBundle_trivializationAt]
  rfl

theorem contMDiffAt_oneJetBundle_mk {f : N â†’ M} {g : N â†’ M'} {Ï• : N â†’ E â†’L[ğ•œ] E'} {xâ‚€ : N} :
    ContMDiffAt J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ
        (fun x â†¦ OneJetBundle.mk (f x) (g x) (Ï• x) : N â†’ JÂ¹MM') xâ‚€ â†”
      ContMDiffAt J I âˆ f xâ‚€ âˆ§
        ContMDiffAt J I' âˆ g xâ‚€ âˆ§ ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') âˆ (inTangentCoordinates I I' f g Ï• xâ‚€) xâ‚€ :=
  contMDiffAt_oneJetBundle

theorem ContMDiffAt.oneJetBundle_mk {f : N â†’ M} {g : N â†’ M'} {Ï• : N â†’ E â†’L[ğ•œ] E'} {xâ‚€ : N}
    (hf : ContMDiffAt J I âˆ f xâ‚€) (hg : ContMDiffAt J I' âˆ g xâ‚€)
    (hÏ• : ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') âˆ (inTangentCoordinates I I' f g Ï• xâ‚€) xâ‚€) :
    ContMDiffAt J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ
      (fun x â†¦ OneJetBundle.mk (f x) (g x) (Ï• x) : N â†’ JÂ¹MM') xâ‚€ :=
  contMDiffAt_oneJetBundle.mpr âŸ¨hf, hg, hÏ•âŸ©

variable (I I')

/-- The one-jet extension of a function -/
def oneJetExt (f : M â†’ M') : M â†’ OneJetBundle I M I' M' := fun x â†¦
  OneJetBundle.mk x (f x) (mfderiv I I' f x)

variable {I I'}

theorem ContMDiffAt.oneJetExt {f : M â†’ M'} {x : M} (hf : ContMDiffAt I I' âˆ f x) :
    ContMDiffAt I ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ (oneJetExt I I' f) x :=
  contMDiffAt_id.oneJetBundle_mk hf (hf.mfderiv_const le_rfl)

theorem ContMDiff.oneJetExt {f : M â†’ M'} (hf : ContMDiff I I' âˆ f) :
    ContMDiff I ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ (oneJetExt I I' f) :=
  fun x â†¦ ((hf x).contMDiffAt univ_mem).oneJetExt

theorem ContinuousAt.inTangentCoordinates_comp {f : N â†’ M} {g : N â†’ M'} {h : N â†’ N'}
    {Ï•' : N â†’ E' â†’L[ğ•œ] F'} {Ï• : N â†’ E â†’L[ğ•œ] E'} {xâ‚€ : N} (hg : ContinuousAt g xâ‚€) :
    inTangentCoordinates I J' f h (fun x â†¦ Ï•' x âˆ˜L Ï• x) xâ‚€ =á¶ [ğ“ xâ‚€] fun x â†¦
      inTangentCoordinates I' J' g h Ï•' xâ‚€ x âˆ˜L inTangentCoordinates I I' f g Ï• xâ‚€ x := by
  refine eventually_of_mem (hg.preimage_mem_nhds <|
    (achart H' (g xâ‚€)).1.open_source.mem_nhds <| mem_achart_source H' (g xâ‚€))
    fun x hx â†¦ ?_
  ext v
  beta_reduce
  simp_rw [inTangentCoordinates, inCoordinates,
    ContinuousLinearMap.comp_apply]
  rw [Trivialization.symmL_continuousLinearMapAt]
  Â· rfl
  exact hx

theorem ContMDiffAt.clm_comp_inTangentCoordinates {f : N â†’ M} {g : N â†’ M'} {h : N â†’ N'}
    {Ï•' : N â†’ E' â†’L[ğ•œ] F'} {Ï• : N â†’ E â†’L[ğ•œ] E'} {n : N} (hg : ContinuousAt g n)
    (hÏ•' : ContMDiffAt J ğ“˜(ğ•œ, E' â†’L[ğ•œ] F') âˆ (inTangentCoordinates I' J' g h Ï•' n) n)
    (hÏ• : ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] E') âˆ (inTangentCoordinates I I' f g Ï• n) n) :
    ContMDiffAt J ğ“˜(ğ•œ, E â†’L[ğ•œ] F') âˆ (inTangentCoordinates I J' f h (fun n â†¦ Ï•' n âˆ˜L Ï• n) n) n :=
  (hÏ•'.clm_comp hÏ•).congr_of_eventuallyEq hg.inTangentCoordinates_comp

variable (I')

variable [IsManifold J âˆ N]

omit [IsManifold J' âˆ N'] in
theorem ContMDiffAt.oneJet_comp {f1 : N' â†’ M} (f2 : N' â†’ M') {f3 : N' â†’ N} {xâ‚€ : N'}
    {h : âˆ€ x : N', OneJetSpace I' J (f2 x, f3 x)} {g : âˆ€ x : N', OneJetSpace I I' (f1 x, f2 x)}
    (hh : ContMDiffAt J' ((I'.prod J).prod ğ“˜(ğ•œ, E' â†’L[ğ•œ] F)) âˆ (fun x â†¦ OneJetBundle.mk _ _ (h x)) xâ‚€)
    (hg : ContMDiffAt J' ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ (fun x â†¦ OneJetBundle.mk _ _ (g x)) xâ‚€) :
    ContMDiffAt J' ((I.prod J).prod ğ“˜(ğ•œ, E â†’L[ğ•œ] F)) âˆ
      (fun x â†¦ OneJetBundle.mk (f1 x) (f3 x) (h x âˆ˜L g x) : N' â†’ OneJetBundle I M J N) xâ‚€ := by
  rw [contMDiffAt_oneJetBundle_mk] at hh hg âŠ¢
  exact âŸ¨hg.1, hh.2.1, hh.2.2.clm_comp_inTangentCoordinates hg.2.1.continuousAt hg.2.2âŸ©

omit [IsManifold J' âˆ N'] in
theorem ContMDiff.oneJet_comp {f1 : N' â†’ M} (f2 : N' â†’ M') {f3 : N' â†’ N}
    {h : âˆ€ x : N', OneJetSpace I' J (f2 x, f3 x)} {g : âˆ€ x : N', OneJetSpace I I' (f1 x, f2 x)}
    (hh : ContMDiff J' ((I'.prod J).prod ğ“˜(ğ•œ, E' â†’L[ğ•œ] F)) âˆ fun x â†¦ OneJetBundle.mk _ _ (h x))
    (hg : ContMDiff J' ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ fun x â†¦ OneJetBundle.mk _ _ (g x)) :
    ContMDiff J' ((I.prod J).prod ğ“˜(ğ•œ, E â†’L[ğ•œ] F)) âˆ
      (fun x â†¦ OneJetBundle.mk (f1 x) (f3 x) (h x âˆ˜L g x) : N' â†’ OneJetBundle I M J N) :=
  fun xâ‚€ â†¦ hh.contMDiffAt.oneJet_comp I' f2 (hg xâ‚€)

variable {I'}

open Trivialization in
omit [IsManifold J âˆ N] in
theorem ContMDiff.oneJet_add {f : N â†’ M} {g : N â†’ M'} {Ï• Ï•' : âˆ€ x : N, OneJetSpace I I' (f x, g x)}
    (hÏ• : ContMDiff J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ fun x â†¦ OneJetBundle.mk _ _ (Ï• x))
    (hÏ•' : ContMDiff J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ fun x â†¦ OneJetBundle.mk _ _ (Ï•' x)) :
    ContMDiff J ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ fun x â†¦
      OneJetBundle.mk (f x) (g x) (Ï• x + Ï•' x) := by
  intro x
  specialize hÏ• x
  specialize hÏ•' x
  rw [contMDiffAt_oneJetBundle_mk] at hÏ• hÏ•' âŠ¢
  refine âŸ¨hÏ•.1, hÏ•.2.1, ?_âŸ©
  -- Porting note: next 5 lines should be
  -- `simp_rw [inTangentCoordinates, inCoordinates, ContinuousLinearMap.add_comp,
  --           ContinuousLinearMap.comp_add]
  simp_rw (config := { unfoldPartialApp := true }) [inTangentCoordinates, inCoordinates]
  conv =>
    enter [4, x, 2]
    rw [ContinuousLinearMap.add_comp]
  simp only [ContinuousLinearMap.comp_add]
  exact hÏ•.2.2.add hÏ•'.2.2

variable (I' J')

/-- A useful definition to define maps between two `OneJetBundle`s. -/
protected def OneJetBundle.map (f : M â†’ N) (g : M' â†’ N')
    (Dfinv : âˆ€ x : M, TangentSpace J (f x) â†’L[ğ•œ] TangentSpace I x) :
    OneJetBundle I M I' M' â†’ OneJetBundle J N J' N' := fun p â†¦
  OneJetBundle.mk (f p.1.1) (g p.1.2) ((mfderiv I' J' g p.1.2 âˆ˜L p.2) âˆ˜L Dfinv p.1.1)

variable {I' J'}

omit [IsManifold I âˆ M] [IsManifold I' âˆ M']
  [IsManifold Iâ‚‚ âˆ Mâ‚‚] [IsManifold Iâ‚ƒ âˆ Mâ‚ƒ]
  [IsManifold J' âˆ N'] [IsManifold J âˆ N] in
theorem OneJetBundle.map_map {fâ‚‚ : N â†’ Mâ‚‚} {f : M â†’ N} {gâ‚‚ : N' â†’ Mâ‚ƒ} {g : M' â†’ N'}
    {Dfinv : âˆ€ x : M, TangentSpace J (f x) â†’L[ğ•œ] TangentSpace I x}
    {Dfâ‚‚inv : âˆ€ x : N, TangentSpace Iâ‚‚ (fâ‚‚ x) â†’L[ğ•œ] TangentSpace J x} {x : JÂ¹MM'}
    (hgâ‚‚ : MDifferentiableAt J' Iâ‚ƒ gâ‚‚ (g x.1.2)) (hg : MDifferentiableAt I' J' g x.1.2) :
    OneJetBundle.map J' Iâ‚ƒ fâ‚‚ gâ‚‚ Dfâ‚‚inv (OneJetBundle.map I' J' f g Dfinv x) =
      OneJetBundle.map I' Iâ‚ƒ (fâ‚‚ âˆ˜ f) (gâ‚‚ âˆ˜ g) (fun x â†¦ Dfinv x âˆ˜L Dfâ‚‚inv (f x)) x := by
  ext
  Â· rfl
  Â· rfl
  Â· dsimp only [OneJetBundle.map, OneJetBundle.mk]
    simp_rw [â† ContinuousLinearMap.comp_assoc, mfderiv_comp x.1.2 hgâ‚‚ hg]

omit [IsManifold I âˆ M] [IsManifold I' âˆ M']
  [IsManifold Iâ‚‚ âˆ Mâ‚‚] [IsManifold Iâ‚ƒ âˆ Mâ‚ƒ]
  [IsManifold J' âˆ N'] [IsManifold J âˆ N] in
theorem OneJetBundle.map_id (x : JÂ¹MM') :
    OneJetBundle.map I' I' id id (fun x â†¦ ContinuousLinearMap.id ğ•œ (TangentSpace I x)) x = x := by
  -- Porting note: was `ext _` in Lean 3
  ext
  Â· rfl
  Â· rfl
  dsimp only [OneJetBundle.map, OneJetBundle.mk]
  simp_rw [mfderiv_id]
  -- note: rw fails since we have to unfold the type `Bundle.Pullback`
  erw [ContinuousLinearMap.id_comp]

theorem ContMDiffAt.oneJetBundle_map {f : M'' â†’ M â†’ N} {g : M'' â†’ M' â†’ N'} {xâ‚€ : M''}
    {Dfinv : âˆ€ (z : M'') (x : M), TangentSpace J (f z x) â†’L[ğ•œ] TangentSpace I x} {k : M'' â†’ JÂ¹MM'}
    (hf : ContMDiffAt (I''.prod I) J âˆ f.uncurry (xâ‚€, (k xâ‚€).1.1))
    (hg : ContMDiffAt (I''.prod I') J' âˆ g.uncurry (xâ‚€, (k xâ‚€).1.2))
    (hDfinv :
      ContMDiffAt I'' ğ“˜(ğ•œ, F â†’L[ğ•œ] E) âˆ
        (inTangentCoordinates J I (fun x â†¦ f x (k x).1.1) (fun x â†¦ (k x).1.1)
          (fun x â†¦ Dfinv x (k x).1.1) xâ‚€)
        xâ‚€)
    (hk : ContMDiffAt I'' ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ k xâ‚€) :
    ContMDiffAt I'' ((J.prod J').prod ğ“˜(ğ•œ, F â†’L[ğ•œ] F')) âˆ
      (fun z â†¦ OneJetBundle.map I' J' (f z) (g z) (Dfinv z) (k z)) xâ‚€ := by
  rw [contMDiffAt_oneJetBundle] at hk
  refine ContMDiffAt.oneJet_comp _ _ ?_ ?_
  Â· refine ContMDiffAt.oneJet_comp _ _ ?_ ?_
    Â· refine hk.2.1.oneJetBundle_mk (hg.comp xâ‚€ (contMDiffAt_id.prodMk hk.2.1)) ?_
      exact ContMDiffAt.mfderiv g (fun x â†¦ (k x).1.2) hg hk.2.1 le_rfl
    Â· exact hk.1.oneJetBundle_mk hk.2.1 hk.2.2
  apply (hf.comp xâ‚€ (contMDiffAt_id.prodMk hk.1)).oneJetBundle_mk hk.1
  apply hDfinv


/-- A useful definition to define maps between two `OneJetBundle`s. -/
def mapLeft (f : M â†’ N) (Dfinv : âˆ€ x : M, TangentSpace J (f x) â†’L[ğ•œ] TangentSpace I x) :
    JÂ¹MM' â†’ OneJetBundle J N I' M' := fun p â†¦ OneJetBundle.mk (f p.1.1) p.1.2 (p.2 âˆ˜L Dfinv p.1.1)

set_option linter.style.multiGoal false in
omit [IsManifold I âˆ M] [IsManifold I' âˆ M']
  [IsManifold Iâ‚‚ âˆ Mâ‚‚] [IsManifold Iâ‚ƒ âˆ Mâ‚ƒ]
  [IsManifold J' âˆ N'] [IsManifold J âˆ N] in
theorem mapLeft_eq_map (f : M â†’ N) (Dfinv : âˆ€ x : M, TangentSpace J (f x) â†’L[ğ•œ] TangentSpace I x) :
    mapLeft f Dfinv = OneJetBundle.map I' I' f (id : M' â†’ M') Dfinv := by
  ext x; rfl; rfl; dsimp only [OneJetBundle.map, mapLeft, oneJetBundle_mk_snd]
  simp_rw [mfderiv_id, ContinuousLinearMap.id_comp]

theorem ContMDiffAt.mapLeft {f : N' â†’ M â†’ N} {xâ‚€ : N'}
    {Dfinv : âˆ€ (z : N') (x : M), TangentSpace J (f z x) â†’L[ğ•œ] TangentSpace I x} {g : N' â†’ JÂ¹MM'}
    (hf : ContMDiffAt (J'.prod I) J âˆ f.uncurry (xâ‚€, (g xâ‚€).1.1))
    (hDfinv :
      ContMDiffAt J' ğ“˜(ğ•œ, F â†’L[ğ•œ] E) âˆ
        (inTangentCoordinates J I (fun x â†¦ f x (g x).1.1) (fun x â†¦ (g x).1.1)
          (fun x â†¦ Dfinv x (g x).1.1) xâ‚€)
        xâ‚€)
    (hg : ContMDiffAt J' ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E')) âˆ g xâ‚€) :
    ContMDiffAt J' ((J.prod I').prod ğ“˜(ğ•œ, F â†’L[ğ•œ] E')) âˆ (fun z â†¦ mapLeft (f z) (Dfinv z) (g z)) xâ‚€ := by
  simp_rw [mapLeft_eq_map]; exact hf.oneJetBundle_map contMDiffAt_snd hDfinv hg

/-- The projection `JÂ¹(E Ã— P, F) â†’ JÂ¹(E, F)`. Not actually used. -/
def bundleFst : OneJetBundle (J.prod I) (N Ã— M) I' M' â†’ OneJetBundle J N I' M' :=
  mapLeft Prod.fst fun _ â†¦ ContinuousLinearMap.inl ğ•œ F E

/-- The projection `JÂ¹(P Ã— E, F) â†’ JÂ¹(E, F)`. -/
def bundleSnd : OneJetBundle (J.prod I) (N Ã— M) I' M' â†’ JÂ¹MM' :=
  mapLeft Prod.snd fun x â†¦ mfderiv I (J.prod I) (fun y â†¦ (x.1, y)) x.2

omit [IsManifold I âˆ M] [IsManifold I' âˆ M']
  [IsManifold Iâ‚‚ âˆ Mâ‚‚] [IsManifold Iâ‚ƒ âˆ Mâ‚ƒ]
  [IsManifold J' âˆ N'] [IsManifold J âˆ N] in
theorem bundleSnd_eq (x : OneJetBundle (J.prod I) (N Ã— M) I' M') :
    bundleSnd x = (mapLeft Prod.snd (fun _ â†¦ ContinuousLinearMap.inr ğ•œ F E) x : JÂ¹MM') := by
  simp_rw [bundleSnd, mfderiv_prod_right]; rfl

theorem contMDiff_bundleSnd :
    ContMDiff (((J.prod I).prod I').prod ğ“˜(ğ•œ, F Ã— E â†’L[ğ•œ] E')) ((I.prod I').prod ğ“˜(ğ•œ, E â†’L[ğ•œ] E'))
      âˆ (bundleSnd : OneJetBundle (J.prod I) (N Ã— M) I' M' â†’ JÂ¹MM') := by
  intro xâ‚€
  refine ContMDiffAt.mapLeft contMDiffAt_snd.snd ?_ contMDiffAt_id
  have : ContMDiffAt (((J.prod I).prod I').prod ğ“˜(ğ•œ, F Ã— E â†’L[ğ•œ] E')) ğ“˜(ğ•œ, E â†’L[ğ•œ] F Ã— E) âˆ
      (inTangentCoordinates I (J.prod I) _ _ _ xâ‚€) xâ‚€ :=
    ContMDiffAt.mfderiv (n := âˆ)
      (fun (x : OneJetBundle (J.prod I) (N Ã— M) I' M') (y : M) â†¦ (x.1.1.1, y))
      (fun x : OneJetBundle (J.prod I) (N Ã— M) I' M' â†¦ x.1.1.2) ?_ ?_ le_rfl
  Â· exact this
  Â· exact (contMDiff_oneJetBundle_proj.fst.fst.prodMap contMDiff_id).contMDiffAt
  Â· exact contMDiff_oneJetBundle_proj.fst.snd.contMDiffAt

-- slow
end Maps

-- move
theorem partialEquiv_eq_equiv {Î± Î²} {f : PartialEquiv Î± Î²} {e : Î± â‰ƒ Î²} (h1 : âˆ€ x, f x = e x)
    (h2 : f.source = univ) (h3 : f.target = univ) : f = e.toPartialEquiv := by
  refine PartialEquiv.ext h1 (fun y â†¦ ?_) h2
  conv_rhs => rw [â† f.right_inv ((Set.ext_iff.mp h3 y).mpr (mem_univ y)), h1]
  exact (e.left_inv _).symm

@[inherit_doc] local notation "ğ“œ" => ModelProd (ModelProd H H') (E â†’L[ğ•œ] E')

/-- In the `OneJetBundle` to the model space, the charts are just the canonical identification
between a product type and a bundle total space type, a.k.a. `Bundle.TotalSpace.toProd`. -/
@[simp, mfld_simps]
theorem oneJetBundle_model_space_chartAt (p : OneJetBundle I H I' H') :
    (chartAt ğ“œ p).toPartialEquiv = (Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E')).toPartialEquiv := by
  apply partialEquiv_eq_equiv
  Â· intro x
    rw [PartialHomeomorph.coe_coe, oneJetBundle_chartAt_apply p x,
      inCoordinates_tangent_bundle_core_model_space]
    ext <;> rfl
  Â· simp_rw [oneJetBundle_chart_source, prodChartedSpace_chartAt, chartAt_self_eq,
      PartialHomeomorph.refl_prod_refl]
    rfl
  Â· simp_rw [oneJetBundle_chart_target, prodChartedSpace_chartAt, chartAt_self_eq,
      PartialHomeomorph.refl_prod_refl]
    rfl

@[simp, mfld_simps]
theorem oneJetBundle_model_space_coe_chartAt (p : OneJetBundle I H I' H') :
    â‡‘(chartAt ğ“œ p) = Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E') := by
  ext q e
  Â· rfl
  Â· rfl
  Â· rw [oneJetBundle_chartAt_apply, TotalSpace.toProd_apply,
        inCoordinates_tangent_bundle_core_model_space]

@[simp, mfld_simps]
theorem oneJetBundle_model_space_coe_chartAt_symm (p : OneJetBundle I H I' H') :
    ((chartAt ğ“œ p).symm : ğ“œ â†’ OneJetBundle I H I' H') =
      (Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E')).symm := by
  ext x
  Â· rfl
  Â· rfl
  Â· rw [â† PartialHomeomorph.coe_coe_symm, oneJetBundle_model_space_chartAt]
    rfl

variable (I I')

-- note: this proof works for all vector bundles where we have proven
-- `âˆ€ p, chartAt _ p = f.toPartialEquiv`
/-- The canonical identification between the one-jet bundle to the model space and the product,
as a homeomorphism -/
def oneJetBundleModelSpaceHomeomorph : OneJetBundle I H I' H' â‰ƒâ‚œ ğ“œ :=
  { Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E') with
    continuous_toFun := by
      let p : OneJetBundle I H I' H' := âŸ¨(I.symm (0 : E), I'.symm (0 : E')), 0âŸ©
      have : Continuous (chartAt ğ“œ p) := by
        rw [continuous_iff_continuousOn_univ]
        convert PartialHomeomorph.continuousOn _
        simp only [mfld_simps]
      simpa only [mfld_simps] using this
    continuous_invFun := by
      let p : OneJetBundle I H I' H' := âŸ¨(I.symm (0 : E), I'.symm (0 : E')), 0âŸ©
      have : Continuous (chartAt ğ“œ p).symm := by
        rw [continuous_iff_continuousOn_univ]
        convert PartialHomeomorph.continuousOn _
        simp only [mfld_simps]
      simpa only [mfld_simps] using this }

-- unused
@[simp, mfld_simps]
theorem oneJetBundleModelSpaceHomeomorph_coe :
    (oneJetBundleModelSpaceHomeomorph I I' : OneJetBundle I H I' H' â†’ ğ“œ) =
      Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E') :=
  rfl

-- unused
@[simp, mfld_simps]
theorem oneJetBundleModelSpaceHomeomorph_coe_symm :
    ((oneJetBundleModelSpaceHomeomorph I I').symm : ğ“œ â†’ OneJetBundle I H I' H') =
      (Bundle.TotalSpace.toProd (H Ã— H') (E â†’L[ğ•œ] E')).symm :=
  rfl
