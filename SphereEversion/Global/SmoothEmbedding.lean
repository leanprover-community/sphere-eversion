import Mathlib.Topology.MetricSpace.HausdorffDistance
import Mathlib.Topology.UniformSpace.Separation
import Mathlib.Geometry.Manifold.ContMDiffMFDeriv
import SphereEversion.Notations
import SphereEversion.Indexing
import SphereEversion.ToMathlib.Topology.Paracompact
import SphereEversion.ToMathlib.Topology.Algebra.Order.Compact
import SphereEversion.ToMathlib.Topology.NhdsSet
import SphereEversion.ToMathlib.Topology.Misc
import SphereEversion.ToMathlib.Geometry.Manifold.ChartedSpace
import SphereEversion.ToMathlib.Geometry.Manifold.SmoothManifoldWithCorners
import SphereEversion.ToMathlib.Analysis.NormedSpace.Misc

noncomputable section

open Set Equiv

open scoped Manifold Topology

section General

variable {ğ•œ : Type _} [NontriviallyNormedField ğ•œ] {E : Type _} [NormedAddCommGroup E]
  [NormedSpace ğ•œ E] {H : Type _} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H) (M : Type _)
  [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M] {E' : Type _}
  [NormedAddCommGroup E'] [NormedSpace ğ•œ E'] {H' : Type _} [TopologicalSpace H']
  (I' : ModelWithCorners ğ•œ E' H') (M' : Type _) [TopologicalSpace M'] [ChartedSpace H' M']
  [SmoothManifoldWithCorners I' M']

structure OpenSmoothEmbedding where
  toFun : M â†’ M'
  invFun : M' â†’ M
  left_inv' : âˆ€ {x}, invFun (toFun x) = x
  isOpen_range : IsOpen (range toFun)
  smooth_to : Smooth I I' toFun
  smooth_inv : SmoothOn I' I invFun (range toFun)

attribute [coe] OpenSmoothEmbedding.toFun

-- Note: this cannot be a `FunLike` instance since `toFun` is not injective in general.
instance : CoeFun (OpenSmoothEmbedding I M I' M') fun _ => M â†’ M' :=
  âŸ¨OpenSmoothEmbedding.toFunâŸ©

namespace OpenSmoothEmbedding

variable {I I' M M'}

variable (f : OpenSmoothEmbedding I M I' M')

@[simp]
theorem coe_mk (f g hâ‚ hâ‚‚ hâ‚ƒ hâ‚„) : â‡‘(âŸ¨f, g, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ© : OpenSmoothEmbedding I M I' M') = f :=
  rfl

@[simp]
theorem left_inv (x : M) : f.invFun (f x) = x := by apply f.left_inv'

@[simp]
theorem invFun_comp_coe : f.invFun âˆ˜ f = id :=
  funext f.left_inv

@[simp]
theorem right_inv {y : M'} (hy : y âˆˆ range f) : f (f.invFun y) = y := by
  obtain âŸ¨x, rflâŸ© := hy;
  rw [f.left_inv]

theorem smoothAt_inv {y : M'} (hy : y âˆˆ range f) : SmoothAt I' I f.invFun y :=
  (f.smooth_inv y hy).contMDiffAt <| f.isOpen_range.mem_nhds hy

theorem smoothAt_inv' {x : M} : SmoothAt I' I f.invFun (f x) :=
  f.smoothAt_inv <| mem_range_self x

theorem leftInverse : Function.LeftInverse f.invFun f :=
  left_inv f

theorem injective : Function.Injective f :=
  f.leftInverse.injective

protected theorem continuous : Continuous f :=
  f.smooth_to.continuous

theorem open_map : IsOpenMap f :=
  f.leftInverse.isOpenMap f.isOpen_range f.smooth_inv.continuousOn

theorem coe_comp_invFun_eventuallyEq (x : M) : f âˆ˜ f.invFun =á¶ [ğ“ (f x)] id :=
  Filter.eventually_of_mem (f.open_map.range_mem_nhds x) fun _ hy => f.right_inv hy

/- Note that we are slightly abusing the fact that `tangent_space I x` and
`tangent_space I (f.inv_fun (f x))` are both definitionally `E` below. -/
def fderiv (x : M) : TangentSpace I x â‰ƒL[ğ•œ] TangentSpace I' (f x) :=
  have hâ‚ : MDifferentiableAt I' I f.invFun (f x) :=
    ((f.smooth_inv (f x) (mem_range_self x)).mdifferentiableWithinAt le_top).mdifferentiableAt
      (f.open_map.range_mem_nhds x)
  have hâ‚‚ : MDifferentiableAt I I' f x := f.smooth_to.contMDiff.mdifferentiable le_top _
  ContinuousLinearEquiv.equivOfInverse (mfderiv I I' f x) (mfderiv I' I f.invFun (f x))
    (by
      intro v
      erw [â† ContinuousLinearMap.comp_apply, â† mfderiv_comp x hâ‚ hâ‚‚, f.invFun_comp_coe, mfderiv_id,
        ContinuousLinearMap.coe_id', id.def])
    (by
      intro v
      have hx : x = f.invFun (f x) := by rw [f.left_inv]
      have hx' : f (f.invFun (f x)) = f x := by rw [f.left_inv]
      rw [hx] at hâ‚‚
      erw [hx, hx', â† ContinuousLinearMap.comp_apply, â† mfderiv_comp (f x) hâ‚‚ hâ‚,
        ((hasMFDerivAt_id I' (f x)).congr_of_eventuallyEq
            (f.coe_comp_invFun_eventuallyEq x)).mfderiv,
        ContinuousLinearMap.coe_id', id.def])

@[simp]
theorem fderiv_coe (x : M) :
    (f.fderiv x : TangentSpace I x â†’L[ğ•œ] TangentSpace I' (f x)) = mfderiv I I' f x := by ext; rfl

@[simp]
theorem fderiv_symm_coe (x : M) :
    ((f.fderiv x).symm : TangentSpace I' (f x) â†’L[ğ•œ] TangentSpace I x) =
      mfderiv I' I f.invFun (f x) :=
  by ext; rfl

theorem fderiv_symm_coe' {x : M'} (hx : x âˆˆ range f) :
    ((f.fderiv (f.invFun x)).symm :
        TangentSpace I' (f (f.invFun x)) â†’L[ğ•œ] TangentSpace I (f.invFun x)) =
      (mfderiv I' I f.invFun x : TangentSpace I' x â†’L[ğ•œ] TangentSpace I (f.invFun x)) :=
  by rw [fderiv_symm_coe, f.right_inv hx]

open Filter

theorem openEmbedding : OpenEmbedding f :=
  openEmbedding_of_continuous_injective_open f.continuous f.injective f.open_map

theorem inducing : Inducing f :=
  f.openEmbedding.toInducing

theorem forall_near' {P : M â†’ Prop} {A : Set M'} (h : âˆ€á¶  m near f â»Â¹' A, P m) :
    âˆ€á¶  m' near A âˆ© range f, âˆ€ m, m' = f m â†’ P m :=
  by
  rw [eventually_nhdsSet_iff] at h âŠ¢
  rintro _ âŸ¨hfmâ‚€, mâ‚€, rflâŸ©
  have : âˆ€ U âˆˆ ğ“ mâ‚€, âˆ€á¶  m' in ğ“ (f mâ‚€), m' âˆˆ f '' U :=
    by
    intro U U_in
    exact f.open_map.image_mem_nhds U_in
  apply (this _ <| h mâ‚€ hfmâ‚€).mono
  rintro _ âŸ¨mâ‚€, hmâ‚€, hmâ‚€'âŸ© mâ‚ rfl
  rwa [â† f.injective hmâ‚€']

theorem eventually_nhdsSet_mono {Î± : Type _} [TopologicalSpace Î±] {s t : Set Î±} {P : Î± â†’ Prop}
    (h : âˆ€á¶  x near t, P x) (h' : s âŠ† t) : âˆ€á¶  x near s, P x :=
  h.filter_mono (nhdsSet_mono h')

-- TODO: optimize this proof which is probably more complicated than it needs to be
theorem forall_near [T2Space M'] {P : M â†’ Prop} {P' : M' â†’ Prop} {K : Set M} (hK : IsCompact K)
    {A : Set M'} (hP : âˆ€á¶  m near f â»Â¹' A, P m) (hP' : âˆ€á¶  m' near A, m' âˆ‰ f '' K â†’ P' m')
    (hPP' : âˆ€ m, P m â†’ P' (f m)) : âˆ€á¶  m' near A, P' m' :=
  by
  rw [show A = A âˆ© range f âˆª A âˆ© (range f)á¶œ by simp]
  apply Filter.Eventually.union
  Â· have : âˆ€á¶  m' near A âˆ© range f, m' âˆˆ range f :=
      f.isOpen_range.forall_near_mem_of_subset (inter_subset_right _ _)
    apply (this.and <| f.forall_near' hP).mono
    rintro _ âŸ¨âŸ¨m, rflâŸ©, hmâŸ©
    exact hPP' _ (hm _ rfl)
  Â· have op : IsOpen ((f '' K)á¶œ) := by
      rw [isOpen_compl_iff]
      exact (hK.image f.continuous).isClosed
    have : A âˆ© (range f)á¶œ âŠ† A âˆ© (f '' K)á¶œ :=
      inter_subset_inter_right _ (compl_subset_compl.mpr (image_subset_range f K))
    apply eventually_nhdsSet_mono _ this
    rw [eventually_nhdsSet_iff] at hP' âŠ¢
    rintro x âŸ¨hx, hx'âŸ©
    have hx' : âˆ€á¶  y in ğ“ x, y âˆˆ (f '' K)á¶œ := isOpen_iff_eventually.mp op x hx'
    apply ((hP' x hx).and hx').mono
    rintro y âŸ¨hy, hy'âŸ©
    exact hy hy'

variable (I M)

-- unused
/-- The identity map is a smooth open embedding. -/
@[simps]
nonrec def id : OpenSmoothEmbedding I M I M where
  toFun := id
  invFun := id
  left_inv' := rfl
  isOpen_range := IsOpenMap.id.isOpen_range
  smooth_to := smooth_id
  smooth_inv := smoothOn_id

variable {I M}

/- -- unused
@[simps!]
def comp {E'' : Type _} [NormedAddCommGroup E''] [NormedSpace ğ•œ E''] {H'' : Type _}
    [TopologicalSpace H''] {I'' : ModelWithCorners ğ•œ E'' H''} {M'' : Type _} [TopologicalSpace M'']
    [ChartedSpace H'' M''] [SmoothManifoldWithCorners I'' M'']
    (g : OpenSmoothEmbedding I' M' I'' M'') (f : OpenSmoothEmbedding I M I' M') :
    OpenSmoothEmbedding I M I'' M'' where
  toFun := g âˆ˜ f
  invFun := f.invFun âˆ˜ g.invFun
  left_inv' x := by simp only [Function.comp_apply, left_inv]
  isOpen_range := (g.open_map.comp f.open_map).isOpen_range
  smooth_to := g.smooth_to.comp f.smooth_to
  smooth_inv :=
    (f.smooth_inv.comp' g.smooth_inv).mono
      (by
        change range (g âˆ˜ f) âŠ† range g âˆ© g.inv_fun â»Â¹' range f
        refine' subset_inter_iff.mpr âŸ¨range_comp_subset_range f g, _âŸ©
        rintro x' âŸ¨x, rflâŸ©
        exact âŸ¨x, by simp only [left_inv]âŸ©) -/

end OpenSmoothEmbedding

namespace ContinuousLinearEquiv

variable (e : E â‰ƒL[ğ•œ] E') [CompleteSpace E] [CompleteSpace E']

-- unused
@[simps]
def toOpenSmoothEmbedding : OpenSmoothEmbedding ğ“˜(ğ•œ, E) E ğ“˜(ğ•œ, E') E'
    where
  toFun := e
  invFun := e.symm
  left_inv' {x} := e.symm_apply_apply x
  isOpen_range := e.isOpenMap.isOpen_range
  smooth_to := (e : E â†’L[ğ•œ] E').contMDiff
  smooth_inv := (e.symm : E' â†’L[ğ•œ] E).contMDiff.contMDiffOn

end ContinuousLinearEquiv

end General

section WithoutBoundary

open Metric hiding mem_nhds_iff

open Function

universe u

section GeneralNonsense

variable {ğ•œ E H M : Type _} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  [TopologicalSpace H] {I : ModelWithCorners ğ•œ E H} [TopologicalSpace M] [ChartedSpace H M]
  [SmoothManifoldWithCorners I M] {x : M} {n : â„•âˆ}

theorem extChartAt_target_eq_image_chart_target :
    (extChartAt I x).target = I '' (chartAt H x).target :=
  by
  erw [(chartAt H x).toLocalEquiv.trans_target'' I.toLocalEquiv, I.source_eq, univ_inter]
  rfl

@[simp]
theorem modelWithCornersSelf.extChartAt {e : E} : extChartAt ğ“˜(ğ•œ, E) e = LocalEquiv.refl E := by
  simp

theorem contMDiffOn_ext_chart_symm :
    ContMDiffOn ğ“˜(ğ•œ, E) I n (extChartAt I x).symm (extChartAt I x).target :=
  by
  have hs : (extChartAt I x).target âŠ† (chartAt E (extChartAt I x x)).source := by
    simp only [subset_univ, mfld_simps]
  have h2s : MapsTo (extChartAt I x).symm (extChartAt I x).target (chartAt H x).source := by
    rw [â† extChartAt_source I]; exact (extChartAt I x).symm_mapsTo
  refine' (contMDiffOn_iff_of_subset_source hs h2s).mpr âŸ¨continuousOn_extChartAt_symm I x, _âŸ©
  simp_rw [modelWithCornersSelf.extChartAt, LocalEquiv.refl_symm, LocalEquiv.refl_coe,
    comp.right_id, id.def, image_id']
  exact (contDiffOn_congr fun y hy => (extChartAt I x).right_inv hy).mpr contDiffOn_id

end GeneralNonsense

variable {F H : Type _} (M : Type u) [NormedAddCommGroup F] [NormedSpace â„ F] [TopologicalSpace H]
  [TopologicalSpace M] [ChartedSpace H M] [T2Space M] [LocallyCompactSpace M] [SigmaCompactSpace M]
  (IF : ModelWithCorners â„ F H) [SmoothManifoldWithCorners IF M]

/- Clearly should be generalised. Maybe what we really want is a theory of local diffeomorphisms.

Note that the input `f` is morally an `open_smooth_embedding` but stated in terms of `cont_diff`
instead of `cont_mdiff`. This is more convenient for our purposes. -/
def openSmoothEmbOfDiffeoSubsetChartTarget (x : M) {f : LocalHomeomorph F F} (hfâ‚ : f.source = univ)
    (hfâ‚‚ : ContDiff â„ âˆ f) (hfâ‚ƒ : ContDiffOn â„ âˆ f.symm f.target)
    (hfâ‚„ : range f âŠ† IF '' (chartAt H x).target) : OpenSmoothEmbedding ğ“˜(â„, F) F IF M
    where
  toFun := (extChartAt IF x).symm âˆ˜ f
  invFun := f.invFun âˆ˜ extChartAt IF x
  left_inv' {y} := by
    obtain âŸ¨z, hz, hz'âŸ© := hfâ‚„ (mem_range_self y)
    have aux : f.symm (IF z) = y := by rw [hz']; exact f.left_inv (hfâ‚.symm â–¸ mem_univ _)
    simp only [â† hz', (chartAt H x).right_inv hz, aux, extChartAt, LocalHomeomorph.extend,
      LocalEquiv.coe_trans, LocalHomeomorph.invFun_eq_coe, ModelWithCorners.toLocalEquiv_coe,
      LocalHomeomorph.coe_coe, LocalEquiv.coe_trans_symm, LocalHomeomorph.coe_coe_symm,
      ModelWithCorners.left_inv, ModelWithCorners.toLocalEquiv_coe_symm, Function.comp_apply, aux]
  isOpen_range :=
    IsOpenMap.isOpen_range fun u hu =>
      by
      have aux : IsOpen (f '' u) := f.image_open_of_open hu (hfâ‚.symm â–¸ subset_univ u)
      convert isOpen_extChartAt_preimage' IF x aux
      rw [image_comp]
      refine'
        (extChartAt IF x).symm_image_eq_source_inter_preimage ((image_subset_range f u).trans _)
      rw [extChartAt_target_eq_image_chart_target]
      exact hfâ‚„
  smooth_to :=
    by
    refine' contMDiffOn_ext_chart_symm.comp_contMDiff hfâ‚‚.contMDiff fun y => _
    rw [extChartAt_target_eq_image_chart_target]
    exact hfâ‚„ (mem_range_self y)
  smooth_inv := by
    rw [â† extChartAt_target_eq_image_chart_target] at hfâ‚„
    have hf' : range ((extChartAt IF x).symm âˆ˜ f) âŠ† extChartAt IF x â»Â¹' f.target :=
      by
      rw [range_comp, â† image_subset_iff, â† f.image_source_eq_target, hfâ‚, image_univ]
      exact (LocalEquiv.image_symm_image_of_subset_target _ hfâ‚„).subset
    have hf'' : range ((extChartAt IF x).symm âˆ˜ f) âŠ† (chartAt H x).source :=
      by
      rw [â† extChartAt_source IF, range_comp, â† LocalEquiv.symm_image_target_eq_source]
      exact (monotone_image hfâ‚„).trans Subset.rfl
    exact hfâ‚ƒ.contMDiffOn.comp (contMDiffOn_extChartAt.mono hf'') hf'

@[simp]
theorem coe_openSmoothEmbOfDiffeoSubsetChartTarget (x : M) {f : LocalHomeomorph F F}
    (hfâ‚ : f.source = univ) (hfâ‚‚ : ContDiff â„ âˆ f) (hfâ‚ƒ : ContDiffOn â„ âˆ f.symm f.target)
    (hfâ‚„ : range f âŠ† IF '' (chartAt H x).target) :
    (openSmoothEmbOfDiffeoSubsetChartTarget M IF x hfâ‚ hfâ‚‚ hfâ‚ƒ hfâ‚„ : F â†’ M) =
      (extChartAt IF x).symm âˆ˜ f :=
  by simp [openSmoothEmbOfDiffeoSubsetChartTarget]

theorem range_openSmoothEmbOfDiffeoSubsetChartTarget (x : M) {f : LocalHomeomorph F F}
    (hfâ‚ : f.source = univ) (hfâ‚‚ : ContDiff â„ âˆ f) (hfâ‚ƒ : ContDiffOn â„ âˆ f.symm f.target)
    (hfâ‚„ : range f âŠ† IF '' (chartAt H x).target) :
    range (openSmoothEmbOfDiffeoSubsetChartTarget M IF x hfâ‚ hfâ‚‚ hfâ‚ƒ hfâ‚„) =
      (extChartAt IF x).symm '' range f :=
  by rw [coe_openSmoothEmbOfDiffeoSubsetChartTarget, range_comp]

variable {M} (F)
variable [ModelWithCorners.Boundaryless IF] [FiniteDimensional â„ F]

theorem nice_atlas' {Î¹ : Type _} {s : Î¹ â†’ Set M} (s_op : âˆ€ j, IsOpen <| s j)
    (cov : (â‹ƒ j, s j) = univ) (U : Set F) (hUâ‚ : (0 : F) âˆˆ U) (hUâ‚‚ : IsOpen U) :
    âˆƒ (Î¹' : Type u) (t : Set Î¹') (Ï† : t â†’ OpenSmoothEmbedding ğ“˜(â„, F) F IF M),
      t.Countable âˆ§
        (âˆ€ i, âˆƒ j, range (Ï† i) âŠ† s j) âˆ§
          (LocallyFinite fun i => range (Ï† i)) âˆ§ (â‹ƒ i, Ï† i '' U) = univ :=
  by
  set W : M â†’ â„ â†’ Set M := fun x r =>
    (extChartAt IF x).symm âˆ˜ diffeomorphToNhd (extChartAt IF x x) r '' U with W_def
  let B : M â†’ â„ â†’ Set M := ChartedSpace.ball IF
  let p : M â†’ â„ â†’ Prop := fun x r =>
    0 < r âˆ§ ball (extChartAt IF x x) r âŠ† (extChartAt IF x).target âˆ§ âˆƒ j, B x r âŠ† s j
  have hWâ‚€ : âˆ€ x r, p x r â†’ x âˆˆ W x r := fun x r h => âŸ¨0, hUâ‚, by simp [h.1]âŸ©
  have hWâ‚ : âˆ€ x r, p x r â†’ IsOpen (W x r) :=
    by
    rintro x r âŸ¨hâ‚, hâ‚‚, -, -âŸ©
    simp only [W_def]
    rw [image_comp]
    let V := diffeomorphToNhd (extChartAt IF x x) r '' U
    change IsOpen ((extChartAt IF x).symm '' V)
    have hVâ‚ : IsOpen V :=
      ((diffeomorphToNhd (extChartAt IF x x) r).isOpen_image_iff_of_subset_source (by simp)).mp hUâ‚‚
    have hVâ‚‚ : V âŠ† (extChartAt IF x).target :=
      Subset.trans ((image_subset_range _ _).trans (by simp [hâ‚])) hâ‚‚
    rw [(extChartAt IF x).symm_image_eq_source_inter_preimage hVâ‚‚]
    exact isOpen_extChartAt_preimage' IF x hVâ‚
  have hB : âˆ€ x, (ğ“ x).HasBasis (p x) (B x) := fun x =>
    ChartedSpace.nhds_hasBasis_balls_of_open_cov IF x s_op cov
  obtain âŸ¨t, htâ‚, htâ‚‚, htâ‚ƒ, htâ‚„âŸ© := exists_countable_locallyFinite_cover surjective_id hWâ‚€ hWâ‚ hB
  let g : M Ã— â„ â†’ LocalHomeomorph F F := fun z => diffeomorphToNhd (extChartAt IF z.1 z.1) z.2
  have hgâ‚ : âˆ€ z, (g z).source = univ := by simp
  have hgâ‚‚ : âˆ€ z, ContDiff â„ âˆ (g z) := by simp
  have hgâ‚ƒ : âˆ€ z, ContDiffOn â„ âˆ (g z).symm (g z).target := by simp
  refine'
    âŸ¨M Ã— â„, t, fun z =>
      openSmoothEmbOfDiffeoSubsetChartTarget M IF z.1.1 (hgâ‚ z.1) (hgâ‚‚ z.1) (hgâ‚ƒ z.1) _, htâ‚,
      fun z => _, _, _âŸ©
  Â· obtain âŸ¨âŸ¨x, râŸ©, hxrâŸ© := z
    obtain âŸ¨hr : 0 < r, hr' : ball (extChartAt IF x x) r âŠ† _, -âŸ© := htâ‚‚ _ hxr
    rw [â† extChartAt_target_eq_image_chart_target]
    exact (range_diffeomorphToNhd_subset_ball (extChartAt IF x x) hr).trans hr'
  Â· obtain âŸ¨âŸ¨x, râŸ©, hxrâŸ© := z
    obtain âŸ¨hr : 0 < r, -, j, hj : B x r âŠ† s jâŸ© := htâ‚‚ _ hxr
    simp_rw [range_openSmoothEmbOfDiffeoSubsetChartTarget]
    exact âŸ¨j, (monotone_image (range_diffeomorphToNhd_subset_ball _ hr)).trans hjâŸ©
  Â· simp_rw [range_openSmoothEmbOfDiffeoSubsetChartTarget]
    refine' htâ‚„.subset _
    rintro âŸ¨âŸ¨x, râŸ©, hxrâŸ©
    obtain âŸ¨hr : 0 < r, -, -âŸ© := htâ‚‚ _ hxr
    exact monotone_image (range_diffeomorphToNhd_subset_ball _ hr)
  Â· simpa only [iUnion_coe_set] using htâ‚ƒ

variable [Nonempty M]

theorem nice_atlas {Î¹ : Type _} {s : Î¹ â†’ Set M} (s_op : âˆ€ j, IsOpen <| s j)
    (cov : (â‹ƒ j, s j) = univ) :
    âˆƒ n,
      âˆƒ Ï† : IndexType n â†’ OpenSmoothEmbedding ğ“˜(â„, F) F IF M,
        (âˆ€ i, âˆƒ j, range (Ï† i) âŠ† s j) âˆ§
          (LocallyFinite fun i => range (Ï† i)) âˆ§ (â‹ƒ i, Ï† i '' ball 0 1) = univ :=
  by
  obtain âŸ¨Î¹', t, Ï†, hâ‚, hâ‚‚, hâ‚ƒ, hâ‚„âŸ© := nice_atlas' F IF s_op cov (ball 0 1) (by simp) isOpen_ball
  have htne : t.Nonempty := by
    by_contra contra
    simp only [iUnion_coe_set, not_nonempty_iff_eq_empty.mp contra, mem_empty_iff_false,
      iUnion_of_empty, iUnion_empty, @eq_comm _ _ univ, univ_eq_empty_iff] at hâ‚„
    exact not_isEmpty_of_nonempty M hâ‚„
  obtain âŸ¨n, âŸ¨fnâŸ©âŸ© := (Set.countable_iff_exists_nonempty_indexType_equiv htne).mp hâ‚
  refine' âŸ¨n, Ï† âˆ˜ fn, fun i => hâ‚‚ (fn i), hâ‚ƒ.comp_injective fn.injective, _âŸ©
  erw [fn.surjective.iUnion_comp fun i => Ï† i '' ball 0 1, hâ‚„]

end WithoutBoundary

namespace OpenSmoothEmbedding

section Updating

variable {ğ•œ EX EM EY EN EM' X M Y N M' : Type _} [NontriviallyNormedField ğ•œ] [NormedAddCommGroup EX]
  [NormedSpace ğ•œ EX] [NormedAddCommGroup EM] [NormedSpace ğ•œ EM] [NormedAddCommGroup EM']
  [NormedSpace ğ•œ EM'] [NormedAddCommGroup EY] [NormedSpace ğ•œ EY] [NormedAddCommGroup EN]
  [NormedSpace ğ•œ EN] {HX : Type _} [TopologicalSpace HX] {IX : ModelWithCorners ğ•œ EX HX}
  {HY : Type _} [TopologicalSpace HY] {IY : ModelWithCorners ğ•œ EY HY} {HM : Type _}
  [TopologicalSpace HM] {IM : ModelWithCorners ğ•œ EM HM} {HM' : Type _} [TopologicalSpace HM']
  {IM' : ModelWithCorners ğ•œ EM' HM'} {HN : Type _} [TopologicalSpace HN]
  {IN : ModelWithCorners ğ•œ EN HN} [TopologicalSpace X] [ChartedSpace HX X]
  [SmoothManifoldWithCorners IX X] [TopologicalSpace M] [ChartedSpace HM M]
  [SmoothManifoldWithCorners IM M] [TopologicalSpace M'] [ChartedSpace HM' M']

section NonMetric

variable [TopologicalSpace Y] [ChartedSpace HY Y] [SmoothManifoldWithCorners IY Y]
  [TopologicalSpace N] [ChartedSpace HN N] [SmoothManifoldWithCorners IN N]
  (Ï† : OpenSmoothEmbedding IX X IM M) (Ïˆ : OpenSmoothEmbedding IY Y IN N) (f : M â†’ N) (g : X â†’ Y)

section

attribute [local instance] Classical.dec

/-- This is definition `def:update` in the blueprint. -/
def update (m : M) : N :=
  if m âˆˆ range Ï† then Ïˆ (g (Ï†.invFun m)) else f m

end

@[simp]
theorem update_of_nmem_range {m : M} (hm : m âˆ‰ range Ï†) : update Ï† Ïˆ f g m = f m := if_neg hm

@[simp]
theorem update_of_mem_range {m : M} (hm : m âˆˆ range Ï†) : update Ï† Ïˆ f g m = Ïˆ (g (Ï†.invFun m)) :=
  if_pos hm

@[simp]
theorem update_apply_embedding (x : X) : update Ï† Ïˆ f g (Ï† x) = Ïˆ (g x) := by simp [update]

-- This small auxiliary result is used in the next two lemmas.
theorem nice_update_of_eq_outside_compact_aux {K : Set X} (g : X â†’ Y)
    (hg : âˆ€ x : X, x âˆ‰ K â†’ f (Ï† x) = Ïˆ (g x)) {m : M} (hm : m âˆ‰ Ï† '' K) : Ï†.update Ïˆ f g m = f m :=
  by
  by_cases hm' : m âˆˆ range Ï†
  Â· obtain âŸ¨x, rflâŸ© := hm'
    replace hm : x âˆ‰ K; Â· contrapose! hm; exact mem_image_of_mem Ï† hm
    simp [hg x hm]
  Â· exact if_neg hm'

open Function

/-- This is lemma `lem:smooth_updating` in the blueprint. -/
theorem smooth_update (f : M' â†’ M â†’ N) (g : M' â†’ X â†’ Y) {k : M' â†’ M} {K : Set X}
    (hK : IsClosed (Ï† '' K)) (hf : Smooth (IM'.prod IM) IN (uncurry f))
    (hg : Smooth (IM'.prod IX) IY (uncurry g)) (hk : Smooth IM' IM k)
    (hg' : âˆ€ y x, x âˆ‰ K â†’ f y (Ï† x) = Ïˆ (g y x)) :
    Smooth IM' IN fun x => update Ï† Ïˆ (f x) (g x) (k x) :=
  by
  have hK' : âˆ€ x, k x âˆ‰ Ï† '' K â†’ update Ï† Ïˆ (f x) (g x) (k x) = f x (k x) := fun x hx =>
    nice_update_of_eq_outside_compact_aux Ï† Ïˆ (f x) (g x) (hg' x) hx
  refine' contMDiff_of_locally_contMDiffOn fun x => _
  let U := range Ï†
  let V := (Ï† '' K)á¶œ
  have hâ‚‚ : IsOpen (k â»Â¹' V) := hK.isOpen_compl.preimage hk.continuous
  have hâ‚ƒ : V âˆª U = univ := by
    rw [â† compl_subset_iff_union, compl_compl]
    exact image_subset_range Ï† K
  have hâ‚„ : âˆ€ x, k x âˆˆ U â†’ update Ï† Ïˆ (f x) (g x) (k x) = (Ïˆ âˆ˜ g x âˆ˜ Ï†.invFun) (k x) := fun m hm =>
    by intros ; exact if_pos hm
  by_cases hx : k x âˆˆ U
  Â·
    refine'
      âŸ¨k â»Â¹' U, Ï†.isOpen_range.preimage hk.continuous, hx,
        (contMDiffOn_congr hâ‚„).mpr <|
          Ïˆ.smooth_to.comp_contMDiffOn <|
            hg.comp_contMDiffOn
              (smoothOn_id.prod_mk <| Ï†.smooth_inv.comp hk.smoothOn Subset.rfl)âŸ©
  Â· refine'
      âŸ¨k â»Â¹' V, hâ‚‚, _, (contMDiffOn_congr hK').mpr (hf.comp (smooth_id.prod_mk hk)).contMDiffOnâŸ©
    exact ((Set.ext_iff.mp hâ‚ƒ (k x)).mpr trivial).resolve_right hx

end NonMetric

section Metric

variable [MetricSpace Y] [ChartedSpace HY Y] [SmoothManifoldWithCorners IY Y] [MetricSpace N]
  [ChartedSpace HN N] [SmoothManifoldWithCorners IN N] (Ï† : OpenSmoothEmbedding IX X IM M)
  (Ïˆ : OpenSmoothEmbedding IY Y IN N) (f : M â†’ N) (g : X â†’ Y)

/-- This is `lem:dist_updating` in the blueprint. -/
theorem dist_update [ProperSpace Y] {K : Set X} (hK : IsCompact K) {P : Type _} [MetricSpace P]
    {KP : Set P} (hKP : IsCompact KP) (f : P â†’ M â†’ N) (hf : Continuous â†¿f)
    (hf' : âˆ€ p, f p '' range Ï† âŠ† range Ïˆ) {Îµ : M â†’ â„} (hÎµ : âˆ€ m, 0 < Îµ m) (hÎµ' : Continuous Îµ) :
    âˆƒ Î· > (0 : â„),
      âˆ€ g : P â†’ X â†’ Y,
        âˆ€ p âˆˆ KP,
          âˆ€ p' âˆˆ KP,
            âˆ€ x âˆˆ K,
              dist (g p' x) (Ïˆ.invFun (f p (Ï† x))) < Î· â†’
                dist (update Ï† Ïˆ (f p') (g p') <| Ï† x) (f p <| Ï† x) < Îµ (Ï† x) :=
  by
  let F : P Ã— X â†’ Y := fun q => (Ïˆ.invFun âˆ˜ f q.1 âˆ˜ Ï†) q.2
  let Kâ‚ := Metric.cthickening 1 (F '' KP.prod K)
  have hKâ‚ : IsCompact Kâ‚ :=
    by
    refine'
      Metric.isCompact_of_isClosed_isBounded Metric.isClosed_cthickening
        (Bornology.IsBounded.cthickening <| IsCompact.isBounded <| _)
    apply (hKP.prod hK).image
    exact
      Ïˆ.smooth_inv.continuousOn.comp_continuous
        (hf.comp <| continuous_fst.prod_mk <| Ï†.continuous.comp continuous_snd) fun q =>
        hf' q.1 âŸ¨Ï† q.2, mem_range_self _, rflâŸ©
  have hâ‚ : UniformContinuousOn Ïˆ Kâ‚ :=
    hKâ‚.uniformContinuousOn_of_continuous Ïˆ.continuous.continuousOn
  have hÎµÏ† : âˆ€ x âˆˆ K, 0 < (Îµ âˆ˜ Ï†) x := fun x _hx => hÎµ _
  obtain âŸ¨Îµâ‚€, hÎµâ‚€, hÎµâ‚€'âŸ© := hK.exists_forall_le' (hÎµ'.comp Ï†.continuous).continuousOn hÎµÏ†
  obtain âŸ¨Ï„, hÏ„ : 0 < Ï„, hÏ„'âŸ© := Metric.uniformContinuousOn_iff.mp hâ‚ Îµâ‚€ hÎµâ‚€
  refine' âŸ¨min Ï„ 1, by simp [hÏ„], fun g p hp p' _hp' x hx hÎ· => _âŸ©
  cases' lt_min_iff.mp hÎ· with H H'
  specialize hÎµÏ† x hx
  apply lt_of_lt_of_le _ (hÎµâ‚€' x hx); clear hÎµâ‚€'
  simp only [update_apply_embedding]
  have hâ‚ : g p' x âˆˆ Kâ‚ :=
    Metric.mem_cthickening_of_dist_le (g p' x) (F (p, x)) 1 _ âŸ¨(p, x), âŸ¨hp, hxâŸ©, rflâŸ© H'.le
  have hâ‚‚ : f p (Ï† x) âˆˆ range Ïˆ := hf' p âŸ¨Ï† x, mem_range_self _, rflâŸ©
  rw [â† Ïˆ.right_inv hâ‚‚]
  exact hÏ„' _ hâ‚ _ (Metric.self_subset_cthickening _ âŸ¨(p, x), âŸ¨hp, hxâŸ©, rflâŸ©) H

end Metric

end Updating

end OpenSmoothEmbedding
