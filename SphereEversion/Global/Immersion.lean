import Mathlib.Geometry.Manifold.Instances.Sphere
import SphereEversion.ToMathlib.LinearAlgebra.FiniteDimensional
import SphereEversion.ToMathlib.Analysis.InnerProductSpace.Rotation
import SphereEversion.Global.Gromov
import SphereEversion.Global.TwistOneJetSec

-- set_option trace.filter_inst_type true
noncomputable section

open Metric FiniteDimensional Set Function LinearMap Filter ContinuousLinearMap

open scoped Manifold Topology

section General

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners â„ E H) {M : Type*} [TopologicalSpace M]
  [ChartedSpace H M] [SmoothManifoldWithCorners I M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners â„ E' H')
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] {G : Type*} [TopologicalSpace G]
  (J : ModelWithCorners â„ F G)
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] [SmoothManifoldWithCorners J N]

local notation "TM" => TangentSpace I

local notation "TM'" => TangentSpace I'

local notation "HJ" => ModelProd (ModelProd H H') (E â†’L[â„] E')

local notation "ÏˆJ" => chartAt HJ

/-- A map between manifolds is an immersion if it is differentiable and its differential at
any point is injective. Note the formalized definition doesn't require differentiability.
If `f` is not differentiable at `m` then, by definition, `mfderiv I I' f m` is zero, which
is not injective unless the source dimension is zero, which implies differentiability. -/
def Immersion (f : M â†’ M') : Prop :=
  âˆ€ m, Injective (mfderiv I I' f m)

variable (M M')

/-- The relation of immersions for maps between two manifolds. -/
def immersionRel : RelMfld I M I' M' :=
  {Ïƒ | Injective Ïƒ.2}

variable {M M'}

@[simp]
theorem mem_immersionRel_iff {Ïƒ : OneJetBundle I M I' M'} :
    Ïƒ âˆˆ immersionRel I M I' M' â†” Injective (Ïƒ.2 : TangentSpace I _ â†’L[â„] TangentSpace I' _) :=
  Iff.rfl

/-- A characterisation of the immersion relation in terms of a local chart. -/
theorem mem_immersionRel_iff' {Ïƒ Ïƒ' : OneJetBundle I M I' M'} (hÏƒ' : Ïƒ' âˆˆ (ÏˆJ Ïƒ).source) :
    Ïƒ' âˆˆ immersionRel I M I' M' â†” Injective (ÏˆJ Ïƒ Ïƒ').2 := by
  simp_rw [mem_immersionRel_iff]
  rw [oneJetBundle_chartAt_apply, inCoordinates_eq]
  simp_rw [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, EquivLike.comp_injective,
    EquivLike.injective_comp]
  exacts [hÏƒ'.1.1, hÏƒ'.1.2]

theorem chartAt_image_immersionRel_eq {Ïƒ : OneJetBundle I M I' M'} :
    ÏˆJ Ïƒ '' ((ÏˆJ Ïƒ).source âˆ© immersionRel I M I' M') = (ÏˆJ Ïƒ).target âˆ© {q : HJ | Injective q.2} :=
  PartialEquiv.IsImage.image_eq fun _Ïƒ' hÏƒ' â†¦ (mem_immersionRel_iff' I I' hÏƒ').symm

variable [FiniteDimensional â„ E] [FiniteDimensional â„ E']

theorem immersionRel_open : IsOpen (immersionRel I M I' M') := by
  simp_rw [ChartedSpace.isOpen_iff HJ (immersionRel I M I' M'), chartAt_image_immersionRel_eq]
  refine fun Ïƒ â†¦ (ÏˆJ Ïƒ).open_target.inter ?_
  convert isOpen_univ.prod ContinuousLinearMap.isOpen_injective
  Â· ext x
    -- Porting note: `mem_prod` is a simp lemma, but the next line is still needed.
    rw [mem_prod]
    simp
  Â· infer_instance
  Â· infer_instance

@[simp]
theorem immersionRel_slice_eq {m : M} {m' : M'} {p : DualPair <| TangentSpace I m}
    {Ï† : TangentSpace I m â†’L[â„] TangentSpace I' m'} (hÏ† : Injective Ï†) :
    (immersionRel I M I' M').slice âŸ¨(m, m'), Ï†âŸ© p = ((ker p.Ï€).map Ï† : Set <| TM' m')á¶œ :=
  Set.ext_iff.mpr fun _ â†¦ p.injective_update_iff hÏ†

theorem immersionRel_ample (h : finrank â„ E < finrank â„ E') : (immersionRel I M I' M').Ample := by
  rw [RelMfld.ample_iff]
  rintro âŸ¨âŸ¨m, m'âŸ©, Ï† : TangentSpace I m â†’L[â„] TangentSpace I' m'âŸ© (p : DualPair (TangentSpace I m))
    (hÏ† : Injective Ï†)
  haveI : FiniteDimensional â„ (TangentSpace I m) := (by infer_instance : FiniteDimensional â„ E)
  have hcodim := two_le_rank_of_rank_lt_rank p.ker_pi_ne_top h Ï†.toLinearMap
  rw [immersionRel_slice_eq I I' hÏ†]
  exact AmpleSet.of_two_le_codim hcodim

/-- This is lemma `lem:open_ample_immersion` from the blueprint. -/
theorem immersionRel_open_ample (h : finrank â„ E < finrank â„ E') :
    IsOpen (immersionRel I M I' M') âˆ§ (immersionRel I M I' M').Ample :=
  âŸ¨immersionRel_open I I', immersionRel_ample I I' hâŸ©

end General

section Generalbis

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E] {H : Type*}
  [TopologicalSpace H] (I : ModelWithCorners â„ E H) [ModelWithCorners.Boundaryless I] {M : Type*}
  [TopologicalSpace M] [ChartedSpace H M] [SmoothManifoldWithCorners I M] {E' : Type*}
  [NormedAddCommGroup E'] [NormedSpace â„ E'] [FiniteDimensional â„ E'] {H' : Type*}
  [TopologicalSpace H'] (I' : ModelWithCorners â„ E' H') [ModelWithCorners.Boundaryless I']
  {M' : Type*} [MetricSpace M'] [ChartedSpace H' M'] [SmoothManifoldWithCorners I' M']

variable [FiniteDimensional â„ E] [FiniteDimensional â„ E']

variable {EP : Type*} [NormedAddCommGroup EP] [NormedSpace â„ EP] [FiniteDimensional â„ EP]
  {HP : Type*} [TopologicalSpace HP] {IP : ModelWithCorners â„ EP HP}
  [ModelWithCorners.Boundaryless IP] {P : Type*} [TopologicalSpace P] [ChartedSpace HP P]
  [SmoothManifoldWithCorners IP P] {C : Set (P Ã— M)} {Îµ : M â†’ â„}

variable (M M' IP P)

/-- parametric h-principle for immersions. -/
theorem immersionRel_satisfiesHPrincipleWith
    [T2Space P] [SigmaCompactSpace P] [T2Space M] [SigmaCompactSpace M] [SigmaCompactSpace M']
    (h : finrank â„ E < finrank â„ E') (hC : IsClosed C) (hÎµ_pos : âˆ€ x, 0 < Îµ x)
    (hÎµ_cont : Continuous Îµ) : (immersionRel I M I' M').SatisfiesHPrincipleWith IP C Îµ :=
  (immersionRel_ample I I' h).satisfiesHPrincipleWith (immersionRel_open I I') hC hÎµ_pos hÎµ_cont

end Generalbis

section sphere_eversion

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E] [Fact (finrank â„ E = 3)]

set_option synthInstance.checkSynthOrder false
attribute [local instance] FiniteDimensional.of_fact_finrank_eq_succ
set_option synthInstance.checkSynthOrder true

local notation "ğ•ŠÂ²" => sphere (0 : E) 1

/- Maybe the next two lemmas won't be used directly, but they should be done first as
sanity checks. -/
theorem immersion_inclusion_sphere : Immersion (ğ“¡ 2) ğ“˜(â„, E) fun x : ğ•ŠÂ² â†¦ (x : E) :=
  mfderiv_coe_sphere_injective

theorem immersion_antipodal_sphere : Immersion (ğ“¡ 2) ğ“˜(â„, E) fun x : ğ•ŠÂ² â†¦ -(x : E) := by
  intro x
  change Injective (mfderiv (ğ“¡ 2) ğ“˜(â„, E) (-fun x : ğ•ŠÂ² â†¦ (x : E)) x)
  rw [mfderiv_neg]
  exact neg_injective.comp (mfderiv_coe_sphere_injective x)

-- The relation of immersion of a two-sphere into its ambient Euclidean space.
local notation "ğ“¡_imm" => immersionRel (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E

variable (Ï‰ : Orientation â„ E (Fin 3))

theorem smooth_bs :
    Smooth (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, E) fun p : â„ Ã— ğ•ŠÂ² â†¦ (1 - p.1) â€¢ (p.2 : E) + p.1 â€¢ -(p.2: E) := by
  refine (ContMDiff.smul ?_ ?_).add (contMDiff_fst.smul ?_)
  Â· exact (contDiff_const.sub contDiff_id).contMDiff.comp contMDiff_fst
  Â· exact contMDiff_coe_sphere.comp contMDiff_snd
  Â· exact (contDiff_neg.contMDiff.comp contMDiff_coe_sphere).comp contMDiff_snd

def formalEversionAux : FamilyOneJetSec (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E ğ“˜(â„, â„) â„ :=
  familyJoin (smooth_bs E) <|
    familyTwist (drop (oneJetExtSec âŸ¨((â†‘) : ğ•ŠÂ² â†’ E), contMDiff_coe_sphereâŸ©))
      (fun p : â„ Ã— ğ•ŠÂ² â†¦ Ï‰.rot (p.1, p.2))
      (by
        intro p
        have : SmoothAt ğ“˜(â„, â„ Ã— E) ğ“˜(â„, E â†’L[â„] E) Ï‰.rot (p.1, p.2) := by
          refine (Ï‰.contDiff_rot ?_).contMDiffAt
          exact ne_zero_of_mem_unit_sphere p.2
        refine this.comp p (Smooth.smoothAt ?_)
        exact smooth_fst.prod_mk (contMDiff_coe_sphere.comp smooth_snd))

/-- A formal eversion of a two-sphere into its ambient Euclidean space. -/
def formalEversionAux2 : HtpyFormalSol ğ“¡_imm :=
  { formalEversionAux E Ï‰ with
    is_sol' := fun t x â†¦ (Ï‰.isometry_rot t x).injective.comp (mfderiv_coe_sphere_injective x) }

def formalEversion : HtpyFormalSol ğ“¡_imm :=
  (formalEversionAux2 E Ï‰).reindex âŸ¨smoothStep, contMDiff_iff_contDiff.mpr smoothStep.smoothâŸ©

@[simp]
theorem formalEversion_bs (t : â„) :
    (formalEversion E Ï‰ t).bs = fun x : ğ•ŠÂ² â†¦
      (1 - smoothStep t : â„) â€¢ (x : E) + (smoothStep t : â„) â€¢ (-x : E) :=
  rfl

theorem formalEversion_zero (x : ğ•ŠÂ²) : (formalEversion E Ï‰ 0).bs x = x := by simp

theorem formalEversion_one (x : ğ•ŠÂ²) : (formalEversion E Ï‰ 1).bs x = -x := by simp

theorem formalEversionHolAtZero {t : â„} (ht : t < 1 / 4) :
    (formalEversion E Ï‰ t).toOneJetSec.IsHolonomic := by
  intro x
  change
    mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ ((1 : â„) - smoothStep t) â€¢ (y : E) +
      smoothStep t â€¢ -(y : E)) x =
      (Ï‰.rot (smoothStep t, x)).comp (mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ (y : E)) x)
  simp_rw [smoothStep.of_lt ht, Ï‰.rot_zero, ContinuousLinearMap.id_comp]
  congr
  ext y
  simp [smoothStep.of_lt ht]

theorem formalEversionHolAtOne {t : â„} (ht : 3 / 4 < t) :
    (formalEversion E Ï‰ t).toOneJetSec.IsHolonomic := by
  intro x
  change
    mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ ((1 : â„) - smoothStep t) â€¢ (y : E) +
      smoothStep t â€¢ -(y : E)) x =
      (Ï‰.rot (smoothStep t, x)).comp (mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ (y : E)) x)
  trans mfderiv (ğ“¡ 2) ğ“˜(â„, E) (-fun y : ğ•ŠÂ² â†¦ (y : E)) x
  Â· congr 2
    ext y
    simp [smoothStep.of_gt ht]
  ext v
  erw [mfderiv_neg, ContinuousLinearMap.coe_comp', Function.comp_apply,
       ContinuousLinearMap.neg_apply, smoothStep.of_gt ht]
  rw [Ï‰.rot_one]; rfl
  rw [â† range_mfderiv_coe_sphere (n := 2) x]
  exact LinearMap.mem_range_self _ _

theorem formalEversion_hol_near_zero_one :
    âˆ€á¶  s : â„ Ã— ğ•ŠÂ² near {0, 1} Ã—Ë¢ univ, (formalEversion E Ï‰ s.1).toOneJetSec.IsHolonomicAt s.2 := by
  have : (Iio (1 / 4 : â„) âˆª Ioi (3 / 4)) Ã—Ë¢ (univ : Set ğ•ŠÂ²) âˆˆ ğ“Ë¢ (({0, 1} : Set â„) Ã—Ë¢ univ) := by
    refine ((isOpen_Iio.union isOpen_Ioi).prod isOpen_univ).mem_nhdsSet.mpr ?_
    rintro âŸ¨s, xâŸ© âŸ¨hs, hxâŸ©
    refine âŸ¨?_, mem_univ _âŸ©
    simp_rw [mem_insert_iff, mem_singleton_iff] at hs
    rcases hs with (rfl | rfl)
    Â· exact Or.inl (show (0 : â„) < 1 / 4 by norm_num)
    Â· exact Or.inr (show (3 / 4 : â„) < 1 by norm_num)
  refine eventually_of_mem this ?_
  rintro âŸ¨t, xâŸ© âŸ¨ht | ht, _hxâŸ©
  Â· exact formalEversionHolAtZero E Ï‰ ht x
  Â· exact formalEversionHolAtOne E Ï‰ ht x

theorem sphere_eversion :
    âˆƒ f : â„ â†’ ğ•ŠÂ² â†’ E,
      ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, E) âˆ (uncurry f) âˆ§
        (f 0 = fun x : ğ•ŠÂ² â†¦ (x : E)) âˆ§ (f 1 = fun x : ğ•ŠÂ² â†¦ -(x : E)) âˆ§
        âˆ€ t, Immersion (ğ“¡ 2) ğ“˜(â„, E) (f t) := by
  classical
  let Ï‰ : Orientation â„ E (Fin 3) :=
    ((stdOrthonormalBasis _ _).reindex <|
          finCongr (Fact.out : finrank â„ E = 3)).toBasis.orientation
  have rankE : finrank â„ E = 3 := Fact.out
  haveI : FiniteDimensional â„ E := FiniteDimensional.of_finrank_eq_succ rankE
  have ineq_rank : finrank â„ (EuclideanSpace â„ (Fin 2)) < finrank â„ E := by simp [rankE]
  let Îµ : ğ•ŠÂ² â†’ â„ := fun _ â†¦ 1
  have hÎµ_pos : âˆ€ x, 0 < Îµ x := fun _ â†¦ zero_lt_one
  have hÎµ_cont : Continuous Îµ := continuous_const
  haveI : Nontrivial E := nontrivial_of_finrank_eq_succ (Fact.out : finrank â„ E = 3)
  haveI : Nonempty (sphere 0 1 : Set E) :=
    (NormedSpace.sphere_nonempty.mpr zero_le_one).to_subtype
  rcases(immersionRel_satisfiesHPrincipleWith (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E ğ“˜(â„, â„) â„ ineq_rank
          ((Finite.isClosed (by simp : ({0, 1} : Set â„).Finite)).prod isClosed_univ) hÎµ_pos
          hÎµ_cont).bs
      (formalEversion E Ï‰) (formalEversion_hol_near_zero_one E Ï‰) with
    âŸ¨f, hâ‚, hâ‚‚, -, hâ‚…âŸ©
  have := hâ‚‚.forall_mem principal_le_nhdsSet
  refine âŸ¨f, hâ‚, ?_, ?_, hâ‚…âŸ©
  Â· ext x
    rw [this (0, x) (by simp)]
    convert formalEversion_zero E Ï‰ x
  Â· ext x
    rw [this (1, x) (by simp)]
    convert formalEversion_one E Ï‰ x

-- The next instance will be used in the main file
instance (n : â„•) : Fact (finrank â„ (EuclideanSpace â„ <| Fin n) = n) :=
  âŸ¨finrank_euclideanSpace_finâŸ©

-- The next notation will be used in the main file
notation "â„^" n:arg => EuclideanSpace â„ (Fin n)

end sphere_eversion
