import Mathlib.Analysis.Convex.AmpleSet
import Mathlib.Geometry.Manifold.Instances.Sphere
import SphereEversion.ToMathlib.LinearAlgebra.FiniteDimensional
import SphereEversion.ToMathlib.Geometry.Manifold.Immersion
import SphereEversion.ToMathlib.Analysis.InnerProductSpace.Rotation
import SphereEversion.Global.Gromov
import SphereEversion.Global.TwistOneJetSec

-- set_option trace.filter_inst_type true
noncomputable section

open Metric Module Set Function LinearMap Filter ContinuousLinearMap

open scoped Manifold Topology

-- Can't open ContDiff because `Ï‰` conflicts with the notation for orientation below
local notation "âˆ" => ((âŠ¤ : â„•âˆ) : WithTop â„•âˆ)

section General

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners â„ E H) {M : Type*} [TopologicalSpace M]
  [ChartedSpace H M] [IsManifold I âˆ M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E']
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners â„ E' H')
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M'] [IsManifold I' âˆ M']
  {F : Type*} [NormedAddCommGroup F] [NormedSpace â„ F] {G : Type*} [TopologicalSpace G]
  (J : ModelWithCorners â„ F G)
  (N : Type*) [TopologicalSpace N] [ChartedSpace G N] [IsManifold J âˆ N]

@[inherit_doc] local notation "TM" => TangentSpace I
@[inherit_doc] local notation "TM'" => TangentSpace I'
@[inherit_doc] local notation "HJ" => ModelProd (ModelProd H H') (E â†’L[â„] E')
@[inherit_doc] local notation "ÏˆJ" => chartAt HJ

variable (M M') in
/-- The relation of immersions for maps between two manifolds. -/
def immersionRel : RelMfld I M I' M' :=
  {Ïƒ | Injective Ïƒ.2}

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
@[simp]
theorem mem_immersionRel_iff {Ïƒ : OneJetBundle I M I' M'} :
    Ïƒ âˆˆ immersionRel I M I' M' â†” Injective (Ïƒ.2 : TangentSpace I _ â†’L[â„] TangentSpace I' _) :=
  Iff.rfl

/-- A characterisation of the immersion relation in terms of a local chart. -/
theorem mem_immersionRel_iff' {Ïƒ Ïƒ' : OneJetBundle I M I' M'} (hÏƒ' : Ïƒ' âˆˆ (ÏˆJ Ïƒ).source) :
    Ïƒ' âˆˆ immersionRel I M I' M' â†” Injective (ÏˆJ Ïƒ Ïƒ').2 := by
  simp_rw [mem_immersionRel_iff]
  rw [oneJetBundle_chartAt_apply, inCoordinates_eq]
  Â· simp_rw [ContinuousLinearMap.coe_comp', ContinuousLinearEquiv.coe_coe, EquivLike.comp_injective,
    EquivLike.injective_comp]
  exacts [hÏƒ'.1.1, hÏƒ'.1.2]

theorem chartAt_image_immersionRel_eq {Ïƒ : OneJetBundle I M I' M'} :
    ÏˆJ Ïƒ '' ((ÏˆJ Ïƒ).source âˆ© immersionRel I M I' M') = (ÏˆJ Ïƒ).target âˆ© {q : HJ | Injective q.2} :=
  PartialEquiv.IsImage.image_eq fun _Ïƒ' hÏƒ' â†¦ (mem_immersionRel_iff' I I' hÏƒ').symm

theorem immersionRel_open [FiniteDimensional â„ E] : IsOpen (immersionRel I M I' M') := by
  simp_rw [ChartedSpace.isOpen_iff HJ (immersionRel I M I' M'), chartAt_image_immersionRel_eq]
  refine fun Ïƒ â†¦ (ÏˆJ Ïƒ).open_target.inter ?_
  convert isOpen_univ.prod ContinuousLinearMap.isOpen_injective
  Â· ext x
    -- Porting note: `mem_prod` is a simp lemma, but the next line is still needed.
    rw [mem_prod]
    simp
  Â· infer_instance
  Â· infer_instance

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
@[simp]
theorem immersionRel_slice_eq {m : M} {m' : M'} {p : DualPair <| TangentSpace I m}
    {Ï† : TangentSpace I m â†’L[â„] TangentSpace I' m'} (hÏ† : Injective Ï†) :
    (immersionRel I M I' M').slice âŸ¨(m, m'), Ï†âŸ© p = ((ker p.Ï€).map Ï† : Set <| TM' m')á¶œ :=
  Set.ext_iff.mpr fun _ â†¦ p.injective_update_iff hÏ†

variable [FiniteDimensional â„ E] [FiniteDimensional â„ E']

omit [IsManifold I âˆ M] [IsManifold I' âˆ M'] in
theorem immersionRel_ample (h : finrank â„ E < finrank â„ E') : (immersionRel I M I' M').Ample := by
  rw [RelMfld.ample_iff]
  rintro âŸ¨âŸ¨m, m'âŸ©, Ï† : TangentSpace I m â†’L[â„] TangentSpace I' m'âŸ© (p : DualPair (TangentSpace I m))
    (hÏ† : Injective Ï†)
  haveI : FiniteDimensional â„ (TangentSpace I m) := (by infer_instance : FiniteDimensional â„ E)
  have hcodim := one_lt_rank_of_rank_lt_rank p.ker_pi_ne_top h Ï†.toLinearMap
  rw [immersionRel_slice_eq I I' hÏ†]
  exact AmpleSet.of_one_lt_codim hcodim

/-- This is lemma `lem:open_ample_immersion` from the blueprint. -/
theorem immersionRel_open_ample (h : finrank â„ E < finrank â„ E') :
    IsOpen (immersionRel I M I' M') âˆ§ (immersionRel I M I' M').Ample :=
  âŸ¨immersionRel_open I I', immersionRel_ample I I' hâŸ©

end General

section Generalbis

variable {E : Type*} [NormedAddCommGroup E] [NormedSpace â„ E] [FiniteDimensional â„ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners â„ E H) [I.Boundaryless]
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M] [IsManifold I âˆ M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E'] [FiniteDimensional â„ E']
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners â„ E' H') [I'.Boundaryless]
  {M' : Type*} [MetricSpace M'] [ChartedSpace H' M'] [IsManifold I' âˆ M']
  {EP : Type*} [NormedAddCommGroup EP] [NormedSpace â„ EP] [FiniteDimensional â„ EP]
  {HP : Type*} [TopologicalSpace HP] {IP : ModelWithCorners â„ EP HP} [IP.Boundaryless]
  {P : Type*} [TopologicalSpace P] [ChartedSpace HP P] [IsManifold IP âˆ P]
  {C : Set (P Ã— M)} {Îµ : M â†’ â„}

variable (M M' IP P)

/-- parametric h-principle for immersions. -/
theorem immersionRel_satisfiesHPrincipleWith
    [T2Space P] [SigmaCompactSpace P] [T2Space M] [SigmaCompactSpace M] [SigmaCompactSpace M']
    (h : finrank â„ E < finrank â„ E') (hC : IsClosed C) (hÎµ_pos : âˆ€ x, 0 < Îµ x)
    (hÎµ_cont : Continuous Îµ) : (immersionRel I M I' M').SatisfiesHPrincipleWith IP C Îµ :=
  (immersionRel_ample I I' h).satisfiesHPrincipleWith (immersionRel_open I I') hC hÎµ_pos hÎµ_cont

end Generalbis

/-! The inclusion and antipodal map on a sphere are immersions:
these results are not used directly, but are good sanity checks. -/
section sanitycheck

variable {n : â„•} (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E] [Fact (finrank â„ E = n + 1)]

/-- The inclusion of `ğ•Š^n` into `â„^{n+1}` is an immersion. -/
theorem immersion_inclusion_sphere : Immersion (ğ“¡ n) ğ“˜(â„, E)
    (fun x : sphere (0 : E) 1 â†¦ (x : E)) âˆ where
  contMDiff := contMDiff_coe_sphere.of_le le_top
  diff_injective := mfderiv_coe_sphere_injective

/-- The antipodal map on `ğ•Š^n âŠ† â„^{n+1}` is an immersion. -/
theorem immersion_antipodal_sphere : Immersion (ğ“¡ n) ğ“˜(â„, E)
    (fun x : sphere (0 : E) 1 â†¦ -(x : E)) âˆ where
  contMDiff :=
    -- Write this as the composition of `coe_sphere` and the antipodal map on `E`.
    -- The other direction elaborates much worse.
    (contDiff_neg.contMDiff).comp (contMDiff_coe_sphere.of_le le_top)
  diff_injective x := by
    change Injective (mfderiv (ğ“¡ n) ğ“˜(â„, E) (-fun x : sphere (0 : E) 1 â†¦ (x : E)) x)
    rw [mfderiv_neg]
    exact neg_injective.comp (mfderiv_coe_sphere_injective x)

end sanitycheck

section sphere_eversion

variable (E : Type*) [NormedAddCommGroup E] [InnerProductSpace â„ E] [Fact (finrank â„ E = 3)]

set_option synthInstance.checkSynthOrder false
attribute [local instance] FiniteDimensional.of_fact_finrank_eq_succ
set_option synthInstance.checkSynthOrder true

@[inherit_doc] local notation "ğ•ŠÂ²" => sphere (0 : E) 1

-- TODO: generalise these statements to `n` dimensions
-- the only obstacle is the construction of rotations requires working on â„Â³.

-- The relation of immersion of a two-sphere into its ambient Euclidean space.
@[inherit_doc] local notation "ğ“¡_imm" => immersionRel (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E

variable (Ï‰ : Orientation â„ E (Fin 3))

-- this result holds mutatis mutandis in `â„^n`
theorem smooth_bs :
    ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, E) âˆ
      fun p : â„ Ã— (sphere (0 : E) 1) â†¦ (1 - p.1) â€¢ (p.2 : E) + p.1 â€¢ -(p.2: E) := by
  refine (ContMDiff.smul ?_ ?_).add (contMDiff_fst.smul ?_)
  Â· exact (contDiff_const.sub contDiff_id).contMDiff.comp contMDiff_fst
  Â· exact (contMDiff_coe_sphere.of_le le_top).comp contMDiff_snd
  Â· exact (contDiff_neg.contMDiff.comp (contMDiff_coe_sphere.of_le le_top)).comp contMDiff_snd

def formalEversionAux : FamilyOneJetSec (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E ğ“˜(â„, â„) â„ :=
  familyJoin (smooth_bs E) <|
    familyTwist (drop (oneJetExtSec âŸ¨((â†‘) : ğ•ŠÂ² â†’ E), contMDiff_coe_sphere.of_le le_topâŸ©))
      (fun p : â„ Ã— ğ•ŠÂ² â†¦ Ï‰.rot (p.1, p.2))
      (by
        intro p
        have : ContMDiffAt ğ“˜(â„, â„ Ã— E) ğ“˜(â„, E â†’L[â„] E) âˆ Ï‰.rot (p.1, p.2) := by
          refine ((Ï‰.contDiff_rot ?_).of_le le_top).contMDiffAt
          exact ne_zero_of_mem_unit_sphere p.2
        apply this.comp p (f := fun (p : â„ Ã— sphere 0 1) â†¦ (p.1, (p.2 : E)))
        apply contMDiff_fst.prodMk_space ((contMDiff_coe_sphere.of_le le_top).comp contMDiff_snd))

/-- A formal eversion of a two-sphere into its ambient Euclidean space. -/
def formalEversionAux2 : HtpyFormalSol ğ“¡_imm :=
  { formalEversionAux E Ï‰ with
    is_sol' := fun t x â†¦ (Ï‰.isometry_rot t x).injective.comp (mfderiv_coe_sphere_injective x) }

def formalEversion : HtpyFormalSol ğ“¡_imm :=
  (formalEversionAux2 E Ï‰).reindex âŸ¨smoothStep, contMDiff_iff_contDiff.mpr smoothStep.smoothâŸ©

@[simp]
theorem formalEversion_bs (t : â„) :
    (formalEversion E Ï‰ t).bs = fun x : ğ•ŠÂ² â†¦
      (1 - smoothStep t : â„) â€¢ (x : E) + (smoothStep t : â„) â€¢ (-x : E) :=
  rfl

theorem formalEversion_zero (x : ğ•ŠÂ²) : (formalEversion E Ï‰ 0).bs x = x := by simp

theorem formalEversion_one (x : ğ•ŠÂ²) : (formalEversion E Ï‰ 1).bs x = -x := by simp

theorem formalEversionHolAtZero {t : â„} (ht : t < 1 / 4) :
    (formalEversion E Ï‰ t).toOneJetSec.IsHolonomic := by
  intro x
  change
    mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ ((1 : â„) - smoothStep t) â€¢ (y : E) +
      smoothStep t â€¢ -(y : E)) x =
      (Ï‰.rot (smoothStep t, x)).comp (mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ (y : E)) x)
  simp_rw [smoothStep.of_lt ht, Ï‰.rot_zero, ContinuousLinearMap.id_comp]
  congr with y
  simp [smoothStep.of_lt ht]

theorem formalEversionHolAtOne {t : â„} (ht : 3 / 4 < t) :
    (formalEversion E Ï‰ t).toOneJetSec.IsHolonomic := by
  intro x
  change
    mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ ((1 : â„) - smoothStep t) â€¢ (y : E) +
      smoothStep t â€¢ -(y : E)) x =
      (Ï‰.rot (smoothStep t, x)).comp (mfderiv (ğ“¡ 2) ğ“˜(â„, E) (fun y : ğ•ŠÂ² â†¦ (y : E)) x)
  trans mfderiv (ğ“¡ 2) ğ“˜(â„, E) (-fun y : ğ•ŠÂ² â†¦ (y : E)) x
  Â· congr 2 with y
    simp [smoothStep.of_gt ht]
  ext v
  erw [mfderiv_neg, ContinuousLinearMap.coe_comp', Function.comp_apply,
       ContinuousLinearMap.neg_apply, smoothStep.of_gt ht]
  rw [Ï‰.rot_one]; Â· rfl
  rw [â† range_mfderiv_coe_sphere (n := 2) x]
  exact LinearMap.mem_range_self ..

theorem formalEversion_hol_near_zero_one :
    âˆ€á¶  s : â„ Ã— ğ•ŠÂ² near {0, 1} Ã—Ë¢ univ, (formalEversion E Ï‰ s.1).toOneJetSec.IsHolonomicAt s.2 := by
  have : (Iio (1 / 4 : â„) âˆª Ioi (3 / 4)) Ã—Ë¢ (univ : Set ğ•ŠÂ²) âˆˆ ğ“Ë¢ (({0, 1} : Set â„) Ã—Ë¢ univ) := by
    refine ((isOpen_Iio.union isOpen_Ioi).prod isOpen_univ).mem_nhdsSet.mpr ?_
    rintro âŸ¨s, xâŸ© âŸ¨hs, hxâŸ©
    refine âŸ¨?_, mem_univ _âŸ©
    simp_rw [mem_insert_iff, mem_singleton_iff] at hs
    rcases hs with (rfl | rfl)
    Â· exact Or.inl (show (0 : â„) < 1 / 4 by norm_num)
    Â· exact Or.inr (show (3 / 4 : â„) < 1 by norm_num)
  filter_upwards [this]
  rintro âŸ¨t, xâŸ© âŸ¨ht | ht, _hxâŸ©
  Â· exact formalEversionHolAtZero E Ï‰ ht x
  Â· exact formalEversionHolAtOne E Ï‰ ht x

-- general helper lemmas, useful for showing smoothness of the formal immersions below
section helper

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E F G : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E] [NormedAddCommGroup F] [NormedSpace ğ•œ F]
  [NormedAddCommGroup G] [NormedSpace ğ•œ G]

-- move to Analysis.Calculus.ContDiff.Basic, or so
theorem contDiff_prod_iff (f : E â†’ F Ã— G) (n : â„•âˆ) :
    ContDiff ğ•œ n f â†”
      ContDiff ğ•œ n (Prod.fst âˆ˜ f) âˆ§ ContDiff ğ•œ n (Prod.snd âˆ˜ f) :=
  âŸ¨fun h â†¦ âŸ¨h.fst, h.sndâŸ©, fun h â†¦ h.1.prodMk h.2âŸ©

-- move to Analysis.Calculus.ContDiff.Defs, or so
lemma ContDiff.inr (x : E) (n : â„•âˆ) : ContDiff ğ•œ n fun p : F â†¦ (âŸ¨x, pâŸ© : E Ã— F) := by
  rw [contDiff_prod_iff]
  exact âŸ¨contDiff_const, contDiff_idâŸ©

-- is this form actually useful, compared to `uncurry_left`?
theorem ContDiff.uncurry_left' (n : â„•âˆ) {f : E Ã— F â†’ G}
    (hf : ContDiff ğ•œ n f) (x : E) :
    ContDiff ğ•œ n (fun p : F â†¦ f âŸ¨x, pâŸ©) :=
  hf.comp (ContDiff.inr x n)

theorem ContDiff.uncurry_left {f : E â†’ F â†’ G} (n : â„•âˆ) (hf : ContDiff ğ•œ n â†¿f) (x : E) :
    ContDiff ğ•œ n (f x) := by
  have : f x = (uncurry f) âˆ˜ fun p : F â†¦ (âŸ¨x, pâŸ© : E Ã— F) := by ext; simp
  rw [this]; exact hf.comp (ContDiff.inr x n)

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
  {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
  {H : Type*} [TopologicalSpace H] (I : ModelWithCorners ğ•œ E H)
  {M : Type*} [TopologicalSpace M] [ChartedSpace H M]
  {E' : Type*} [NormedAddCommGroup E'] [NormedSpace ğ•œ E']
  {H' : Type*} [TopologicalSpace H'] (I' : ModelWithCorners ğ•œ E' H')
  {M' : Type*} [MetricSpace M'] [ChartedSpace H' M']
  {EP : Type*} [NormedAddCommGroup EP] [NormedSpace ğ•œ EP]
  {HP : Type*} [TopologicalSpace HP] (IP : ModelWithCorners ğ•œ EP HP)
  {P : Type*} [TopologicalSpace P] [ChartedSpace HP P]

-- move to Mathlib.Geometry.Manifold.ContMDiff.Product
lemma ContMDiff.prod_left {n : â„•âˆ} (x : M) :
    ContMDiff I' (I.prod I') n fun p : M' â†¦ (âŸ¨x, pâŸ© : M Ã— M') := by
  rw [contMDiff_prod_iff]
  exact âŸ¨contMDiff_const, contMDiff_idâŸ©

-- move to Mathlib.Geometry.Manifold.ContMDiff.Product
theorem ContMDiff.uncurry_left {n : â„•âˆ}
    {f : M â†’ M' â†’ P} (hf : ContMDiff (I.prod I') IP n â†¿f) (x : M) :
    ContMDiff I' IP n (f x) := by
  have : f x = (uncurry f) âˆ˜ fun p : M' â†¦ âŸ¨x, pâŸ© := by ext; simp
  -- or just `apply hf.comp (ContMDiff.prod_left I I' x)`
  rw [this]; exact hf.comp (ContMDiff.prod_left I I' x)

end helper

theorem sphere_eversion :
    âˆƒ f : â„ â†’ ğ•ŠÂ² â†’ E,
      ContMDiff (ğ“˜(â„, â„).prod (ğ“¡ 2)) ğ“˜(â„, E) âˆ â†¿f âˆ§
        (f 0 = fun x : ğ•ŠÂ² â†¦ (x : E)) âˆ§ (f 1 = fun x : ğ•ŠÂ² â†¦ -(x : E)) âˆ§
        âˆ€ t, Immersion (ğ“¡ 2) ğ“˜(â„, E) (f t) âˆ := by
  classical
  let Ï‰ : Orientation â„ E (Fin 3) :=
    ((stdOrthonormalBasis _ _).reindex <|
          finCongr (Fact.out : finrank â„ E = 3)).toBasis.orientation
  have rankE : finrank â„ E = 3 := Fact.out
  haveI : FiniteDimensional â„ E := FiniteDimensional.of_finrank_eq_succ rankE
  have ineq_rank : finrank â„ (EuclideanSpace â„ (Fin 2)) < finrank â„ E := by simp [rankE]
  let Îµ : ğ•ŠÂ² â†’ â„ := fun _ â†¦ 1
  have hÎµ_pos : âˆ€ x, 0 < Îµ x := fun _ â†¦ zero_lt_one
  have hÎµ_cont : Continuous Îµ := continuous_const
  haveI : Nontrivial E := nontrivial_of_finrank_eq_succ (Fact.out : finrank â„ E = 3)
  haveI : Nonempty (sphere 0 1 : Set E) :=
    (NormedSpace.sphere_nonempty.mpr zero_le_one).to_subtype
  rcases(immersionRel_satisfiesHPrincipleWith (ğ“¡ 2) ğ•ŠÂ² ğ“˜(â„, E) E ğ“˜(â„, â„) â„ ineq_rank
          ((Finite.isClosed (by simp : ({0, 1} : Set â„).Finite)).prod isClosed_univ) hÎµ_pos
          hÎµ_cont).bs
      (formalEversion E Ï‰) (formalEversion_hol_near_zero_one E Ï‰) with
    âŸ¨f, hâ‚, hâ‚‚, -, hâ‚…âŸ©
  have := hâ‚‚.forall_mem principal_le_nhdsSet
  refine âŸ¨f, hâ‚, ?_, ?_, ?_/-hâ‚…-/âŸ©
  Â· ext x
    rw [this (0, x) (by simp)]
    convert formalEversion_zero E Ï‰ x
  Â· ext x
    rw [this (1, x) (by simp)]
    convert formalEversion_one E Ï‰ x
  Â· exact fun t â†¦ {
      contMDiff := ContMDiff.uncurry_left ğ“˜(â„, â„) (ğ“¡ 2) ğ“˜(â„, E) hâ‚ t
      diff_injective := hâ‚… t
    }

-- The next instance will be used in the main file.
instance (n : â„•) : Fact (finrank â„ (EuclideanSpace â„ <| Fin n) = n) :=
  âŸ¨finrank_euclideanSpace_finâŸ©

-- The next notation will be used in the main file.
notation "â„^" n:arg => EuclideanSpace â„ (Fin n)

end sphere_eversion
