import SphereEversion.Global.LocalisationData
import SphereEversion.Global.LocalizedConstruction
import SphereEversion.Global.ParametricityForFree
import SphereEversion.ToMathlib.Geometry.Manifold.Metrizable

/-!
# Gromov's theorem

We prove the h-principle for open and ample first order differential relations.
-/


noncomputable section

open Set Filter ModelWithCorners Metric

open scoped Topology Manifold ContDiff

variable {EM : Type*} [NormedAddCommGroup EM] [NormedSpace â„ EM] [FiniteDimensional â„ EM]
  {HM : Type*} [TopologicalSpace HM] {IM : ModelWithCorners â„ EM HM} [Boundaryless IM]
  {M : Type*} [TopologicalSpace M] [ChartedSpace HM M] [IsManifold IM âˆ M]
  [T2Space M] [SigmaCompactSpace M]
  {EX : Type*} [NormedAddCommGroup EX] [NormedSpace â„ EX] [FiniteDimensional â„ EX]
  {HX : Type*} [TopologicalSpace HX] {IX : ModelWithCorners â„ EX HX} [Boundaryless IX]
  -- note: X is a metric space
  {X : Type*}
  [MetricSpace X] [ChartedSpace HX X] [IsManifold IX âˆ X] [SigmaCompactSpace X]
  {R : RelMfld IM M IX X} {A : Set M} {Î´ : M â†’ â„}

@[inherit_doc] local notation "JÂ¹" => OneJetBundle IM M IX X

theorem RelMfld.Ample.satisfiesHPrinciple (hRample : R.Ample) (hRopen : IsOpen R) (hA : IsClosed A)
    (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : Continuous Î´) : R.SatisfiesHPrinciple A Î´ := by
  borelize EX
  letI := manifoldMetric IM M
  haveI := Manifold.locallyCompact_of_finiteDimensional (M := M) (I := IM)
  haveI := Manifold.locallyCompact_of_finiteDimensional (M := X) (I := IX)
  refine RelMfld.satisfiesHPrinciple_of_weak hA fun A hA ğ“•â‚€ hğ“•â‚€ â†¦ ?_
  obtain (hM | hM) := isEmpty_or_nonempty M
  Â· refine âŸ¨emptyHtpyFormalSol R, ?_, ?_, ?_, ?_âŸ© <;> intro
    all_goals try intro
    all_goals
      first
      | exact empty_htpy_formal_sol_eq
      | exact (IsEmpty.false â€¹Mâ€º).elim
  obtain (hX | hX) := isEmpty_or_nonempty X
  Â· exfalso
    inhabit M
    exact (IsEmpty.false <| ğ“•â‚€.bs default).elim
  -- We now start the main proof under the assumption that `M` and `X` are nonempty.
  have cont : Continuous ğ“•â‚€.bs := ğ“•â‚€.contMDiff_bs.continuous
  rcases exists_stability_dist IM IX cont with âŸ¨Îµ, Îµ_pos, Îµ_cont, hÎµâŸ©
  let Ï„ := fun x : M â†¦ min (Î´ x) (Îµ x)
  have Ï„_pos : âˆ€ x, 0 < Ï„ x := fun x â†¦ lt_min (hÎ´_pos x) (Îµ_pos x)
  have Ï„_cont : Continuous Ï„ := hÎ´_cont.min Îµ_cont
  let Pâ‚€ : âˆ€ x : M, Germ (ğ“ x) JÂ¹ â†’ Prop := fun x F â†¦
    F.value.1.1 = x âˆ§
      F.value âˆˆ R âˆ§
        F.ContMDiffAt' IM ((IM.prod IX).prod ğ“˜(â„, EM â†’L[â„] EX)) âˆ âˆ§
          RestrictGermPredicate (fun x F' â†¦ F'.value = ğ“•â‚€ x) A x F âˆ§
            dist F.value.1.2 (ğ“•â‚€.bs x) < Ï„ x
  let Pâ‚ : âˆ€ x : M, Germ (ğ“ x) JÂ¹ â†’ Prop := fun x F â†¦ IsHolonomicGerm F
  let Pâ‚‚ : âˆ€ p : â„ Ã— M, Germ (ğ“ p) JÂ¹ â†’ Prop := fun p F â†¦
    F.ContMDiffAt' (ğ“˜(â„).prod IM) ((IM.prod IX).prod ğ“˜(â„, EM â†’L[â„] EX)) âˆ
  have hPâ‚‚ : âˆ€ (a b : â„) (p : â„ Ã— M) (f : â„ Ã— M â†’ JÂ¹),
      Pâ‚‚ (a * p.1 + b, p.2) f â†’ Pâ‚‚ p fun p : â„ Ã— M â†¦ f (a * p.1 + b, p.2) := by
    rintro a b âŸ¨t, xâŸ© f h
    change ContMDiffAt _ _ _ (f âˆ˜ fun p : â„ Ã— M â†¦ (a * p.1 + b, p.2)) (t, x)
    change ContMDiffAt _ _ _ f ((fun p : â„ Ã— M â†¦ (a * p.1 + b, p.2)) (t, x)) at h
    have :
      ContMDiffAt (ğ“˜(â„, â„).prod IM) (ğ“˜(â„, â„).prod IM) âˆ (fun p : â„ Ã— M â†¦ (a * p.1 + b, p.2))
        (t, x) :=
      haveI hâ‚ : ContMDiffAt ğ“˜(â„, â„) ğ“˜(â„, â„) âˆ (fun t â†¦ a * t + b) t :=
        contMDiffAt_iff_contDiffAt.mpr
          (((contDiffAt_id : ContDiffAt â„ âˆ id t).const_smul a).add contDiffAt_const)
      hâ‚.prodMap contMDiffAt_id
    exact h.comp (t, x) this
  have init : âˆ€ x : M, Pâ‚€ x (ğ“•â‚€ : M â†’ JÂ¹) := by
    refine fun x â†¦ âŸ¨rfl, ğ“•â‚€.is_sol x, ğ“•â‚€.contMDiff x, ?_, ?_âŸ©
    Â· revert x
      exact forall_restrictGermPredicate_of_forall fun x â†¦ rfl
    Â· erw [dist_self]
      exact Ï„_pos x
  have hPâ‚‚' : âˆ€ (t : â„) (x : M) (f : M â†’ JÂ¹), Pâ‚€ x f â†’ Pâ‚‚ (t, x) fun p : â„ Ã— M â†¦ f p.2 :=
    fun t x f hf â†¦ ContMDiffAt.comp (t, x) hf.2.2.1 contMDiffAt_snd
  have ind : âˆ€ m : M, âˆƒ V âˆˆ ğ“ m, âˆ€ Kâ‚ âŠ† V, âˆ€ Kâ‚€ âŠ† interior Kâ‚, IsCompact Kâ‚€ â†’ IsCompact Kâ‚ â†’
      âˆ€ (C : Set M) (f : M â†’ JÂ¹), IsClosed C â†’
      (âˆ€ x, Pâ‚€ x f) â†’ (âˆ€á¶  x in ğ“Ë¢ C, Pâ‚ x f) â†’
        âˆƒ F : â„ â†’ M â†’ JÂ¹, (âˆ€ t x, Pâ‚€ x (F t)) âˆ§
                          (âˆ€á¶  x in ğ“Ë¢ (C âˆª Kâ‚€), Pâ‚ x (F 1)) âˆ§
                          (âˆ€ (p : â„ Ã— M), Pâ‚‚ p â†¿F) âˆ§
                          (âˆ€ t, âˆ€ x âˆ‰ Kâ‚, F t x = f x) âˆ§
                          (âˆ€á¶  t in ğ“Ë¢ (Iic 0), F t = f) âˆ§
                          âˆ€á¶  t in ğ“Ë¢ (Ici 1), F t = F 1 := by
    intro m
    rcases hÎµ m with âŸ¨Ï†, Ïˆ, âŸ¨e, rflâŸ©, hÏ†ÏˆâŸ©
    have : Ï† '' ball e 1 âˆˆ ğ“ (Ï† e) := by
      rw [â† Ï†.isOpenEmbedding.map_nhds_eq]
      exact image_mem_map (ball_mem_nhds e zero_lt_one)
    use Ï† '' (ball e 1), this
    intro Kâ‚ hKâ‚ Kâ‚€ Kâ‚€Kâ‚ Kâ‚€_cpct Kâ‚_cpct C f C_closed Pâ‚€f fC
    have Kâ‚Ï† : Kâ‚ âŠ† range Ï† := SurjOn.subset_range hKâ‚
    have Kâ‚€Ï† : Kâ‚€ âŠ† range Ï† := Kâ‚€Kâ‚.trans interior_subset |>.trans Kâ‚Ï†
    replace Kâ‚€_cpct : IsCompact (Ï† â»Â¹' Kâ‚€) :=
      Ï†.isOpenEmbedding.toIsInducing.isCompact_preimage' Kâ‚€_cpct Kâ‚€Ï†
    replace Kâ‚_cpct : IsCompact (Ï† â»Â¹' Kâ‚) :=
      Ï†.isOpenEmbedding.toIsInducing.isCompact_preimage' Kâ‚_cpct Kâ‚Ï†
    have Kâ‚€Kâ‚' : Ï† â»Â¹' Kâ‚€ âŠ† interior (Ï† â»Â¹' Kâ‚) := by
      rw [â† Ï†.isOpenMap.preimage_interior_eq_interior_preimage Ï†.continuous]
      gcongr
    simp only [Pâ‚€, forall_and] at Pâ‚€f
    rcases Pâ‚€f with âŸ¨hf_sec, hf_sol, hf_smooth, hf_A, hf_distâŸ©
    rw [forall_restrictGermPredicate_iff] at hf_A
    let F : FormalSol R := mkFormalSol f hf_sec hf_sol hf_smooth
    have hFAC : âˆ€á¶  x near A âˆª C, F.IsHolonomicAt x := by
      rw [Eventually.union_nhdsSet]
      refine âŸ¨(hf_A.and hğ“•â‚€).eventually_nhdsSet.mono fun x hx â†¦ ?_, fCâŸ©
      rw [eventually_and] at hx
      refine hx.2.self_of_nhds.congr (hx.1.mono fun x' hx' â†¦ ?_)
      simp only [FormalSol.toOneJetSec_coe, mkFormalSol_apply, F]
      exact hx'.symm
    have hFÏ†Ïˆ : F.bs '' (range Ï†) âŠ† range Ïˆ := by
      rw [â† range_comp]
      apply hÏ†Ïˆ
      intro x
      calc
        dist (F.bs x) (ğ“•â‚€.bs x) = dist (f x).1.2 (ğ“•â‚€.bs x) := by simp only [F, mkFormalSol_bs_apply]
        _ < Ï„ x := (hf_dist x)
        _ â‰¤ Îµ x := min_le_right _ _
    let Î· : M â†’ â„ := fun x â†¦ Ï„ x - dist (f x).1.2 (ğ“•â‚€.bs x)
    have Î·_pos : âˆ€ x, 0 < Î· x := fun x â†¦ sub_pos.mpr (hf_dist x)
    have Î·_cont : Continuous Î· := by
      have : ContMDiff IM ((IM.prod IX).prod ğ“˜(â„, EM â†’L[â„] EX)) âˆ f := fun x â†¦ hf_smooth x
      apply Ï„_cont.sub
      exact (oneJetBundle_proj_continuous.comp this.continuous).snd.dist ğ“•â‚€.contMDiff_bs.continuous
    rcases Ï†.improve_formalSol Ïˆ hRample hRopen (hA.union C_closed) Î·_pos Î·_cont hFÏ†Ïˆ hFAC Kâ‚€_cpct
        Kâ‚_cpct Kâ‚€Kâ‚' with
      âŸ¨F', hF'â‚€, hF'â‚, hF'AC, hF'Kâ‚, hF'Î·, hF'holâŸ©
    refine âŸ¨fun t x â†¦ F' t x, ?_, ?_, ?_, ?_, ?_, ?_âŸ©; all_goals beta_reduce
    Â· refine fun t x â†¦ âŸ¨rfl, F'.is_sol, (F' t).contMDiff x, ?_, ?_âŸ©
      Â· revert x
        rw [forall_restrictGermPredicate_iff]
        rw [Eventually.union_nhdsSet] at hF'AC
        apply (hF'AC.1.and hf_A).mono
        rintro x âŸ¨hx, hx'âŸ©
        change F' t x = _
        rw [hx t, â† hx', mkFormalSol_apply]
        rfl
      Â· calc
          dist (F' t x).1.2 (ğ“•â‚€.bs x) â‰¤ dist (F' t x).1.2 (F.bs x) + dist (F.bs x) (ğ“•â‚€.bs x) :=
            dist_triangle _ _ _
          _ < Î· x + dist (F.bs x) (ğ“•â‚€.bs x) := (add_lt_add_right (hF'Î· t x) _)
          _ = Ï„ x := by simp [F, Î·]
    Â· rw [union_assoc, Eventually.union_nhdsSet, image_preimage_eq_of_subset Kâ‚€Ï†] at hF'hol
      exact hF'hol.2
    Â· exact F'.contMDiff
    Â· intro t x hx
      replace hx : x âˆ‰ Ï† '' (Ï† â»Â¹' Kâ‚) := by rwa [image_preimage_eq_of_subset Kâ‚Ï†]
      simpa [F] using hF'Kâ‚ t x hx
    Â· apply hF'â‚€.mono fun x hx â†¦ ?_
      erw [hx]
      ext1 y
      simp [F]
    Â· exact hF'â‚.mono (fun _ hx â†¦ DFunLike.ext'_iff.mp hx)
  rcases inductive_htpy_construction Pâ‚€ Pâ‚ Pâ‚‚ hPâ‚‚ hPâ‚‚' init ind with âŸ¨F, hFâ‚€, hFPâ‚€, hFPâ‚, hFPâ‚‚âŸ©
  simp only [Pâ‚€, forall_and] at hFPâ‚€
  rcases hFPâ‚€ with âŸ¨hF_sec, hF_sol, _hF_smooth, hF_A, hF_distâŸ©
  refine âŸ¨mkHtpyFormalSol F hF_sec hF_sol hFPâ‚‚, ?_, hFPâ‚, ?_, ?_âŸ©
  Â· intro x
    rw [mkHtpyFormalSol_apply, hFâ‚€]
  Â· intro x hx t
    rw [mkHtpyFormalSol_apply]
    exact (forall_restrictGermPredicate_iff.mp <| hF_A t).self_of_nhdsSet x hx
  Â· intro t x
    change dist (mkHtpyFormalSol F hF_sec hF_sol hFPâ‚‚ t x).1.2 (ğ“•â‚€.bs x) â‰¤ Î´ x
    rw [mkHtpyFormalSol_apply]
    exact (hF_dist t x).le.trans (min_le_left _ _)

variable {EP : Type*} [NormedAddCommGroup EP] [NormedSpace â„ EP] [FiniteDimensional â„ EP]
  {HP : Type*} [TopologicalSpace HP] {IP : ModelWithCorners â„ EP HP} [Boundaryless IP]
  {P : Type*} [TopologicalSpace P] [ChartedSpace HP P] [IsManifold IP âˆ P]
  [SigmaCompactSpace P] [T2Space P] {C : Set (P Ã— M)}

/-
We now deduce the parametric case from the unparametric one using
`RelMfld.satisfiesHPrinciple.satisfiesHPrincipleWith` which reduces the parametric
`h`-principle to the non-parametric one for a different relation and `RelMfld.Ample.relativize`
which ensures the ampleness assumption survives this reduction.
-/
/-- **Gromov's Theorem** -/
theorem RelMfld.Ample.satisfiesHPrincipleWith (hRample : R.Ample) (hRopen : IsOpen R)
    (hC : IsClosed C) (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : Continuous Î´) :
    R.SatisfiesHPrincipleWith IP C Î´ := by
  have hÎ´_pos' : âˆ€ x : P Ã— M, 0 < Î´ x.2 := fun x : P Ã— M â†¦ hÎ´_pos x.snd
  have hÎ´_cont' : Continuous fun x : P Ã— M â†¦ Î´ x.2 := hÎ´_cont.comp continuous_snd
  have is_op : IsOpen (RelMfld.relativize IP P R) := R.isOpen_relativize hRopen
  apply RelMfld.SatisfiesHPrinciple.satisfiesHPrincipleWith
  exact (hRample.relativize IP P).satisfiesHPrinciple is_op hC hÎ´_pos' hÎ´_cont'

variable {E' : Type*} [NormedAddCommGroup E'] [NormedSpace â„ E'] [FiniteDimensional â„ E']
  {H' : Type*} [TopologicalSpace H'] {I' : ModelWithCorners â„ E' H'} [I'.Boundaryless]
  {M' : Type*} [TopologicalSpace M'] [ChartedSpace H' M']
  [IsManifold I' âˆ M'] [SigmaCompactSpace M'] [T2Space M']

/-
Since every (Ïƒ-compact) manifold is metrizable, the metric space assumption can be removed.
-/
/-- Gromov's Theorem without metric space assumption -/
theorem RelMfld.Ample.satisfiesHPrincipleWith' {R : RelMfld IM M I' M'} (hRample : R.Ample)
    (hRopen : IsOpen R) (hC : IsClosed C) (hÎ´_pos : âˆ€ x, 0 < Î´ x) (hÎ´_cont : Continuous Î´) :
    letI := manifoldMetric I' M'
    R.SatisfiesHPrincipleWith IP C Î´ := by
  letI := manifoldMetric I' M'
  apply RelMfld.Ample.satisfiesHPrincipleWith <;> assumption
